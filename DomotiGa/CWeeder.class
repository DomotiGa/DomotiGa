' Gambas class file

' Description:
' CWeeder.class
' Support for Weeder I/O Modules.

' Development Status:
' Started to implement WT-SSR (Solid State Relay Module) to control my Home Ventilation System.

' Links:
' http://www.weedertech.com

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

' Notes:
' You can stack Weeder modules on the same serial port.

PROPERTY Port AS String
PROPERTY WeederDebug AS Boolean
PRIVATE sPort AS String
PRIVATE bWeederDebug AS Boolean

PUBLIC hWeeder AS NEW SerialPort

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Connect() AS Boolean

  ' try to close the port
  TRY hWeeder.Close

  ' get a new one
  hWeeder = NEW Serialport AS "Weeder"
  WITH hWeeder
    .PortName = sPort
    .Speed = 9600
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog("Weeder Error: " & ERROR.Text)
  RETURN FALSE

END

PUBLIC FUNCTION ControlITHO(sAddress AS String, sSpeed AS String)

  DIM sCmd, sPort AS String

  SELECT CASE sSpeed
    CASE "1"
      sPort = "A"
    CASE "2"
      sPort = "B"
    CASE "3"
      sPort = "C"
    CASE "T"
      sPort = "D"
  END SELECT

  sCmd = sAddress & "C" & sPort & "1000" ' close relay sPort for 1 second
  Send(sCmd)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  TRY hWeeder.Close
  Main.WriteLog("Weeder serial port close.")

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog("Weeder Error: " & ERROR.Text & "")
  RETURN FALSE

END

PUBLIC SUB Send(sCommand AS String)

  IF hWeeder.Status <> Net.Active THEN RETURN

  IF Main.bWeederDebug THEN Main.WriteDebugLog("[Weeder] Write: " & sCommand)
  PRINT #hWeeder, sCommand & "\r"

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' got data input
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Weeder_Read()

  DIM sData AS String

  LINE INPUT #hWeeder, sData
  IF Main.bWeederDebug THEN
    IF InStr(sData, "?")
      Main.WriteDebugLog("[Weeder] Syntax error -> " & sData)
    ELSE
      Main.WriteDebugLog("[Weeder] Read: " & sData)
    END IF
  END IF

END

' implement the properties
PRIVATE FUNCTION Port_Read() AS String

  RETURN sPort

END

PRIVATE SUB Port_Write(sValue AS String)

  sPort = sValue

END

PRIVATE FUNCTION WeederDebug_Read() AS Boolean

  RETURN bWeederDebug

END

PRIVATE SUB WeederDebug_Write(sValue AS Boolean)

  bWeederDebug = sValue

END
