' Gambas class file

' Description:
' FDBInstallWizard.class
' Install an empty or demo database.

' Development Status:
' Finished and working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)
  lblVersion.Text = ("version ") & Main.sProgramVersion
  txtCopyright.Text = "Copyright (C) 2008-" & Year(Now) & " Ron Klinkien"
  txtHost.Text = Main.sSQLHost
  txtDatabase.Text = Main.sSQLDatabase
  txtPassword.SetFocus

END

PUBLIC SUB CreateDatabase()

  DIM sDumpFile AS String = IIf(rbEmpty.Value, "domotiga-empty.sql", "domotiga.sql")
  DIM sLog AS String

  IF Main.ProgramExist("mysql") THEN
    SHELL "mysql --host=" & txtHost.Text & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --execute='CREATE DATABASE " & txtDatabase.Text & ";' 2>&1" TO sLog
    WriteLog(sLog)
    WriteLog("Created database called '" & txtDatabase.Text & "' ...")
    SHELL "mysql --host=" & txtHost.Text & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --database=" & txtDatabase.Text & " --execute='SOURCE " & Main.sBaseDir &/ "install" &/ sDumpFile & "' 2>&1" TO sLog
    WriteLog(sLog)
    WriteLog("Loaded database tables from '" & sDumpFile & "' ...")
    SHELL "mysql --host=" & txtHost.Text & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --execute='FLUSH PRIVILEGES;' 2>&1" TO sLog
    WriteLog(sLog)
    WriteLog("Flushed database privileges ...")
  ELSE
    WriteLog("Couldn't find the mysql command!")
  ENDIF

END

PRIVATE SUB WriteLog(sText AS String)

  txtLog.Pos = txtLog.Length
  txtLog.Insert(sText & "\n")

END

PUBLIC SUB wInstall_Cancel()

  QUIT

END

PUBLIC SUB wInstall_Close()

  ME.Close

END

PUBLIC SUB wInstall_BeforeChange()

  SELECT CASE wInstall.Index
    CASE 0
      IF rbAgree.Value = FALSE THEN
        Balloon("Please agree or click cancel", rbAgree)
        STOP EVENT
        RETURN
      ENDIF
    CASE 2
      IF Len(txtHost.Text) = 0 THEN
        Balloon("Please enter a hostname", txtHost)
        STOP EVENT
        RETURN
      ENDIF
      IF Len(txtDatabase.Text) = 0 THEN
        Balloon("Please enter a database name", txtDatabase)
        STOP EVENT
        RETURN
      ENDIF
      IF Len(txtUser.Text) = 0 THEN
        Balloon("Please enter a user", txtUser)
        STOP EVENT
        RETURN
      ENDIF
      IF Len(txtPassword.Text) = 0 THEN
        Balloon("Please enter a password", txtPassword)
        STOP EVENT
        RETURN
      ENDIF
  END SELECT

END

PUBLIC SUB wInstall_Change()

  SELECT CASE wInstall.Index
    CASE 3
      CreateDatabase()
  END SELECT

END
