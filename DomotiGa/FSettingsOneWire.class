' Gambas class file

' Description:
' FSettingsOneWire.class
' Settings form for 1-Wire support.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)

  txtSerialPort.Text = Main.sOneWireSerialPort
  cmbBaudrate.Text = Main.sOneWireBaudrate
  chkDebug.Value = Main.bOneWireDebug
  chkEnabled.Value = Main.bOneWireEnabled
  btnSave.Enabled = FALSE

  IF Main.bOneWireEnabled = FALSE THEN
    txtSerialPort.Enabled = FALSE
    cmbBaudrate.Enabled = FALSE
    chkDebug.Enabled = FALSE
  END IF

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PUBLIC SUB txtSerialPort_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbBaudrate_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkDebug_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB btnSave_Click()

  DIM rResult AS Result

  ' save new OneWire settings
  rResult = Main.hDB.Exec("UPDATE settings_onewire SET debug = &1, serialport = &2, baudrate = &3, enabled = &4 WHERE id = 1", chkDebug.Value, txtSerialPort.Text, cmbBaudrate.Text, chkEnabled.Value)
  rResult = Main.GetSettingTable("onewire") ' reload settings
  IF rResult.Count THEN
    Main.bOneWireEnabled = rResult!enabled
    Main.sOneWireSerialPort = rResult!serialport
    Main.sOneWireBaudrate = rResult!baudrate
    Main.bOneWireDebug = rResult!debug
  END IF
  Main.Restart_OneWire()

  ME.Close

END

PUBLIC SUB btnDefaults_Click()

  DIM rResult AS Result

  rResult = Main.GetSettingTable("onewire", TRUE) ' get defaults
  IF rResult.Count THEN
    chkEnabled.Value = rResult!enabled
    txtSerialPort.text = rResult!serialport
    cmbBaudrate.text = rResult!baudrate
    chkDebug.Value = rResult!debug
  END IF
  btnSave.Enabled = TRUE

END

PUBLIC SUB chkEnabled_Click()

  txtSerialPort.Enabled = chkEnabled.Value
  cmbBaudrate.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnSave.Enabled = TRUE

END
