' Gambas class file

' Description:
' FTriggerEditor.class
' Support for creating and edit event triggers.

' Development Status:
' Just started, not working 100% yet.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE rResult AS Result
PRIVATE rResultTriggerTypes AS Result
PRIVATE rResultDevices AS Result
PUBLIC bAddTrigger AS Boolean
PRIVATE sVarCompare AS String
PRIVATE sDevCompare AS String

PUBLIC SUB Form_Open()

  DIM vValue AS Variant

  IF bAddTrigger THEN ME.Text = ("Add Trigger")
  ME.Move(FMain.X + 50, FMain.Y + 70)

  ' fill combo with available global vars
  cmbVariables.Add("")
  FOR EACH vValue IN Main.GlobalVar
    cmbVariables.Add(Main.GlobalVar.Key)
  NEXT
  rbVarEqual_Click()
  ' fill combo with available devices
  cmbDevices.Add("")
  rResultDevices = Main.hDB.Exec("SELECT * FROM devices")
  IF rResultDevices THEN
    IF rResultDevices.Count THEN
      FOR EACH rResultDevices
        cmbDevices.Add(rResultDevices!name)
      NEXT
    END IF
  ELSE
    Message.Info("Error: couldn't find trigger details!")
    RETURN
  END IF
  IF NOT bAddTrigger THEN
    ' fetch trigger details
    rResult = Main.hDB.Exec("SELECT * FROM triggers WHERE id = &1", FEventEditor.iTrigger)
    IF NOT rResult THEN
      Message.Info("Error: couldn't find trigger details!")
      RETURN
    END IF
    IF rResult.Count = 1 THEN
      txtTriggerName.Text = rResult!name
      ' fetch triggertype values
      rResultTriggerTypes = Main.hDB.Exec("SELECT * FROM triggertypes WHERE id = &1", rResult!type)
      IF rResultTriggerTypes THEN
        IF rResultTriggerTypes.Count THEN
          ' select tab
          tabTriggerTypes.Index = rResultTriggerTypes!tab
          tabTriggerTypes.SetFocus()
        END IF
      END IF
      ' preload controls to values from database
      SELECT rResult!type
        CASE 1 ' time now
          spbHours.Value = rResult!param1
          spbMinutes.Value = rResult!param2
        CASE 2 ' variable
          cmbVariables.Text = rResult!param1
          SelectVarCompare(rResult!param2)
          txtVariable.Text = rResult!param3
          cmbVariables_Click()
        CASE 3 ' device change
          TRY cmbDevices.Text = GetDeviceName(rResult!param1)
          TRY cmbValue.Text = rResult!param2
          SelectDevCompare(rResult!param3)
          TRY txtDevice.Text = rResult!param4
          cmbDevices_Click()
      END SELECT
    ELSE
      bAddTrigger = TRUE
    END IF
  END IF
END

PUBLIC SUB rbVarEqual_Click()

  sVarCompare = "="

END

PUBLIC SUB rbVarSmaller_Click()

  sVarCompare = "<"

END

PUBLIC SUB rbVarBigger_Click()

  sVarCompare = ">"

END

PUBLIC SUB rbVarUnEqual_Click()

  sVarCompare = "<>"

END

PUBLIC SUB rbVarMod_Click()

  sVarCompare = "MOD 0"

END

PRIVATE SUB EnableButtons()

  btnSave.Enabled = TRUE
  btnDeleteTrigger.Enabled = TRUE
  btnNewTrigger.Enabled = TRUE

END

PUBLIC SUB txtTriggerName_Change()

  EnableButtons()

END

PUBLIC SUB txtTriggerDescription_Change()

  EnableButtons()

END

PUBLIC SUB tabTriggerTypes_Click()

  EnableButtons()

END

PUBLIC SUB spbHours_Change()

  EnableButtons()

END

PUBLIC SUB spbMinutes_Change()

  EnableButtons()

END

PUBLIC SUB btnSave_Click()

  DIM rResultUpdate AS Result
  DIM iType AS Integer
  DIM sSql, sParam1, sParam2, sParam3, sParam4, sParam5 AS String

  IF bAddTrigger THEN 
    btnNewTrigger_Click
    RETURN
  END IF

  IF NOT txtTriggerName.Text THEN
    Balloon("Please enter a name for this trigger!", txtTriggerName)
    RETURN
  END IF

  iType = tabTriggerTypes.Index + 1
  SELECT iType
    CASE 1
      sParam1 = Format(spbHours.Value, "0#")
      sParam2 = Format(spbMinutes.Value, "0#")
    CASE 2
      IF NOT cmbVariables.Text THEN
        Balloon("Please select a variable!", cmbVariables)
        RETURN
      END IF
      sParam1 = cmbVariables.Text
      sParam2 = sVarCompare
      IF NOT txtVariable.Text THEN
        Balloon("Please enter a value to check!", txtVariable)
        RETURN
      END IF
      sParam3 = txtVariable.Text
    CASE 3
      IF NOT cmbDevices.Text THEN
        Balloon("Please select a device!", cmbDevices)
        RETURN
      END IF
      sParam1 = GetDeviceId(cmbDevices.Text)
      IF NOT cmbValue.Text THEN
        Balloon("Please select the field to check!", txtDevice)
        RETURN
      END IF
      IF NOT cmbValue.Text THEN
        Balloon("Please select a value to compare!", cmbValue)
        RETURN
      END IF
      sParam2 = cmbValue.Text
      sParam3 = sDevCompare
      IF NOT txtDevice.Text THEN
        Balloon("Please enter a value to check!", txtDevice)
        RETURN
      END IF
      sParam4 = txtDevice.Text
  END SELECT

  ' save new trigger settings
  sSql = "UPDATE triggers SET name = &1, type = &2, param1 = &3, param2 = &4, param3 = &5, param4 = &6, param5 = &7 WHERE id = &8"
  rResultUpdate = Main.hDB.Exec(sSql, txtTriggerName.Text, iType, sParam1, sParam2, sParam3, sParam4, sParam5, rResult!id)
  bAddTrigger = FALSE
  ME.Close

END

PUBLIC SUB btnNewTrigger_Click()

  DIM rResultNew AS Result
  DIM iType AS Integer
  DIM sParam1, sParam2, sParam3, sParam4, sParam5 AS String

  iType = tabTriggerTypes.Index + 1
  SELECT iType
    CASE 1
      sParam1 = Format(spbHours.Value, "0#")
      sParam2 = Format(spbMinutes.Value, "0#")
    CASE 2
      IF NOT cmbVariables.Text THEN
        Balloon("Please select a variable!", cmbVariables)
        RETURN
      END IF
      sParam1 = cmbVariables.Text
      sParam2 = sVarCompare
      IF NOT txtVariable.Text THEN
        Balloon("Please enter a value to check!", txtVariable)
        RETURN
      END IF
      sParam3 = txtVariable.Text
  END SELECT
  IF NOT txtTriggerName.Text THEN
    Balloon("Please enter a name for this trigger!", txtTriggerName)
    RETURN
  END IF
  IF TriggerNameExist(txtTriggerName.Text) THEN
    Balloon("Please enter a unique name for this trigger!", txtTriggerName)
    RETURN
  END IF
  Main.hDB.Begin()
  rResultNew = Main.hDB.Create("triggers")
  rResultNew!name = txtTriggerName.Text
  rResultNew!type = iType
  rResultNew!param1 = sParam1
  rResultNew!param2 = sParam2
  rResultNew!param3 = sParam3
  rResultNew!param4 = sParam4
  rResultNew!param5 = sParam5
  rResultNew.Update()
  Main.hDB.Commit()
  FEvents.GetEventList()
  bAddTrigger = FALSE

  ME.Close

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PRIVATE SUB TriggerNameExist(sName AS String) AS Boolean

  DIM rResultTrig AS Result

  rResultTrig = Main.hDB.Exec("SELECT id FROM triggers WHERE name = &1", sName)
  IF rResultTrig THEN
    IF rResultTrig.Count THEN
      RETURN TRUE
    END IF
  END IF
  RETURN FALSE

END

PRIVATE SUB SelectVarCompare(sValue AS String)

  SELECT sValue
    CASE "="
      rbVarEqual.Value = TRUE
    CASE "<"
      rbVarSmaller.Value = TRUE
    CASE ">"
      rbVarBigger.Value = TRUE
    CASE "<>"
      rbVarUnEqual.Value = TRUE
    CASE "MOD 0"
      rbVarMod.Value = TRUE
  END SELECT

END

PUBLIC SUB cmbVariables_Click()

  IF cmbVariables.Text THEN
    IF IsBoolean(Main.GlobalVar[cmbVariables.Text]) THEN
      txtVariableValue.Text = Main.DisplayBool(Main.GlobalVar[cmbVariables.Text])
      rbVarSmaller.Enabled = FALSE
      rbVarBigger.Enabled = FALSE
      rbVarMod.Enabled = FALSE
    ELSE IF NOT IsNumber(Main.GlobalVar[cmbVariables.Text]) THEN
      rbVarSmaller.Enabled = FALSE
      rbVarBigger.Enabled = FALSE
      rbVarMod.Enabled = FALSE
      txtVariableValue.Text = Main.GlobalVar[cmbVariables.Text]
    ELSE IF NOT Main.IsTime(Main.GlobalVar[cmbVariables.Text]) THEN
      rbVarMod.Enabled = FALSE
      txtVariableValue.Text = Main.GlobalVar[cmbVariables.Text]
    ELSE
      txtVariableValue.Text = Main.GlobalVar[cmbVariables.Text]
    END IF
  END IF

END


PUBLIC SUB GetDeviceName(iId AS Integer) AS String

  DIM rResultDevName AS result

  rResultDevName = Main.hDB.Exec("SELECT * FROM devices WHERE id = &1", iId)
  IF rResultDevName THEN
    IF rResultDevName.Count >= 1 THEN
      RETURN rResultDevName!name
    END IF
  END IF
  RETURN ""

END

PUBLIC SUB GetDeviceId(sName AS String) AS Integer

  DIM rResultDevName AS result

  rResultDevName = Main.hDB.Exec("SELECT id FROM devices WHERE name = &1", sName)
  IF rResultDevName THEN
    IF rResultDevName.Count >= 1 THEN
      RETURN rResultDevName!id
    END IF
  END IF
  RETURN 0

END

PRIVATE SUB SelectDevCompare(sValue AS String)

  SELECT sValue
    CASE "="
      rbDevEqual.Value = TRUE
    CASE "<"
      rbDevSmaller.Value = TRUE
    CASE ">"
      rbDevBigger.Value = TRUE
    CASE "<>"
      rbDevUnEqual.Value = TRUE
  END SELECT

END

PUBLIC SUB rbDevEqual_Click()

  sDevCompare = "="

END

PUBLIC SUB rbDevSmaller_Click()

  sDevCompare = "<"

END

PUBLIC SUB rbDevBigger_Click()

  sDevCompare = ">"

END

PUBLIC SUB rbDevUnEqual_Click()

  sDevCompare = "<>"

END

PUBLIC SUB cmbDevices_Click()

  DIM iId AS Integer

  IF cmbValue.Text AND cmbDevices.Text THEN 
    iId = GetDeviceId(cmbDevices.Text)
    IF iId THEN
      SELECT cmbValue.Text
        CASE "Value"
          txtDeviceValue.Text = Devices.GetCurrentValueForDevice(iId)
        CASE "Value2"
          txtDeviceValue.Text = Devices.GetCurrentValue2ForDevice(iId)
        CASE "Value3"
          txtDeviceValue.Text = Devices.GetCurrentValue3ForDevice(iId)
        CASE "Value4"
          txtDeviceValue.Text = Devices.GetCurrentValue4ForDevice(iId)
      END SELECT
    END IF
    IF IsNumber(Val(txtDeviceValue.Text)) THEN
      rbDevBigger.Enabled = TRUE
      rbDevSmaller.Enabled = TRUE
    ELSE
      rbDevEqual.Value = TRUE
      rbDevBigger.Enabled = FALSE
      rbDevSmaller.Enabled = FALSE
    END IF
  END IF

END

PUBLIC SUB cmbValue_Click()

  cmbDevices_Click()

END

PUBLIC SUB btnDeleteTrigger_Click()

  DIM iId AS Integer

  SELECT Message.Question("Are you sure that you want to delete this trigger?", "Yes", "No")
    CASE 1
      iId = GetTriggerId(txtTriggerName.Text) 
      IF iId THEN
        Main.hDB.Exec("DELETE FROM triggers WHERE id = &1", iId)
      END IF
      ME.Close
  END SELECT

END

PRIVATE SUB GetTriggerId(sName AS String) AS Integer

  DIM rResultTrig AS Result

  rResultTrig = Main.hDB.Exec("SELECT id FROM triggers WHERE name = &1", sName)
  TRY RETURN rResultTrig!id

END
