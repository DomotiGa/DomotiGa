' Gambas class file

' Description:
' FTriggerEditor.class
' Support for creating and edit event triggers.

' Development Status:
' Just started, not working 100% yet.

' DomotiGa - an open source home automation program.
' Copyright(C) 2010 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE rResult AS Result
PRIVATE rResultDevices AS Result
PUBLIC bAddTrigger AS Boolean
PRIVATE sVarCompare AS String
PRIVATE sDevCompare AS String
PUBLIC bTriggerEdit AS Boolean

PUBLIC SUB LoadTreeView()

  tvVariables.Clear
  LoadVariables("Variables")
  LoadDevices("Devices")

END

PUBLIC SUB LoadTriggers()

  DIM aTriggers AS String[]
  DIM sTrigger AS String

  aTriggers = Split(rResult!param1, "|")
  IF aTriggers.Count THEN
    FOR EACH sTrigger IN aTriggers
      TRY lvTriggerList.Add(sTrigger, sTrigger)
    NEXT
  ENDIF

END

PUBLIC SUB lvTriggerList_DblClick()

  TRY lvTriggerList.Remove(lvTriggerList.Item.Key)

END

PUBLIC SUB LoadVariables(sParent AS String)  

  DIM vValue AS Variant

  tvVariables.Add(Left(sParent, 3), sParent)
  FOR EACH vValue IN Main.GlobalVar
    LoadItem(Main.GlobalVar.Key, Main.GlobalVar.Key, Left(sParent, 3))
  NEXT

END

PUBLIC SUB LoadDevices(sParent AS String)

  DIM iCount AS Integer
  DIM rResult AS Result
  DIM sSql AS String

  tvVariables.Add(Left(sParent, 3), sParent)

  sSql = "SELECT * FROM devices WHERE enabled IS TRUE ORDER BY name"
  rResult = Main.hDB.Exec(sSql)
  IF NOT rResult THEN
    LoadItem("ERROR", "Error: table 'devices' not found!", Left(sParent, 3))
    RETURN
  END IF

  FOR iCount = 0 TO rResult.Max
    tvVariables.Add(Left(sParent, 3) & "_" & rResult!id, rResult!name & " (" & rResult!id & ")",, Left(sParent, 3))
    LoadItem("Value", "Value", Left(sParent, 3) & "_" & rResult!id)
    LoadItem("Value2", "Value2", Left(sParent, 3) & "_" & rResult!id)
    LoadItem("Value3", "Value3", Left(sParent, 3) & "_" & rResult!id)
    LoadItem("Value4", "Value4", Left(sParent, 3) & "_" & rResult!id)
    LoadItem("LastChanged", "LastChanged", Left(sParent, 3) & "_" & rResult!id)
    LoadItem("LastSeen", "LastSeen", Left(sParent, 3) & "_" & rResult!id)
    LoadItem("BatteryStatus", "BatteryStatus", Left(sParent, 3) & "_" & rResult!id)
    rResult.MoveNext
  NEXT

END

PUBLIC SUB LoadItem(sKey AS String, sText AS String, sParent AS String)

  tvVariables.Add(sParent & "_" & sKey, sText,, sParent)
  ' tvVariables.Add(sParent & sKey, sText, Picture[Main.sBaseDir &/ "icons/list.png"], sParent)

END

PUBLIC SUB tvVariables_DblClick()

  DIM sVal AS String

  IF tvVariables.Current.Children = 0 THEN
    IF Left(tvVariables.Current.Key, 3) = "Var" THEN
      ' here a Variable is added to the formula
      sVal = tvVariables.Current.Key
    ELSE IF Left(tvVariables.Current.Key, 3) = "Dev" THEN
      ' here a Device value is added to the formula
      sVal = tvVariables.Current.Key
    ELSE
      sval = tvVariables.Current.Text
    ENDIF
    TRY lvTriggerList.Add(sVal, sVal)
    'tvVariables.Remove(tvVariables.Current.Key)
  ENDIF

END

PUBLIC SUB btnApply_Click()

  DIM aCron AS String
  aCron = "TimeCron(" & txtCronTabMulti.Text & ")"
  TRY lvTriggerList.Add(aCron, aCron)

END


PUBLIC SUB lvTriggerList_Activate()

    TRY lvTriggerList.Remove(lvTriggerList.Item.Key)

END

PUBLIC SUB Form_Open()

  DIM vValue AS Variant

  IF bAddTrigger THEN ME.Text = ("Create Trigger")
  ME.Move(FMain.X + 50, FMain.Y + 70)

  ' fill combo with available global vars
  cmbVariables.Add("")
  FOR EACH vValue IN Main.GlobalVar
    cmbVariables.Add(Main.GlobalVar.Key)
  NEXT
  rbVarEqual_Click()
  ' fill combo with available devices
  cmbDevices.Add("")
  rResultDevices = Main.hDB.Exec("SELECT * FROM devices ORDER BY name")
  IF rResultDevices THEN
    IF rResultDevices.Count THEN
      FOR EACH rResultDevices
        cmbDevices.Add(rResultDevices!name)
      NEXT
    ENDIF
  ELSE
    Message.Info(("Error: couldn't find trigger details!"))
    RETURN
  ENDIF
  
  
LoadTreeView()

  IF NOT bAddTrigger THEN
    ' fetch trigger details
    IF bTriggerEdit THEN
      TRY rResult = Main.hDB.Exec("SELECT * FROM triggers WHERE id = &1", FEvents.tbvEvents[FEvents.iCurRow, 0].Text)
    ELSE
      TRY rResult = Main.hDB.Exec("SELECT * FROM triggers WHERE id = &1", FEventEditor.iTrigger)
    ENDIF
    IF NOT rResult THEN
      Message.Info(("Error: couldn't find trigger details!"))
      RETURN
    ENDIF
    IF rResult.Count = 1 THEN
      txtTriggerName.Text = rResult!name
      txtDescription.text = rResult!description
      ' preload controls to values from database
      SELECT rResult!type
        CASE 1 ' time now
          txtCronTab.Text = rResult!param1
        CASE 2 ' variable
          cmbVariables.Text = rResult!param1
          SelectVarCompare(rResult!param2)
          txtVariable.Text = rResult!param3
          cmbVariables_Click()
        CASE 3 ' device change
          TRY cmbDevices.Text = GetDeviceName(rResult!param1)
          TRY cmbValue.Text = rResult!param2
          SelectDevCompare(rResult!param3)
          TRY txtDevice.Text = rResult!param4
          cmbDevices_Click()
        CASE 4 ' ir remote
          TRY txtRemote.Text = rResult!param1
          TRY txtButton.Text = rResult!param2
        CASE 5 ' iviewer remote
          TRY txtIViewerRemote.Text = rResult!param1
          TRY txtIViewerJoin.Text = rResult!param2
          TRY txtIViewerValue.Text = rResult!param3
          
        CASE 6 ' temp multi trigger
          LoadTriggers()
          
      END SELECT
      TRY tabTriggerTypes.Index = rResult!type - 1
      tabTriggerTypes.SetFocus()
    ELSE
      bAddTrigger = TRUE
    ENDIF
  ENDIF

END

PUBLIC SUB GetTriggers() AS String

  DIM iCount AS Integer
  DIM sTriggers AS String = "|"

  lvTriggerList.MoveFirst

  FOR iCount = 1 TO lvTriggerList.Count
    sTriggers &= lvTriggerList.Item.Text & "|"
    lvTriggerList.MoveNext
  NEXT

  RETURN sTriggers

END

PUBLIC SUB rbVarEqual_Click()

  sVarCompare = "="

END

PUBLIC SUB rbVarSmaller_Click()

  sVarCompare = "<"

END

PUBLIC SUB rbVarBigger_Click()

  sVarCompare = ">"

END

PUBLIC SUB rbVarUnEqual_Click()

  sVarCompare = "<>"

END

PUBLIC SUB rbVarMod_Click()

  sVarCompare = "MOD 0"

END

PRIVATE SUB EnableButtons()

  btnSave.Enabled = TRUE
  btnDeleteTrigger.Enabled = TRUE
  btnNewTrigger.Enabled = TRUE

END

PUBLIC SUB txtTriggerName_Change()

  EnableButtons()

END

PUBLIC SUB txtTriggerDescription_Change()

  EnableButtons()

END

PUBLIC SUB tabTriggerTypes_Click()

  EnableButtons()

END

PUBLIC SUB spbHours_Change()

  EnableButtons()

END

PUBLIC SUB spbMinutes_Change()

  EnableButtons()

END

PUBLIC SUB btnSave_Click()

  DIM rResultUpdate AS Result
  DIM iType AS Integer
  DIM sSql, sParam1, sParam2, sParam3, sParam4, sParam5, sCron AS String
  DIM aCron AS String[]

  IF NOT txtTriggerName.Text THEN
    Balloon(("Please enter a name for this trigger!"), txtTriggerName)
    RETURN
  ENDIF

  IF NOT bAddTrigger THEN
    ' name has changed
    IF rResult!name <> txtTriggerName.Text THEN
      IF TriggerNameExist(txtTriggerName.Text) THEN
        Balloon(("Please enter a unique name for this trigger!"), txtTriggerName)
        RETURN
      ENDIF
    ENDIF
  ENDIF

  iType = tabTriggerTypes.Index + 1
  SELECT iType
    CASE 1
      sCron = LTrim(txtCronTab.Text)
      sCron = RTrim(sCron)
      aCron = Split(sCron, " ")
      IF aCron.Count <> 5 THEN
        Balloon(("Please enter 5 fields!"), txtCronTab)
        RETURN
      ENDIF
      sParam1 = sCron
    CASE 2
      IF NOT cmbVariables.Text THEN
        Balloon(("Please select a variable!"), cmbVariables)
        RETURN
      ENDIF
      sParam1 = cmbVariables.Text
      sParam2 = sVarCompare
      IF NOT txtVariable.Text THEN
        Balloon(("Please enter a value to check!"), txtVariable)
        RETURN
      ENDIF
      sParam3 = txtVariable.Text
    CASE 3
      IF NOT cmbDevices.Text THEN
        Balloon(("Please select a device!"), cmbDevices)
        RETURN
      ENDIF
      sParam1 = GetDeviceId(cmbDevices.Text)
      IF NOT cmbValue.Text THEN
        Balloon(("Please select the field to check!"), txtDevice)
        RETURN
      ENDIF
      IF NOT cmbValue.Text THEN
        Balloon(("Please select a value to compare!"), cmbValue)
        RETURN
      ENDIF
      sParam2 = cmbValue.Text
      sParam3 = sDevCompare
      IF NOT txtDevice.Text THEN
        Balloon(("Please enter a value to check!"), txtDevice)
        RETURN
      ENDIF
      sParam4 = txtDevice.Text
    CASE 4
      IF NOT txtRemote.Text THEN
        Balloon(("Please enter a remote name!"), txtRemote)
        RETURN
      ENDIF
      IF NOT txtButton.Text THEN
        Balloon(("Please enter a button value!"), txtButton)
        RETURN
      ENDIF
      sParam1 = txtRemote.Text
      sParam2 = txtButton.Text
      sParam3 = spRepeat.Value
    CASE 5
      IF NOT txtIViewerJoin.Text THEN
        Balloon(("Please enter a join name!"), txtIViewerJoin)
        RETURN
      ENDIF
      IF NOT txtIViewerValue.Text THEN
        Balloon(("Please enter a value!"), txtIViewerValue)
        RETURN
      ENDIF
      sParam1 = txtIViewerRemote.Text
      sParam2 = txtIViewerJoin.Text
      sParam3 = txtIViewerValue.Text
    CASE 6
      sParam1 = GetTriggers()
  END SELECT

  IF bAddTrigger THEN
    IF TriggerNameExist(txtTriggerName.Text) THEN
      Balloon(("Please enter a unique name for this trigger!"), txtTriggerName)
      RETURN
    ENDIF
    Main.hDB.Begin()
    rResult = Main.hDB.Create("triggers")
    rResult!name = txtTriggerName.Text
    rResult!type = iType
    rResult!description = txtDescription.Text
    rResult!param1 = sParam1
    rResult!param2 = sParam2
    rResult!param3 = sParam3
    rResult!param4 = sParam4
    rResult!param5 = sParam5
    rResult.Update()
    Main.hDB.Commit()
  ELSE
    ' save new trigger settings
    sSql = "UPDATE triggers SET name = &1, type = &2, param1 = &3, param2 = &4, param3 = &5, param4 = &6, param5 = &7, description = &8 WHERE id = &9"
    TRY rResultUpdate = Main.hDB.Exec(sSql, txtTriggerName.Text, iType, sParam1, sParam2, sParam3, sParam4, sParam5, txtDescription.Text, rResult!id)
  ENDIF

  IF FEventEditor.Id THEN
    FEventEditor.GetTriggers()
    FEventEditor.cmbTrigger.Text = txtTriggerName.Text
  ENDIF

  bAddTrigger = FALSE
  ME.Close

END

PUBLIC SUB btnNewTrigger_Click()

  bAddTrigger = TRUE
  btnSave_Click()

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PRIVATE SUB TriggerNameExist(sName AS String) AS Boolean

  DIM rResultTrig AS Result

  rResultTrig = Main.hDB.Exec("SELECT id FROM triggers WHERE name = &1", sName)
  IF rResultTrig THEN
    IF rResultTrig.Count THEN
      RETURN TRUE
    ENDIF
  ENDIF
  RETURN FALSE

END

PRIVATE SUB SelectVarCompare(sValue AS String)

  SELECT sValue
    CASE "="
      rbVarEqual.Value = TRUE
    CASE "<"
      rbVarSmaller.Value = TRUE
    CASE ">"
      rbVarBigger.Value = TRUE
    CASE "<>"
      rbVarUnEqual.Value = TRUE
    CASE "MOD 0"
      rbVarMod.Value = TRUE
  END SELECT

END

PUBLIC SUB cmbVariables_Click()

  IF cmbVariables.Text THEN
    IF IsBoolean(Main.GlobalVar[cmbVariables.Text]) THEN
      txtVariableValue.Text = Main.DisplayBool(Main.GlobalVar[cmbVariables.Text])
      rbVarSmaller.Enabled = FALSE
      rbVarBigger.Enabled = FALSE
      rbVarMod.Enabled = FALSE
    ELSE IF NOT IsNumber(Main.GlobalVar[cmbVariables.Text]) THEN
      rbVarSmaller.Enabled = FALSE
      rbVarBigger.Enabled = FALSE
      rbVarMod.Enabled = FALSE
      txtVariableValue.Text = Main.GlobalVar[cmbVariables.Text]
    ELSE IF NOT Main.IsTime(Main.GlobalVar[cmbVariables.Text]) THEN
      rbVarMod.Enabled = FALSE
      txtVariableValue.Text = Main.GlobalVar[cmbVariables.Text]
    ELSE
      txtVariableValue.Text = Main.GlobalVar[cmbVariables.Text]
    ENDIF
  ENDIF

END

PUBLIC SUB GetDeviceName(iId AS Integer) AS String

  DIM rResultDevName AS result

  rResultDevName = Main.hDB.Exec("SELECT * FROM devices WHERE id = &1", iId)
  IF rResultDevName THEN
    IF rResultDevName.Count >= 1 THEN
      RETURN rResultDevName!name
    ENDIF
  ENDIF
  RETURN ""

END

PUBLIC SUB GetDeviceId(sName AS String) AS Integer

  DIM rResultDevName AS result

  rResultDevName = Main.hDB.Exec("SELECT id FROM devices WHERE name = &1", sName)
  IF rResultDevName THEN
    IF rResultDevName.Count >= 1 THEN
      RETURN rResultDevName!id
    ENDIF
  ENDIF
  RETURN 0

END

PRIVATE SUB SelectDevCompare(sValue AS String)

  SELECT sValue
    CASE "="
      rbDevEqual.Value = TRUE
    CASE "<"
      rbDevSmaller.Value = TRUE
    CASE ">"
      rbDevBigger.Value = TRUE
    CASE "<>"
      rbDevUnEqual.Value = TRUE
  END SELECT

END

PUBLIC SUB rbDevEqual_Click()

  sDevCompare = "="

END

PUBLIC SUB rbDevSmaller_Click()

  sDevCompare = "<"

END

PUBLIC SUB rbDevBigger_Click()

  sDevCompare = ">"

END

PUBLIC SUB rbDevUnEqual_Click()

  sDevCompare = "<>"

END

PUBLIC SUB cmbDevices_Click()

  DIM iId AS Integer

  IF cmbValue.Text AND cmbDevices.Text THEN 
    iId = GetDeviceId(cmbDevices.Text)
    IF iId THEN txtDeviceValue.Text = Devices.GetCurrentValueForDevice(iId, cmbValue.Index)
    IF IsNumber(Val(txtDeviceValue.Text)) THEN
      rbDevBigger.Enabled = TRUE
      rbDevSmaller.Enabled = TRUE
    ELSE
      rbDevEqual.Enabled = TRUE
      rbDevBigger.Enabled = FALSE
      rbDevSmaller.Enabled = FALSE
    ENDIF
  ENDIF

END

PUBLIC SUB cmbValue_Click()

  cmbDevices_Click()

END

PUBLIC SUB btnDeleteTrigger_Click()

  DIM iId AS Integer

  SELECT Message.Question(("Are you sure that you want to delete this trigger?"), ("Yes"), ("No"))
    CASE 1
      iId = GetTriggerId(txtTriggerName.Text) 
      IF iId THEN
        Main.hDB.Exec("DELETE FROM triggers WHERE id = &1", iId)
      END IF
      IF FEventEditor.Id THEN FEventEditor.GetTriggers()
      ME.Close
  END SELECT

END

PRIVATE SUB GetTriggerId(sName AS String) AS Integer

  DIM rResultTrig AS Result

  rResultTrig = Main.hDB.Exec("SELECT id FROM triggers WHERE name = &1", sName)
  TRY RETURN rResultTrig!id

END

PUBLIC SUB txtDescription_KeyPress()

  btnSave.Enabled = TRUE

END