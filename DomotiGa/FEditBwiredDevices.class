' Gambas class file

' Description:
' FEditBwiredDevices.class
' Support for creating and edit bwired devices.

' Development Status:
' Finished and working.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2010 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC rResult AS Result
PUBLIC rResultDevices AS Result
PUBLIC bAddDevice AS Boolean

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)
  LoadBwiredDevice()

END

PUBLIC SUB LoadBwiredDevice()

  DIM iId AS Integer

  ' fill combo with available devices
  cmbDevices.Add("")
  rResultDevices = Main.hDB.Exec("SELECT * FROM devices WHERE enabled is TRUE")
  IF rResultDevices.Available THEN
    IF rResultDevices.Count THEN
      FOR EACH rResultDevices
        cmbDevices.Add(rResultDevices!name)
      NEXT
    END IF
  ELSE
    Message.Info(("Error: couldn't find devices details!"))
    RETURN
  END IF

  TRY iId = FSettingsBwiredMap.tbvBwiredDevices[FSettingsBwiredMap.iCurRow, 0].Text
  IF NOT ERROR THEN
    ' get values
    rResult = Main.hDB.Exec("SELECT * FROM devices_bwired WHERE id = &1", iId)
    IF rResult.Count >= 1 THEN
      cmbDevices.Text = rResult!devicename
      txtDescription.Text = rResult!description
      cmbValue.Text = rResult!value
      cmbLabel.Text = rResult!devicelabel
    END IF
  ELSE
    bAddDevice = TRUE
  END IF
  FillExample()

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PUBLIC SUB btnDelete_Click()

  Main.hDB.Exec("DELETE FROM devices_bwired WHERE id = &1", rResult!id)
  ME.Close
  FSettingsBwiredMap.FillBwiredDevices()

END

PUBLIC SUB btnSave_Click()

  DIM rResultUpdate AS Result
  DIM iId AS Integer

  IF NOT ValidInput() THEN RETURN

  iId = GetDeviceId(cmbDevices.Text)

  IF bAddDevice THEN
    Main.hDB.Begin()
    rResult = Main.hDB.Create("devices_bwired")
    rResult!description = txtDescription.Text
    rResult!devicename = cmbDevices.Text
    rResult!deviceid = iId
    rResult!devicelabel = cmbLabel.Text
    rResult!value = cmbValue.Index
    rResult.Update()
    Main.hDB.Commit()
    bAddDevice = FALSE
  ELSE
    ' save new bwired device settings
    rResultUpdate = Main.hDB.Exec("UPDATE devices_bwired SET description = &1, devicename = &2, deviceid = &3, devicelabel = &4, value = &5 WHERE id = &6", txtDescription.Text, cmbDevices.Text, iId, cmbLabel.Text, cmbValue.Index, rResult!id)
  END IF
  ME.Close
  FSettingsBwiredMap.FillBwiredDevices()

END

PUBLIC SUB GetDeviceId(sName AS String) AS Integer

  DIM rDevice AS Result

  TRY rDevice = Main.hDB.Exec("SELECT id FROM devices WHERE name = &1", sName)
  IF rDevice.Count THEN
    RETURN rDevice!id
  END IF

END

PUBLIC SUB txtDescription_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbDevices_Click()

  btnSave.Enabled = TRUE
  FillExample()

END

PUBLIC SUB cmbValue_Click()

  btnSave.Enabled = TRUE
  FillExample()

END

PRIVATE SUB ValidInput() AS Boolean

  IF NOT txtDescription.Text THEN
    Balloon(("Please enter a description!"), txtDescription)
    RETURN FALSE
  END IF
  IF NOT cmbDevices.Text THEN
    Balloon(("Please select a device!"), cmbDevices)
    RETURN FALSE
  END IF
  IF NOT cmbValue.Text THEN
    Balloon(("Please select a value field!"), cmbValue)
    RETURN FALSE
  END IF
  RETURN TRUE

END

PUBLIC SUB btnAdd_Click()

  bAddDevice = TRUE
  btnSave_Click()

END

PUBLIC SUB FillExample()

  DIM iId AS Integer

  IF cmbValue.Text AND cmbDevices.Text THEN 
    iId = GetDeviceId(cmbDevices.Text)
    IF iId THEN
      SELECT cmbValue.Index
        CASE 1
          txtDeviceValue.Text = Devices.GetCurrentValueForDevice(iId) & " " & cmbLabel.Text
        CASE 2
          txtDeviceValue.Text = Devices.GetCurrentValue2ForDevice(iId) & " " & cmbLabel.Text
        CASE 3
          txtDeviceValue.Text = Devices.GetCurrentValue3ForDevice(iId) & " " & cmbLabel.Text
        CASE 4
          txtDeviceValue.Text = Devices.GetCurrentValue3ForDevice(iId) & " " & cmbLabel.Text
      END SELECT
    END IF
  END IF

END

PUBLIC SUB cmbLabel_Click()

  btnSave.Enabled = TRUE
  FillExample()

END

PUBLIC SUB cmbLabel_Change()

  btnSave.Enabled = TRUE
  FillExample()

END
