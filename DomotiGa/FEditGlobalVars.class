' Gambas class file

' Description:
' FEditGlobalVars.class
' Add a way to edit/delete globalvars.

' Development Status:
' Just created.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PRIVATE sOldGlobalVar AS String

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)
  txtEditBox.Hide()
  GetVarList()

END

PUBLIC SUB btnClose_Click()

  ME.Close

END

PUBLIC SUB GetVarList()

  DIM iCount AS Integer
  DIM vValue AS Variant

  WITH tbvGlobalVar
    .Columns.Count = 2
    .Rows.Count = Main.GlobalVar.Count
    .Columns[0].Title = ("Name")
    .Columns[0].Width = 180
    .Columns[1].Title = ("Value")
    .Columns[1].Width = 150
  END WITH

  FOR EACH vValue IN Main.GlobalVar
    tbvGlobalVar[iCount, 0].Text = Main.GlobalVar.Key
    IF gb.Boolean = TypeOf(vValue) THEN
      tbvGlobalVar[iCount, 1].Text = Main.DisplayBool(vValue)
    ELSE
      tbvGlobalVar[iCount, 1].Text = vValue
    END IF
    iCount = iCount + 1
  NEXT

END

PUBLIC SUB tbvGlobalVar_ColumnClick(Column AS Integer)

  Main.SortTableView(ME.tbvGlobalVar, Column, TRUE)

END

PUBLIC SUB btnRefresh_Click()

  GetVarList()

END

PUBLIC SUB btnDelete_Click()

  IF Message.Question("Are you sure you want to delete var '" & tbvGlobalVar[tbvGlobalVar.Row, 0].Text & "'?", "Yes", "No") = 1 THEN
    IF Main.bServer THEN
      Main.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
    ELSE
      Main.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
      XMLClient.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
    ENDIF
    tbvGlobalVar.Rows.Remove(tbvGlobalVar.Row)
  ENDIF

END

PUBLIC SUB tbvGlobalVar_Activate()

  IF tbvGlobalVar.Column = 0 THEN
    txtEditBox.X = tbvGlobalVar.Current.X + tbvGlobalVar.X
    txtEditBox.Y = tbvGlobalVar.Current.Y + tbvGlobalVar.Y
    txtEditBox.Width = tbvGlobalVar.Current.Width
    txtEditBox.Text = tbvGlobalVar[tbvGlobalVar.Row, 0].Text
    sOldGlobalVar = tbvGlobalVar[tbvGlobalVar.Row, 0].Text
    txtEditBox.Show()
    tbvGlobalVar.Enabled = FALSE
  ELSE IF tbvGlobalVar.Column = 1 THEN
    txtEditBox.X = tbvGlobalVar.Current.X + tbvGlobalVar.X
    txtEditBox.Y = tbvGlobalVar.Current.Y + tbvGlobalVar.Y
    txtEditBox.Width = tbvGlobalVar.Current.Width
    txtEditBox.Text = tbvGlobalVar[tbvGlobalVar.Row, 1].Text
    txtEditBox.Show()
    tbvGlobalVar.Enabled = FALSE
  ENDIF

END

PUBLIC SUB txtEditBox_Activate()

  txtEditBox.Hide()
  IF tbvGlobalVar.Column = 0 THEN
    tbvGlobalVar[tbvGlobalVar.Row, 0].Text = txtEditBox.Text
    IF Main.bServer THEN
      Main.SetGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text, tbvGlobalVar[tbvGlobalVar.Row, 1].Text)
      Main.DelGlobalVar(sOldGlobalVar)
    ELSE
      Main.SetGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text, tbvGlobalVar[tbvGlobalVar.Row, 1].Text)
      Main.DelGlobalVar(sOldGlobalVar)
      XMLClient.SetGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text, tbvGlobalVar[tbvGlobalVar.Row, 1].Text)
      XMLClient.DelGlobalVar(sOldGlobalVar)
    ENDIF
  ELSE IF tbvGlobalVar.Column = 1 THEN
    tbvGlobalVar[tbvGlobalVar.Row, 1].Text = txtEditBox.Text
    IF Main.bServer THEN
      Main.SetGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text, tbvGlobalVar[tbvGlobalVar.Row, 1].Text)
    ELSE
      Main.SetGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text, tbvGlobalVar[tbvGlobalVar.Row, 1].Text)
      XMLClient.SetGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text, tbvGlobalVar[tbvGlobalVar.Row, 1].Text)
    ENDIF
  ENDIF
  tbvGlobalVar.Enabled = TRUE

END

PUBLIC SUB btnAdd_Click()

  IF Len(txtName.Text) > 0 AND IF Len(txtValue.Text) > 0 THEN
    IF Main.bServer THEN
      Main.SetGlobalVar(txtName.Text, txtValue.Text)
    ELSE
      Main.SetGlobalVar(txtName.Text, txtValue.Text)
      XMLClient.SetGlobalVar(txtName.Text, txtValue.Text)
    ENDIF
    GetVarList()
    txtName.Clear
    txtValue.Clear
  ELSE
    Balloon("Fill in a variable name and a value first!", txtName)
  ENDIF

END
