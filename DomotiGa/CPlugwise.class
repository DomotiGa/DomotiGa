' Gambas class file

' Description:
' CPlugwise.class
' Use Plugwise to control devices.

' Development Status:
' Just started.

' Links:
' http://www.plugwise.nl
' http://mindprod.com/jgloss/crc.html

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PROPERTY Port AS String
PROPERTY PlugwiseDebug AS Boolean
PROPERTY PollTime AS Integer

PRIVATE sPort AS String
PRIVATE bPlugwiseDebug AS Boolean
PRIVATE iPollTime AS Integer

PUBLIC hPlugwise AS NEW SerialPort
PUBLIC tPlugwise AS Timer

' CRC-16(CCITT) table with Poly = 0x1021
PRIVATE aCRCTable AS Integer[256]
PRIVATE bTableInit AS Boolean

CONST POWERCHANGECODE AS String = "0017"
CONST DEVICEINFOCODE AS String = "0023"
CONST DEVICEINFORESPONSECODE AS String = "0024"
CONST CALIBRATIONCODE AS String = "0026"
CONST CALIBRATIONRESPONSECODE AS String = "0027"
CONST POWERINFOCODE AS String = "0012"
CONST POWERINFORESPONSECODE AS String = "0013"

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Connect() AS Boolean

  ' try to close the port
  TRY hPlugwise.Close

  ' get a new one
  hPlugwise = NEW Serialport AS "Plugwise"

  WITH hPlugwise
    .PortName = sPort
    .Speed = 115200
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog("Plugwise Error: " & ERROR.Text)
  RETURN FALSE

END

PUBLIC SUB Run()

  ' start poll timer for power usage check
  tPlugwise = NEW Timer AS "tPlugwise"
  tPlugwise.Delay = iPollTime * 1000 ' multiply for seconds
  tPlugwise.Start

  CheckRelay()

END

PUBLIC SUB tPlugwise_Timer()

  CheckPower()
  CheckRelay()

END

PUBLIC SUB CheckPower()

  DIM rResult AS Result
  DIM iCount AS Integer

  TRY rResult = Main.hDB.Exec("SELECT * FROM devices WHERE enabled is TRUE")

  IF rResult.Count THEN
    FOR EACH rResult
      IF InStr(Devices.FindTypeForDevice(rResult!id), "Plugwise")
        IF Main.bPlugwiseDebug THEN Main.WriteDebugLog("[Plugwise] checking power usage of device '" & rResult!name & "' with address " & rResult!address)
        GetPowerInfo(rResult!address)
      END IF
    NEXT
  END IF

END

PUBLIC SUB CheckRelay()

  DIM rResult AS Result
  DIM iCount AS Integer

  TRY rResult = Main.hDB.Exec("SELECT * FROM devices WHERE enabled is TRUE")

  IF rResult.Count THEN
    FOR EACH rResult
      IF InStr(Devices.FindTypeForDevice(rResult!id), "Plugwise")
        IF Main.bPlugwiseDebug THEN Main.WriteDebugLog("[Plugwise] checking relay state of device '" & rResult!name & "' with address " & rResult!address)
        GetDeviceInfo(rResult!address)
      END IF
    NEXT
  END IF

END

PUBLIC SUB SendCommand(sAddress AS String, sCmd AS String)

  DIM sHeader, sString, sFunc, sCRC, sComplete, sMac AS String

  sMac = Replace(sAddress, ":", "")

  SELECT UCase(sCmd)
    CASE "ON"
      sFunc = "01"
    CASE "OFF"
      sFunc = "00"
  END SELECT

  ' <ENQ><ENQ><ETX><ETX>0017000A1100003111AB01AC92<CR><LF>
  sHeader = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  sString = POWERCHANGECODE & sMAC & sFunc
  sCRC = Hex(CalculateCRC(sString))
  sComplete = sHeader & sString & sCRC & Chr(13) & Chr(10)

  IF bPlugwiseDebug THEN
    Main.WriteDebugLog("[Plugwise] MAC: " & sMAC)
    Main.WriteDebugLog("[Plugwise] String: " & sString & " CRC: " & sCRC)
  END IF
  Send(sComplete)

END

PUBLIC SUB GetDeviceInfo(sAddress AS String) AS String

  DIM sHeader, sString, sCRC, sComplete, sResult, sRelay, sMac, sMacRecv, sState AS String
  DIM iDeviceId AS Integer

  sMac = Replace(sAddress, ":", "")

  sHeader = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  sString = DEVICEINFOCODE & sMAC
  sCRC = Hex(CalculateCRC(sString))
  sComplete = sHeader & sString & sCRC & Chr(13) & Chr(10)

  IF bPlugwiseDebug THEN
    Main.WriteDebugLog("[Plugwise] MAC: " & sMAC)
    Main.WriteDebugLog("[Plugwise] String: " & sString & " CRC: " & sCRC)
  END IF
  Send(sComplete)
  sResult = GetResult(DEVICEINFORESPONSECODE)
  IF sResult THEN
    sMacRecv = Mid(sResult, 1, 16)
    sRelay = Mid(sResult, 33, 2)
    sState = IIf(sRelay = "01", "On", "Off")
    IF bPlugwiseDebug THEN
      Main.WriteDebugLog("[Plugwise] GetDeviceInfo Result: " & sResult)
      Main.WriteDebugLog("[Plugwise] MAC Received: " & sMacRecv)
      Main.WriteDebugLog("[Plugwise] RelayState: " & sRelay & " " & sState)
    END IF
    ' update device state
    iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Plugwise Stick"))
    IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, sState, "", "", "")
  ELSE
    IF bPlugwiseDebug THEN Main.WriteDebugLog("[Plugwise] GetDeviceInfo() didn't get expected answer.")
  END IF

END

PUBLIC SUB GetPowerInfo(sAddress AS String) AS String

  DIM sHeader, sString, sCRC, sComplete, sResult, sPulses, sMac AS String
  DIM iPulses, iDeviceId AS Integer
  DIM fGainA, fGainB, fOffTot, fOffRuis, fCorrectPulses, fWatt, fkWh AS Float

  sMac = Replace(sAddress, ":", "")

  ' <ENQ><ENQ><ETX><ETX>0017000A1100003111AB01AC92<CR><LF>
  sHeader = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  sString = CALIBRATIONCODE & sMAC
  sCRC = Hex(CalculateCRC(sString))
  sComplete = sHeader & sString & sCRC & Chr(13) & Chr(10)

  IF bPlugwiseDebug THEN
    Main.WriteDebugLog("[Plugwise] MAC: " & sMAC)
    Main.WriteDebugLog("[Plugwise] String: " & sString & " CRC: " & sCRC)
  END IF
  Send(sComplete)
  sResult = GetResult(CALIBRATIONRESPONSECODE)
  IF sResult THEN
    fGainA = HexToFloat(Mid(sResult, 1, 8))
    fGainB = HexToFloat(Mid(sResult, 9, 8))
    fOffTot = HexToFloat(Mid(sResult, 17, 8))
    fOffRuis = HexToFloat(Mid(sResult, 25, 8))
    IF bPlugwiseDebug THEN
      Main.WriteDebugLog("[Plugwise] GetCalibration Result: " & sResult)
      Main.WriteDebugLog("[Plugwise] GainA: " & Str(fGainA))
      Main.WriteDebugLog("[Plugwise] GainB: " & Str(fGainB))
      Main.WriteDebugLog("[Plugwise] OffTot: " & Str(fOffTot))
      Main.WriteDebugLog("[Plugwise] OffRuis: " & Str(fOffRuis))
    END IF

    ' <ENQ><ENQ><ETX><ETX>0017000A1100003111AB01AC92<CR><LF>
    sString = POWERINFOCODE & sMAC
    sCRC = Hex(CalculateCRC(sString))
    sComplete = sHeader & sString & sCRC & Chr(13) & Chr(10)
    Send(sComplete)
    sResult = GetResult(POWERINFORESPONSECODE)
    IF sResult THEN
      sPulses = Mid(sResult, 5, 4)
      iPulses = HexToInt(sPulses) / 1.0
      IF iPulses THEN fCorrectPulses = 1.0 * ((((iPulses + fOffRuis) ^ 2.0 * fGainB) + ((iPulses + fOffRuis) * fGainA)) + fOffTot)
      fkWh = (fCorrectPulses / 1) / 468.9385193
      fWatt = fkWh * 1000

      IF bPlugwiseDebug THEN
        Main.WriteDebugLog("[Plugwise] GetPowerInfo Result: " & sResult)
        Main.WriteDebugLog("[Plugwise] Pulses: " & sPulses & " (hex) " & iPulses)
        Main.WriteDebugLog("[Plugwise] Corrected Pulses: " & fCorrectPulses)
        Main.WriteDebugLog("[Plugwise] kWh: " & Str(fkWh))
        Main.WriteDebugLog("[Plugwise] Watt: " & Str(fWatt))
      END IF
      iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Plugwise Stick"))
      IF iDeviceId THEN
        Devices.ValueUpdate(iDeviceId, "", Format(CStr(fWatt), "0.###"), Format(CStr(fkWh), "0.###"), "")
      END IF
    END IF
  ELSE
    IF bPlugwiseDebug THEN Main.WriteDebugLog("[Plugwise] GetPowerInfo() didn't get expected answer.")
  END IF

END

PRIVATE FUNCTION HexToFloat(sHex AS String) AS Float

  DIM iSign AS Integer
  DIM fExponent, fMant AS Float

  IF sHex THEN
    ' sign
    iSign = IIf((Val("&H" & Mid(sHex, 1, 2)) AND &H80) = 128, -1, 1)
    ' exponent
    fExponent = (Val("&H" & Mid(sHex, 1, 3)) AND &H7F8) / 2 ^ 3 - 127
    ' mantissa
    fMant = (Val("&H" & Mid(sHex, 3, 6)) AND &H7FFFFF) / 2 ^ 23 + 1
  END IF
  RETURN iSign * fMant * 2 ^ fExponent

END

PRIVATE FUNCTION HexToInt(sHex AS String) AS Integer

  RETURN Val("&H" & sHex)

END

PRIVATE SUB GetResult(sType AS String) AS String

  DIM iTries AS Integer = 10
  DIM sData, sTemp, sResult AS String
  DIM iBegin AS Integer

  WHILE (iTries > 0)
    ' wait a bit and read response.
    WAIT 0.01
    ' see if we got some data.
    TRY READ #hPlugwise, sTemp, Lof(hPlugwise)
    sData &= sTemp
    DEC iTries
  WEND

  SELECT sType
    CASE CALIBRATIONRESPONSECODE
      iBegin = InStr(sData, Chr(5) & Chr(5) & Chr(3) & Chr(3) & CALIBRATIONRESPONSECODE)
      IF iBegin THEN sResult = Mid(sData, iBegin + 24, 52)
    CASE POWERINFORESPONSECODE
      iBegin = InStr(sData, Chr(5) & Chr(5) & Chr(3) & Chr(3) & POWERINFORESPONSECODE)
      IF iBegin THEN sResult = Mid(sData, iBegin + 24, 37)
    CASE DEVICEINFORESPONSECODE
      iBegin = InStr(sData, Chr(5) & Chr(5) & Chr(3) & Chr(3) & DEVICEINFORESPONSECODE)
      IF iBegin THEN sResult = Mid(sData, iBegin + 8, 34)
  END SELECT

  RETURN sResult

END

PRIVATE SUB Send(sCommand AS String)

  IF hPlugwise.Status <> Net.Active THEN RETURN

  IF Main.bPlugwiseDebug THEN Main.WriteDebugLog("[Plugwise] Write: " & sCommand)
  WRITE #hPlugwise, sCommand, Len(sCommand)

END

PRIVATE SUB CalculateCRC(sbuf AS String) AS Integer

  DIM iWork, iCnt AS Integer

  IF NOT bTableInit THEN InitCrcTable()

  iWork = &H0
  FOR iCnt = 1 TO Len(sbuf)
    iWork = (aCRCTable[(Asc(Mid(sBuf, iCnt, 1)) XOR (Lsr(iWork, 8))) AND &HFF] XOR (Lsl(iWork, 8))) AND &HFFFF&
  NEXT
  RETURN iWork

END

PRIVATE SUB InitCRCTable()

  DIM iTableIndex, iIndex, iAccumulator, iMask AS Integer

  FOR iTableIndex = 0 TO 255
    iMask = Lsl(iTableIndex, 8)
    iAccumulator = 0
    FOR iIndex = 0 TO 7
      IF ((iMask XOR iAccumulator) AND &H8000&) THEN
        iAccumulator = Lsl(iAccumulator, 1) XOR &H1021&
      ELSE
        iAccumulator = Lsl(iAccumulator, 1)
      END IF
      iMask = Lsl(iMask, 1)
    NEXT
    aCRCTable[iTableIndex] = iAccumulator AND &HFFFF&
  NEXT
  bTableInit = TRUE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  TRY hPlugwise.Close
  Main.WriteLog("Plugwise serial port close.")

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog("Plugwise Error: " & ERROR.Text)
  RETURN FALSE

END

' implement properties
PRIVATE FUNCTION Port_Read() AS String

  RETURN sPort

END

PRIVATE SUB Port_Write(Value AS String)

  sPort = Value

END

PRIVATE FUNCTION PlugwiseDebug_Read() AS Boolean

  RETURN bPlugwiseDebug

END

PRIVATE SUB PlugwiseDebug_Write(Value AS Boolean)

  bPlugwiseDebug = Value

END

PRIVATE FUNCTION PollTime_Read() AS Integer

  RETURN iPollTime

END

PRIVATE FUNCTION PollTime_Write(Value AS Integer)

  iPollTime = Value

END
