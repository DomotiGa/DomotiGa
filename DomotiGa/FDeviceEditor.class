' Gambas class file

' Description:
' FDeviceEditor.class
' Support for creating and edit all device parameters.

' Development Status:
' Finished and working, maybe needs better error checking.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2010 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC rResult AS Result
PUBLIC rResultType AS Result
PUBLIC rResultInterface AS Result
PUBLIC rResultInterfaces AS Result
PUBLIC rResultLocations AS Result
PUBLIC rResultLocation AS Result
PUBLIC rResultGroups AS Result
PUBLIC rResultDeviceTypes AS Result
PUBLIC rResultFloorplan AS Result
PUBLIC bAddDevice AS Boolean

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)
  LoadDevice()

END

PUBLIC SUB LoadDevice()

  ' fill combo with available modules/devicetypes
  cmbModule.Add(" ")
  TRY rResultDeviceTypes = Main.hDB.Exec("SELECT * FROM devicetypes ORDER BY name")
  IF rResultDeviceTypes THEN
    IF (rResultDeviceTypes.Count >= 1) THEN
      FOR EACH rResultDeviceTypes
        cmbModule.Add(rResultDeviceTypes!name)
      NEXT
    ELSE
      Message(("No devicetypes found!"))
    END IF
  ELSE
    Message(("No devicetypes database table found!"))
  END IF

  ' fill combo with available interfaces
  cmbInterface.Add(" ")
  TRY rResultInterfaces = Main.hDB.Exec("SELECT * FROM interfaces")
  IF rResultInterfaces THEN
    IF (rResultInterfaces.Count >= 1) THEN
      FOR EACH rResultInterfaces
        cmbInterface.Add(rResultInterfaces!name)
      NEXT
    ELSE
      Message(("No interfaces found!"))
    END IF
  ELSE
    Message(("No interfaces database table found!"))
  END IF

  ' put groups in selected or available groups list
  rResultGroups = Main.hDB.Exec("SELECT * FROM groups")
  IF rResultGroups THEN
    IF (rResultGroups.Count >= 1) THEN
      FOR EACH rResultGroups
        lvGroupAvail.Add(rResultGroups!name, rResultGroups!name)
      NEXT
    ELSE
      Message(("No groups found!"))
    END IF
  ELSE
    Message(("No groups database table found!"))
  END IF

  ' fill combo with available locations
  rResultLocations = Main.hDB.Exec("SELECT * FROM locations")
  IF rResultLocations THEN
    IF (rResultLocations.Count >= 1) THEN
      FOR EACH rResultLocations
        cmbLocation.Add(rResultLocations!name)
      NEXT
    ELSE
      Message(("No locations found!"))
    END IF
  ELSE
    Message(("No locations database table found!"))
  END IF

  ' fill combo with available floorplans
  rResultFloorplan = Main.hDB.Exec("SELECT * FROM floors")
  IF rResultFloorplan THEN
    IF (rResultFloorplan.Count >= 1) THEN
      FOR EACH rResultFloorplan
        cmbFloorplan.Add(rResultFloorplan!name)
      NEXT
    ELSE
      Message(("No floorplans found!"))
    END IF
  ELSE
    Message(("No floorplans database table found!"))
  END IF

  IF NOT bAddDevice THEN
    ' get selected device values
    rResult = Main.hDB.Exec("SELECT * FROM devices WHERE id = &1", FDevices.tbvDevices[FDevices.iCurRow, 0].Text)
    IF rResult.Count >= 1 THEN
      tbName.Text = rResult!name
      TRY rResultType = Main.hDB.Exec("SELECT * FROM devicetypes WHERE id = &1", rResult!module)
      IF rResultType.Count THEN
        cmbModule.Text = rResultType!name
        tbDeviceType.Text = rResultType!type
        tbDeviceDescr.Text = rResultType!description
        FillAddressFields(rResultType!type)
        SelectAddressFields(rResultType!type, rResultType!addressformat)
      END IF
      ' load device picture if available
      LoadDeviceImage()
      ' select interface
      TRY rResultInterface = Main.hDB.Exec("SELECT * FROM interfaces WHERE id = &1", rResult!interface)
      IF rResultInterface.Count THEN cmbInterface.Text = rResultInterface!name

      ' select location
      rResultLocation = Main.hDB.Exec("SELECT * FROM locations WHERE id = &1", rResult!location)
      IF rResultLocation.Count THEN
        TRY cmbLocation.Text = rResultLocation!name
      ELSE
        cmbLocation.Text = ("Not Defined")
      END IF

      ' put groups in selected or available groups list
      rResultGroups = Main.hDB.Exec("SELECT * FROM groups")
      IF (rResultGroups.Count >= 1) THEN 
        FOR EACH rResultGroups
          IF InStr("|" & rResult!groups & "|", "|" & rResultGroups!name & "|") THEN
            lvGroupActive.Add(rResultGroups!name, rResultGroups!name)
            lvGroupAvail.Remove(rResultGroups!name)
          END IF
        NEXT
      ELSE
        Message(("No groups found!"))
      END IF

      ' rest of fields
      cmbLabel.Text = rResult!label
      cmbLabel2.Text = rResult!label2
      cmbLabel3.Text = rResult!label3
      cmbLabel4.Text = rResult!label4
      tbValue.Text = rResult!value
      tbValue2.Text = rResult!value2
      tbValue3.Text = rResult!value3
      tbValue4.Text = rResult!value4
      tbOnIcon.Text = rResult!onicon
      tbOffIcon.Text = rResult!officon
      tbDimIcon.Text = rResult!dimicon
      tbLastSeen.Text = rResult!lastseen
      tbFirstSeen.Text = rResult!firstseen

      tbDivider1.Text = IIf(rResult!divider, rResult!divider, 1)
      tbDivider2.Text = IIf(rResult!divider2, rResult!divider2, 1)
      tbDivider3.Text = IIf(rResult!divider3, rResult!divider3, 1)
      tbDivider4.Text = IIf(rResult!divider4, rResult!divider4, 1)
      tbCorrection1.Text = IIf(rResult!calibration, rResult!calibration, 0)
      tbCorrection2.Text = IIf(rResult!calibration2, rResult!calibration2, 0)
      tbCorrection3.Text = IIf(rResult!calibration3, rResult!calibration3, 0)
      tbCorrection4.Text = IIf(rResult!calibration4, rResult!calibration4, 0)

      FillCorrectionFields()

      DisplayIcons()

      TRY chkEnabled.Value = rResult!enabled
      TRY chkHide.Value = rResult!hide
      TRY chkLog.Value = rResult!log
      TRY chkSwitchable.Value = rResult!switchable
      TRY chkDimable.Value = rResult!dimable
      TRY chkExtended.Value = rResult!extcode

      tbComments.Text = rResult!comments
      tbBatt.Text = rResult!batterystatus
      ' floorplan values
      tbX.Text = rResult!x
      tbY.Text = rResult!y

      ' select floorplan
      rResultFloorplan = Main.hDB.Exec("SELECT * FROM floors WHERE floor = &1", rResult!floorplan)
      IF rResultFloorplan.Count THEN
        cmbFloorplan.Text = rResultFloorplan!name
      ELSE
        cmbFloorplan.Text = ("Not Defined")
      END IF

      ' rrdtool graph stuff
      TRY chkGraph.Value = rResult!graph
      tbValueDS.Text = rResult!valuerrddsname
      tbValue2DS.Text = rResult!value2rrddsname
      tbValue3DS.Text = rResult!value3rrddsname
      tbValue4DS.Text = rResult!value4rrddsname
      cmbValueType.Text = rResult!valuerrdtype
      cmbValue2Type.Text = rResult!value2rrdtype
      cmbValue3Type.Text = rResult!value3rrdtype
      cmbValue4Type.Text = rResult!value4rrdtype
    ELSE
      Message(("Couldn't load device record!"))
      ME.Close
    END IF
  END IF

END

PUBLIC SUB FillCorrectionFields()

  TRY tbResult1.Text = (rResult!value / tbDivider1.Text) + tbCorrection1.Text
  TRY tbResult2.Text = (rResult!value2 / tbDivider2.Text) + tbCorrection2.Text
  TRY tbResult3.Text = (rResult!value3 / tbDivider3.Text) + tbCorrection3.Text
  TRY tbResult4.Text = (rResult!value4 / tbDivider4.Text) + tbCorrection4.Text

END

PUBLIC SUB RefreshDeviceType()

  rResultType = Main.hDB.Exec("SELECT * FROM devicetypes WHERE name = &1", cmbModule.Text)
  IF rResultType.Count THEN
    tbDeviceType.Text = rResultType!type
    tbDeviceDescr.Text = rResultType!description

    ' enable correct address fields
    SelectAddressFields(rResultType!type, rResultType!addressformat)

    ' enable the default interface for this type
    SELECT tbDeviceType.Text
      CASE "Plugwise"
        cmbInterface.Text = "Plugwise Stick"
      CASE "HDDTemp"
        cmbInterface.Text = "HDDTemp Socket"
      CASE "Ping"
        cmbInterface.Text = "Ping Socket"
      CASE "UPS"
        cmbInterface.Text = "UPS Socket"
      CASE "RFXCom", "HE", "Oregon", "X10Security", "ATI", "Visonic"
        cmbInterface.Text = "RFXCom Receiver"
      CASE "Bluetooth"
        cmbInterface.Text = "Bluetooth Dongle"
      CASE "KNX/EIB"
        cmbInterface.Text = "KNX/EIB Interface"
      CASE "Digitemp"
        cmbInterface.Text = "Digitemp"
      CASE "ARC", "AC"
        cmbInterface.Text = "RFXCom Transmitter"
      CASE "Z-Wave"
        cmbInterface.Text = "Z-Wave Controller"
      CASE "Virtual"
        cmbInterface.Text = "Virtual Interface"
      CASE "Squeeze"
        cmbInterface.Text = "SqueezeServer Socket"
    END SELECT
  ENDIF

END

PUBLIC SUB DoesInterfaceDoDeviceType() AS Boolean

  DIM rResultInt, rResultDevType AS Result
  DIM sDevProtocol, sIntProtocol AS String

  rResultDevType = Main.hDB.Exec("SELECT type FROM devicetypes WHERE name = &1", cmbModule.Text)
  sDevProtocol = rResultDevType!type

  rResultInt = Main.hDB.Exec("SELECT type FROM interfaces WHERE name = &1", cmbInterface.Text)
  sIntProtocol = rResultInt!type

  IF InStr(sIntProtocol, sDevProtocol) THEN
    RETURN TRUE
  ELSE
    RETURN FALSE
  END IF

END

PUBLIC SUB GetDeviceType() AS Integer

  rResultType = Main.hDB.Exec("SELECT id FROM devicetypes WHERE name = &1", cmbModule.Text)
  RETURN rResultType!id

END

PUBLIC SUB GetInterface() AS Integer

  rResultInterface = Main.hDB.Exec("SELECT id FROM interfaces WHERE name = &1", cmbInterface.Text)
  RETURN rResultInterface!id

END

PUBLIC SUB GetLocation() AS Integer

  rResultLocation = Main.hDB.Exec("SELECT id FROM locations WHERE name = &1", cmbLocation.Text)
  RETURN rResultLocation!id

END

PUBLIC SUB SelectAddressFields(sType AS String, sAddressFormat AS String)

  IF sType = "X10" OR sType = "A10" OR sType = "KAKU" OR sType = "ARC" OR sType = "AC" THEN
    cmbHouseCode.Show
    spDeviceCode.Show
    tbOtherAddress.Hide
    lblOther.Hide
    lblX10.Show
    cmbHouseCode.Enabled = TRUE
  ELSE
    cmbHouseCode.Enabled = FALSE
    lblOther.Show
    cmbHouseCode.Hide
    spDeviceCode.Hide
    tbOtherAddress.Show
    lblX10.Hide
  END IF

  SELECT CASE sType
    CASE "Ping"
      lblOther.Text = ("IP Addr.")
    CASE "1-Wire"
      lblOther.Text = ("1-Wire ID")
    CASE "Bluetooth"
      lblOther.Text = ("MAC Addr.")
    CASE "HDDTemp"
      lblOther.Text = ("Device")
    CASE "Plugwise"
      lblOther.Text = ("MAC Addr.")
    CASE "Oregon"
      lblOther.Text = ("Address")
    CASE "RFXCom"
      lblOther.Text = ("Address")
    CASE "HE"
      lblOther.Text = ("DevUnit")
    CASE "UPS"
      lblOther.Text = ("UPS Name")
    CASE "DSC"
      lblOther.Text = ("Zone Name")
    CASE "Weeder"
      lblOther.Text = ("Output")
    CASE "X10", "A10", "KAKU", "ARC", "AC"
    CASE "KNX/EIB"
    CASE "Z-Wave"
      lblOther.Text = ("Node ID")
  END SELECT
  lblInfo.Text = ("e.g. ") & sAddressFormat

END

PUBLIC SUB FillAddressFields(sType AS String)

  IF sType = "X10" OR sType = "A10" OR sType = "KAKU" OR sType = "ARC" OR sType = "AC" THEN
    ' fill address fields
    IF rResult!address THEN
      cmbHouseCode.Text = Left$(rResult!address, 1)
      spDeviceCode.Value = Right$(rResult!address, 1)
    END IF
  ELSE
    IF rResult!address THEN
      tbOtherAddress.Text = rResult!address
    END IF
  END IF

END

PUBLIC SUB GetAddress() AS String

  IF cmbHouseCode.Enabled = TRUE THEN
    RETURN cmbHouseCode.Text & Format$(spDeviceCode.Value, "0#")
  ELSE
    RETURN tbOtherAddress.Text
  END IF

END

PUBLIC SUB tbName_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbInterface_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbHouseCode_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB spDeviceCode_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbOtherAddress_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbLocation_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbLabel_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbLabel2_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbLabel3_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbLabel4_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbValue_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB btnCancel_Click()

  bAddDevice = FALSE
  ME.Close

END

PUBLIC SUB cmbModule_Click()

  RefreshDeviceType()
  btnSave.Enabled = TRUE
  LoadDeviceImage()

END

PRIVATE SUB LoadDeviceImage()

  DIM sImage, sName AS String

  sName = LCase(Replace(cmbModule.Text, " ", "-"))
  sName = Replace(sName, "/", "-")

  sImage = Main.sBaseDir &/ "icons/devices" &/ sName & ".jpg"
  TRY pbDevice.Picture = Picture.Load(sImage)
  IF ERROR THEN
    pbDevice.Hide
  ELSE
    pbDevice.Show
  END IF

END

PUBLIC SUB GetGroups() AS String

  DIM iCount AS Integer
  DIM sGroups AS String = "|"

  lvGroupActive.MoveFirst

  FOR iCount = 1 TO lvGroupActive.Count
    sGroups = sGroups & lvGroupActive.Item.Text & "|"
    lvGroupActive.MoveNext
  NEXT

  RETURN sGroups

END

PUBLIC SUB lvGroupAvail_Click()

  btnSave.Enabled = TRUE
  lvGroupActive.Add(lvGroupAvail.Item.Key, lvGroupAvail.Item.Key) 
  lvGroupAvail.Remove(lvGroupAvail.Item.Key)

END

PUBLIC SUB lvGroupActive_Click()

  btnSave.Enabled = TRUE
  lvGroupAvail.Add(lvGroupActive.Item.Key, lvGroupActive.Item.Key) 
  lvGroupActive.Remove(lvGroupActive.Item.Key)

END

PUBLIC SUB btnDeleteDevice_Click()

  SELECT Message.Question(("Are you sure that you want to delete this device?"), ("Yes"), ("No"))
    CASE 1
      Main.hDB.Exec("DELETE FROM devices WHERE id = &1", rResult!id)
      ME.Close
      FDevices.GetDeviceList()
  END SELECT

END

PRIVATE SUB DeviceNameExist(sName AS String, OPTIONAL iDeviceId AS Integer) AS Boolean

  DIM rResultDevice AS Result

  rResultDevice = Main.hDB.Exec("SELECT id FROM devices WHERE name = &1", sName)
  IF rResultDevice THEN
    IF rResultDevice.Count THEN
      FOR EACH rResultDevice
        IF iDeviceId THEN
          IF rResultDevice!id <> iDeviceId THEN RETURN TRUE
        ELSE
          RETURN TRUE
        END IF
      NEXT
    END IF
  END IF
  RETURN FALSE

END

PRIVATE SUB DeviceAddressExist(sAddress AS String, iInterface AS Integer, OPTIONAL iDeviceId AS Integer) AS Boolean

  DIM rResultDevice AS Result

  IF Len(sAddress) = 0 THEN RETURN TRUE

  rResultDevice = Main.hDB.Exec("SELECT id,address FROM devices WHERE interface = &1", iInterface)
  IF rResultDevice THEN
    IF rResultDevice.Count THEN
      FOR EACH rResultDevice
        IF iDeviceId THEN
          IF sAddress = rResultDevice!address AND IF rResultDevice!id <> iDeviceId THEN RETURN TRUE
        ELSE
          IF sAddress = rResultDevice!address THEN RETURN TRUE
        END IF
      NEXT
    END IF
  END IF
  RETURN FALSE

END

PUBLIC SUB btnNewDevice_Click()

  IF NOT tbName.Text THEN
    Balloon(("Please enter a name for this device!"), tbName)
    RETURN
  END IF
  IF DeviceNameExist(tbName.Text) THEN
    Balloon(("Please enter a unique name for this device!"), tbName)
    RETURN
  END IF
  IF Len(cmbModule.Text) < 2 THEN
    Balloon(("Please select a device type for this device!"), cmbModule)
    RETURN
  END IF
  IF Len(cmbInterface.Text) < 2 THEN
    Balloon(("Please select an interface for this device!"), cmbInterface)
    RETURN
  END IF
  IF NOT DoesInterfaceDoDeviceType() THEN 
    Balloon(("Selected interface does not support this devicetype!"), tbDeviceType)
    RETURN
  END IF
  IF DeviceAddressExist(GetAddress(), GetInterface()) THEN
    Balloon(("Please enter a unique address for this device!"), Frame2)
    RETURN
  END IF
  IF chkEnabled.Value = FALSE THEN
    SELECT Message.Question(("You haven't enabled this device, shall I enabled it?"), ("Yes"), ("No"))
      CASE 1
        chkEnabled.Value = TRUE
      CASE 0
        ' you want it
    END SELECT
  END IF
  Main.hDB.Begin()
  rResult = Main.hDB.Create("devices")
  rResult!name = tbName.Text
  rResult!module = GetDeviceType()
  rResult!interface = GetInterface()
  rResult!address = GetAddress()
  rResult!location = GetLocation()
  rResult!groups = GetGroups()
  rResult!label = cmbLabel.Text
  rResult!label2 = cmbLabel2.Text
  rResult!label3 = cmbLabel3.Text
  rResult!label4 = cmbLabel4.Text
  rResult!onicon = tbOnIcon.Text
  rResult!divider = IIf(tbDivider1.Text, tbDivider1.Text, 1)
  rResult!divider2 = IIf(tbDivider2.Text, tbDivider2.Text, 1)
  rResult!divider3 = IIf(tbDivider3.Text, tbDivider3.Text, 1)
  rResult!divider4 = IIf(tbDivider4.Text, tbDivider4.Text, 1)
  rResult!calibration = IIf(tbCorrection1.Text, tbCorrection1.Text, 0)
  rResult!calibration2 = IIf(tbCorrection2.Text, tbCorrection2.Text, 0)
  rResult!calibration3 = IIf(tbCorrection3.Text, tbCorrection3.Text, 0)
  rResult!calibration4 = IIf(tbCorrection4.Text, tbCorrection4.Text, 0)
  rResult!officon = tbOffIcon.Text
  rResult!dimicon = tbDimIcon.Text
  rResult!comments = tbComments.Text
  rResult!enabled = chkEnabled.Value
  rResult!hide = chkHide.Value
  rResult!log = chkLog.Value
  rResult!switchable = chkSwitchable.Value
  rResult!dimable = chkDimable.Value
  rResult!graph = chkGraph.Value
  rResult!valuerrddsname = LCase(tbValueDS.Text)
  rResult!value2rrddsname = LCase(tbValue2DS.Text)
  rResult!value3rrddsname = LCase(tbValue3DS.Text)
  rResult!value4rrddsname = LCase(tbValue4DS.Text)
  rResult!valuerrdtype = cmbValueType.Text
  rResult!value2rrdtype = cmbValue2Type.Text
  rResult!value3rrdtype = cmbValue3Type.Text
  rResult!value4rrdtype = cmbValue4Type.Text
  IF tbX.Text THEN rResult!x = tbX.Text
  IF tbY.Text THEN rResult!y = tbY.Text
  rResult!floorplan = GetFloorplan()
  rResult.Update()
  Main.hDB.Commit()
  FDevices.GetDeviceList()
  bAddDevice = FALSE

  ME.Close

END

PUBLIC SUB btnSave_Click()

  DIM rResultUpdate AS Result
  DIM sSql, sAddress, sGroups AS String
  DIM iLocation, iInterface, iDeviceType, iFloorplan AS Integer
  DIM sDivider1, sDivider2, sDivider3, sDivider4, sCorrection1, sCorrection2, sCorrection3, sCorrection4 AS String

  IF bAddDevice THEN
    btnNewDevice_Click()
    RETURN
  END IF
  IF NOT tbName.Text THEN
    Balloon(("Please enter a name for this device!"), tbName)
    RETURN
  END IF
  IF DeviceNameExist(tbName.Text, rResult!id) THEN
    Balloon(("Please enter a unique name for this device!"), tbName)
    RETURN
  END IF
  IF Len(cmbModule.Text) < 2 THEN
    Balloon(("Please select an device type for this device!"), cmbModule)
    RETURN
  END IF
  IF Len(cmbInterface.Text) < 2 THEN
    Balloon(("Please select an interface for this device!"), cmbInterface)
    RETURN
  END IF
  IF NOT DoesInterfaceDoDeviceType() THEN
    Balloon(("Selected interface does not support this devicetype!"), tbDeviceType)
    RETURN
  END IF
  IF DeviceAddressExist(GetAddress(), GetInterface(), rResult!id) THEN
    Balloon(("Please enter a unique address for this device!"), Frame2)
    RETURN
  END IF
  IF chkEnabled.Value = FALSE THEN
    SELECT Message.Question(("You haven't enabled this device, shall I enabled it?"), ("Yes"), ("No"))
      CASE 1
        chkEnabled.Value = TRUE
      CASE 0
        ' you want it
    END SELECT
  END IF
  iDeviceType = GetDeviceType()
  iInterface = GetInterface()
  sAddress = GetAddress()
  iLocation = GetLocation()
  sGroups = GetGroups()
  iFloorplan = GetFloorplan()

  sDivider1 = IIf(tbDivider1.Text, tbDivider1.Text, 1)
  sDivider2 = IIf(tbDivider2.Text, tbDivider2.Text, 1)
  sDivider3 = IIf(tbDivider3.Text, tbDivider3.Text, 1)
  sDivider4 = IIf(tbDivider4.Text, tbDivider4.Text, 1)
  sCorrection1 = IIf(tbCorrection1.Text, tbCorrection1.Text, 0)
  sCorrection2 = IIf(tbCorrection2.Text, tbCorrection2.Text, 0)
  sCorrection3 = IIf(tbCorrection3.Text, tbCorrection3.Text, 0)
  sCorrection4 = IIf(tbCorrection4.Text, tbCorrection4.Text, 0)

  ' save new device settings
  sSql = "UPDATE devices SET name = &1, module = &2, interface = &3, address = &4, location = &5,"
  sSql &= "label = &6, label2 = &7, label3 = &8, value = &9, officon = &10, onicon = &11,"
  sSql &= "batterystatus = &12, comments = &13, enabled = &14, groups = &15, value2 = &16, value3 = &17,"
  sSql &= "hide = &18, switchable = &19, dimable = &20, log = &21, graph = &22, valuerrddsname = &23,"
  sSql &= "value2rrddsname = &24, value3rrddsname = &25, valuerrdtype = &26, x = &27, y = &28, floorplan = &29,"
  sSql &= "value2rrdtype = &30, value3rrdtype = &31, value4 = &32, label4 = &33, value4rrddsname = &34,"
  sSql &= "value4rrdtype = &35, dimicon = &36, extcode = &37, divider = &38, divider2 = &39, divider3 = &40, divider4 = &41,"
  sSql &= "calibration = &42, calibration2 = &43, calibration3 = &44, calibration4 = &45 WHERE id = &46"
  rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, rResultType!id, iInterface, sAddress, iLocation, cmbLabel.Text, cmbLabel2.Text, cmbLabel3.Text, tbValue.Text, tbOffIcon.Text, tbOnIcon.Text, tbBatt.Text, tbComments.Text, chkEnabled.Value, sGroups, tbValue2.Text, tbValue3.Text, chkHide.Value, chkSwitchable.Value, chkDimable.Value, chkLog.Value, chkGraph.Value, LCase(tbValueDS.Text), LCase(tbValue2DS.Text), LCase(tbValue3DS.Text), cmbValueType.Text, tbX.Text, tbY.Text, iFloorplan, cmbValue2Type.Text, cmbValue3Type.Text, tbValue4.Text, cmbLabel4.Text, LCase(tbValue4DS.Text), cmbValue4Type.Text, tbDimIcon.Text, chkExtended.Value, sDivider1, sDivider2, sDivider3, sDivider4, sCorrection1, sCorrection2, sCorrection3, sCorrection4, rResult!id)

  FDevices.GetDeviceList()
  RRDTool.CreateRRDs()
  IF FControl.Visible THEN FControl.ReloadPage()
  bAddDevice = FALSE
  ME.Close

END

PUBLIC SUB tbValue2_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbValue3_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbValue4_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbBatt_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkEnabled_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkHide_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkLog_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbComments_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB spDeviceCode_Change()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkGraph_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbValueDS_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbValueType_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbValue2DS_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbValue2Type_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB tbValue4DS_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbValue4Type_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkDimable_Click()

  chkSwitchable.Value = FALSE
  btnSave.Enabled = TRUE

END

PUBLIC SUB chkSwitchable_Click()

  chkDimable.Value = FALSE
  btnSave.Enabled = TRUE

END

PUBLIC SUB btnBattReplaced_Click()

  tbComments.Text &= ("Battery is replaced on ") & Format(Now(), "yyyy-mm-dd hh:nn:ss")
  tbBatt.Text = NULL
  btnSave.Enabled = TRUE

END

PUBLIC SUB btnIconOn_Click()

  tbOnIcon.Text = FSelectImage.Run(Main.sBaseDir &/ "icons" &/ tbOnIcon.Text, "icons")
  btnSave.Enabled = TRUE
  DisplayIcons()

END

PUBLIC SUB btnIconOff_Click()

  tbOffIcon.Text = FSelectImage.Run(Main.sBaseDir &/ "icons" &/ tbOffIcon.Text, "icons")
  btnSave.Enabled = TRUE
  DisplayIcons()

END

PUBLIC SUB btnIconDim_Click()

  tbDimIcon.Text = FSelectImage.Run(Main.sBaseDir &/ "icons" &/ tbDimIcon.Text, "icons")
  btnSave.Enabled = TRUE
  DisplayIcons()

END

PUBLIC SUB btnEditor_Click()

  ' only start editor if location/floorplan is chosen
  IF cmbFloorplan.Text THEN
    FFloorplanEditor.Show()
  ELSE
    Message(("Select a floorplan first!"))
  END IF

END

PUBLIC SUB GetFloorplan() AS Integer

  rResultFloorplan = Main.hDB.Exec("SELECT floor FROM floors WHERE name = &1", cmbFloorplan.Text)
  RETURN rResultFloorplan!floor

END

PUBLIC SUB DisplayIcons()

  IF tbOnIcon.Text THEN
    TRY mbIconOn.Path = Main.sBaseDir &/ "icons" &/ tbOnIcon.Text
    IF NOT ERROR THEN mbIconOn.Playing = TRUE
  END IF
  IF tbDimIcon.Text THEN
    TRY mbIconDim.Path = Main.sBaseDir &/ "icons" &/ tbDimIcon.Text
    IF NOT ERROR THEN mbIconDim.Playing = TRUE
  END IF
  IF tbOffIcon.Text THEN
    TRY mbIconOff.Path = Main.sBaseDir &/ "icons" &/ tbOffIcon.Text
    IF NOT ERROR THEN mbIconOff.Playing = TRUE
  END IF

END

PUBLIC SUB cmbFloorplan_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbLabel_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbLabel2_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbLabel3_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB btnDeleteRRD_Click()

  RRDTool.DeleteRRD(rResult!name)

END

PUBLIC SUB chkExtended_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbLabel4_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB Corr_Change()

  FillCorrectionFields()

END
