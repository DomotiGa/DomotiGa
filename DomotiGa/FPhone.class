' Gambas class file

' Description:
' FPhone.class
' Display all phone calls going through Asterisk. (from table cdr)

' Development Status:
' Just build, bugs not killed yet, need to add doubleclick event to edit contacts.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE tRefresh AS NEW Timer
PRIVATE bAutoRefresh AS Boolean
PRIVATE iCalls AS Integer
PRIVATE bNoAnswer AS Boolean

PUBLIC SUB Form_Open()

  btnAll.Enabled = FALSE
  GetPhoneCallsList()

  ' create refresh timer
  tRefresh = NEW Timer AS "tRefresh"
  tRefresh.Delay = 3000 ' 3 seconds

END

PUBLIC SUB GetPhoneCallsList()

  DIM iRows, iCount AS Integer
  DIM rResult, rResult2 AS Result
  DIM sNumber, sSql, sSql2, sName AS String

  sSql = "SELECT * FROM cdr "
  sSql2 = " ORDER BY calldate DESC"

  ' select in or outgoing calls only
  IF iCalls = 1 THEN sSql &= " WHERE dcontext='default'"
  IF iCalls = 2 THEN sSql &= " WHERE dcontext='incoming'"

  IF bNoAnswer THEN
    IF iCalls = 0 THEN
      sSql &= " WHERE disposition='NO ANSWER'"
    ELSE
      sSql &= " AND disposition='NO ANSWER'"
    END IF
  END IF

  rResult = Main.hDB.Exec(sSql & sSql2)

  IF NOT rResult THEN RETURN
  iRows = rResult.Count

  WITH tbvPhoneCalls
    .Columns.Count = 7
    .Rows.Count = iRows
    .Columns[0].Title = ("Type")
    .Columns[0].Width = 80
    .Columns[1].Title = ("Name")
    .Columns[1].Width = 130
    .Columns[2].Title = ("Number")
    .Columns[2].Width = 100
    .Columns[3].Title = ("Line")
    .Columns[3].Width = 80
    .Columns[4].Title = ("Time")
    .Columns[4].Width = 140
    .Columns[5].Title = ("Duration")
    .Columns[5].Width = 70
    .Columns[6].Title = ("Status")
    .Columns[6].Width = 130
  END WITH

  FOR iCount = 0 TO iRows - 1
    sName = ("Unknown")
    IF rResult!dcontext == "default" THEN
      tbvPhoneCalls[iCount, 0].Text = ("Incoming")
      sNumber = rResult!src
    ELSE
      tbvPhoneCalls[iCount, 0].Text = ("Outgoing")
      sNumber = rResult!dst
    END IF

    rResult2 = Main.hDB.Exec("SELECT name, cidphone FROM contacts WHERE phoneno = &1", sNumber)
    IF rResult2.Count THEN
      IF rResult2!cidphone THEN
        sName = rResult2!cidphone
      ELSE
        sName = rResult2!name
      END IF
    END IF
    rResult2 = Main.hDB.Exec("SELECT name,cidmobile FROM contacts WHERE mobileno = &1", sNumber)
    IF rResult2.Count THEN 
      IF rResult2!cidmobile THEN
        sName = rResult2!cidmobile
      ELSE
        sName = rResult2!name
      END IF
    END IF

    tbvPhoneCalls[iCount, 1].Text = sName
    tbvPhoneCalls[iCount, 2].Text = sNumber
    tbvPhoneCalls[iCount, 3].Text = rResult!channel
    tbvPhoneCalls[iCount, 4].Text = Replace$(Str$(Format(rResult!calldate, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
    tbvPhoneCalls[iCount, 5].Text = TimeString(rResult!billsec)
    tbvPhoneCalls[iCount, 6].Text = Main.Caps(LCase(rResult!disposition))
    rResult.MoveNext
  NEXT

END

PRIVATE FUNCTION TimeString(lSeconds AS Long) AS String

  DIM lHrs, lMinutes AS Long
  DIM sTime AS String

  lHrs = Int(lSeconds / 3600)
  lMinutes = (Int(lSeconds / 60)) - (lHrs * 60)
  lSeconds = Int(lSeconds MOD 60)

  IF lSeconds = 60 THEN
    INC lMinutes
    lSeconds = 0
  END IF

  IF lMinutes = 60 THEN
    INC lHrs
    lMinutes = 0
  END IF
  sTime = Format(CStr(lHrs), "####00") & ":" & Format(CStr(lMinutes), "00") & ":" & Format(CStr(lSeconds), "00")
  RETURN sTime

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' refresh tableview contents
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tRefresh_Timer()

  GetPhoneCallsList()

END

PUBLIC SUB Form_Close()

  tRefresh.Stop

END

PUBLIC SUB Form_LostFocus()

  tRefresh.Stop

END

PUBLIC SUB Form_GotFocus()

  IF bAutoRefresh THEN tRefresh.Start

END

PUBLIC SUB Form_Resize()

  tbvPhoneCalls.Move(4, 30, ME.ClientWidth - 8, ME.ClientHeight - 31)

END

PUBLIC SUB tbtnRefresh_Click()

  IF tbtnRefresh.Value = TRUE THEN
    tRefresh.Start
    bAutoRefresh = TRUE
    btnRefresh.Enabled = FALSE
  ELSE
    tRefresh.Stop
    bAutoRefresh = FALSE
    btnRefresh.Enabled = TRUE
  END IF

END

PUBLIC SUB btnRefresh_Click()

  GetPhoneCallsList()

END

PUBLIC SUB btnAll_Click()

  iCalls = 0
  btnAll.Enabled = FALSE
  btnIncoming.Enabled = TRUE
  btnOutgoing.Enabled = TRUE
  GetPhoneCallsList()

END

PUBLIC SUB btnOutgoing_Click()

  iCalls = 1
  btnOutgoing.Enabled = FALSE
  btnAll.Enabled = TRUE
  btnIncoming.Enabled = TRUE
  GetPhoneCallsList()

END

PUBLIC SUB btnIncoming_Click()

  iCalls = 2
  btnIncoming.Enabled = FALSE
  btnAll.Enabled = TRUE
  btnOutgoing.Enabled = TRUE
  GetPhoneCallsList()

END

PUBLIC SUB tbtnNoAnswer_Click()

  bNoAnswer = tbtnNoAnswer.Value
  GetPhoneCallsList()

END

PUBLIC SUB tbvPhoneCalls_DblClick()

  ' open contacts editor window
  FEditContacts.Show()

END

PUBLIC SUB tbvPhoneCalls_ColumnClick(Column AS Integer)

  Main.SortTableView(FPhone.tbvPhoneCalls, Column, TRUE)

END
