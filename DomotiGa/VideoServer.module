' Gambas module file

' Description:
' VideoServer.module
' Support getting captures from for Aviosys IP9100 Videoserver

' Development Status:
' Sort of working, needs better error checking.

' Links:
' http://www.sunspot.co.uk/Projects/IP_KAM_9000and9100_notes.htm

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE hDownloadImage AS NEW HttpClient

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' delete and create capture tables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB InitTables()

  DIM iCam AS Integer
  DIM tableCaptures AS Table

  FOR iCam = 0 TO 3
    Main.hDB.Tables.Remove("capture_camera" & iCam)
    tableCaptures = Main.hDB.Tables.Add("capture_camera" & iCam)
    tableCaptures.Fields.Add("id", db.Serial)
    tableCaptures.Fields.Add("stamp", db.Date, 32)
    tableCaptures.Fields.Add("image", db.Blob)
    tableCaptures.PrimaryKey = ["id"]
    tableCaptures.Update()
  NEXT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' grab image from channel #iCam and store it as a blob
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Grab(iCam AS Integer)

  DIM sUrl, sGetImgUrl, sDownload AS String

  sUrl = Main.sVideoServerAddress
  IF Main.iVideoServerPort > 0 AND Main.iVideoServerPort <> 80 THEN sUrl = Main.sVideoServerAddress & ":" & Main.iVideoServerPort

  sGetImgUrl = sUrl
  IF Main.sVideoServerUser THEN sGetImgUrl = Subst("&1:&2@&3", Main.sVideoServerUser, Main.sVideoServerPassword, sUrl)

  IF Main.bVideoServerDebug THEN Main.WriteDebugLog("[VideoServer] Download image " & sUrl)

  hDownloadImage = NEW HttpClient AS "Download"
  hDownloadImage.URL = "http://" & sGetImgUrl & "/usr/yoics" & iCam & ".jpg"
  hDownloadImage.Async = FALSE
  hDownloadImage.TimeOut = 5
  hDownloadImage.Get()

  IF hDownloadImage.Status < 0 THEN
    IF Main.bVideoServerDebug THEN Main.WriteDebugLog("[VideoServer] Error downloading image " & sUrl)
  ELSE
    ' success - read the data
    IF Lof(hDownloadImage) THEN READ #hDownloadImage, sDownload, Lof(hDownloadImage)
  END IF
  SaveCapture(sDownload, iCam)

END

PUBLIC SUB SaveCapture(sBuffer AS String, iCam AS Integer)

  DIM rResult AS Result

  ' write image to capture table
  Main.hDB.Begin()
  rResult = Main.hDB.Create("capture_camera" & iCam)
  rResult!image = sBuffer
  rResult!stamp = Now()
  rResult.Update()
  Main.hDB.Commit()

END