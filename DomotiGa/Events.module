' Gambas module file

' Description:
' Events.module
' This module provides support for events, triggers, actions and conditions.

' Development Status:
' Works 99.99% ok now. Partly rewritten by Geert-Jan van den Hurk.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2010 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE oDelayTimers AS NEW Object[]
PUBLIC tDelayTimer AS CTimer

' trigger database table
' description        | type | param1   | param2  | param3  | param4 | param5  | param6 | param7
' time now           |   1  | crontab
' globalvar change   |   2  | variable | operand | value
' device change      |   3  | id       | fieldno | operand | value
' ir remote received |   4  | remote   | button  | repeat
' iviewer remote     |   5  | remote   | join    | value

' condition database table
' description        | type | param1   | param2  | param3  | param4 | param5  | param6 | param7
' time now           |   1  | crontab
' globalvar value    |   2  | variable | operand | value
' device value       |   3  | id       | fieldno | operand | value

' called every new minute from EventLoop.Run()
PUBLIC SUB CheckTimeNowEvents()

  DIM rResult AS Result
  DIM sSql AS String = "SELECT * FROM events, triggers WHERE events.trigger1 = triggers.id AND triggers.type = 1 AND events.enabled"

  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1a. Running TimeNow query '") & sSql & "'")
  rResult = Main.hDB.Exec(sSql)
  IF rResult THEN
    IF rResult.Count THEN
      DoEvent(rResult)
    ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1b. No result."))
    END IF
  ELSE
    Main.WriteDebugLog(("[Events] Error while running CheckTimeNowEvents query!"))
    RETURN
  ENDIF

END

' called when a value of a device is set, and is different from last one from EventLoop.DeviceChanged()
' iID = id of device, sField = the field that has changed, sValue = the new value it's set to
PUBLIC SUB CheckDeviceChangeEvents(iId AS Integer, sField AS String, sValue AS String)

  DIM rResult AS Result
  DIM sSql AS String = "SELECT * FROM events, triggers WHERE events.trigger1 = triggers.id AND triggers.type = 3 AND events.enabled AND triggers.param1 = &1 AND triggers.param2 = &2"

  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1a. Running DeviceChange query '") & Subst(sSql, iId, sField) & "'")
  rResult = Main.hDB.Exec(sSql, iId, sField)
  IF rResult THEN
    IF rResult.Count THEN
      DoEvent(rResult, sValue)
    ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1b. No result."))
    END IF
  ELSE
    Main.WriteDebugLog(("[Events] Error while running CheckDeviceChangeEvents query!"))
    RETURN
  ENDIF

END

' called when a IR signal is received from CLIRC.ParseLine(), CIRMan.ParseLine() or CIRTrans.ParseLine()
PUBLIC SUB CheckIRRemoteEvents(sRemote AS String, sButton AS String)

  DIM rResult AS Result
  DIM sSql AS String = "SELECT * FROM events, triggers WHERE events.trigger1 = triggers.id AND triggers.type = 4 AND events.enabled AND triggers.param1 = &1 AND triggers.param2 = &2"

  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1a. Running IRRemote query '") & Subst(sSql, sRemote, sButton) & "'")
  rResult = Main.hDB.Exec(sSql, sRemote, sButton)
  IF rResult THEN
    IF rResult.Count THEN
      DoEvent(rResult)
     ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1b. No result."))
     ENDIF
  ELSE
    Main.WriteDebugLog(("[Events] Error while running CheckIRRemoteEvents query!"))
    RETURN
  ENDIF

END

' called when a iViewer string is received from CIViewer.ParseLine()
PUBLIC SUB CheckIViewerRemoteEvents(sRemote AS String, sJoin AS String, sValue AS String)

  DIM rResult AS Result
  DIM sSql AS String = "SELECT * FROM events, triggers WHERE events.trigger1 = triggers.id AND triggers.type = 5 AND events.enabled AND triggers.param2 = &1 AND triggers.param3 = &2"

  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1a. Running IViewerRemote query '") & Subst(sSql, sJoin, sValue) & "'")
  rResult = Main.hDB.Exec(sSql, sJoin, sValue)
  IF rResult THEN
    IF rResult.Count THEN
      DoEvent(rResult)
     ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1b. No result."))
     ENDIF
  ELSE
    Main.WriteDebugLog(("[Events] Error while running CheckIViewerRemoteEvents query!"))
    RETURN
  ENDIF

END

' called when a globalvar value has changed from Main.SetGlobalVar()
PUBLIC SUB CheckGlobalVarEvents(sVar AS String, vValue AS Variant)

  DIM rResult AS Result
  DIM sSql AS String = "SELECT * FROM events, triggers WHERE events.trigger1 = triggers.id AND triggers.type = 2 AND events.enabled AND triggers.param1 = &1"

  Main.GlobalVar[sVar] = vValue

  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1a. Running GlobalVar query '") & Subst(sSql, sVar) & "'")
  rResult = Main.hDB.Exec(sSql, sVar)
  IF rResult THEN
    IF rResult.Count THEN
      DoEvent(rResult, vValue)
    ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 1b. No result."))
    ENDIF
  ELSE
    Main.WriteDebugLog(("[Events] Error while running CheckGlobalVarEvents query!"))
    RETURN
  ENDIF

END

PRIVATE SUB DoEvent(rResult AS Result, OPTIONAL sValue AS Variant)

  IF rResult.Count THEN
    IF Main.bEventsDebug THEN main.WriteDebugLog(("[Events] 1b. Got ") & rResult.Count & (" result(s)."))
    FOR EACH rResult
      ' check if event is allowed to get triggered again
      IF rResult!rerunenabled = TRUE AND IF IsDate(rResult!lastrun) THEN
        IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2a. ReRun condition set for event id ") & rResult!id & (" named '") & rResult!name & "'")
        IF NOT CheckReRunCondition(rResult!lastrun, rResult!reruntype, rResult!rerunvalue) THEN
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2b. Event id ") & rResult!id & (" named '") & rResult!name & ("' already ran in the last ") & rResult!rerunvalue & " " & rResult!reruntype & (" so discarding!"))
          CONTINUE
        ENDIF
      ENDIF

      SELECT rResult["triggers.type"]
        CASE 1
          ' timenow trigger
          IF TimeCron(rResult["triggers.param1"]) = FALSE THEN
            IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. TimeNow trigger check TimeCron() for event id ") & rResult!id & (" and trigger condition '") & rResult["triggers.param1"] & ("' returned False"))
            CONTINUE
          ELSE
            IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. TimeNow trigger check TimeCron() for event id ") & rResult!id & (" and trigger condition '") & rResult["triggers.param1"] & ("' returned True"))
          ENDIF
        CASE 2
          ' check globalvar change trigger
          IF NOT CheckCondition(sValue, rResult["triggers.param2"], rResult["triggers.param3"]) THEN CONTINUE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. GlobalVar Change trigger on event id ") & rResult!id & (" named '") & rResult!name & ("' with trigger condition '") & rResult["triggers.param1"] & " " & rResult["triggers.param2"] & " " & rResult["triggers.param3"] & "'")
        CASE 3
          ' check device change trigger
          IF NOT CheckCondition(sValue, rResult["triggers.param3"], rResult["triggers.param4"]) THEN CONTINUE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. Device Change trigger on event id ") & rResult!id & (" named '") & rResult!name & ("' with trigger condition '") & rResult["triggers.param2"] & "' " & sValue & " " & rResult["triggers.param3"] & " " & rResult["triggers.param4"])
        CASE 4
          ' irremote trigger
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. IRRemote trigger on event id ") & rResult!id & (" named '") & rResult!name & ("' with Remote name '") & rResult["triggers.param1"] & "' and Button '" & rResult["triggers.param2"] & "'")
        CASE 5
          ' iviewer trigger
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. IViewerRemote trigger on event id ") & rResult!id & (" named '") & rResult!name & ("' with Remote name '") & rResult["triggers.param1"] & "' with Join '" & rResult["triggers.param2"] & " and Value " & rResult["triggers.param3"] & "'")
        CASE ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. Event with id ") & rResult!id & (" named '") & rResult!name & ("' has unknown trigger type ") & rResult["triggers.type"])
      END SELECT

      ' check optional conditions
      IF CheckConditions(rResult!condition1, rResult!condition2, rResult!operand) = TRUE THEN
        ' run action(s)
        IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2f. Event id " & rResult!id & " named '") & rResult["events.name"] & ("' is validated, running action(s)."))
        RunActions(rResult!id)
      ELSE
        IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2f. Event id " & rResult!id & " named '") & rResult["events.name"] & ("' has failed CheckConditions, discarding."))
      ENDIF
    NEXT
  ENDIF

END

PUBLIC SUB CheckCondition(sValue AS Variant, sOperand AS String, sCond AS Variant, OPTIONAL bMute AS Boolean) AS Boolean

  DIM bReturn AS Boolean

  IF IsBoolean(sValue) THEN sValue = Main.DisplayBool(sValue)
  IF IsBoolean(sCond) THEN sCond = Main.DisplayBool(sCond)

  SELECT sOperand
    CASE "="
      IF Comp(sValue, sCond) = 0 THEN bReturn = TRUE
    CASE "<>"
      IF sValue <> sCond THEN bReturn = TRUE
    CASE ">"
      IF CFloat(Replace(sValue, ",", ".", gb.String)) > CFloat(Replace(sCond, ",", ".", gb.String)) THEN bReturn = TRUE
    CASE "<"
      IF CFloat(Replace(sValue, ",", ".", gb.String)) < CFloat(Replace(sCond, ",", ".", gb.String)) THEN bReturn = TRUE
    CASE ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. Unsupported operand '" & sOperand & "' found!"))
  END SELECT
  IF Main.bEventsDebug AND IF NOT bMute THEN Main.WriteDebugLog(("[Events] 2c. Check condition '") & sValue & " " & sOperand & " " & sCond & "' = " & Main.DisplayBool(bReturn))
  RETURN bReturn

CATCH
  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2c. Invalid comparison in CheckCondition() routine!"))
  RETURN FALSE

END

PRIVATE SUB AddVariablesToContext(cContext AS Collection)

  DIM vValue AS Variant

  FOR EACH vValue IN Main.GlobalVar
    cContext["Var_" & Main.GlobalVar.Key] = vValue
  NEXT

END

PRIVATE SUB AddDevicesToContext(cContext AS Collection)

  DIM iCount AS Integer
  DIM rResult AS Result
  DIM sSql AS String

  sSql = "SELECT * FROM devices WHERE enabled IS TRUE ORDER BY name"
  rResult = Main.hDB.Exec(sSql)

  FOR iCount = 0 TO rResult.Max
    cContext["Dev_" & rResult!id & "_Value"] = rResult!value
    cContext["Dev_" & rResult!id & "_Value2"] = rResult!value2
    cContext["Dev_" & rResult!id & "_Value3"] = rResult!value3
    cContext["Dev_" & rResult!id & "_Value4"] = rResult!value4
    cContext["Dev_" & rResult!id & "_LastChanged"] = rResult!lastchanged
    cContext["Dev_" & rResult!id & "_LastSeen"] = rResult!lastseen
    cContext["Dev_" & rResult!id & "_BatteryStatus"] = rResult!batterystatus
    rResult.MoveNext
  NEXT

END

PUBLIC FUNCTION EvalFormula(sFormula AS String) AS String

  DIM sToEval, sResult AS String 
  DIM cContext AS NEW Collection
  DIM vResult AS Variant

  sToEval = Replace(sFormula, "\n", " ")
  ' here we replace variables & devices by their values
  AddVariablesToContext(cContext)
  AddDevicesToContext(cContext)
  ' here the formula is evaluated
  TRY vResult = Eval(sToEval, cContext)
  IF ERROR THEN
    sResult = ("Error: ") & Error.Text
  ELSE IF IsBoolean(vResult) THEN
    sResult = Main.DisplayBool(vResult)
  ELSE
    sResult = vResult
  ENDIF
  RETURN sResult

CATCH
  RETURN Error.Text

END

PRIVATE SUB CheckSingleCondition(iCondition AS Integer) AS Boolean

  DIM bResult AS Boolean
  DIM rResultCondition AS Result

  rResultCondition = Main.hDB.Exec("SELECT * FROM conditions WHERE id = " & iCondition)
  IF rResultCondition THEN
    IF EvalFormula(rResultCondition!formula) = "True" THEN bResult = TRUE
  ENDIF
  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2d. Condition with id ") & iCondition & (" and formula '") & rResultCondition!formula & ("' returned ") & Main.DisplayBool(bResult))
  RETURN bResult

END

' check the optional conditions 1 and 2
PRIVATE SUB CheckConditions(iCondition1 AS Integer, iCondition2 AS Integer, sOperand AS String) AS Boolean

  DIM bCondition1, bCondition2 AS Boolean

  IF iCondition1 THEN
      bCondition1 = CheckSingleCondition(iCondition1)
      IF iCondition2 THEN
        bCondition2 = CheckSingleCondition(iCondition2)
      ELSE
        sOperand = ""
      ENDIF

      ' check the OR and AND clause
      SELECT CASE UCase(sOperand)
        CASE ""
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2e. CheckConditions checking only iCondition1 with id ") & iCondition1)
          IF bCondition1 THEN RETURN TRUE
        CASE "OR"
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2e. CheckConditions checking iCondition1 with id ") & iCondition1 & (" OR iCondition2 with id ") & iCondition2)
          IF bCondition1 OR IF bCondition2 THEN RETURN TRUE
        CASE "AND"
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 2e. CheckConditions checking iCondition1 with id ") & iCondition1 & (" AND iCondition2 with id ") & iCondition2)
          IF bCondition1 AND IF bCondition2 THEN RETURN TRUE
        CASE ELSE
      END SELECT

  ELSE ' no conditions defined so return true
    RETURN TRUE
  ENDIF
  RETURN FALSE

END

' run all configured actions
PRIVATE SUB RunActions(iEventId AS Integer, OPTIONAL iFrom AS Integer)

  DIM rActions AS Result

  UpdateEvent(iEventId)
  rActions = Main.hDB.Exec("SELECT * FROM events_actions WHERE event = &1 ORDER BY events_actions.order", iEventId)
  IF rActions THEN
    FOR EACH rActions
      IF rActions!order < iFrom THEN CONTINUE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3a. Running action with id ") & rActions!action & (" for event with id ") & iEventId)
      IF RunAction(rActions!action, rActions!order, iEventId) THEN BREAK
    NEXT
  ENDIF

END

PRIVATE SUB CheckReRunCondition(dDate AS Date, sPeriod AS String, iValue AS Integer) AS Boolean

  IF DateDiff(dDate, Now(), Eval(sPeriod)) > iValue THEN
    RETURN TRUE
  ELSE
    IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] Event Rerun condition ") & iValue & " " & sPeriod & (" is false."))
    RETURN FALSE
  ENDIF

END

' action database table
' description       | rResultAction!type | rResultAction!param1      | rResultAction!param2 | rResultAction!param3 | rResultAction!param4 | rResultAction!param5
' set device value  | 1                  | deviceid                  | value fieldname      | value
' set globalvar     | 2                  | globalvar name            | value
' send e-mail       | 3                  | to address                | subject              | body
' speak text        | 4                  | voice or 'female', 'male' | text
' execute command   | 5                  | shell command to run
' send tweet        | 6                  | tweet message to send
' send SMS message  | 7                  | to SMS number             | message
' send IRTrans IR   | 8                  | command string
' play sound        | 9                  | sound file                | volume
' write log entry   | 10                 | log text
' display led msg   | 11                 | message to display        | display id           | color                | speed
' av control        | 12                 | model                     | command              | value                | address
' timer delay       | 13                 | delay in secs or rnd min  | random max seconds   | mode fixed or random
' notify-send       | 14                 | title                     | text

PRIVATE SUB RunAction(iAction AS Integer, iOrder AS Integer, iEventId AS Integer) AS Boolean

  DIM rResultAction AS Result
  DIM bOk AS Boolean
  DIM sDeviceName AS String

  rResultAction = Main.hDB.Exec("SELECT * FROM actions WHERE id = &1 ", iAction)
  IF rResultAction.Available THEN
    SELECT rResultAction!type
      CASE 1 ' set device value
        ' only changing field Value will trigger interface code, and Value2-Value4 will only set field value
        IF rResultAction!param1 AND IF rResultAction!param2 AND IF rResultAction!param3 THEN
          sDeviceName = Devices.FindNameForDevice(rResultAction!param1)
          IF Len(sDeviceName) THEN
            IF Comp(rResultAction!param2, "Value1") = 0 THEN
              bOk = Devices.SetDevice(sDeviceName, ParseText(rResultAction!param3))
            ELSE IF Comp(rResultAction!param2, "Value2") = 0 THEN
              bOk = Devices.ValueUpdate(rResultAction!param1, "", ParseText(rResultAction!param3), "", "")
            ELSE IF Comp(rResultAction!param2, "Value3") = 0 THEN
              bOk = Devices.ValueUpdate(rResultAction!param1, "", "", ParseText(rResultAction!param3), "")
            ELSE IF Comp(rResultAction!param2, "Value4") = 0 THEN
              bOk = Devices.ValueUpdate(rResultAction!param1, "", "", "", ParseText(rResultAction!param3))
            ENDIF
          ELSE
            Main.WriteDebugLog(("[Events] 3b. Device with id ") & rResultAction!param1 & (" doesn't exists!"))
            bOk = FALSE
          ENDIF
        ENDIF
      CASE 2 ' set globalvar value
        ' if globalvar doesn't exist it will be created
        IF rResultAction!param1 AND IF rResultAction!param2 THEN
          TRY Main.GlobalVar[rResultAction!param1] = ParseText(rResultAction!param2)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ENDIF
      CASE 3 ' send e-mail
        IF Main.bEmailEnabled THEN
          TRY Mail.SendMail(ParseText(rResultAction!param2), ParseText(rResultAction!param3), rResultAction!param1)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. e-mail support is disabled!"))
        ENDIF
      CASE 4 ' speak text
        IF Main.bVoiceTextEnabled THEN
          TRY VoiceText.Speak(ParseText(rResultAction!param2), rResultAction!param1)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. VoiceText support is disabled!"))
        ENDIF
      CASE 5 ' execute command
        TRY SHELL rResultAction!param1
        IF ERROR THEN
          bOk = FALSE
        ELSE
          bOk = TRUE
        ENDIF
      CASE 6 ' send tweet
        IF Main.bTwitterEnabled THEN
          TRY Twitter.PostTweet(ParseText(rResultAction!param1))
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. Twitter support is disabled!"))
        ENDIF
      CASE 7 ' send SMS message
        IF Main.bSMSEnabled THEN
          TRY Main.hSMS.SendSMS(ParseText(rResultAction!param2), rResultAction!param1)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. SMS support is disabled!"))
        ENDIF
      CASE 8 ' send IRTrans IR
        IF Main.bIRTransEnabled THEN
          TRY Main.hIRTrans.SendIRCommand(rResultAction!param1)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. IRTrans support is disabled!"))
        ENDIF
      CASE 9 ' play sound
        IF Main.bSoundEnabled THEN
          TRY Sounds.PlaySnd(rResultAction!param1, rResultAction!param2)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. Sound support is disabled!"))
        ENDIF
      CASE 10 ' write log entry
        TRY Main.WriteLog(ParseText(rResultAction!param1))
        IF ERROR THEN
          bOk = FALSE
        ELSE
          bOk = TRUE
        ENDIF
      CASE 11 ' display message on led matrix display
        IF Main.bLEDMatrixEnabled THEN
          TRY Main.hLEDMatrix.DisplayMessage(rResultAction!param2, ParseText(rResultAction!param1), rResultAction!param3, rResultAction!param4)
          IF ERROR THEN
            bOk = FALSE
          ELSE
            bOk = TRUE
          ENDIF
        ELSE
          IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. LED Matrix support is disabled!"))
        ENDIF
      CASE 12 ' av control command
        TRY AVControl.Set(rResultAction!param1, rResultAction!param2, rResultAction!param3, rResultAction!param4)
        IF ERROR THEN
          bOk = FALSE
        ELSE
          bOk = TRUE
        ENDIF
      CASE 13 ' timer delay
        IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. Action with id ") & rResultAction!id & (" named '") & rResultAction!name & ("' is a delay timer!"))
        StartDelayTimer(iEventId, iOrder + 1, rResultAction!param1, rResultAction!param2, rResultAction!param3)
        RETURN TRUE
      CASE 14 ' notify-send
        Main.NotifySend(ParseText(rResultAction!param1), ParseText(rResultAction!param2))
        bOk = TRUE
      CASE ELSE
        bOk = FALSE
    END SELECT

    IF bOk THEN
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. Action with id ") & rResultAction!id & (" named '") & rResultAction!name & ("' executed!"))
      RETURN FALSE
    ELSE
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. Action with id ") & rResultAction!id & (" named '") & rResultAction!name & ("' failed to execute!"))
      RETURN FALSE
    ENDIF
  ELSE
    IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3b. Action with id ") & iAction & (" not found!"))
  ENDIF

END

' update event timestamps
PRIVATE SUB UpdateEvent(iId AS Integer)

  DIM rResult AS Result

  rResult = Main.hDB.Exec("SELECT * FROM events where id = &1", iId)
  IF rResult.Available THEN
    IF rResult.Count = 1 THEN
      IF rResult!firstrun = "00:00:00" OR rResult!firstrun = "" THEN
        rResult = Main.hDB.Exec("UPDATE events SET firstrun = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), iId)
      ENDIF
    ENDIF
    rResult = Main.hDB.Exec("UPDATE events SET lastrun = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), iId)
  ENDIF

END

' replace <%global var%> and <#device values|1#> fields in text
PRIVATE SUB ParseText(sText AS String) AS String

  DIM iPos1, iPos2, iPos3 AS Integer
  DIM sTag, sResult AS String

  IF InStr(stext, "<") AND IF InStr(stext, ">") THEN
    iPos1 = InStr(sText, "<")
    DO WHILE iPos1 > 0
      iPos2 = InStr(sText, ">", iPos1 + 1)
      IF iPos2 > 0 THEN ' tags found
        sTag = Mid(sText, iPos1 + 1, iPos2 - iPos1 - 1)
        IF Left(sTag, 1) = "%" AND IF Right(sTag, 1) = "%" THEN ' globalvar tag
          sText = Replace(sText, "<" & sTag & ">", InsertGlobalVar(sTag))
        ENDIF
        IF Left(sTag, 1) = "#" AND IF Right(sTag, 1) = "#" THEN ' deviceval tag
          sText = Replace(SText, "<" & sTag & ">", InsertDeviceValue(sTag))
        ENDIF
      ENDIF
      iPos3 = InStr(sText, "<")
      IF iPos3 <= iPos1 THEN
        BREAK
      ELSE
        iPos1 = iPos3
      ENDIF
    LOOP
  ENDIF
  RETURN sText

END

' return value of %global var%
PRIVATE SUB InsertGlobalVar(sTag AS String) AS String

  TRY RETURN Main.GlobalVar[Mid(sTag, 2, Len(sTag) - 2)]
  IF ERROR THEN RETURN ("ERROR!")

END

' return value of <#device name|field#>
PRIVATE SUB InsertDeviceValue(sTag AS String) AS String

  DIM aScan AS String[]
  DIM sValue AS String

  IF InStr(sTag, "|") THEN
    aScan = Scan(sTag, "#*|*#")
    IF aScan.Count = 2 THEN
      SELECT CASE aScan[1]
        CASE "1", "2", "3", "4"
          sValue = Devices.GetValueForDevice(aScan[0], aScan[1])
        CASE ELSE
          sValue = Devices.GetValueForDevice(aScan[0])
      END SELECT
    ENDIF
  ELSE
    sValue = Devices.GetValueForDevice(Mid(sTag, 2, Len(sTag) - 2))
  ENDIF
  RETURN sValue

CATCH
  ' RETURN ERROR.Text & " at " & Error.Where
  RETURN ("ERROR!")

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse cron function
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION TimeCron(sTimeDate AS String) AS Boolean

  DIM aCron, aMore, aCron_Now, LowHigh AS String[]
  DIM sField AS String
  DIM iField AS Integer
  DIM sTag AS Boolean

  ' * * * * *
  ' - - - - -
  ' | | | | |
  ' | | | | +----- day of week (0 - 6) (Sunday=0)
  ' | | | +------- month (1 - 12)
  ' | | +--------- day of month (1 - 31)
  ' | +----------- hour (0 - 23)
  ' +------------- min(0 - 59)

  aCron = Split(sTimeDate, " ")

  IF aCron.Count <> 5 THEN
    Main.WriteDebugLog(("[Cron] Bad TimeCron() format (needs 5 fields): ") & sTimeDate)
    RETURN FALSE
  END IF

  aCron_Now = [Str(Main.GlobalVar["Minute"]), Str(Main.GlobalVar["Hour"]), Str(Main.GlobalVar["Day"]), Str(Main.GlobalVar["Month"]), Str(Main.GlobalVar["Weekday"])]
  FOR iField = 0 TO 4
    ' wildcard used
    IF aCron[iField] = "*" THEN
      IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & ("' | * so Continue"))
      CONTINUE
    ' normal value
    ELSE IF aCron[iField] = aCron_Now[iField] THEN
      IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | aCron[" & iField & "] " & aCron[iField] & " = aCron_Now[" & iField & "] " & aCron_Now[iField] & " so Continue")
      CONTINUE
    ' more values given
    ELSE IF InStr(aCron[iField], ",") THEN
      aMore = Split(aCron[iField], ",")
      FOR EACH sField IN aMore
          IF sField = aCron_Now[iField] THEN
            IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | sField " & sField & " = aCron_Now[" & iField & "] " & aCron_Now[iField] & (" so tag=True"))
            sTag = TRUE
          ELSE
            IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | sField " & sField & " <> aCron_Now[" & iField & "] " & aCron_Now[iField])
          END IF
      NEXT
      IF sTag THEN
        CONTINUE
      ELSE
        IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & ("' | none of , sFields match so return False"))
        RETURN FALSE
      END IF
    ' range given
    ELSE IF InStr(aCron[iField], "-") THEN
      LowHigh = Scan(aCron[iField], "*-*")
      IF aCron_Now[iField] >= LowHigh[0] AND aCron_Now[iField] <= LowHigh[1] THEN
        IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | within range so Continue")
        CONTINUE
      ELSE
        IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | not within range so return False")
        RETURN FALSE
      END IF
    END IF
    IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | aCron[" & iField & "] " & aCron[iField] & " <> aCron_Now[" & iField & "] " & aCron_Now[iField] & (" so return False"))
    RETURN FALSE ' not all of the entries qualified
    sTag = FALSE
  NEXT
  IF Main.bEventsDebug THEN Main.WriteDebugLog("[Cron] '" & sTimeDate & "' | return True")
  RETURN TRUE

END

' start delay timer for iEventId and iOrder with param1-3
PRIVATE SUB StartDelayTimer(iEventId AS Integer, iOrder AS Integer, sParam1 AS String, sParam2 AS String, sParam3 AS String)

  DIM iDelay AS Integer

  ' sParam3 contains timer mode
  ' "fixed" sParam1 is timer delay in seconds
  ' "random" sParam1 is min value, sParam2 is max value for rnd
  IF LCase(sParam3) = "random" THEN
    iDelay = Rnd(Val(sParam1), Val(sParam2))
  ELSE
    iDelay = Val(sParam1)
  ENDIF

  IF NOT FindTimer(iEventId, iOrder, iDelay) THEN
    tDelayTimer = NEW CTimer AS "tDelayTimer"
    oDelayTimers.Add(tDelayTimer)
    tDelayTimer.Delay = iDelay * 1000
    tDelayTimer.EventId = iEventId
    tDelayTimer.Order = iOrder
    tDelayTimer.Started = Now()
    tDelayTimer.Start
    IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3c. Started delay timer for event id ") & iEventId & (", delay for ") & iDelay & (" second(s)"))
  ENDIF

END

' looks if there is a timer running for iEventId and iOrder
PRIVATE SUB FindTimer(iEventId AS Integer, iOrder AS Integer, iDelay AS Integer) AS Boolean

  DIM oObject AS Object

  FOR EACH oObject IN oDelayTimers
    IF oObject.EventId = iEventId AND IF oObject.Order = iOrder THEN
      IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3c. Delay timer for event id ") & iEventId & (" and order# ") & iOrder & (" with delay of ") & iDelay & (" second(s) already runs, skipping"))
      RETURN TRUE
    ELSE
      RETURN FALSE
    ENDIF
  NEXT
  RETURN FALSE

END

' timer is finished, run remaining action(s)
PUBLIC SUB tDelayTimer_Timer()

  IF Main.bEventsDebug THEN Main.WriteDebugLog(("[Events] 3d. Delay timer finished for event id ") & tDelayTimer.EventId & (", continue running it's actions from order# ") & tDelayTimer.Order)
  RunActions(tDelayTimer.EventId, tDelayTimer.Order)
  tDelayTimer.Stop
  oDelayTimers.Remove(oDelayTimers.Find(LAST))

END
