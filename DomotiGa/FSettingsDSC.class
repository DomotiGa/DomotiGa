' Gambas class file

' Description:
' FSettingsDSC.class
' Settings form for DSC Security support.

' Development Status:
' Alpha.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009-2012 Ron Klinkien
' This module is written by and Copyright(C) 2009 Timo Sariwating

' Credits:
' Tradiuz for IT100 code.

' Read file called COPYING for license details.

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)

  txtSerialPort.Text = Main.sDSCSerialPort
  cmbBaudrate.Text = Main.sDSCBaudrate
  chkDebug.Value = Main.bDSCDebug
  chkEnabled.Value = Main.bDSCEnabled
  txtMasterCode.Text = Main.sDSCMasterCode
  cmbType.Index = Main.iDSCType

  btnSave.Enabled = FALSE

  IF Main.bDSCEnabled = FALSE THEN
    txtSerialPort.Enabled = FALSE
    cmbBaudrate.Enabled = FALSE
    chkDebug.Enabled = FALSE
    btnSend_Time.Enabled = FALSE
    btnArm_Stay.Enabled = FALSE
    btnArm_Away.Enabled = FALSE
    cmbType.Enabled = FALSE
    txtMasterCode.Enabled = FALSE
  ENDIF

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PUBLIC SUB txtSerialPort_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbBaudrate_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkDebug_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB btnSave_Click()

  DIM rResult AS Result

  ' save new DSC Security settings
  rResult = Main.hDB.Exec("UPDATE settings_dsc SET debug = &1, serialport = &2, baudrate = &3, enabled = &4, mastercode=&5, type=&6 WHERE id = 1", chkDebug.Value, txtSerialPort.Text, cmbBaudrate.Text, chkEnabled.Value, txtMasterCode.Text, cmbType.Index)
  rResult = Main.GetSettingTable("dsc") ' reload settings
  IF rResult.Count THEN
    Main.bDSCEnabled = rResult!enabled
    Main.sDSCSerialPort = rResult!serialport
    Main.sDSCBaudrate = rResult!baudrate
    Main.bDSCDebug = rResult!debug
    Main.sDSCMasterCode = rResult!mastercode
    Main.iDSCType = rResult!type
  ENDIF
  IF Main.bServer THEN
    Main.Restart_DSC()
  ELSE
    XMLClient.ModuleRestart("DSC")
  ENDIF
  FMain.UpdateStatusPanel()
  ME.Close

END

PUBLIC SUB btnDefaults_Click()

  DIM rResult AS Result

  rResult = Main.GetSettingTable("dsc", TRUE) ' get defaults
  IF rResult.Count THEN
    chkEnabled.Value = rResult!enabled
    txtSerialPort.text = rResult!serialport
    cmbBaudrate.text = rResult!baudrate
    chkDebug.Value = rResult!debug
    txtMasterCode.Text = rResult!mastercode
    cmbType.Index = rResult!type
  ENDIF
  btnSave.Enabled = TRUE

END

PUBLIC SUB chkEnabled_Click()

  txtSerialPort.Enabled = chkEnabled.Value
  cmbBaudrate.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnSend_Time.Enabled = chkEnabled.Value
  btnArm_Stay.Enabled = chkEnabled.Value
  btnArm_Away.Enabled = chkEnabled.Value
  cmbType.Enabled = chkEnabled.Value
  txtMasterCode.Enabled = chkEnabled.Value
  btnSave.Enabled = TRUE

END

PUBLIC SUB btnArm_Stay_Click()

  IF Main.bServer AND IF Main.hDSC THEN
    Main.hDSC.TX("0311")
  ELSE IF Main.bDSCEnabled THEN
    XMLClient.SetAlarmMode("0311")
  ELSE
    Main.WriteDebugLog(("[DSC] Connection with alarm system not enabled yet."))
  ENDIF

END

PUBLIC SUB btnSend_Time_Click()

  DIM sTime_Now AS String

  sTime_Now = "010" & Format$(Now, "hhnnmmddyy")
  IF Main.bServer AND IF Main.hDSC THEN
    Main.hDSC.TX(sTime_Now)
    Main.WriteDebugLog(("[DSC] System time set to ") & Now & (" on alarm system"))
  ELSE
    Main.WriteDebugLog(("[DSC] Connection with alarm system not enabled yet."))
  ENDIF

END

PUBLIC SUB btnArm_Away_Click()

  IF Main.bServer AND IF Main.hDSC THEN
    Main.hDSC.TX("0301")
  ELSE IF Main.bDSCEnabled THEN
    XMLClient.SetAlarmMode("0301")
  ELSE
    Main.WriteDebugLog(("[DSC] Connection with alarm system not enabled yet."))
  ENDIF

END

PUBLIC SUB txtMasterCode_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbType_Click()

  btnSave.Enabled = TRUE

END
