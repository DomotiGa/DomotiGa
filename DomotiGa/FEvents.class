' Gambas class file

' Description:
' FEvents.class
' Future event management functions.

' Development Status:
' Not started yet, things to build are:
' Store events, triggers and actions inside the database.
' Display overview of these items on this page.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC iCurRow AS Integer
PRIVATE tRefresh AS NEW Timer
PRIVATE bAutoRefresh AS Boolean
PRIVATE bExpand AS Boolean
PRIVATE iTable AS Integer = 1

PUBLIC SUB Form_Open()

  btnEvents.Enabled = FALSE
  GetEventList()

  ' create refresh timer
  tRefresh = NEW Timer AS "tRefresh"
  tRefresh.Delay = 3000 ' 3 seconds

END

PUBLIC SUB tbvEvents_ColumnClick(Column AS Integer)

  Main.SortTableView(FEvents.tbvEvents, Column, TRUE)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' refresh tableview contents
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tRefresh_Timer()

  GetEventList()

END

PUBLIC SUB Form_Close()

  tRefresh.Stop

END

PUBLIC SUB Form_LostFocus()

  tRefresh.Stop

END

PUBLIC SUB Form_GotFocus()

  IF bAutoRefresh THEN tRefresh.Enabled = TRUE

END

PUBLIC SUB GetEventList()

  DIM iRows, iCount AS Integer
  DIM rResult AS Result
  DIM aTriggerType AS String[] = ["Time Now", "Variable Change", "Device Change", "IR Remote", "iViewer Remote"]
  DIM aActionType AS String[] = ["Set Device Value", "Set GlobalVar Value", "Send e-mail", "Speak Text", "Execute Shell Command", "Send Tweet", "Send SMS Message", "Send IRTrans Code", "Play Sound", "Write Log Entry", "Display LED Message", "AV Control"]
  INC Application.Busy

  tbvEvents.Clear
  tbvEvents.Rows.Count = 0
  SELECT CASE iTable
    CASE 1 ' display events
      rResult = Main.hDB.Exec("SELECT * FROM events")

      IF NOT rResult THEN
        Message.Info(("Error: table 'events' not found!"))
        RETURN
      END IF
      iRows = rResult.Count

      WITH tbvEvents
        .Font.Size = "9"
        .Columns.Count = IIf(bExpand, 7, 5)
        .Rows.Count = iRows
        .Columns[0].Title = ("Id")
        .Columns[0].Width = 20
        .Columns[1].Title = ("Name")
        .Columns[1].Width = 200
        .Columns[2].Title = ("Description")
        .Columns[2].Width = 250
        .Columns[3].Title = ("Last Run")
        .Columns[3].Width = 180
        .Columns[4].Title = ("Enabled")
        .Columns[4].Width = 55
        IF bExpand THEN
          .Columns[5].Title = ("Log")
          .Columns[5].Width = 40
          .Columns[6].Title = ("First Run")
          .Columns[6].Width = 125
        END IF
      END WITH

      FOR iCount = 0 TO iRows - 1
        tbvEvents[iCount, 0].Text = rResult!id
        tbvEvents[iCount, 1].Text = rResult!name
        tbvEvents[iCount, 2].Text = rResult!comments
        IF rResult!lastrun THEN tbvEvents[iCount, 3].Text = Replace$(Str$(Format(rResult!lastrun, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
        tbvEvents[iCount, 4].Text = Main.DisplayBool(rResult!enabled)
        IF bExpand THEN
          tbvEvents[iCount, 5].Text = Main.DisplayBool(rResult!log)
          IF rResult!firstrun THEN tbvEvents[iCount, 6].Text = Replace$(Str$(Format(rResult!firstrun, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
        END IF
        rResult.MoveNext
      NEXT
    CASE 2 ' display triggers
      rResult = Main.hDB.Exec("SELECT * FROM triggers")

      IF NOT rResult THEN
        Message.Info(("Error: table 'triggers' not found!"))
        RETURN
      END IF
      iRows = rResult.Count

      WITH tbvEvents
        .Font.Size = "9"
        .Columns.Count = 4
        .Rows.Count = iRows
        .Columns[0].Title = ("Id")
        .Columns[0].Width = 20
        .Columns[1].Title = ("Name")
        .Columns[1].Width = 200
        .Columns[2].Title = ("Description")
        .Columns[2].Width = 250
        .Columns[3].Title = ("Type")
        .Columns[3].Width = 240
      END WITH
      FOR iCount = 0 TO iRows - 1
        tbvEvents[iCount, 0].Text = rResult!id
        tbvEvents[iCount, 1].Text = rResult!name
        tbvEvents[iCount, 2].Text = rResult!description
        TRY tbvEvents[iCount, 3].Text = aTriggerType[rResult!type - 1]
        rResult.MoveNext
      NEXT
    CASE 3 ' display conditions
      rResult = Main.hDB.Exec("SELECT * FROM conditions")

      IF NOT rResult THEN
        Message.Info(("Error: table 'conditions' not found!"))
        RETURN
      END IF
      iRows = rResult.Count
      WITH tbvEvents
        .Font.Size = "9"
        .Columns.Count = 4
        .Rows.Count = iRows
        .Columns[0].Title = ("Id")
        .Columns[0].Width = 20
        .Columns[1].Title = ("Name")
        .Columns[1].Width = 200
        .Columns[2].Title = ("Description")
        .Columns[2].Width = 250
        .Columns[3].Title = ("Formula")
        .Columns[3].Width = 340
      END WITH
      FOR iCount = 0 TO iRows - 1
        tbvEvents[iCount, 0].Text = rResult!id
        tbvEvents[iCount, 1].Text = rResult!name
        tbvEvents[iCount, 2].Text = rResult!description
        TRY tbvEvents[iCount, 3].Text = rResult!formula
        rResult.MoveNext
      NEXT
    CASE 4 ' display actions
      rResult = Main.hDB.Exec("SELECT * FROM actions")

      IF NOT rResult THEN
        Message.Info(("Error: table 'actions' not found!"))
        RETURN
      END IF
      iRows = rResult.Count
      WITH tbvEvents
        .Font.Size = "9"
        .Columns.Count = 4
        .Rows.Count = iRows
        .Columns[0].Title = ("Id")
        .Columns[0].Width = 20
        .Columns[1].Title = ("Name")
        .Columns[1].Width = 200
        .Columns[2].Title = ("Description")
        .Columns[2].Width = 250
        .Columns[3].Title = ("Type")
        .Columns[3].Width = 340
      END WITH
      FOR iCount = 0 TO iRows - 1
        tbvEvents[iCount, 0].Text = rResult!id
        tbvEvents[iCount, 1].Text = rResult!name
        tbvEvents[iCount, 2].Text = rResult!description
        TRY tbvEvents[iCount, 3].Text = aActionType[rResult!type - 1]
        rResult.MoveNext
      NEXT
  END SELECT

FINALLY
  DEC Application.Busy

END

PUBLIC SUB Form_Resize()

  tbvEvents.Move(4, 30, ME.ClientWidth - 8, ME.ClientHeight - 31)

END

PUBLIC SUB tbvEvents_DblClick()

  ' open event editor window
  iCurRow = tbvEvents.Row
  IF Main.bUserisAdmin OR NOT Main.bAuthEnabled THEN
    SELECT CASE iTable
      CASE 1
        FEventEditor.Show()
      CASE 2
        FTriggerEditor.bTriggerEdit = TRUE
        FTriggerEditor.Show()
      CASE 3
        FConditionEditor.bConditionEdit = TRUE
        FConditionEditor.Show()
      CASE 4
        FActionEditor.bActionEdit = TRUE
        FActionEditor.Show()
    END SELECT
  ELSE
    Message(("Sorry, editing events is not enabled for users.\nLogin as an admin."))
  END IF

END

PUBLIC SUB tbtnRefresh_Click()

  IF tbtnRefresh.Value = TRUE THEN
    tRefresh.Start
    bAutoRefresh = TRUE
    btnRefresh.Enabled = FALSE
  ELSE
    tRefresh.Stop
    bAutoRefresh = FALSE
    btnRefresh.Enabled = TRUE
  END IF

END

PUBLIC SUB btnRefresh_Click()

  GetEventList()

END

PUBLIC SUB tbtnExpand_Click()

  IF tbtnExpand.Value = TRUE THEN
    bExpand = TRUE
    tbtnExpand.Text = ("Shrink")
  ELSE
    bExpand = FALSE
    tbtnExpand.Text = ("Expand")
  END IF

  GetEventList()

END

PUBLIC SUB btnEvents_Click()

  iTable = 1
  btnEvents.Enabled = FALSE
  btnTriggers.Enabled = TRUE
  btnConditions.Enabled = TRUE
  btnActions.Enabled = TRUE
  GetEventList()

END

PUBLIC SUB btnTriggers_Click()

  iTable = 2
  btnEvents.Enabled = TRUE
  btnTriggers.Enabled = FALSE
  btnConditions.Enabled = TRUE
  btnActions.Enabled = TRUE
  GetEventList()

END

PUBLIC SUB btnConditions_Click()

  iTable = 3
  btnEvents.Enabled = TRUE
  btnTriggers.Enabled = TRUE
  btnConditions.Enabled = FALSE
  btnActions.Enabled = TRUE
  GetEventList()

END

PUBLIC SUB btnActions_Click()

  iTable = 4
  btnEvents.Enabled = TRUE
  btnTriggers.Enabled = TRUE
  btnConditions.Enabled = TRUE
  btnActions.Enabled = FALSE
  GetEventList()

END
