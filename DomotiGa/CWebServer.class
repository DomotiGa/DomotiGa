' Gambas class file

' Description:
' CWebServer.class
' Built-in -very basic- webserver support
' only suited for passing xml-rpc or ajax values in the future.

' Development Status:
' Only some test pages available, not suited for high load/rich pages.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called LICENSE for license details.

PROPERTY DocRoot AS String
PROPERTY HTTPPort AS String
PROPERTY WebserverDebug AS Boolean

PRIVATE sDocRoot AS String
PRIVATE sHTTPPort AS String
PRIVATE bWebserverDebug AS Boolean

PUBLIC hWebserver AS ServerSocket

PRIVATE oClients AS NEW Object[]

PUBLIC FUNCTION Connect() AS Boolean

  hWebserver = NEW ServerSocket AS "Webserver"
  hWebserver.Type = Net.Internet
  hWebserver.Port = sHTTPPort
  hWebserver.Listen(0)

  IF hWebserver.Status = Net.Active THEN
    RETURN TRUE
  END IF
  RETURN FALSE

END

' shutdown our web server
PUBLIC SUB Disconnect()

  hWebserver.Close()

END

PUBLIC SUB Webserver_Connection(RemoteHostIP AS String)

  DIM s AS Socket

  IF hWebserver.Status <= Net.Active THEN RETURN
  s = hWebserver.Accept()
  oClients.Add(s)
  Main.WriteDebugLog("[Webserver] HTTP connect from Client with IP " & RemoteHostIP & " (" & DNS.ResolveHost(RemoteHostIP) & ")")

END

PUBLIC SUB Webserver_Error()

  Main.WriteLog("HTTP Error: Unable to bind socket.")

END

PUBLIC SUB Socket_Read()

  DIM sBuffer AS String
  DIM sReplyHeader AS String
  DIM sHtmlPage AS String
  DIM i, j AS Integer

  IF LAST.Status <> Net.Connected THEN RETURN
  READ #LAST, sBuffer, Lof(LAST)
  sReplyHeader = "HTTP/1.1 200/OK\r\nContent-type:text/html\r\n\r\n"
  WRITE #LAST, sReplyHeader, Len(sReplyHeader)
  i = String.InStr(sBuffer, "GET /")
  j = String.InStr(sBuffer, "HTTP")

  IF i > 0 AND j > (i + 5) THEN
    sBuffer = Trim(String.Mid(sBuffer, i + 5, j - i - 5))
    ' display default index page
    IF NOT sBuffer THEN
      sBuffer = "index.html"
    END IF
    TRY sHtmlPage = File.Load(sDocRoot &/ sBuffer)
    sHtmlPage = Replace(sHtmlPage, "##APPLICATION##", Application.Name)
    sHtmlPage = Replace(sHtmlPage, "##VERSION##", Application.Version)
    IF ERROR THEN
      Main.WriteDebugLog("[Webserver] HTTP document not found: " & sBuffer & " " & Error.Text)
    END IF
  END IF

  IF NOT sHtmlPage THEN
    sHtmlPage = "Web page not found."
    Main.WriteDebugLog("[Webserver] " & sHtmlPage)
  END IF

  WRITE #LAST, sHtmlPage, Len(sHtmlPage)
  LAST.Close
  oClients.Remove(oClients.Find(LAST))

END

PRIVATE SUB PrintPage(sPart AS String) AS String

  DIM sContent AS String

  TRY sContent = File.Load(sDocRoot &/ "page" &/ sPart)
  RETURN sContent

END

PUBLIC SUB Socket_Closed()

  oClients.Remove(oClients.Find(LAST))

END

PRIVATE FUNCTION DocRoot_Read() AS String

  RETURN sDocRoot

END

PRIVATE SUB DocRoot_Write(Value AS String)

  sDocRoot = Value

END

PRIVATE FUNCTION HTTPPort_Read() AS String

  RETURN sHTTPPort

END

PRIVATE SUB HTTPPort_Write(Value AS String)

  sHTTPPort = Value

END

PRIVATE FUNCTION WebserverDebug_Read() AS Boolean

  RETURN bWebserverDebug

END

PRIVATE SUB WebserverDebug_Write(Value AS Boolean)

  bWebserverDebug = Value

END
