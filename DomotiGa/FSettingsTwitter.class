' Gambas class file

' Description:
' FSettingsTwitter.class
' Settings form for Twitter support.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2010 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hForm AS Form
PUBLIC hSendButton AS Button
PUBLIC hTextBox AS TextBox

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)

  txtUser.Text = Main.sTwitterUser
  txtPassword.Text = Main.sTwitterPassword
  chkDebug.Value = Main.bTwitterDebug
  chkEnabled.Value = Main.bTwitterEnabled

  IF Main.bTwitterEnabled = FALSE THEN
    txtUser.Enabled = FALSE
    txtPassword.Enabled = FALSE
    chkDebug.Enabled = FALSE
    btnTest.Enabled = FALSE
  END IF
  btnSave.Enabled = FALSE

END

PUBLIC SUB ButtonCancel_Click()

  ME.Close

END

PUBLIC SUB txtUser_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB txtPassword_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkDebug_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkEnabled_Click()

  txtUser.Enabled = chkEnabled.Value
  txtPassword.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnTest.Enabled = chkEnabled.Value
  btnSave.Enabled = TRUE

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PUBLIC SUB btnDefaults_Click()

  DIM rResult AS Result

  rResult = Main.GetSettingTable("twitter", TRUE) ' get defaults
  IF rResult.Count THEN
    chkEnabled.Value = rResult!enabled
    txtUser.Text = rResult!username
    txtPassword.Text = rResult!password
    chkDebug.Value = rResult!debug
  END IF
  btnSave.Enabled = TRUE

END

PUBLIC SUB btnSave_Click()

  DIM rResult AS Result

  ' save new twitter settings
  rResult = Main.hDB.Exec("UPDATE settings_twitter SET debug = &1, enabled = &2, username = &3, password = &4 WHERE id = 1", chkDebug.Value, chkEnabled.Value, txtUser.Text, txtPassword.Text)
  rResult = Main.GetSettingTable("twitter") ' reload settings
  IF rResult.Count THEN
    Main.bTwitterEnabled = rResult!enabled
    Main.sTwitterUser = rResult!username
    Main.sTwitterPassword = rResult!password
    Main.bTwitterDebug = rResult!debug
  END IF
  IF Main.bStandalone THEN
    Main.Restart_Twitter()
  ELSE
    XMLClient.ModuleRestart("Twitter")
  END IF
  ME.Close

END

PUBLIC SUB btnTest_Click()

  EnterTweet()

END

PUBLIC SUB btnWeb_Click()

  Desktop.Open("http://www.twitter.com/domotiga")

END

PUBLIC SUB EnterTweet()

  hForm = NEW Form
  WITH hForm
    .X = FMain.X + 50
    .Y = FMain.Y + 70
    .BackColor = Color.TextBackground
    .Height = 36
    .Width = 500
    .Text = "Enter your tweet"
  END WITH
  hTextBox = NEW TextBox(hForm) AS "TweetBox"
  WITH hTextBox
    .X = 7
    .Y = 7
    .Height = 21
    .Width = 430
  END WITH
  hSendButton = NEW Button(hForm) AS "SendButton"
  WITH hSendButton
    .X = 445
    .Y = 7
    .Width = 49
    .Height = 21
    .Text = "Send"
  END WITH
  hForm.Show

END

PUBLIC SUB SendButton_Click()

  IF Len(hTextBox.Text) < 5 THEN
    Balloon(("Please type some text first!"), hTextBox)
  ELSE
    IF Main.bStandalone THEN
      Twitter.PostTweet(hTextBox.Text)
    ELSE
      XMLClient.SendTweet(hTextBox.Text)
    ENDIF
    hForm.Close
  ENDIF

END