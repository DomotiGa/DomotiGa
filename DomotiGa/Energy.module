' Gambas module file

' Description:
' Energy.module
' Routines for logging energy usage.
' Need to check accuracy!

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called LICENSE for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create energy table
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB InitTable()

  DIM tableEnergy AS Table

  Main.hDB.Tables.Remove("power_usage")
  tableEnergy = Main.hDB.Tables.Add("power_usage")
  tableEnergy.Fields.Add("id", db.Serial)
  tableEnergy.Fields.Add("counter", db.Long)
  tableEnergy.Fields.Add("stamp", db.Date)
  tableEnergy.Fields.Add("watt", db.Long)
  tableEnergy.PrimaryKey = ["id"]
  tableEnergy.Update()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read value from rfxmeter device and write it to power_usage table
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB LogPower()

  DIM rResult AS Result
  DIM iRFX, iRFXMeter AS Integer
  DIM iWatt, iNewCounter, iPrevCounter AS Float
  'DIM sSql AS String

  IF NOT Main.bRFXComEnabled THEN RETURN

  ' first find devicetype id for RFXMeter
  rResult = Main.hDB.Exec("SELECT id FROM devicetypes WHERE name = &1 ", "RFXPwr Module")

  IF rResult THEN
    iRFX = rResult!id
  ELSE
    Main.WriteDebugLog("[Energy] No Devicetype called RFXPwr Module found!")
    RETURN
  END IF

  ' then find device with devicetype
  rResult = Main.hDB.Exec("SELECT value,id FROM devices WHERE module = &1", iRFX)

  IF rResult.Count = 1 THEN
    iNewCounter = CFloat(rResult!value)
    iRFXMeter = rResult!id
  ELSE
    Main.WriteDebugLog("[Energy] More then one RFXPwr Module device found!")
    RETURN
  END IF

  ' get previous counter value
  rResult = Main.hDB.Exec("SELECT counter FROM power_usage ORDER BY stamp DESC LIMIT 1")
  IF rResult.Count = 1 THEN
    iPrevCounter = rResult!counter
    iWatt = iNewCounter - iPrevCounter
  END IF

  ' write value in power_usage table
  Main.hDB.Begin()
  rResult = Main.hDB.Create("power_usage")
  rResult!watt = iWatt
  rResult!stamp = Now()
  rResult!counter = iNewCounter
  rResult.Update()
  Main.hDB.Commit()

  ' update value2 field of device
  rResult = Main.hDB.Exec("UPDATE devices SET value2 = &1 WHERE id = &2", CStr(CInt(iWatt) * 10), iRFXMeter)

END
