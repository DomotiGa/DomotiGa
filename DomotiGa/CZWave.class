' Gambas class file

' Description:
' CZWave.class
' Support for Z-Wave protocol.

' Development Status:
' Under heavy development.

' Credits:
' Thanks to Willem-Jan from ezHome.nl, Enver from Tane.nl and Jaren for the open-zwave wrapper code.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
LIBRARY "libdomozwave"
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

PUBLIC sLibDomoZwave AS String = "DomoZWave version r1054"

' Initialize and destroy the wrapper - no HomeId required
EXTERN DomoZWave_Init(deviceConfigPath AS String, zwConfigPath AS String, logname AS String, rpcPort AS Integer, enableLog AS Boolean, enableOZWLog AS Boolean, polltime AS Integer)
EXTERN DomoZWave_AddSerialPort(serialPort AS String)
EXTERN DomoZWave_Destroy()
' Write zwcfg*xml, normally not needed
EXTERN DomoZWave_WriteConfig(home AS Integer)
' Retrieve wrapper and controller information
EXTERN DomoZWave_Version() AS String
EXTERN DomoZWave_OZWVersion() AS String
EXTERN DomoZWave_GetLibraryVersion(home AS Integer) AS String
EXTERN DomoZWave_GetLibraryTypeName(home AS Integer) AS String
EXTERN DomoZWave_ControllerType(home AS Integer) AS String
' Enable/disable polling of the devices by open-zwave
EXTERN DomoZWave_EnablePolling(home AS Integer, node AS Integer, polltime AS Integer)
EXTERN DomoZWave_DisablePolling(home AS Integer, node AS Integer)
' Retrieve or set node information
EXTERN DomoZWave_GetNodeManufacturerName(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeProductName(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeName(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeLocation(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeManufacturerId(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeProductType(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeProductId(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_SetNodeManufacturerName(home AS Integer, node AS Integer, manufacturerName AS String)
EXTERN DomoZWave_SetNodeProductName(home AS Integer, node AS Integer, productName AS String)
EXTERN DomoZWave_SetNodeName(home AS Integer, node AS Integer, nodeName AS String)
EXTERN DomoZWave_SetNodeLocation(home AS Integer, node AS Integer, nodeLocation AS String)
EXTERN DomoZWave_GetNodeLibraryVersion(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeProtocolVersion(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeApplicationVersion(home AS Integer, node AS Integer) AS String
' Request or update node state
EXTERN DomoZWave_RequestNodeState(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_RequestNodeDynamic(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_RequestNodeNeighborUpdate(home AS Integer, node AS Integer, OPTIONAL addqueue AS Boolean = FALSE) AS Boolean
EXTERN DomoZWave_RefreshNodeInfo(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_RequestNodeVersion(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_RequestNodeMeter(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_SetValue(home AS Integer, node AS Integer, instance AS Integer, value AS Integer) AS Boolean
' Request or update node configuration
EXTERN DomoZWave_SetConfigParam(home AS Integer, node AS Integer, param AS Integer, value AS Integer, size AS Integer) AS Boolean
EXTERN DomoZWave_RequestConfigParam(home AS Integer, node AS Integer, param AS Integer)
EXTERN DomoZWave_RequestAllConfigParams(home AS Integer, node AS Integer)
EXTERN DomoZWave_GetNodeConfigList(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeConfigLabel(home AS Integer, node AS Integer, item AS Integer) AS String
EXTERN DomoZWave_GetNodeConfigHelp(home AS Integer, node AS Integer, item AS Integer) AS String
EXTERN DomoZWave_GetNodeConfigValue(home AS Integer, node AS Integer, item AS Integer) AS String
EXTERN DomoZWave_GetNodeConfigValueType(home AS Integer, node AS Integer, item AS Integer) AS String
EXTERN DomoZWave_GetNodeConfigValueList(home AS Integer, node AS Integer, item AS Integer) AS String
' Retrieve the node neighborhood list or commandclass list
EXTERN DomoZWave_GetNodeNeighborsList(home AS Integer, node AS Integer) AS String
EXTERN DomoZWave_GetNodeCommandClassList(home AS Integer, node AS Integer, OPTIONAL instance AS Integer) AS String
' Request or update the group/association of a node
EXTERN DomoZWave_GetNodeGroupCount(home AS Integer, node AS Integer) AS Integer
EXTERN DomoZWave_GetNodeGroupList(home AS Integer, node AS Integer, ogroup AS Integer) AS String
EXTERN DomoZWave_GetNodeGroupMax(home AS Integer, node AS Integer, ogroup AS Integer) AS Integer
EXTERN DomoZWave_GetNodeGroupLabel(home AS Integer, node AS Integer, ogroup AS Integer) AS String
EXTERN DomoZWave_AddAssociation(home AS Integer, node AS Integer, group AS Integer, othernode AS Integer)
EXTERN DomoZWave_RemoveAssociation(home AS Integer, node AS Integer, group AS Integer, othernode AS Integer)
' Soft or hard reset the controller
EXTERN DomoZWave_ControllerSoftReset(home AS Integer)
EXTERN DomoZWave_ControllerHardReset(home AS Integer)
' Controller commands
EXTERN DomoZWave_ControllerState(home AS Integer) AS Integer
EXTERN DomoZWave_CancelControllerCommand(home AS Integer) AS Boolean
EXTERN DomoZWave_AddDevice(home AS Integer) AS Boolean
EXTERN DomoZWave_RemoveDevice(home AS Integer) AS Boolean
EXTERN DomoZWave_AddController(home AS Integer) AS Boolean
EXTERN DomoZWave_RemoveController(home AS Integer) AS Boolean
EXTERN DomoZWave_AssignReturnRoute(home AS Integer, node AS Integer, destnode AS Integer) AS Boolean
EXTERN DomoZWave_DeleteAllReturnRoutes(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_RequestNetworkUpdateFromPrimary(home AS Integer) AS Boolean
EXTERN DomoZWave_CreateNewPrimary() AS Boolean
EXTERN DomoZWave_TransferPrimaryRole() AS Boolean
EXTERN DomoZWave_HasNodeFailed(home AS Integer, node AS Integer, OPTIONAL addqueue AS Boolean = FALSE) AS Boolean
EXTERN DomoZWave_RemoveFailedNode(home AS Integer, node AS Integer) AS Boolean
EXTERN DomoZWave_ReplaceFailedNode(home AS Integer, node AS Integer) AS Boolean
' Statistical information
EXTERN DomoZWave_GetDriverStatistics(home AS Integer) AS String
EXTERN DomoZWave_GetSendQueueCount(home AS Integer) AS Long
' Make certain information readable - no HomeId required
EXTERN DomoZWave_CommandClassIdName(class_value AS Integer) AS String
EXTERN DomoZWave_GenreIdName(genre AS Integer) AS String
EXTERN DomoZWave_BasicTypeName(basictype AS Integer) AS String
EXTERN DomoZWave_GenericTypeName(generictype AS Integer) AS String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PROPERTY Port AS String
PROPERTY ZWaveDebug AS Boolean
PROPERTY PollTime AS Integer
PROPERTY PollTimeListening AS String
PROPERTY EnablePollListening AS Boolean
PROPERTY PollTimeSleeping AS String
PROPERTY EnablePollSleeping AS Boolean
PROPERTY UpdateNeighbor AS String
PROPERTY EnableUpdateNeighbor AS Boolean

PRIVATE sPort AS String
PRIVATE bZWaveDebug AS Boolean
PRIVATE iPollTime AS Integer
PRIVATE sPollTimeListening AS String
PRIVATE bEnablePollListening AS Boolean
PRIVATE sPollTimeSleeping AS String
PRIVATE bEnablePollSleeping AS Boolean
PRIVATE sUpdateNeighbor AS String
PRIVATE bEnableUpdateNeighbor AS Boolean

PUBLIC bOZWInitialized AS Boolean = FALSE
PUBLIC tZWaveLED AS NEW Timer
PUBLIC tOZWKeepAlive AS NEW Timer
PUBLIC bOZWKeepAliveState AS Boolean
PUBLIC bOZWKeepAlivePrevState AS Boolean

PUBLIC sZWaveAPIVersion AS String
PUBLIC sZWaveZDKVersion AS String
PUBLIC iHomeID AS Integer
PUBLIC iControllerID AS Integer
PUBLIC sControllerType AS String = ("Unknown")

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' run zwave network related tasks
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RunTasks()

  ' check if neighbor needs updating
  IF bEnableUpdateNeighbor THEN
    IF Events.TimeCron(sUpdateNeighbor) THEN

      IF bOZWInitialized = FALSE THEN
        IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] RunTasks Update Neighbors can't start, because the Open Z-Wave library isn't fully initialized."))
        RETURN
      ENDIF

      RequestNodeNeighborUpdate(0)
    ENDIF
  ENDIF

  ' check if listening nodes needs polling
  IF bEnablePollListening THEN
    IF Events.TimeCron(sPollTimeListening) THEN

      IF bOZWInitialized = FALSE THEN
        IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] RunTasks Poll Listening can't start, because the Open Z-Wave library isn't fully initialized."))
        RETURN
      ENDIF

      RequestNodeDynamicReport(0)
    ENDIF
  ENDIF

  ' check if sleeping nodes needs polling
  IF bEnablePollSleeping THEN
    IF Events.TimeCron(sPollTimeSleeping) THEN

      IF bOZWInitialized = FALSE THEN
        IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] RunTasks Poll Sleeping can't start, because the Open Z-Wave library isn't fully initialized."))
        RETURN
      ENDIF

      RequestNodeDynamicReport(0, TRUE)
    ENDIF
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' get information about version and nodes from stick
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Connect()

  ' We (re)start the Z-Wave, set OZW initialized to FALSE
  bOZWInitialized = FALSE

  ' Reset home ID & controller ID to 0 after a (re)start
  iHomeID = 0
  iControllerID = 0
  Main.ControlLed("ZWave", "Off")

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Initialising open-zwave wrapper."))
  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] " & DomoZWave_Version()))
  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] " & DomoZWave_OZWVersion()))

  ' First we initialize the open-zwave wrapper, but we also need to add 1 or more
  ' serialports to really allow it to function
  DomoZWave_Init(Main.sBaseDir &/ "wrappers/domozwave/open-zwave/config/", Main.sBaseDir &/ "wrappers/domozwave/", Main.sBaseDir &/ "logs" &/ Main.sDomoZWaveFile, Main.hXMLRPC.HTTPPort, bZWaveDebug, bZWaveDebug, iPollTime)
  DomoZWave_AddSerialPort(sPort)

  ' start timer for Z-Wave keepalive of when using the Open Z-Wave library
  ' We will wait 10 seconds to receive the controllerid, because this should happen within 1-2 seconds
  ' After we received the controllerid, we set the timer 60 seconds and we will send a DynamicReport request to the controllerid
  tOZWKeepAlive = NEW Timer AS "tOZWKeepAlive"
  tOZWKeepAlive.Delay = 10000
  tOZWKeepAlive.Start
  bOZWKeepAliveState = FALSE
  bOZWKeepAlivePrevState = FALSE

CATCH ' some errors
  Main.WriteLog(("Z-Wave Error: ") & ERROR.Text & (" at ") & ERROR.Where)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' called from devices module
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB SendCommand(sAddress AS String, sCmd AS String)

  OZW_SendCommand(sAddress, sCmd)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request or update node state
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RequestBasicReport(iNodeId AS Integer, OPTIONAL Sleeping AS Boolean = FALSE) AS Boolean

  DIM rResultNodes AS Result

  IF iNodeId == 0 THEN

    IF Sleeping THEN
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger sleeping nodes to update their basic values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is TRUE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ELSE
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger listening nodes to update their basic values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is FALSE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ENDIF

    IF rResultNodes THEN
      IF (rResultNodes.Count >= 1) THEN
        FOR EACH rResultNodes
          ' We can't query a controller or static controller
          IF rResultNodes!typebasic >= 3 THEN
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting state for node id " & rResultNodes!nodeid & "."))
            DomoZWave_RequestNodeState(iHomeID, rResultNodes!nodeid)
          ENDIF
        NEXT
      ENDIF
    ENDIF

    RETURN TRUE

  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting node state of node id " & iNodeId))
    RETURN DomoZWave_RequestNodeState(iHomeID, iNodeId)
  ENDIF

END

PUBLIC SUB RequestNodeDynamicReport(iNodeId AS Integer, OPTIONAL Sleeping AS Boolean = FALSE) AS Boolean

  DIM rResultNodes AS Result

  IF iNodeId == 0 THEN

    IF Sleeping THEN
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger sleeping nodes to update their dynamic values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is TRUE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ELSE
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger listening nodes to update their dynamic values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is FALSE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ENDIF

    IF rResultNodes THEN
      IF (rResultNodes.Count >= 1) THEN
        FOR EACH rResultNodes
          ' We can't query a controller or static controller
          IF rResultNodes!typebasic >= 3 THEN
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting dynamic for node id " & rResultNodes!nodeid & "."))
            DomoZWave_RequestNodeDynamic(iHomeID, rResultNodes!nodeid)
          ENDIF
        NEXT
      ENDIF
    ENDIF

    RETURN TRUE

  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting dynamic of node id " & iNodeId))
    RETURN DomoZWave_RequestNodeDynamic(iHomeID, iNodeId)
  ENDIF

END

PUBLIC SUB RequestNodeVersionReport(iNodeId AS Integer, OPTIONAL Sleeping AS Boolean = FALSE) AS Boolean

  DIM rResultNodes AS Result

  IF iNodeId == 0 THEN

    IF Sleeping THEN
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger sleeping nodes to update their version values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is TRUE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ELSE
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger listening nodes to update their verion values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is FALSE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ENDIF

    IF rResultNodes THEN
      IF (rResultNodes.Count >= 1) THEN
        FOR EACH rResultNodes
          ' We can't query a controller or static controller
          IF rResultNodes!typebasic >= 3 THEN
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting version for node id " & rResultNodes!nodeid & "."))
            DomoZWave_RequestNodeVersion(iHomeID, rResultNodes!nodeid)
          ENDIF
        NEXT
      ENDIF
    ENDIF

    RETURN TRUE

  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting version of node id " & iNodeId))
    RETURN DomoZWave_RequestNodeVersion(iHomeID, iNodeId)
  ENDIF

END

PUBLIC SUB RequestNodeMeterReport(iNodeId AS Integer, OPTIONAL Sleeping AS Boolean = FALSE) AS Boolean

  DIM rResultNodes AS Result

  IF iNodeId == 0 THEN

    IF Sleeping THEN
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger sleeping nodes to update their meter values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is TRUE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ELSE
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger listening nodes to update their meter values."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is FALSE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ENDIF

    IF rResultNodes THEN
      IF (rResultNodes.Count >= 1) THEN
        FOR EACH rResultNodes
          ' We can't query a controller or static controller
          IF rResultNodes!typebasic >= 3 THEN
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting meter for node id " & rResultNodes!nodeid & "."))
            DomoZWave_RequestNodeMeter(iHomeID, rResultNodes!nodeid)
          ENDIF
        NEXT
      ENDIF
    ENDIF

    RETURN TRUE

  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting meter of node id " & iNodeId))
    RETURN DomoZWave_RequestNodeMeter(iHomeID, iNodeId)
  ENDIF

END

PUBLIC SUB RequestNodeNeighborUpdate(iNodeId AS Integer, OPTIONAL Sleeping AS Boolean = FALSE) AS Boolean

  DIM rResultNodes AS Result

  IF iNodeId == 0 THEN

    IF Sleeping THEN
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger sleeping nodes to update their neighbor routing."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is TRUE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ELSE
      IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Trigger listening nodes to update their neighbor routing."))
      rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is FALSE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
    ENDIF

    IF rResultNodes THEN
      IF (rResultNodes.Count >= 1) THEN
        FOR EACH rResultNodes
          ' We can't query a controller or static controller
          IF rResultNodes!typebasic >= 3 THEN
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting neighbor update for node id " & rResultNodes!nodeid & "."))

            ' Has to be TRUE, but the open-zwave has a bug ...
            'DomoZWave_RequestNodeNeighborUpdate(iHomeID, rResultNodes!nodeid, TRUE)
            DomoZWave_RequestNodeNeighborUpdate(iHomeID, rResultNodes!nodeid, FALSE)
          ENDIF
        NEXT
      ENDIF
    ENDIF

    RETURN TRUE

  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting node id " & iNodeId & " to update it's neighbors."))
    RETURN DomoZWave_RequestNodeNeighborUpdate(iHomeID, iNodeId, FALSE)
  ENDIF

END

PUBLIC SUB RefreshNodeInfo(iNodeId AS Integer) AS Boolean

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting node id " & iNodeId & " to refresh ALL its node information."))
  RETURN DomoZWave_RefreshNodeInfo(iHomeID, iNodeId)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request or update node configuration
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB SetConfigParam(iNodeId AS Integer, iParam AS Integer, iValue AS Integer, OPTIONAL iSize AS Integer = 2)

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Set config param " & iParam & " to value " & iValue & " for node id " & iNodeId & ", size is " & iSize & "."))
  DomoZWave_SetConfigParam(iHomeID, iNodeId, iParam, iValue, iSize)

END

PUBLIC SUB RequestConfigParam(iNodeId AS Integer, iParam AS Integer)

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Get config param " & iParam & " for node id " & iNodeId & "."))
  DomoZWave_RequestConfigParam(iHomeID, iNodeId, iParam)

END

PUBLIC SUB RequestAllConfigParams(iNodeId AS Integer)

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting all config params for node id " & iNodeId & "."))
  DomoZWave_RequestAllConfigParams(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeConfigList(iNodeId AS Integer) AS String

  RETURN DomoZWave_GetNodeConfigList(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeConfigLabel(iNodeId AS Integer, Item AS Integer) AS String

  RETURN DomoZWave_GetNodeConfigLabel(iHomeID, iNodeId, Item)

END

PUBLIC SUB GetNodeConfigHelp(iNodeId AS Integer, Item AS Integer) AS String

  RETURN DomoZWave_GetNodeConfigHelp(iHomeID, iNodeId, Item)

END

PUBLIC SUB GetNodeConfigValue(iNodeId AS Integer, Item AS Integer) AS String

  RETURN DomoZWave_GetNodeConfigValue(iHomeID, iNodeId, Item)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Retrieve the node neighborhood list or commandclass list
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB GetNodeNeighborsList(iNodeId AS Integer) AS String

  DIM NList AS String

  'IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting neighborslist for node id " & iNodeId & "."))
  NList = DomoZWave_GetNodeNeighborsList(iHomeID, iNodeId)

  ' Replace the "|" with ", " to make it more human readable
  RETURN Replace$(NList, "|", ", ")
  
END

PUBLIC SUB GetNodeCommandClassList(iNodeId AS Integer, OPTIONAL Instance AS Integer = 0) AS String

  DIM NList AS String

  'IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting commandclasslist for node id " & iNodeId & IIf(Instance >= 1, " instance " & Instance, "") & "."))
  NList = DomoZWave_GetNodeCommandClassList(iHomeID, iNodeId, Instance)

  ' Replace the "COMMAND_CLASS_", because it is in each value (makes the string too long)
  NList = Replace$(NList, "COMMAND_CLASS_", "")
  ' Replace the "|" with ", " to make it more human readable
  RETURN Replace$(NList, "|", ", ")

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Group Association
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

PUBLIC SUB GetNodeGroupCount(iNodeId AS Integer) AS Integer

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requested group count for node id " & iNodeId & "."))
  RETURN DomoZWave_GetNodeGroupCount(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeGroupList(iNodeId AS Integer) AS Integer

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requested group list for node id " & iNodeId & "."))
  RETURN DomoZWave_GetNodeGroupList(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeGroupMax(iNodeId AS Integer, group AS Integer) AS Integer

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requested group max for node id " & iNodeId & " and group " & group & "."))
  RETURN DomoZWave_GetNodeGroupMax(iHomeID, iNodeId, group)

END

PUBLIC SUB GetNodeGroupLabel(iNodeId AS Integer, group AS Integer) AS String

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requested group label for node id " & iNodeId & " and group " & group & "."))
  RETURN DomoZWave_GetNodeGroupLabel(iHomeID, iNodeId, group)

END

PUBLIC SUB AddAssociation(iNodeId AS Integer, iAssociationGroup AS Integer, iAssociationNode AS Integer)

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Add association node id " & iNodeId & " between group " & iAssociationGroup & " and node " & iAssociationNode & "."))
  DomoZWave_AddAssociation(iHomeID, iNodeId, iAssociationGroup, iAssociationNode)

END

PUBLIC SUB RemoveAssociation(iNodeId AS Integer, iAssociationGroup AS Integer, iAssociationNode AS Integer)

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Remove association node id " & iNodeId & " between group " & iAssociationGroup & " and node " & iAssociationNode & "."))
  DomoZWave_RemoveAssociation(iHomeID, iNodeId, iAssociationGroup, iAssociationNode)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Soft or hard reset the controller
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB ControllerSoftReset()

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Controller will be Soft Reset (no data will be lost)"))
  DomoZWave_ControllerSoftReset(iHomeID)

END

PUBLIC SUB ControllerHardReset()

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Controller will be HARD Reset (*ALL* data will be lost)"))
  DomoZWave_ControllerHardReset(iHomeID)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Controller commands
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB CancelControllerCommand() AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_CancelControllerCommand(iHomeID)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Cancelling controller command"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Cancelling controller command, controller was already in normal operation mode"))
  ENDIF

  RETURN bResult

END

PUBLIC SUB AddDevice() AS Boolean

  DIM bResult AS Boolean

  ' First we execute the command
  ' If True == Command succeeded
  ' If False == Command failed (most likely controller is already in an add/remove mode)
  bResult = DomoZWave_AddDevice(iHomeID)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin add device, press button on device to add!"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin add device, controller already in add/remove mode, press cancel first"))
  ENDIF

  RETURN bResult

END

PUBLIC SUB RemoveDevice() AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_RemoveDevice(iHomeID)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin remove device, press button on device to remove!"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin remove device, controller already in add/remove mode, press cancel first"))
  ENDIF

  RETURN bResult

END

PUBLIC SUB AddController() AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_AddController(iHomeID)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin add controller, press button on other controller to add!"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin add controller, controller already in add/remove mode, press cancel first"))
  ENDIF

  RETURN bResult

END

PUBLIC SUB RemoveController() AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_RemoveController(iHomeID)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin remove controller, press button on other controller to remove!"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Begin remove controller, controller already in add/remove mode, press cancel first"))
  ENDIF

  RETURN bResult

END

PUBLIC SUB RequestNetworkUpdate()

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Requesting network update."))
  DomoZWave_RequestNetworkUpdateFromPrimary()

END

PUBLIC SUB HasNodeFailed(iNodeId AS Integer) AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_HasNodeFailed(iHomeID, iNodeId)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Check if node has failed, call X to check the response"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Check if node has failed can't be executed. The controller isn't in normal mode"))
  ENDIF

END

PUBLIC SUB RemoveFailedNode(iNodeId AS Integer) AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_RemoveFailedNode(iHomeID, iNodeId)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Remove failed node executed (only works if the node is on the failed list)"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Remove failed node can't be executed. The controller isn't in normal mode"))
  ENDIF

END

PUBLIC SUB ReplaceFailedNode(iNodeId AS Integer) AS Boolean

  DIM bResult AS Boolean

  bResult = DomoZWave_ReplaceFailedNode(iHomeID, iNodeId)

  IF bResult THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Replace failed node executed, press button the new node now (only works if the node is on the failed list)"))
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Replace failed node, can't be executed. The controller isn't in normal mode"))
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Make certain information readable
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB CommandClassIdName(class_value AS Integer) AS String

  RETURN DomoZWave_CommandClassIdName(class_value)

END

PUBLIC SUB GenreIdName(genre AS Integer) AS String

  RETURN DomoZWave_GenreIdName(genre)

END

PUBLIC SUB BasicTypeName(basictype AS Integer) AS String

  RETURN DomoZWave_BasicTypeName(basictype)

END

PUBLIC SUB GenericTypeName(generictype AS Integer) AS String

  RETURN DomoZWave_GenericTypeName(generictype)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' translate between API and ZDK version numbering
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB ExpandVersion(sVersion AS String) AS String

  SELECT sVersion
    CASE "2.78"
      RETURN "5.02 Patch 3"
    CASE "2.64"
      RETURN "5.02 Patch 2"
    CASE "2.51"
      RETURN "5.02 Patch 1"
    CASE "2.48"
      RETURN "5.02"
    CASE "2.74"
      RETURN "4.50"
    CASE "2.67"
      RETURN "4.28"
    CASE "2.40"
      RETURN "4.27"
    DEFAULT
      RETURN "Unknown"
    END SELECT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create a record with node information in database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB CreateNode(iNodeId AS Integer, iTypeBasic AS Integer, iTypeGeneric AS Integer, iTypeSpecific AS Integer, iCapabilities AS Integer, iSecurity AS Integer, bSleeping AS Boolean)

  DIM rResult AS Result

  ' write node to the database table
  Main.hDB.Begin()
  rResult = Main.hDB.Create("devices_zwave")
  rResult!nodeid = iNodeId
  rResult!typebasic = iTypeBasic
  rResult!typegeneric = iTypeGeneric
  rResult!typespecific = iTypeSpecific
  rResult!capabilities = iCapabilities
  rResult!security = iSecurity
  rResult!sleeping = bSleeping
  rResult.Update()
  Main.hDB.Commit()

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Added node " & iNodeId & " to the DomotiGa Z-Wave list."))

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' clear nodes table from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RemoveNodes()

  DIM hTable AS Table

  TRY Main.hDB.Tables.Remove("devices_zwave")
  hTable = Main.hDB.Tables.Add("devices_zwave")

  hTable.Fields.Add("id", db.Serial)
  hTable.Fields.Add("nodeid", gb.String, 32)
  hTable.Fields.Add("value", gb.String, 32)
  hTable.Fields.Add("value2", gb.String, 32)
  hTable.Fields.Add("value3", gb.String, 32)
  hTable.Fields.Add("value4", gb.String, 32)
  hTable.Fields.Add("typebasic", gb.Integer)
  hTable.Fields.Add("typegeneric", gb.Integer)
  hTable.Fields.Add("typespecific", gb.Integer)
  hTable.Fields.Add("capabilities", gb.Integer)
  hTable.Fields.Add("security", gb.Integer)
  hTable.Fields.Add("sleeping", gb.Boolean)
  hTable.Fields.Add("manufacturer", gb.String, 64)
  hTable.Fields.Add("model", gb.String, 64)
  hTable.Fields.Add("version", gb.String, 32)
  hTable.Fields.Add("speed", gb.String, 32)
  hTable.Fields.Add("neighbors", gb.String)
  hTable.Fields.Add("classes", gb.String)
  hTable.PrimaryKey = ["id"]

  hTable.Update

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Removed all nodes from the DomotiGa Z-Wave list."))

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Get manufacturer name from the Open Z-Wave device list and update the table
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OZW_GetManufacturerAndModel()

  DIM rResultNodes AS Result
  DIM sManufacturer AS String
  DIM sModel AS String

  rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave")
  IF rResultNodes THEN
    IF (rResultNodes.Count >= 1) THEN
      FOR EACH rResultNodes
        sManufacturer = DomoZWave_GetNodeManufacturerName(iHomeID, rResultNodes!nodeid)
        sManufacturer = Replace(sManufacturer, "Unknown: ", "")
        sModel = DomoZWave_GetNodeProductName(iHomeID, rResultNodes!nodeid)
        sModel = Replace(sModel, "Unknown: ", "")

        ' Add it to the database now
        Main.hDB.Exec("UPDATE devices_zwave SET manufacturer = &1, model = &2 WHERE nodeid = &3", sManufacturer, sModel, rResultNodes!nodeid)
      NEXT
    END IF
  END IF

END

PUBLIC SUB GetNodeManufacturerName(iNodeId AS Integer) AS String

  RETURN DomoZWave_GetNodeManufacturerName(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeProductName(iNodeId AS Integer) AS String

  RETURN DomoZWave_GetNodeProductName(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeLibraryVersion(iNodeId AS Integer) AS String

  RETURN DomoZWave_GetNodeLibraryVersion(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeProtocolVersion(iNodeId AS Integer) AS String

  RETURN DomoZWave_GetNodeProtocolVersion(iHomeID, iNodeId)

END

PUBLIC SUB GetNodeApplicationVersion(iNodeId AS Integer) AS String

  RETURN DomoZWave_GetNodeApplicationVersion(iHomeID, iNodeId)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' get value from node
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB GetCurrentValueNode(iNodeId AS Integer) AS String

  DIM rResult AS Result

  rResult = Main.hDB.Exec("SELECT * FROM devices_zwave WHERE nodeid = &1", iNodeId)
  IF NOT rResult.Available THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Node with id '") & iNodeId & ("' not found in database!"))
    RETURN 0
  ELSE
    RETURN rResult!value
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update a node record with value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB UpdateValueNode(sAddress AS String, iValue AS Integer, sValue AS String) AS Boolean

  DIM rResult AS Result

  SELECT iValue
    CASE 1
      rResult = Main.hDB.Exec("UPDATE devices_zwave SET value = &1 WHERE nodeid = &2", sValue, sAddress)
    CASE 2
      rResult = Main.hDB.Exec("UPDATE devices_zwave SET value2 = &1 WHERE nodeid = &2", sValue, sAddress)
    CASE 3
      rResult = Main.hDB.Exec("UPDATE devices_zwave SET value3 = &1 WHERE nodeid = &2", sValue, sAddress)
    CASE 4
      rResult = Main.hDB.Exec("UPDATE devices_zwave SET value4 = &1 WHERE nodeid = &2", sValue, sAddress)
    CASE ELSE
      ' We need to skip Value255 - Battery level
      RETURN TRUE
  END SELECT

  IF rResult THEN
    RETURN TRUE
  ELSE
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Node with id '") & sAddress & ("' not found in database!"))
    RETURN FALSE
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Timer for checking Open Z-Wave library connectivity
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

PUBLIC SUB tOZWKeepAlive_Timer()

  IF iControllerID = 0 THEN
    Main.WriteLog(("ERROR: Z-Wave interface FAILED to connect to Port ") & Main.sZWaveSerialPort & (" (retrying)"))
    tOZWKeepAlive.Stop
  ELSE

    IF bOZWKeepAliveState = FALSE THEN

       IF bOZWKeepAlivePrevState = TRUE THEN
          'Main.WriteLog(("ERROR: Z-Wave interface FAILED to respond in the polltime interval"))
          'Main.ControlLed("ZWave", "Off")
       ENDIF
       
    ELSE

       IF bOZWKeepAlivePrevState = FALSE THEN
          'Main.WriteLog(("Z-Wave interface responded again in the polltime interval"))
          'Main.ControlLed("ZWave", "On")
       ENDIF

    ENDIF

    bOZWKeepAlivePrevState = bOZWKeepAliveState
    bOZWKeepAliveState = FALSE

  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reset led status in main status bar
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tZWaveLED_Timer()

  Main.ControlLed("ZWave", "Off")
  tZWaveLED.Stop

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Destroyed open-zwave wrapper."))
  DomoZWave_Destroy()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Retrieves the OpenZWave library version
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

PUBLIC FUNCTION Version() AS String

  RETURN DomoZWave_Version()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Open Z-Wave functions
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OZW_SendCommand(sAddress AS String, sCmd AS String)

  DIM iBrightness AS Integer
  DIM sNode, sInstance AS String
  DIM aAddr AS String[]

  ' address format 8:2, for instance 2 of node 8
  aAddr = Scan(sAddress, "*:*")
  IF aAddr.Count = 2 THEN
    sNode = aAddr[0]
    sInstance = aAddr[1]
  ELSE
    sNode = sAddress
    sInstance = 1 ' no instance specified, use first
  ENDIF

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Sending command " & sCmd & " to address " & sAddress & "."))
  sCmd = UCase(sCmd)
  IF sCmd = "ON"
    SetValue(sNode, sInstance, 255)
  ELSE IF sCmd = "OFF"
    SetValue(sNode, sInstance, 0)
  ELSE IF InStr(sCmd, "DIM ") THEN ' DIM 0-100
    iBrightness = Val(Replace(sCmd, "DIM ", ""))
    SetValue(sNode, sInstance, iBrightness)
  ELSE
    Main.WriteDebugLog(("[Z-Wave] Only DIM and ON/OFF commands are supported!"))
  ENDIF

END

PUBLIC SUB SetValue(iNodeId AS Integer, iInstance AS Integer, iValue AS Integer)

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Set value for node " & iNodeId & " and instance " & iInstance & " to " & iValue))
  DomoZWave_SetValue(iHomeID, iNodeId, iInstance, iValue)

END

PUBLIC SUB OZW_SetIds(homeId AS Integer, controllerId AS Integer)

  iHomeID = homeId
  iControllerID = controllerId
  sControllerType = DomoZWave_ControllerType(iHomeID)
  sZWaveAPIVersion = DomoZWave_GetLibraryVersion(iHomeID)
  ' Strip off "Z-Wave "
  sZWaveAPIVersion = Replace(sZWaveAPIVersion, "Z-Wave ", "")
  sZWaveZDKVersion = ExpandVersion(sZWaveAPIVersion)

  ' Now we can write to the main logfile that we are connected
  Main.WriteLog(("Z-Wave interface successfully connected to Port ") & Main.sZWaveSerialPort)

  IF bZWaveDebug THEN
    Main.WriteDebugLog(("[Z-Wave] Z-Wave interface successfully connected to Port ") & Main.sZWaveSerialPort)
    Main.WriteDebugLog(("[Z-Wave] Received response from Z-Wave controller: homeid=" & homeId & ", nodeid=" & iControllerID & "."))
    Main.WriteDebugLog(("[Z-Wave] Z-Wave controller type: " & sControllerType & "."))
    Main.WriteDebugLog(("[Z-Wave] Z-Wave API version: " & sZWaveAPIVersion & ", SDK version: " & sZWaveZDKVersion & "."))
  ENDIF

  ' Enable LED if we got a response from the Open Z-Wave library
  Main.ControlLed("ZWave", "On")

  ' Set the timer to Open Z-Wave PollTime milliseconds polling now
  tOZWKeepAlive.Stop

  IF iPollTime <> 0 THEN
    tOZWKeepAlive.Delay = iPollTime
    tOZWKeepAlive.Start
    bOZWKeepAliveState = TRUE
    bOZWKeepAlivePrevState = TRUE
  ENDIF

END

PUBLIC SUB OZW_SetValue(iHomeId AS Integer, iNodeId AS String, iInstanceId AS Integer, iValue AS Integer, sValue AS String)

  DIM iDeviceId AS Integer
  DIM sAddress AS String

  sAddress = iNodeId & IIf(iInstanceId > 1, ":" & iInstanceId, "")
  UpdateValueNode(sAddress, iValue, sValue)
  ' update value
  IF iNodeId THEN
    IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Got value changed notification for node " & iNodeid & " and instance " & iInstanceId & " value" & iValue & " changed to " & sValue & "."))
    iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Z-Wave Controller"), "ZWAVE")
    IF iValue = 255 THEN ' battery level
      IF iDeviceId THEN Devices.Battery(iDeviceId, sValue)
    ELSE ' device value
      SELECT iValue
      CASE 1
        IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, sValue, "", "", "")
      CASE 2
        IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, "", sValue, "", "")
      CASE 3
        IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, "", "", sValue, "")
      CASE 4
        IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, "", "", "", sValue)
      END SELECT
    ENDIF
  ENDIF

  ' We got a status update from the Z-Wave network, update the KeepAlive variable
  bOZWKeepAliveState = TRUE

END

PUBLIC SUB OZW_AllQueried()

  DIM rResultNodes AS Result

  ' The Open Z-Wave library is fully initialized now
  bOZWInitialized = TRUE

  Main.WriteLog(("Z-Wave interface is fully initialized."))

  ' In debug mode, first report we are fully initialized
  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] All Z-Wave devices are Queried. Z-Wave devices can be used now."))

  IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Searching for nodes to enable/disable poll."))
  rResultNodes = Main.hDB.Exec("SELECT * from devices_zwave WHERE sleeping is FALSE AND nodeid NOT LIKE '%:%' ORDER BY length(nodeid), nodeid")
  IF rResultNodes THEN
    IF (rResultNodes.Count >= 1) THEN
      FOR EACH rResultNodes
        IF rResultNodes!nodeid <> Main.hZWave.iControllerID THEN ' we cannot query ourselves
          IF iPollTime = 0 THEN
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Disable polling for node " & rResultNodes!nodeid))
            DomoZWave_DisablePolling(iHomeID, rResultNodes!nodeid)
          ELSE
            IF bZWaveDebug THEN Main.WriteDebugLog(("[Z-Wave] Enable polling for node " & rResultNodes!nodeid & " every " & iPollTime & " mSec."))
            DomoZWave_EnablePolling(iHomeID, rResultNodes!nodeid, iPolltime)
          ENDIF
        ENDIF
      NEXT
    ENDIF
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'--------------------------------------------------------
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

' implement properties
FUNCTION Port_Read() AS String

  RETURN sPort

END

SUB Port_Write(Value AS String)

  sPort = Value

END

PRIVATE FUNCTION ZWaveDebug_Read() AS Boolean

  RETURN bZWaveDebug

END

PRIVATE SUB ZWaveDebug_Write(Value AS Boolean)

  bZWaveDebug = Value

END

PRIVATE FUNCTION Polltime_Read() AS Integer

  RETURN iPolltime

END

PRIVATE SUB Polltime_Write(Value AS Integer)

  iPolltime = Value

END

PRIVATE FUNCTION PolltimeListening_Read() AS String

  RETURN sPollTimeListening

END

PRIVATE SUB PolltimeListening_Write(Value AS String)

  sPollTimeListening = Value

END

PRIVATE FUNCTION PolltimeSleeping_Read() AS String

  RETURN sPollTimeSleeping

END

PRIVATE SUB PolltimeSleeping_Write(Value AS String)

  sPollTimeSleeping = Value

END

PRIVATE FUNCTION UpdateNeighbor_Read() AS String

  RETURN sUpdateNeighbor

END

PRIVATE SUB UpdateNeighbor_Write(Value AS String)

  sUpdateNeighbor = Value

END

PRIVATE FUNCTION EnablePollListening_Read() AS Boolean

  RETURN bEnablePollListening

END

PRIVATE SUB EnablePollListening_Write(Value AS Boolean)

  bEnablePollListening = Value

END

PRIVATE FUNCTION EnablePollSleeping_Read() AS Boolean

  RETURN bEnablePollSleeping

END

PRIVATE SUB EnablePollSleeping_Write(Value AS Boolean)

  bEnablePollSleeping = Value

END

PRIVATE FUNCTION EnableUpdateNeighbor_Read() AS Boolean

  RETURN bEnableUpdateNeighbor

END

PRIVATE SUB EnableUpdateNeighbor_Write(Value AS Boolean)

  bEnableUpdateNeighbor = Value

END

