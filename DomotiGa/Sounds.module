' Gambas module file

' Description:
' Sounds.module
' Play sound files on events.

' Development Status:
' Working.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE sDefaultSoundDirectory AS String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' just a function to list available sounds
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION LoadSounds()

  DIM sSound AS String
  DIM sSoundFiles AS String[]

  sDefaultSoundDirectory = Main.sBaseDir &/ "sounds"

  ' fill array with available sounds
  sSoundFiles = Dir(sDefaultSoundDirectory, "*.wav")
  sSoundFiles.Insert(Dir(sDefaultSoundDirectory, "*.mp3"))
  sSoundFiles.Insert(Dir(sDefaultSoundDirectory, "*.ogg"))

  IF Main.bSoundDebug THEN
    FOR EACH sSound IN sSoundFiles
      Main.WriteDebugLog("[Sound] Found " & sSound)
    NEXT
  ENDIF 
  Main.WriteLog(sSoundFiles.Count & If(sSoundFiles.Count <> 1, " soundfiles", " soundfile") & " found.")

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' play sound with selected volume
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION PlaySnd(sFile AS String, OPTIONAL iVol AS Integer)

  DIM iVolume AS Float
  DIM sSnd AS String

  IF NOT Main.bSoundEnabled THEN RETURN ' sound not enabled

  sSnd = sDefaultSoundDirectory &/ sFile

  IF ivol THEN ' custom volume
    iVolume = 1 - iVol / 100
  ELSE ' default volume
    iVolume = 1 - Main.iSoundVolume / 100
  ENDIF

  IF Exist(sSnd) THEN
    ' load sample
    Music.Load(sSnd)

    Music.Volume = iVolume
    IF Main.bSoundDebug THEN 
      Main.WriteDebugLog("[Sound] Volume is set to " & iVolume)
      IF Main.GlobalVar["Mute"] THEN
        Main.WriteDebugLog("[Sound] [Muted] Playing sound " & sSnd)
      ELSE
        Main.WriteDebugLog("[Sound] Playing sound " & sSnd)
      END IF
    END IF

    IF NOT Main.GlobalVar["Mute"] THEN
      Main.WriteLog("Playing sound " & sFile & ".")
      ' play sample
      Music.Play()
    ELSE
      Main.WriteLog("[Muted] Playing sound " & sFile & ".")
    END IF
  ELSE
    Main.WriteDebugLog("[Sound] The soundfile " & sSnd & " doesn't exist!")
  END IF

END
