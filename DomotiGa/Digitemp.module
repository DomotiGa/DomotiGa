' Gambas module file

' Description:
' Digitemp.module
' Support for digitemp 1-wire sensors (only temperature at this moment).

' Development Status:
' Just build, possibly bugs around.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC tDigitemp AS NEW Timer
PUBLIC sOutput AS String

PUBLIC FUNCTION Run()

  ' start poll timer for digitemp
  tDigitemp = NEW Timer AS "tDigitemp"
  tDigitemp.Delay = Main.iDigitempPollTime * 1000 ' multiply for seconds
  tDigitemp.Start

  CheckDigitemp()

END

PUBLIC SUB CheckDigitemp()

  DIM sCommand AS String
  DIM iReadTime AS Integer

  IF Main.iDigitempReadtime < 1000 THEN
    iReadtime = 1500
  ELSE
    iReadtime = Main.iDigitempReadtime
  END IF
  sCommand = Main.sDigitempCommand & " -a -q -r " & iReadTime & " -c " & Main.sDigitempConfig
  SHELL sCommand & " 2>&1" FOR READ AS "Digitemp"
  IF Main.bDigitempDebug THEN Main.WriteDebugLog("[Digitemp] " & sCommand)

END

PUBLIC SUB Digitemp_Read()

  DIM sLine AS String

  READ #LAST, sLine, -256
  sOutput &= sLine

END

PUBLIC SUB Digitemp_Kill()

  DIM sLine AS String
  DIM aScan AS String[]
  DIM iDeviceId AS Integer

  ' Aug 29 16:50:00 Sensor 0 C: 27.38 F: 81.28
  FOR EACH sLine IN Split(sOutput, "\n")
    IF InStr(sLine, "Sensor") THEN
      aScan = Scan(sLine, "* * * * * * * * *")
      IF Main.bDigitempDebug THEN
        Main.WriteDebugLog("[Digitemp] " & sLine)
        Main.WriteDebugLog(("[Digitemp] Sensor# ") & aScan[4] & " " & aScan[6] & "°C " & aScan[8] & "°F")
      END IF
      iDeviceId = Devices.Find(aScan[4], Devices.FindInterface("Digitemp"), "Digitemp DS1820")
      IF iDeviceId THEN
        IF Main.sTemperature = "°C" THEN
          Devices.ValueUpdate(iDeviceId, aScan[6], "", "", "")
        ELSE
          Devices.ValueUpdate(iDeviceId, aScan[8], "", "", "")
        END IF
      END IF
    END IF
  NEXT
  sOutput = NULL

END

PUBLIC SUB tDigitemp_Timer()

  CheckDigitemp()

END
