' Gambas class file

' Description:
' FFloorplans.class
' Display devices on a floorplan.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PRIVATE cLocations AS Collection
PRIVATE iFloor AS Integer = 2
PRIVATE tRefresh AS Timer
PRIVATE bAutoRefresh AS Boolean

PUBLIC SUB Form_Open()

  PopulateButtons()
  ' open first floor
  RefreshPage()
  ' create refresh timer
  tRefresh = NEW Timer AS "tRefresh"
  tRefresh.Delay = 30000 ' 30 seconds

END

PUBLIC SUB Form_Resize()

  tbFloors.Width = ME.Width
  hbFloors.Width = ME.Width
  svFloorplan.Move(15, 45, ME.ClientWidth - 15, ME.ClientHeight - 47)

END

PUBLIC SUB PopulateButtons()

  DIM rFloor AS Result
  DIM hButton AS Button

  ' populate floors toolbutton
  TRY rFloor = Main.hDB.Exec("SELECT * FROM floors")
  IF rFloor.Count THEN
    FOR EACH rFloor
    IF NOT rFloor!name THEN CONTINUE
    hButton = NEW Button(hbFloors) AS "FloorButton"
     WITH hButton
      IF rFloor!floor = 2 THEN .Enabled = FALSE
      .Text = rFloor!name
      .Height = 28
      .Width = 84
      .Tag = rFloor!floor
    END WITH
    NEXT
  END IF

END

PUBLIC SUB FloorButton_Click()

  iFloor = LAST.tag
  RefreshPage()

END

PUBLIC SUB DisplayFloorplan(iFloor AS Integer, oParent AS Object)

  DIM hPictureBox AS PictureBox
  DIM rFloor, rDevice, rLocation AS Result
  DIM hButton AS Button
  DIM imgFloorplan AS Image
  DIM sIcon, sValue AS String
  DIM x, y, iFloorDev AS Integer
  DIM oObject AS Object
  DIM hText AS TextLabel
  DIM hMovieBox AS MovieBox

  ' delete previous floorplan first
  FOR EACH oObject IN oParent.Children
    oObject.Delete()
  NEXT

  ' try loading floorplan image if available
  TRY rFloor = Main.hDB.Exec("SELECT * FROM floors WHERE floor = &1", iFloor)
  IF rFloor.Available THEN
    IF rFloor!image THEN
      TRY imgFloorplan = Image.Load(Main.sBaseDir &/ "floorplans" &/ rFloor!image)
      IF imgFloorplan THEN
        ' floorplan image
        hPictureBox = NEW PictureBox(oParent) AS "Floorplan"
        WITH hPictureBox
          .Picture = imgFloorplan.Picture
          .Height = imgFloorplan.Height
          .Width = imgFloorplan.Width
          .X = 0
          .Y = 0
        END WITH
      ELSE
        Message.Info(("Couldn't open floorplan image"))
      ENDIF
    ENDIF
  ENDIF
  ' search for devices with floorplan filled and valid x and y values
  TRY rDevice = Main.hDB.Exec("SELECT * FROM devices")
  IF rDevice.Count THEN
    FOR EACH rDevice
        sValue = UCase(rDevice!value)
        TRY x = rDevice!x
        TRY y = rDevice!y
        TRY iFloordev = rDevice!floorplan
        IF rDevice!floorplan = iFloor THEN
          IF x AND y THEN
          ' draw icon on x/y location
          IF rDevice!onicon OR rDevice!officon THEN
            IF sValue = "ON" OR sValue = "OPEN" OR sValue = "MOTION" OR sValue = "UP" OR sValue = "AWAKE" THEN
              sIcon = Main.sBaseDir &/ "icons" &/ rDevice!onicon
              IF rDevice!switchable THEN
                hMovieBox = NEW MovieBox(oParent) AS "DeviceIconSwitch_Off"
              ELSE
                hMovieBox = NEW MovieBox(oParent) AS "DeviceIcon"
              ENDIF
            ELSE IF InStr(sValue, "DIM") THEN
              sIcon = Main.sBaseDir &/ "icons" &/ rDevice!dimicon
            ELSE
              sIcon = Main.sBaseDir &/ "icons" &/ rDevice!officon
              IF rDevice!switchable THEN
                hMovieBox = NEW MovieBox(oParent) AS "DeviceIconSwitch_On"
              ELSE
                hMovieBox = NEW MovieBox(oParent) AS "DeviceIcon"
              ENDIF
            END IF
            WITH hMovieBox
              TRY .Path = sIcon
              .Height = 20
              .Width = 20
              .X = X
              .Y = Y
              .ToolTip = rDevice!name & "\r\n" & rDevice!value & rDevice!label & "  " & rDevice!value2 & rDevice!label2
              .Tag = rDevice!name
              .Playing = TRUE
            END WITH
          END IF
          IF Len(RTrim$(rDevice!value)) THEN
            hText = NEW TextLabel(oParent) AS "DeviceText"
            WITH hText
              .Font = Font["Sans Serif, 6, Normal"]
              .X = X
              .Y = Y + 20
              .Width = Len(RTrim$(rDevice!value) & RTrim$(rDevice!label)) * 6
              .Text = RTrim$(rDevice!value) & RTrim$(rDevice!label)
              '.Transparent = TRUE
            END WITH
          END IF
          IF Len(RTrim$(rDevice!value2)) THEN
            hText = NEW TextLabel(oParent) AS "DeviceText"
            WITH hText
              .Font = Font["Sans Serif, 6, Normal"]
              .X = X
              .Y = Y + 32
              .Width = Len(RTrim$(rDevice!value2) & RTrim$(rDevice!label2)) * 6
              .Text = RTrim$(rDevice!value2) & RTrim$(rDevice!label2)
              '.Transparent = TRUE
            END WITH
          END IF
        END IF
      END IF
    NEXT
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' refresh contents
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tRefresh_Timer()

  RefreshPage()

END

PUBLIC SUB Form_Close()

  tRefresh.Stop

END

PUBLIC SUB btnRefresh_Click()

  RefreshPage()

END

PUBLIC SUB DeviceIconSwitch_Off_MouseDown()

  IF Main.bServer THEN
    Devices.SetDevice(LAST.tag, "Off")
  ELSE
    XMLClient.DeviceSetDevice(LAST.tag, "Off")
  END IF
  RefreshPage()

END

PUBLIC SUB DeviceIconSwitch_On_MouseDown()

  IF Main.bServer THEN
    Devices.SetDevice(LAST.tag, "On")
  ELSE
    XMLClient.DeviceSetDevice(LAST.tag, "On")
  END IF
  RefreshPage()

END

PUBLIC SUB RefreshPage()

  DIM oObject AS Object

  ' enable all buttons first, then disable selected one
  FOR EACH oObject IN hbFloors.Children
    oObject.Enabled = TRUE
    IF oObject.Tag = iFloor THEN oObject.Enabled = FALSE
  NEXT

  DisplayFloorplan(iFloor, svFloorplan)

END

PUBLIC SUB tbtnRefresh_Click()

  IF tbtnRefresh.Value = TRUE THEN
    tRefresh.Start
    bAutoRefresh = TRUE
    btnRefresh.Enabled = FALSE
  ELSE
    tRefresh.Stop
    bAutoRefresh = FALSE
    btnRefresh.Enabled = TRUE
  END IF

END
