' Gambas class file

' Description:
' FControl.class
' Device control page.

' Development Status:
' Not implemented fully.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2012 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hDim AS Slider
PRIVATE iLocationMax AS Integer
PRIVATE cLocations AS Collection
PRIVATE sLocation AS String = "All"

PUBLIC SUB Form_Open()

  PopulateButtons()
  RefreshPage()

END

PUBLIC SUB Form_Resize()

  IF iLocationMax < 9 THEN
    ToolBar1.Move(0, 0, ME.ClientWidth - 20)
    HBox1.Move(0, 0, ME.ClientWidth - 20, 28)
    HBox2.Hide
    VPanel1.Move(7, 30, ME.ClientWidth - 20, ME.ClientHeight - 35)
  ELSE
    ToolBar1.Move(0, 0, ME.ClientWidth - 20)
    HBox1.Move(0, 0, ME.ClientWidth - 20, 28)
    HBox2.Move(0, 31, ME.ClientWidth - 20, 28)
    HBox2.Show
    VPanel1.Move(7, 70, ME.ClientWidth - 20, ME.ClientHeight - 70)
  ENDIF

END

PUBLIC SUB LocationButton_Click()

  sLocation = LAST.Tag
  ReloadPage()

END

PUBLIC SUB ReloadPage()

  DIM oObject AS Object

  ' delete previous buttons first
  FOR EACH oObject IN VPanel1.Children
    oObject.Delete()
  NEXT
  RefreshPage()

END

PUBLIC SUB SelectButton(sLoc AS String)

  DIM oObject AS Object

  ' disable selected button
  FOR EACH oObject IN HBox1.Children
    IF oObject.Text = sLoc THEN
      oObject.Enabled = FALSE
    ELSE
      oObject.Enabled = TRUE
    ENDIF
  NEXT
  ' disable selected button
  FOR EACH oObject IN HBox2.Children
    IF oObject.Text = sLoc THEN
      oObject.Enabled = FALSE
    ELSE
      oObject.Enabled = TRUE
    ENDIF
  NEXT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create dynamic buttons for switchable and dimable devices only
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RefreshPage()

  DIM rDevice, rLocation AS Result
  DIM sValue, sValue2, sIcon, sType AS String
  DIM sSql AS String = "SELECT onicon, officon, dimicon, address, switchable, dimable, id, value, value2, name, location, extcode FROM devices WHERE enabled is TRUE"
  DIM hButton AS Button
  DIM iBrightness AS Integer
  DIM oIcon AS Object
  DIM hDimBox AS HBox

  ' fetch location name
  rLocation = Main.hDB.Exec("SELECT * FROM locations WHERE name LIKE &1", sLocation)
  IF (rLocation.Count >= 1) THEN sSql &= " AND location = " & rLocation!id

  SelectButton(sLocation)

  TRY rDevice = Main.hDB.Exec(sSql)
  IF NOT ERROR THEN
    IF (rDevice.Count > 0) THEN
      FOR EACH rDevice
        IF rDevice!dimable THEN
          sValue = UCase(rDevice!value)
          sType = Devices.FindTypeForDevice(rDevice!id)
          IF sValue = "ON" OR sValue = "OPEN" OR sValue = "MOTION" OR sValue = "UP" OR sValue = "AWAKE" THEN
            hButton = NEW Button(VPanel1) AS "SwitchButton_Off"
            TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!onicon]
            iBrightness = 100
          ELSE IF InStr(sValue, "DIM ") THEN
            hButton = NEW Button(VPanel1) AS "SwitchButton_Off"
            iBrightness = Val(Replace(sValue, "DIM ", ""))
            TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!dimicon]
          ELSE
            hButton = NEW Button(VPanel1) AS "SwitchButton_On"
            TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!officon]
            iBrightness = 0
          ENDIF

          WITH hButton
            .Text = rDevice!name
            .Height = 30
            .Width = 130
            .Tag = rDevice!name
            .Picture = oIcon
            .Font = Font["Sans Serif, 7, Normal"]
          END WITH

          SELECT sType
            CASE "X10"
              IF rDevice!extcode = TRUE THEN
                hDim = NEW Slider(VPanel1) AS "DimSlider"
                WITH hDim
                  .Tracking = FALSE
                  .MaxValue = 100
                  .MinValue = 0
                  .PageStep = 6.25
                  .Step = 6.25
                  .Height = 20
                  .Width = 130
                  .Mark = TRUE
                  .Tag = rDevice!name
                  .Value = iBrightness
                END WITH
              ELSE
                hDimBox = NEW HBox(VPanel1)
                WITH hDimBox
                  .Height = 20
                  .Spacing = 10
                  .Width = 130
                END WITH
                hButton = NEW Button(hDimBox) AS "MinusButton"
                WITH hButton
                  .Text = "-"
                  .Height = 10
                  .Width = 60
                  .Tag = rDevice!name
                END WITH
                hButton = NEW Button(hDimBox) AS "PlusButton"
                WITH hButton
                  .Text = "+"
                  .Height = 10
                  .Width = 60
                  .Tag = rDevice!name
                END WITH
                ENDIF
              DEFAULT ' "AC", "HEEU", "Z-Wave", "PLCBUS", "FS20", "KNX/EIB"
                hDim = NEW Slider(VPanel1) AS "DimSlider"
                WITH hDim
                  .Tracking = FALSE
                  .MaxValue = 100
                  .MinValue = 0
                  .PageStep = 6.25
                  .Step = 6.25
                  .Height = 20
                  .Width = 130
                  .Mark = TRUE
                  .Tag = rDevice!name
                  .Value = iBrightness
                END WITH
          END SELECT
        ENDIF

        IF rDevice!switchable THEN
          sValue = UCase(rDevice!value)
          IF sValue = "ON" OR sValue = "OPEN" OR sValue = "MOTION" OR sValue = "UP" OR sValue = "AWAKE" THEN
            hButton = NEW Button(VPanel1) AS "SwitchButton_Off"
            TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!onicon]
          ELSE
            hButton = NEW Button(VPanel1) AS "SwitchButton_On"
            TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!officon]
          ENDIF

          WITH hButton
            .Text = rDevice!name
            .Height = 30
            .Width = 130
            .Tag = rDevice!name
            .Picture = oIcon
            IF Len(rDevice!name) > 12 THEN .Font = Font["Sans Serif, 7, Normal"]
          END WITH
        ENDIF
      NEXT
    ENDIF
  ELSE
    Main.WriteLog(("Error ") & Error.Text & (" while fetching devices!"))
  ENDIF

END

PUBLIC SUB PopulateButtons()

  DIM rDevice, rLocation AS Result
  DIM hButton AS Button
  DIM sLocation AS String
  DIM oBox AS Object
  DIM iLoc AS Integer
  cLocations = NEW Collection

  cLocations.Add("All", 0)
  TRY rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE switchable is TRUE OR dimable is TRUE ORDER BY name ASC")
  IF NOT ERROR THEN
    IF rDevice.Count THEN
      FOR EACH rDevice
        TRY rLocation = Main.hDB.Exec("SELECT * FROM locations WHERE id='" & rDevice!location & "'")
        IF rLocation.Count THEN
          IF NOT rLocation!name THEN CONTINUE
          ' get unique list of used location
          cLocations.Add(rLocation!name, rLocation!id)
        ENDIF
      NEXT
    ENDIF
    iLocationMax = cLocations.Count

    FOR EACH sLocation IN cLocations
      IF iLoc < 9 THEN
        oBox = HBox1
      ELSE
        oBox = HBox2
      ENDIF
      INC iLoc
      hButton = NEW Button(oBox) AS "LocationButton"
      WITH hButton
        .Text = sLocation
        .Height = 20
        .Width = 84
        .Tag = sLocation
        .Font = Font["Sans Serif, 7, Normal"]
      END WITH
    NEXT
  ELSE
    Main.WriteLog(("ERROR: No database connection active!"))
  ENDIF

END

PRIVATE SUB SetDevice(sCmd AS String)

  IF Main.bServer THEN
    TRY Devices.SetDevice(LAST.tag, sCmd)
  ELSE
    XMLClient.DeviceSetDevice(LAST.tag, sCmd)
  END IF

END

PUBLIC SUB DimSlider_MouseUp()

  IF Main.bServer THEN
    TRY Devices.SetDevice(LAST.tag, "Dim " & LAST.Value)
  ELSE
    XMLClient.DeviceSetDevice(LAST.tag, "Dim " & LAST.Value)
  ENDIF

END

PUBLIC SUB SwitchButton_Off_Click()

  SetDevice("Off")

END

PUBLIC SUB SwitchButton_On_Click()

  SetDevice("On")

END

PUBLIC SUB MinusButton_MouseDown()

  SetDevice("Dim")

END

PUBLIC SUB PlusButton_MouseDown()

  SetDevice("Bright")

END

PUBLIC SUB MinusButton_MouseUp()

  SetDevice("Stop")

END

PUBLIC SUB PlusButton_MouseUp()

  SetDevice("Stop")

END

PUBLIC SUB btnRefresh_Click()

  ReloadPage()

END
