' Gambas class file

' Description:
' FControl.class
' Device control page.

' Development Status:
' Not implemented fully.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hDim AS Slider
PUBLIC hVPanelDim AS VPanel
PUBLIC hVPanelSwitch AS VPanel

PUBLIC SUB Form_Open()

  RefreshPage()

END

PUBLIC SUB Form_Resize()

  hVPanelSwitch.Move(35, 42, 140, ME.ClientHeight - 60)
  hVPanelDim.Move(203, 42, 140, ME.ClientHeight - 60)

END

PUBLIC SUB ReloadPage()

  DIM oObject AS Object

  ' delete previous buttons first
  FOR EACH oObject IN FControl
    IF oObject.Name = "ToolBar1" THEN CONTINUE
    oObject.Delete()
  NEXT

  RefreshPage()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create dynamic buttons for switchable and dimable devices only
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RefreshPage()

  DIM rDevice AS Result
  DIM sValue, sIcon, sType AS String
  DIM hButton AS Button
  DIM iBrightness AS Integer
  DIM oIcon AS Object

  hVPanelSwitch = NEW VPanel(FControl)
  WITH hVPanelSwitch
    .X = 35
    .Y = 45
    .Width = 140
    .Height = ME.ClientHeight - 60
    .Padding = 5
    .Spacing = 5
  END WITH

  hVPanelDim = NEW VPanel(FControl)
  WITH hVPanelDim
    .X = 203
    .Y = 42
    .Width = 140
    .Height = ME.ClientHeight - 60
    .Padding = 5
    .Spacing = 5
  END WITH

  TRY rDevice = Main.hDB.Exec("SELECT onicon, officon, dimicon, address, switchable, dimable, id, value, name FROM devices WHERE enabled is TRUE")
  IF (rDevice.Count > 0) THEN
    FOR EACH rDevice
      IF rDevice!dimable THEN
        sValue = UCase(rDevice!value)
        IF sValue = "ON" OR sValue = "OPEN" OR sValue = "MOTION" THEN
          hButton = NEW Button(hVPanelDim) AS "SwitchButton_Off"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!onicon]
          iBrightness = 100
        ELSE IF InStr(sValue, "DIM ") THEN
          hButton = NEW Button(hVPanelDim) AS "SwitchButton_Off"
          iBrightness = Val(Replace(sValue, "DIM ", ""))
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!dimicon]
        ELSE
          hButton = NEW Button(hVPanelDim) AS "SwitchButton_On"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!officon]
          iBrightness = 0
        END IF
        WITH hButton
          .Text = rDevice!name
          .Height = 35
          .Width = 110
          .Tag = rDevice!name
          .Picture = oIcon
          .Font = Font["Sans Serif, 7, Normal"]
        END WITH
        sType = Devices.FindTypeForDevice(rDevice!id)
        SELECT sType
          CASE "AC", "Z-Wave"
            hDim = NEW Slider(hVPanelDim) AS "DimSlider"
            WITH hDim
            .Tracking = FALSE
            .MaxValue = 100
            .MinValue = 0
            .PageStep = 10
            .Step = 10
            .Height = 20
            .Width = 110
            .Tag = rDevice!name
            .Value = iBrightness
            END WITH
          DEFAULT
            hButton = NEW Button(hVPanelDim) AS "MinusButton"
            WITH hButton
              .Text = "-"
              .Height = 35
              .Width = 25
              .Tag = rDevice!name
            END WITH
            hButton = NEW Button(hVPanelDim) AS "PlusButton"
            WITH hButton
              .Text = "+"
              .Height = 35
              .Width = 25
              .Tag = rDevice!name
            END WITH
        END SELECT
      END IF
      IF rDevice!switchable THEN
        sValue = UCase(rDevice!value)
        IF UCase$(rDevice!value) = "ON" OR UCase$(rDevice!value) = "OPEN" OR UCase$(rDevice!value) = "MOTION" THEN
          hButton = NEW Button(hVPanelSwitch) AS "SwitchButton_Off"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!onicon]
        ELSE
          hButton = NEW Button(hVPanelSwitch) AS "SwitchButton_On"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!officon]
        END IF
        WITH hButton
          .Text = rDevice!name
          .Height = 35
          .Width = 130
          .Tag = rDevice!name
          .Picture = oIcon
          IF Len(rDevice!name) > 12 THEN .Font = Font["Sans Serif, 7, Normal"]
        END WITH
      END IF
    NEXT
  END IF

END

PUBLIC SUB DimSlider_MouseUp()

  Devices.SetDevice(LAST.tag, "Dim " & hDim.Value)

END

PUBLIC SUB SwitchButton_Off_Click()

  Devices.SetDevice(LAST.tag, "Off")
  RefreshPage()

END

PUBLIC SUB SwitchButton_On_Click()

  Devices.SetDevice(LAST.tag, "On")
  RefreshPage()

END

PUBLIC SUB MinusButton_MouseDown()

  Devices.SetDevice(LAST.tag, "Dim")
  RefreshPage()

END

PUBLIC SUB PlusButton_MouseDown()

  Devices.SetDevice(LAST.tag, "Bright")
  RefreshPage()

END

PUBLIC SUB MinusButton_MouseUp()

  Devices.SetDevice(LAST.tag, "Stop")
  RefreshPage()

END

PUBLIC SUB PlusButton_MouseUp()

  Devices.SetDevice(LAST.tag, "Stop")
  RefreshPage()

END

PUBLIC SUB btnRefresh_Click()

  RefreshPage()

END
