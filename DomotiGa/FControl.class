' Gambas class file

' Description:
' FControl.class
' Device control page.

' Development Status:
' Not implemented fully.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called LICENSE for license details.

PUBLIC SUB Form_Open()

  RefreshPage()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create dynamic buttons for switchable and dimable devices only
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RefreshPage()

  DIM rDevice AS Result
  DIM sValue, sIcon AS String
  DIM hButton AS Button
  DIM iY, iYy AS Integer = 40
  DIM oIcon AS Object

  TRY rDevice = Main.hDB.Exec("SELECT onicon, officon, address, switchable, dimable, id, value, name FROM devices")
  IF (rDevice.Count > 0) THEN
    FOR EACH rDevice
      IF rDevice!dimable THEN
        hButton = NEW Button(FControl) AS "MinusButton"
        WITH hButton
          .X = 220
          .Y = iYy
          .Text = "-"
          .Height = 35
          .Width = 25
          .Tag = rDevice!address
        END WITH
        hButton = NEW Button(FControl) AS "DimButton"
        WITH hButton
          .X = 220 + 26
          .Y = iYy
          .Text = rDevice!name
          .Height = 35
          .Width = 110
          .Tag = rDevice!address
          .Font = Font["Sans Serif, 7, Normal"]
        END WITH
        hButton = NEW Button(FControl) AS "PlusButton"
        WITH hButton
          .X = 220 + 26 + 111
          .Y = iYy
          .Text = "+"
          .Height = 35
          .Width = 25
          .Tag = rDevice!address
        END WITH
        iYy += 48
      END IF

      IF rDevice!switchable THEN 
        sValue = UCase(rDevice!value)
        IF UCase$(rDevice!value) = "ON" OR UCase$(rDevice!value) = "OPEN" OR UCase$(rDevice!value) = "MOTION" THEN
          hButton = NEW Button(FControl) AS "SwitchButton_Off"
          TRY oIcon = Picture["icons/" & rDevice!onicon]
        ELSE 
          hButton = NEW Button(FControl) AS "SwitchButton_On"
          TRY oIcon = Picture["icons/" & rDevice!officon]
        END IF

        WITH hButton
          .X = 40
          .Y = iY
          .Text = rDevice!name
          .Height = 35
          .Width = 130
          .Tag = rDevice!name
          .Picture = oIcon
          IF Len(rDevice!name) > 12 THEN .Font = Font["Sans Serif, 7, Normal"]
        END WITH
        iY += 48
      END IF
    NEXT
  END IF

END

PUBLIC SUB SwitchButton_Off_Click()

  Devices.SetDevice(LAST.tag, "Off")
  RefreshPage()

END

PUBLIC SUB SwitchButton_On_Click()

  Devices.SetDevice(LAST.tag, "On")
  RefreshPage()

END

PUBLIC SUB DimButton_Click()

  Message(LAST.tag & " was clicked!")
  RefreshPage()

END

PUBLIC SUB MinusButton_Click()

  Main.hCTX35.sCommandToSend = LAST.tag & " DIM"
  ' Message("Dim for " & LAST.tag & " was clicked!")
  RefreshPage()

END

PUBLIC SUB PlusButton_Click()

  Main.hCTX35.sCommandToSend = LAST.tag & " BRIGHT"
  ' Message("Bright for " & LAST.tag & " was clicked!")
  RefreshPage()

END
