' Gambas class file

' Description:
' FControl.class
' Device control page.

' Development Status:
' Not implemented fully.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC SUB Form_Open()

  RefreshPage()

END

PUBLIC SUB ReloadPage()

  DIM oObject AS Object

  ' delete previous buttons first
  FOR EACH oObject IN FControl.Children
    oObject.Delete()
  NEXT
  RefreshPage()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create dynamic buttons for switchable and dimable devices only
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB RefreshPage()

  DIM rDevice AS Result
  DIM sValue, sIcon AS String
  DIM hButton AS Button
  DIM iY, iYy AS Integer = 40
  DIM oIcon AS Object

  TRY rDevice = Main.hDB.Exec("SELECT onicon, officon, address, switchable, dimable, id, value, name FROM devices WHERE enabled is TRUE")
  IF (rDevice.Count > 0) THEN
    FOR EACH rDevice
      IF rDevice!dimable THEN
        sValue = UCase(rDevice!value)
        IF UCase$(rDevice!value) = "ON" OR UCase$(rDevice!value) = "OPEN" OR UCase$(rDevice!value) = "MOTION" THEN
          hButton = NEW Button(FControl) AS "SwitchButton_Off"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!onicon]
        ELSE
          hButton = NEW Button(FControl) AS "SwitchButton_On"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!officon]
        END IF
        WITH hButton
          .X = 220 + 26
          .Y = iYy
          .Text = rDevice!name
          .Height = 35
          .Width = 110
          .Tag = rDevice!name
          .Picture = oIcon
          .Font = Font["Sans Serif, 7, Normal"]
        END WITH
        hButton = NEW Button(FControl) AS "MinusButton"
        WITH hButton
          .X = 220
          .Y = iYy
          .Text = "-"
          .Height = 35
          .Width = 25
          .Tag = rDevice!name
        END WITH
        hButton = NEW Button(FControl) AS "PlusButton"
        WITH hButton
          .X = 220 + 26 + 111
          .Y = iYy
          .Text = "+"
          .Height = 35
          .Width = 25
          .Tag = rDevice!name
        END WITH
        iYy += 48
      END IF
      IF rDevice!switchable THEN
        sValue = UCase(rDevice!value)
        IF UCase$(rDevice!value) = "ON" OR UCase$(rDevice!value) = "OPEN" OR UCase$(rDevice!value) = "MOTION" THEN
          hButton = NEW Button(FControl) AS "SwitchButton_Off"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!onicon]
        ELSE
          hButton = NEW Button(FControl) AS "SwitchButton_On"
          TRY oIcon = Picture[Main.sBaseDir &/ "icons/" & rDevice!officon]
        END IF
        WITH hButton
          .X = 40
          .Y = iY
          .Text = rDevice!name
          .Height = 35
          .Width = 130
          .Tag = rDevice!name
          .Picture = oIcon
          IF Len(rDevice!name) > 12 THEN .Font = Font["Sans Serif, 7, Normal"]
        END WITH
        iY += 48
      END IF
    NEXT
  END IF

END

PUBLIC SUB SwitchButton_Off_Click()

  Devices.SetDevice(LAST.tag, "Off")
  RefreshPage()

END

PUBLIC SUB SwitchButton_On_Click()

  Devices.SetDevice(LAST.tag, "On")
  RefreshPage()

END

PUBLIC SUB MinusButton_MouseDown()

  Devices.SetDevice(LAST.tag, "Dim")
  RefreshPage()

END

PUBLIC SUB MinusButton_MouseUp()

  Devices.SetDevice(LAST.tag, "Stop")
  RefreshPage()

END

PUBLIC SUB PlusButton_MouseDown()

  Devices.SetDevice(LAST.tag, "Bright")
  RefreshPage()

END

PUBLIC SUB PlusButton_MouseUp()

  Devices.SetDevice(LAST.tag, "Stop")
  RefreshPage()

END
