' Gambas class file

' Description:
' FScenarioEditor.class
' Support for creating and editing of all thermostat constants.

' Development Status:
' Works for 99%.

' DomotiGa - an open source home automation program.
' Copyright(C) 2011 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE rResult AS Result
PUBLIC bAddConstant AS Boolean

PUBLIC SUB Form_Open()

  IF bAddConstant THEN ME.Text = ("Create Constant")
  ME.Move(FMain.X + 50, FMain.Y + 70)
  LoadConstant()

END

PUBLIC SUB LoadConstant()

  IF NOT bAddConstant THEN
    ' get selected event values
    TRY rResult = Main.hDB.Exec("SELECT * FROM thermostat_constant WHERE id = &1", FThermostat.txtCurTherm)
    IF NOT ERROR THEN
      IF rResult.Count >= 1 THEN
        tbName.Text = rResult!name
        vbValue.Value = rResult!value
        txtDescription.Text = rResult!description
        cbtnColor.Color = rResult!color
      ELSE
        Message(("Couldn't load constant record!"))
        ME.Close
      ENDIF
    ENDIF
  ENDIF

END

PUBLIC SUB btnCancel_Click()

  bAddConstant = FALSE
  ME.Close

END

PUBLIC SUB btnDelete_Click()

  SELECT Message.Question(("Are you sure that you want to delete this constant ?"), ("Yes"), ("No"))
    CASE 1
      Main.hDB.Exec("DELETE FROM thermostat_constant WHERE id = &1", rResult!id)
      ME.Close
      FThermostat.GetThermList()
  END SELECT

END

PUBLIC SUB btnNew_Click()

  bAddConstant = TRUE
  btnSave_Click()

END

PUBLIC SUB btnSave_Click()

  DIM rResultUpdate AS Result
  DIM sSql AS String
  DIM iRet AS Integer
  IF NOT tbName.Text THEN
    Balloon(("Please enter a name for this constant !"), tbName)
    RETURN
  ENDIF

  IF bAddConstant THEN
    ' create new scenario row
    sSql = "INSERT INTO thermostat_constant SET  name = &1, value = &2, description= &3, color=&4"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, vbValue.Value, txtDescription.Text, cbtnColor.color)
  ELSE
    ' name has changed
    IF rResult!name <> tbName.Text THEN
      IF ConstantNameExist(tbName.Text) THEN
        Balloon(("Please enter a unique name for this constant !"), tbName)
        RETURN
      ENDIF
    ENDIF
    ' update new event details
    sSql = "UPDATE thermostat_constant SET name = &1, value = &2, description= &3, color=&4 WHERE id = &5"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, vbValue.Value, txtDescription.Text, cbtnColor.color, rResult!id)
  ENDIF

  FThermostat.GetThermList()
  bAddConstant = FALSE
  ME.Close

END

PRIVATE SUB ConstantNameExist(sName AS String) AS Boolean

  DIM rResultName AS Result

  rResultName = Main.hDB.Exec("SELECT id FROM thermostat_constant WHERE name = &1", sName)
  IF rResultName THEN
    IF rResultName.Count THEN
      RETURN TRUE
    ENDIF
  ENDIF
  RETURN FALSE

END

PUBLIC SUB tbName_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB vbValue_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB txtDescription_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cbtnColor_Change()

   btnSave.Enabled = TRUE

END
