' Gambas module file

' Description:
' Twitter.module
' Support for sending tweets to twitter.com

' Development Status:
' Working.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hTweet AS NEW HttpClient AS "hTweet"
PUBLIC hForm AS Form
PUBLIC hSendButton AS Button
PUBLIC hTextBox AS TextBox

PUBLIC SUB PostTweet(sMessage AS String)

  IF NOT Main.bTwitterEnabled THEN RETURN

  IF hTweet.Status > 0 THEN
    Main.WriteLog(("I'm already sending a Tweet, skipping."))
    RETURN
  END IF

  hTweet.URL = "http://twitter.com/statuses/update.xml?status=" & EncodeHTML(sMessage)
  hTweet.Auth = 1
  hTweet.Async = TRUE
  hTweet.User = Main.sTwitterUser
  hTweet.Password = Main.sTwitterPassword
  hTweet.TimeOut = 10
  hTweet.Post("text/plain", " ")

END

PUBLIC SUB hTweet_Finished()

  DIM iCount AS Integer

  SELECT hTweet.Code
    CASE 200
      Main.WriteLog(("I have sent a Tweet."))
    CASE 401
      Main.WriteLog(("Error authenticating while sending a Tweet!"))
    CASE ELSE
      Main.WriteLog(("Unknown error occured while sending a Tweet!"))
  END SELECT

  IF NOT Main.bTwitterDebug THEN RETURN
  FOR iCount = 0 TO hTweet.Headers.Count - 1
    Main.WriteDebugLog("[Twitter] " & hTweet.Headers[iCount], 1)
  NEXT

END

PUBLIC SUB hTweet_Error()

  Main.WriteDebugLog(("[Twitter] Error ") & hTweet.Status)

END

PRIVATE FUNCTION EncodeHTML(sStr AS String) AS String

  DIM iPos AS Integer
  DIM sRes, sCar AS String

  FOR iPos = 1 TO Len(sStr)
    sCar = Mid$(sStr, iPos, 1)
    SELECT sCar
      CASE "<"
        sCar = "&lt;"
      CASE ">"
        sCar = "&gt;"
      CASE "&"
        sCar = "&amp;"
      CASE "\""
        sCar = "&quot;"
      CASE " "
        sCar = "%20"
    END SELECT
    sRes = sRes & sCar
  NEXT

  RETURN sRes

END

PUBLIC SUB EnterTweet()

  hForm = NEW Form
  WITH hForm
    .X = FMain.X + 50
    .Y = FMain.Y + 70
    .BackColor = Color.TextBackground
    .Height = 36
    .Width = 500
    .Text = "Enter your tweet"
  END WITH
  hTextBox = NEW TextBox(hForm) AS "TweetBox"
  WITH hTextBox
    .X = 7
    .Y = 7
    .Height = 21
    .Width = 430
  END WITH
  hSendButton = NEW Button(hForm) AS "SendButton"
  WITH hSendButton
    .X = 445
    .Y = 7
    .Width = 49
    .Height = 21
    .Text = "Send"
  END WITH
  hForm.Show

END

PUBLIC SUB SendButton_Click()

  IF Len(hTextBox.Text) < 5 THEN
    Balloon(("Please type some text first!"), hTextBox)
  ELSE
    IF Main.bStandalone THEN
      PostTweet(hTextBox.Text)
    ELSE
      XMLClient.SendTweet(hTextBox.Text)
    END IF
  END IF

END
