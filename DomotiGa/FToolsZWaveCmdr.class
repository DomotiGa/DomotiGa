' Gambas class file

' Description:
' FToolsZWaveCmdr.class
' Test some basic Z-Wave functionality.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PUBLIC iSelectedNode AS Integer

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)
  WITH tbvNodes
    .Columns.Count = 11
    .Columns[0].Title = ("Node")
    .Columns[0].Width = 45
    .Columns[1].Title = ("Basic Type")
    .Columns[1].Width = 125
    .Columns[2].Title = ("Generic Type")
    .Columns[2].Width = 125
    .Columns[3].Title = ("Value")
    .Columns[3].Width = 50
    .Columns[4].Title = ("Value2")
    .Columns[4].Width = 50
    .Columns[5].Title = ("Value3")
    .Columns[5].Width = 50
    .Columns[6].Title = ("Value4")
    .Columns[6].Width = 50
    .Columns[7].Title = ("Status")
    .Columns[7].Width = 65
    .Columns[8].Title = ("Manufacturer")
    .Columns[8].Width = 95
    .Columns[9].Title = ("Product")
    .Columns[9].Width = 130
    .Columns[10].Title = ("Supported Classes")
  END WITH
  tbvNodes.Enabled = FALSE
  btnNodeOn.Enabled = FALSE
  btnNodeOff.Enabled = FALSE
  btnRequestBasicReport.Enabled = FALSE
  btnRequestBasicReportNode.Enabled = FALSE
  btnRequestMeterReportNode.Enabled = FALSE
  btnRequestSensorReportNode.Enabled = FALSE
  btnRequestNodeNeighborUpdate.Enabled = FALSE
  btnRemoveDevice.Enabled = FALSE
  btnSetConfigParam.Enabled = FALSE
  spInstance.Enabled = FALSE
  btnRequestNetworkUpdate.Enabled = FALSE ' hangs when run sometime
  slDim.Enabled = FALSE

  IF NOT Main.bServer THEN
    Message.Info(("Z-Wave Commander will only work in Server-Mode"))
    ME.Close
    RETURN
  ENDIF

  IF NOT Main.bZWaveEnabled OR IF NOT Main.hZWave THEN
    Message.Info(("Z-Wave interface is not enabled\nor couldn't open port!"))
    ME.Close
    RETURN
  ENDIF

  ' if Open ZWave wrapper is used, first query for manufacturer/model. This information isn't directly available after we initialize the serial port the first time. Thus we need to do it now
  IF Main.bZWaveUseOZW THEN
    Main.hZWave.OZW_GetManufacturerAndModel()
    Main.hZWave.OZW_GetLibraryVersion()
  ENDIF

  IF Main.hZWave.sHomeID = "" THEN
    txtHomeID.Text = "Unknown"
  ELSE
    txtHomeID.Text = Main.hZWave.sHomeID
  ENDIF

  txtControllerID.Text = Main.hZWave.iControllerID
  txtZWaveAPIVersion.Text = Main.hZWave.sZWaveAPIVersion
  txtZWaveZDKVersion.Text = Main.hZWave.sZWaveZDKVersion
  txtControllerType.text = Main.hZWave.sControllerType

  FillNodeTable()
  btnRequestBasicReport.Enabled = TRUE

END

PUBLIC SUB FillNodeTable()

  DIM rResult AS Result
  DIM iRows, iCount AS Integer

  rResult = Main.hDB.Exec("SELECT * FROM devices_zwave")
  IF NOT rResult THEN
    Message.Info(("Error: table 'devices_zwave' not found!"))
    RETURN
  END IF
  iRows = rResult.Count
  IF iRows > 0 THEN
    tbvNodes.Enabled = TRUE
    tbvNodes.Rows.Count = iRows
    FOR iCount = 0 TO rResult.Max
      tbvNodes[iCount, 0].Text = rResult!nodeid
      tbvNodes[iCount, 1].Text = Main.hZWave.ExpandbasicType(rResult!typebasic)
      tbvNodes[iCount, 2].Text = Main.hZWave.ExpandGenericType(rResult!typegeneric)
      tbvNodes[iCount, 3].Text = rResult!value
      tbvNodes[iCount, 4].Text = rResult!value2
      tbvNodes[iCount, 5].Text = rResult!value3
      tbvNodes[iCount, 6].Text = rResult!value4
      tbvNodes[iCount, 7].Text = IIf(rResult!sleeping, "Sleeping", "Listening")
      tbvNodes[iCount, 8].Text = rResult!manufacturer
      tbvNodes[iCount, 9].Text = rResult!model
      tbvNodes[iCount, 10].Text = rResult!classes
      'tbvNodes.Columns[10].Width = Len(rResult!classes) * 8
      rResult.MoveNext
    NEXT
  END IF

END

PUBLIC SUB tbvNodes_Click()

  EnableControls(tbvNodes.Row)

END

PUBLIC SUB tbvNodes_Change()

  EnableControls(tbvNodes.Row)

END

PUBLIC SUB EnableControls(iRow AS Integer)

  iSelectedNode = tbvNodes[iRow, 0].Text

  SELECT tbvNodes[iRow, 2].Text
    CASE "Multilevel Switch"
      TRY slDim.Value = Main.hZWave.GetCurrentValueNode(iSelectedNode)
      slDim.Enabled = TRUE
      btnNodeOff.Enabled = TRUE
      btnNodeOn.Enabled = TRUE
      btnRequestBasicReportNode.Enabled = TRUE
      btnRequestMeterReportNode.Enabled = TRUE
      btnRequestSensorReportNode.Enabled = TRUE
      btnRequestVersionReportNode.Enabled = TRUE
      btnRequestNodeNeighborUpdate.Enabled = TRUE
      btnRemoveDevice.Enabled = TRUE
      btnSetConfigParam.Enabled = TRUE
      spInstance.Enabled = TRUE
    CASE "Binary Switch"
      slDim.Value = 0
      slDim.Enabled = FALSE
      btnNodeOff.Enabled = TRUE
      btnNodeOn.Enabled = TRUE
      btnRequestBasicReportNode.Enabled = TRUE
      btnRequestMeterReportNode.Enabled = TRUE
      btnRequestSensorReportNode.Enabled = TRUE
      btnRequestVersionReportNode.Enabled = TRUE
      btnRequestNodeNeighborUpdate.Enabled = TRUE
      btnSetConfigParam.Enabled = TRUE
      btnRemoveDevice.Enabled = TRUE
      spInstance.Enabled = TRUE
    CASE "Sensor Binary"
      btnNodeOn.Enabled = FALSE
      btnNodeOff.Enabled = FALSE
      slDim.Value = 0
      slDim.Enabled = FALSE
      btnRequestBasicReportNode.Enabled = TRUE
      btnRequestMeterReportNode.Enabled = TRUE
      btnRequestSensorReportNode.Enabled = TRUE
      btnRequestVersionReportNode.Enabled = TRUE
      btnRequestNodeNeighborUpdate.Enabled = TRUE
      btnRemoveDevice.Enabled = TRUE
      btnSetConfigParam.Enabled = TRUE
      spInstance.Enabled = TRUE
    DEFAULT
      btnNodeOn.Enabled = FALSE
      btnNodeOff.Enabled = FALSE
      slDim.Value = 0
      slDim.Enabled = FALSE
      btnRequestBasicReportNode.Enabled = FALSE
      btnRequestMeterReportNode.Enabled = FALSE
      btnRequestSensorReportNode.Enabled = FALSE
      btnRequestVersionReportNode.Enabled = FALSE
      btnRequestNodeNeighborUpdate.Enabled = FALSE
      btnRemoveDevice.Enabled = FALSE
      btnSetConfigParam.Enabled = FALSE
      spInstance.Enabled = FALSE
  END SELECT

END

PUBLIC SUB btnClose_Click()

  ME.Close

END

PUBLIC SUB btnNodeOn_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "On command send to node " & iSelectedNode & ", instance " & spInstance.Value
    Main.hZWave.SetValue(iSelectedNode, spInstance.Value, &HFF)
  ENDIF

END

PUBLIC SUB btnNodeOff_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Off command send to node " & iSelectedNode & ", instance " & spInstance.Value
    Main.hZWave.SetValue(iSelectedNode, spInstance.Value, &H0)
  ENDIF

END

PUBLIC SUB slDim_MouseUp()

  IF slDim.Enabled AND iSelectedNode >= 1 THEN 
    txtMessage.Text = "Dim=" & slDim.Value & " request send to node " & iSelectedNode
    Main.hZWave.SetValue(iSelectedNode, spInstance.Value, slDim.Value)
  ENDIF 
  
END

PUBLIC SUB btnRequestBasicReport_Click()

  txtMessage.Text = "Requested basic report for all nodes"
  Main.hZWave.RequestBasicReport(255)

END

PUBLIC SUB btnRequestBasicReportNode_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requested basic report for node " & iSelectedNode
    Main.hZWave.RequestBasicReport(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnRequestCapabilities_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requested capabilities for node " & iSelectedNode
    Main.hZWave.RequestNodeCapabilities(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnRefresh_Click()

  FillNodeTable()

END

PUBLIC SUB btnRequestMeterReportNode_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requested meter report for node " & iSelectedNode
    Main.hZWave.RequestMeterReport(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnRequestSensorReportNode_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requested sensor report for node " & iSelectedNode
    Main.hZWave.RequestMultiLevelSensorReport(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnRequestVersionReportNode_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requested version report for node " & iSelectedNode
    Main.hZWave.RequestVersionReport(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnRequestNetworkUpdate_Click()

  Main.hZWave.RequestNetworkUpdate()

END

PUBLIC SUB btnAddDevice_Click()

  txtMessage.Text = "Press button on device to add!"
  Main.hZWave.AddDevice()

END

PUBLIC SUB btnRemoveDevice_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Removed node " & iSelectedNode
    Main.hZWave.RemoveDevice(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnRequestNodeNeighborUpdate_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requesting neighborhood update for node " & iSelectedNode
    Main.hZWave.RequestNodeNeighborUpdate(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnCancelControllerCommand_Click()

  txtMessage.Text = "Cancelled controller command"
  Main.hZWave.CancelControllerCommand()

END

PUBLIC SUB btnSetConfigParam_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Setting config parm " & vbConfigParam.Value & " for node " & iSelectedNode & " to " & vbConfigParam.Value
    Main.hZWave.SetConfigParam(iSelectedNode, vbConfigParam.Value, vbConfigValue.Value)
  ENDIF

END

PUBLIC SUB btnRequestAllConfigParams_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requested all config parms for node " & iSelectedNode
    Main.hZWave.RequestAllConfigParams(iSelectedNode)
  ENDIF

END

PUBLIC SUB btnAddAssociation_Click()

  IF iSelectedNode >= 1 AND vbAssociationNode.Value >= 1 THEN
    txtMessage.Text = "Adding association for node " & iSelectedNode & " - group " & vbAssociationGroup.Value & " and node " & vbAssociationNode.Value
    Main.hZWave.AddAssociation(iSelectedNode, vbAssociationGroup.Value, vbAssociationNode.Value)
  ENDIF

END

PUBLIC SUB btnRemoveAssociation_Click()

  IF iSelectedNode >= 1 AND vbAssociationNode.Value >= 1 THEN
    txtMessage.Text = "Removing association for node " & iSelectedNode & " - group " & vbAssociationGroup.Value & " and node " & vbAssociationNode.Value
    Main.hZWave.RemoveAssociation(iSelectedNode, vbAssociationGroup.Value, vbAssociationNode.Value)
  ENDIF

END

PUBLIC SUB btnRequestConfigParam_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requesting config parm " & vbConfigParam.Value & " for node " & iSelectedNode
    Main.hZWave.RequestConfigParam(iSelectedNode, vbConfigParam.Value)
  ENDIF

END

PUBLIC SUB btnRequestDynamic_Click()

  IF iSelectedNode >= 1 THEN
    txtMessage.Text = "Requesting dynamic update for node " & iSelectedNode
    Main.hZWave.RequestNodeDynamic(iSelectedNode)
  ENDIF

END
