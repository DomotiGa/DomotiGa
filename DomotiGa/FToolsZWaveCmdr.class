' Gambas class file

' Description:
' FToolsZWaveCmdr.class
' Test some basic Z-Wave functionality.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC iSelectedNode AS Integer
PRIVATE bDevelopment AS Boolean = TRUE

PUBLIC SUB Form_Open()

  IF bDevelopment = FALSE THEN FrameDevelopment.Hide

  ME.Move(FMain.X + 50, FMain.Y + 70)
  WITH tbvNodes
    .Columns.Count = 8
    .Columns[0].Title = ("Node")
    .Columns[0].Width = 45
    .Columns[1].Title = ("Basic Type")
    .Columns[1].Width = 125
    .Columns[2].Title = ("Generic Type")
    .Columns[2].Width = 125
    .Columns[3].Title = ("Value")
    .Columns[3].Width = 50
    .Columns[4].Title = ("Status")
    .Columns[4].Width = 65
    .Columns[5].Title = ("Manufacturer")
    .Columns[5].Width = 95
    .Columns[6].Title = ("Product")
    .Columns[6].Width = 130
    .Columns[7].Title = ("Supported Classes")
  END WITH
  tbvNodes.Enabled = FALSE
  btnNodeOn.Enabled = FALSE
  btnNodeOff.Enabled = FALSE
  btnRequestBasicReport.Enabled = FALSE
  btnRequestBasicReportNode.Enabled = FALSE
  btnRequestMeterReportNode.Enabled = FALSE
  btnRequestSensorReportNode.Enabled = FALSE
  btnRequestNodeNeighborUpdate.Enabled = FALSE
  'btnRequestAllConfigParams.Enabled = FALSE
  btnRemoveDevice.Enabled = FALSE
  btnSetConfigParam.Enabled = FALSE
  slDim.Enabled = FALSE
  IF NOT Main.bZWaveEnabled OR IF NOT Main.hZWave THEN
    Message.Info(("Z-Wave interface is not enabled\nor couldn't open port!"))
  ELSE
    txtHomeID.Text = Main.hZWave.sHomeID
    txtControllerID.Text = Main.hZWave.iControllerID
    TRY txtChipType.Text = Hex(Main.hZWave.sControllerChipType, 2) & " - " & Main.hZWave.ExpandChipType(Main.hZWave.sControllerChipType)
    txtChipRev.Text = Main.hZWave.sControllerChipRev
    txtZWaveAPIVersion.Text = Main.hZWave.sZWaveAPIVersion
    txtZWaveZDKVersion.Text = Main.hZWave.sZWaveZDKVersion
    txtControllerType.text = Main.hZWave.sControllerType

    FillNodeTable()
    btnRequestBasicReport.Enabled = TRUE
  END IF

END

PUBLIC SUB FillNodeTable()

  DIM rResult AS Result
  DIM iRows, iCount AS Integer

  rResult = Main.hDB.Exec("SELECT * FROM devices_zwave")
  IF NOT rResult THEN
    Message.Info(("Error: table 'devices_zwave' not found!"))
    RETURN
  END IF
  iRows = rResult.Count
  IF iRows > 0 THEN
    tbvNodes.Enabled = TRUE
    tbvNodes.Rows.Count = iRows
    FOR iCount = 0 TO rResult.Max
      tbvNodes[iCount, 0].Text = rResult!nodeid
      tbvNodes[iCount, 1].Text = Main.hZWave.ExpandbasicType(rResult!typebasic)
      tbvNodes[iCount, 2].Text = Main.hZWave.ExpandGenericType(rResult!typegeneric)
      tbvNodes[iCount, 3].Text = rResult!value
      tbvNodes[iCount, 4].Text = IIf(rResult!sleeping, "Sleeping", "Listening")
      tbvNodes[iCount, 5].Text = rResult!manufacturer
      tbvNodes[iCount, 6].Text = rResult!model
      tbvNodes[iCount, 7].Text = rResult!classes
      tbvNodes.Columns[7].Width = Len(rResult!classes) * 8
      rResult.MoveNext
    NEXT
  END IF

END

PUBLIC SUB tbvNodes_Click()

  EnableControls(tbvNodes.Row)

END

PUBLIC SUB EnableControls(iRow AS Integer)

  iSelectedNode = tbvNodes[iRow, 0].Text

  SELECT tbvNodes[iRow, 2].Text
    CASE "Multilevel Switch"
      TRY slDim.Value = Main.hZWave.GetCurrentValueNode(iSelectedNode)
      slDim.Enabled = TRUE
      btnNodeOff.Enabled = TRUE
      btnNodeOn.Enabled = TRUE
      btnRequestBasicReportNode.Enabled = TRUE
      btnRequestMeterReportNode.Enabled = TRUE
      btnRequestSensorReportNode.Enabled = TRUE
      btnRequestVersionReportNode.Enabled = TRUE
      btnRequestNodeNeighborUpdate.Enabled = TRUE
      btnRemoveDevice.Enabled = TRUE
      btnSetConfigParam.Enabled = TRUE
    CASE "Binary Switch"
      slDim.Value = 0
      slDim.Enabled = FALSE
      btnNodeOff.Enabled = TRUE
      btnNodeOn.Enabled = TRUE
      btnRequestBasicReportNode.Enabled = TRUE
      btnRequestMeterReportNode.Enabled = TRUE
      btnRequestSensorReportNode.Enabled = TRUE
      btnRequestVersionReportNode.Enabled = TRUE
      btnRequestNodeNeighborUpdate.Enabled = TRUE
      'btnRequestAllConfigParams.Enabled = TRUE
      btnRemoveDevice.Enabled = TRUE
      btnSetConfigParam.Enabled = TRUE
    CASE "Sensor Binary"
      btnRequestBasicReportNode.Enabled = TRUE
      btnRequestMeterReportNode.Enabled = TRUE
      btnRequestSensorReportNode.Enabled = TRUE
      btnRequestVersionReportNode.Enabled = TRUE
      btnRequestNodeNeighborUpdate.Enabled = TRUE
      'btnRequestAllConfigParams.Enabled = TRUE
      btnRemoveDevice.Enabled = TRUE
      btnSetConfigParam.Enabled = TRUE
    DEFAULT
      btnNodeOn.Enabled = FALSE
      btnNodeOff.Enabled = FALSE
      slDim.Value = 0
      slDim.Enabled = FALSE
      btnRequestBasicReportNode.Enabled = FALSE
      btnRequestMeterReportNode.Enabled = FALSE
      btnRequestSensorReportNode.Enabled = FALSE
      btnRequestVersionReportNode.Enabled = FALSE
      btnRequestNodeNeighborUpdate.Enabled = FALSE
      'btnRequestAllConfigParams.Enabled = FALSE
      btnRemoveDevice.Enabled = FALSE
      btnSetConfigParam.Enabled = FALSE
  END SELECT

END

PUBLIC SUB btnClose_Click()

  ME.Close

END

PUBLIC SUB btnNodeOn_Click()

  Main.hZWave.ChangeSwitch(iSelectedNode, &HFF)
  Main.hZWave.RequestBasicReport(iSelectedNode)

END

PUBLIC SUB btnNodeOff_Click()

  Main.hZWave.ChangeSwitch(iSelectedNode, &H0)
  Main.hZWave.RequestBasicReport(iSelectedNode)

END

PUBLIC SUB slDim_MouseUp()

  IF Main.bZWaveEnabled AND IF Main.hZWave THEN
    Main.hZWave.ChangeSwitchLevel(iSelectedNode, slDim.Value)
    Main.hZWave.RequestBasicReport(iSelectedNode)
  END IF

END

PUBLIC SUB btnRequestBasicReport_Click()

  Main.hZWave.RequestBasicReport(255)

END

PUBLIC SUB btnRequestBasicReportNode_Click()

  Main.hZWave.RequestBasicReport(iSelectedNode)

END

PUBLIC SUB btnRequestCapabilities_Click()

  Main.hZWave.RequestNodeCapabilities(iSelectedNode)

END

PUBLIC SUB btnRefresh_Click()

  FillNodeTable()

END

PUBLIC SUB btnRequestMeterReportNode_Click()

  Main.hZWave.RequestMeterReport(iSelectedNode)

END

PUBLIC SUB btnRequestSensorReportNode_Click()

  Main.hZWave.RequestMultiLevelSensorReport(iSelectedNode)

END

PUBLIC SUB btnRequestVersionReportNode_Click()

  Main.hZWave.RequestVersionReport(iSelectedNode)

END

PUBLIC SUB btnRequestNetworkUpdate_Click()

  Main.hZWave.RequestNetworkUpdate()

END

PUBLIC SUB btnAddDevice_Click()

  Main.hZWave.AddDevice

END

PUBLIC SUB btnRemoveDevice_Click()

  Main.hZWave.RemoveDevice(iSelectedNode)

END

PUBLIC SUB btnRequestNodeNeighborUpdate_Click()

  Main.hZWave.RequestNodeNeighborUpdate(iSelectedNode)

END

PUBLIC SUB btnCancelControllerCommand_Click()

  Main.hZWave.CancelControllerCommand()

END

PUBLIC SUB btnSetConfigParam_Click()

  Main.hZWave.SetConfigParam(iSelectedNode, vbConfigParam.Value, vbConfigValue.Value)

END

PUBLIC SUB btnRequestAllConfigParams_Click()

  Main.hZWave.RequestAllConfigParams(iSelectedNode)

END
