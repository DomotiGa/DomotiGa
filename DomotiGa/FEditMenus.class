' Gambas class file

' Description:
' FMenuEditor.class
' Edit program menu's.

' Development Status:
' Just build, possibly bugs ahead.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hToolButton AS ToolButton
PUBLIC iId AS Integer

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)
  FillCombo()
  RefreshMenu()

END

PUBLIC SUB RefreshMenu()

  DIM rResult, rResultMenu AS Result
  DIM iCount AS Integer

  VBoxAvailMenu1.Height = 14
  VBoxAvailMenu2.Height = 14
  VBoxAvailMenu3.Height = 14

  ' fill combo with available menu names
  rResultMenu = Main.hDB.Exec("SELECT * FROM menu_names")
  IF rResultMenu THEN
    IF (rResultMenu.Count >= 1) THEN
      FOR EACH rResultMenu
        rResult = Main.hDB.Exec("SELECT * FROM menu WHERE menu = &1 ORDER BY position", rResultMenu!id)
        IF NOT rResult THEN
          Message.Info("Error: table 'menu' not found!")
          RETURN
        END IF
        FOR iCount = 0 TO rResult.Max
          IF rResult!menu = 1 THEN
            hToolButton = NEW ToolButton(VBoxAvailMenu1) AS "AvailToolButton"
            VBoxAvailMenu1.Height = VBoxAvailMenu1.Height + 42
          ELSE IF rResult!menu = 2 THEN
            hToolButton = NEW ToolButton(VBoxAvailMenu2) AS "AvailToolButton"
            VBoxAvailMenu2.Height = VBoxAvailMenu2.Height + 42
          ELSE
            hToolButton = NEW ToolButton(VBoxAvailMenu3) AS "AvailToolButton"
            VBoxAvailMenu3.Height = VBoxAvailMenu3.Height + 42
          END IF
          WITH hToolButton
            .Name = rResult!id
            IF Main.sLanguage = "nl_NL.UTF-8" THEN
              .Text = rResult!name_nl
            ELSE IF Main.sLanguage = "fr_FR.UTF-8" THEN
              .Text = rResult!name_fr
            ELSE
              .Text = rResult!name_en
            END IF
            .Background = Color.TextBackground
            IF NOT rResult!enabled THEN .Foreground = Color.LightGray
            .Font = Font["-1"]
            .Height = 42
            .Width = 111
            .Picture = Picture["images" &/ rResult!icon]
            .Tag = rResult!id
          END WITH
          rResult.MoveNext
        NEXT
      NEXT
      ' ToolPanelAvail.Count = rResult.Count
    ELSE
      Message("No menu names found!")
    END IF
  ELSE
    Message.Info("Error: table 'menu_names' not found!")
  END IF

END

PUBLIC SUB FillCombo()

  DIM rResultMenu AS Result

  rResultMenu = Main.hDB.Exec("SELECT * FROM menu_names")
  IF rResultMenu THEN
    IF (rResultMenu.Count >= 1) THEN
      FOR EACH rResultMenu
        cmbMenus.Add(rResultMenu!name)
      NEXT
    END IF
  END IF

END

PUBLIC SUB AvailToolbutton_Click()

  DIM rResult, rResultMenu AS Result

  iId = LAST.tag
  ' get values
  rResult = Main.hDB.Exec("SELECT * FROM menu WHERE id = &1", iId)
  IF rResult.Count >= 1 THEN
    txtName.Text = rResult!name_en
    txtNameFR.Text = rResult!name_fr
    txtNameNL.Text = rResult!name_nl
    TRY rResultMenu = Main.hDB.Exec("SELECT name FROM menu_names WHERE id = &1", rResult!menu)
    IF NOT ERROR THEN cmbMenus.Text = rResultMenu!name
    txtPosition.Text = rResult!position
    cmbVisible.Text = Main.DisplayBool(rResult!enabled)
  END IF

END

PUBLIC SUB AvailToolbutton_DblClick()

  DIM rResult AS Result

  iId = LAST.tag
  ' get values
  rResult = Main.hDB.Exec("SELECT * FROM menu WHERE id = &1", iId)
  IF rResult.Count >= 1 THEN
    FMain.OpenMenu(rResult!item)
  END IF

END

PUBLIC SUB SaveMenuItem()

  DIM rResultUpdate AS Result

  ' save new menu settings
  rResultUpdate = Main.hDB.Exec("UPDATE menu SET menu = &1, position = &2, enabled = &3, name_en = &4, name_fr = &5, name_nl = &6 WHERE id = &7", GetMenuId(cmbMenus.Text), txtPosition.Text, GetVisible(), txtName.Text, txtNameFR.Text, txtNameNL.Text, iId)

END

PUBLIC SUB GetMenuId(sMenu AS String) AS Integer

  DIM rResultMenu AS Result

  TRY rResultMenu = Main.hDB.Exec("SELECT id FROM menu_names WHERE name = &1", sMenu)
  IF NOT ERROR THEN RETURN rResultMenu!id

END

PUBLIC SUB GetVisible() AS Boolean

  IF cmbVisible.Text = "True" THEN
    RETURN TRUE
  ELSE
    RETURN FALSE
  END IF

END

PUBLIC SUB btnSave_Click()

  DIM oObject AS Object

  SaveMenuItem()

  ' delete previous menu items first
  FOR EACH oObject IN VBoxAvailMenu1.Children
    oObject.Delete()
  NEXT
  FOR EACH oObject IN VBoxAvailMenu2.Children
    oObject.Delete()
  NEXT
  FOR EACH oObject IN VBoxAvailMenu3.Children
    oObject.Delete()
  NEXT
  RefreshMenu()

  ' delete previous menu items first
  FOR EACH oObject IN FMain.VBoxMenu1.Children
    oObject.Delete()
  NEXT
  FOR EACH oObject IN FMain.VBoxMenu2.Children
    oObject.Delete()
  NEXT
  FOR EACH oObject IN FMain.VBoxMenu3.Children
    oObject.Delete()
  NEXT
  FMain.RefreshMenu()

END

PUBLIC SUB btnClose_Click()

  ME.Close

END

PUBLIC SUB cmbMenus_Change()

  btnSave.Enabled = TRUE

END

PUBLIC SUB txtPosition_Change()

  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbVisible_Change()

  btnSave.Enabled = TRUE

END