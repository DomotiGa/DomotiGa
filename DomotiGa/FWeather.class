' Gambas class file

' Description:
' FWeather.class
' Placeholder for weather forecasts and weathermaps.

' Development Status:
' Not started yet, things to build are:
' Display of weather forecast, weather alarms, moon phase (image), earthquakes etc.
' RSS Feeds to parse for this info can be found on www.knmi.nl
' Inform user if items contains keyword(s) from favorites.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hFeed AS HttpClient

PUBLIC SUB Form_Open()

  PopulateButtons()

END

PUBLIC SUB PopulateButtons()

  DIM rFeed AS Result
  DIM hButton AS Button

  ' populate feeds toolbutton
  TRY rFeed = Main.hDB.Exec("SELECT * FROM weatherfeeds WHERE enabled is TRUE")
  IF rFeed.Count THEN
    FOR EACH rFeed
    IF NOT rFeed!name OR rFeed!enabled = FALSE THEN CONTINUE
    hButton = NEW Button(vbFeeds) AS "FeedButton"
     WITH hButton
      .Text = rFeed!name
      .Height = 28
      .Width = 84
      .Tag = rFeed!id
    END WITH
    NEXT
  END IF

END

PUBLIC SUB FeedButton_Click()

  DIM iFeed AS Integer = LAST.tag
  DIM oObject AS Object

  ' enable all buttons, then disable selected one
  FOR EACH oObject IN vbFeeds.Children
    oObject.Enabled = TRUE
    IF oObject.Tag = iFeed THEN oObject.Enabled = FALSE
  NEXT

  DisplayFeed(iFeed)

END

PUBLIC SUB DisplayFeed(iFeed AS Integer)

  DIM rFeed AS Result
  hFeed = NEW HttpClient AS "hfeed"

  TRY rFeed = Main.hDB.Exec("SELECT * FROM weatherfeeds where id=" & iFeed)
  IF rFeed THEN
    hFeed.URL = rFeed!url
    hFeed.UserAgent = "Firefox"
    hFeed.Get
  END IF

END

PUBLIC SUB hFeed_Finished()

  DIM sBuffer AS String
  DIM sTempFile AS String = Temp()

  IF Lof(hFeed) THEN READ #hFeed, sBuffer, Lof(hFeed)
  File.Save(sTempFile, sBuffer)
  mboxWeather.Enabled = TRUE
  mboxWeather.Visible = TRUE
  mboxWeather.Path = sTempFile
  mboxWeather.Playing = TRUE
  IF Exist(sTempFile) THEN KILL sTempFile

END
