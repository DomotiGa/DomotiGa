' Gambas module file

' Description:
' Bwired.module
' Support for Bwired related functions.

' Development Status:
' Just build, so possible bugs around.

' Links:
' http://www.bwired.nl/domoticaworld.asp

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hPost AS NEW HttpClient AS "hPost"
PUBLIC tBwired AS Timer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Run()

  ' start poll timer for Bwired
  tBwired = NEW Timer AS "tBwired"
  tBwired.Delay = Main.iBwiredMapPushTime * 1000 * 60 ' multiply for minutes
  tBwired.Start

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tBwired_Timer()

  UploadBwiredData()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create xml data and upload it to the service
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB UploadBwiredData()

  DIM sContent AS String

  ' if we are already uploading return
  IF hPost.Status > 0 THEN
    Main.WriteLog(("I'm already uploading XML data to Bwired, skipping."))
    RETURN
  END IF

  ' create xml string
  sContent = CreateBwiredData()

  ' use httpclient to post xml to service
  hPost.URL = "http://www.bwired.nl/Bwiredservice/receive.asp"
  hPost.TimeOut = 10
  hPost.Async = TRUE
  hPost.Post("text/xml", sContent)

  IF Main.bBwiredMapDebug THEN Main.WriteDebugLog("[Bwired] " & sContent)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' scan bwired devices table and create xml document
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION CreateBwiredData() AS String

  DIM xXml AS XmlWriter
  DIM sXml, sValue, sLabel AS String
  DIM rResult AS Result
  DIM iId AS Integer

  ' scan device table
  rResult = Main.hDB.Exec("SELECT * FROM devices_bwired")
  IF NOT rResult THEN
    Main.WriteLog(("Error: table 'devices_bwired' not found!"))
    RETURN
  END IF

  ' build header
  xXml = NEW XmlWriter
  xXml.Open("", TRUE, "UTF-8")
  xXml.StartElement("BWired")
  xXml.StartElement("Init")
    xXml.Element("DateTime", Now())
    xXml.Element("UserName", Main.sBwiredMapUser)
    xXml.Element("Password", Main.sBwiredMapPassword)
    xXml.Element("ScreenName", Main.sBwiredMapScreenName)
    xXml.Element("Gpslat", Main.sBwiredMapGpsLat)
    xXml.Element("Gpslong", Main.sBwiredMapGpsLong)
    xXml.Element("City", Main.sBwiredMapCity)
    xXml.Element("Website", Main.sBwiredMapWebsite)
    xXml.Element("WebCamPicUrl", Main.sBwiredMapWebsitePicUrl)
    xXml.Element("Title", Main.sBwiredMapTitle)
  xXml.EndElement()

  ' create device entries
  IF rResult.Count THEN
    FOR EACH rResult
      xXml.StartElement("Entry")
      xXml.Element("Name", rResult!description)
      xXml.Element("ID", iId)
      xXml.Element("Units", rResult!devicelabel)
      SELECT rResult!value
        CASE "Value"
          sValue = Devices.GetCurrentValueForDevice(rResult!deviceid)
        CASE "Value2"
          sValue = Devices.GetCurrentValue2ForDevice(rResult!deviceid)
        CASE "Value3"
          sValue = Devices.GetCurrentValue3ForDevice(rResult!deviceid)
        CASE "Value4"
          sValue = Devices.GetCurrentValue3ForDevice(rResult!deviceid)
      END SELECT
      xXml.Element("Value", sValue)
      xXml.EndElement()
      INC iId
    NEXT
  END IF

  ' close document
  xXml.EndElement()
  RETURN xXml.EndDocument()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' catch error
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB hPost_Error()

  Main.WriteDebugLog(("[Bwired] XML data post error."))

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' check http return code
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB hPost_Finished()

  DIM iCount AS Integer

  ' select on http result code and display message
  SELECT hPost.Code
    ' do a little magic as microsoft webserver's are strange
    CASE 0
      IF InStr(hPost.Headers[0], "HTTP/1.1 200") THEN
        Main.WriteLog(("Uploaded XML data to Bwired."))
      ELSE
        Main.WriteLog(("Unknown error occured while uploading XML data to Bwired!"))
      END IF
    CASE 401
      Main.WriteLog(("Error authenticating while uploading XML data to Bwired!"))
    CASE 404
      Main.WriteLog(("Error page not found while uploading XML data to Bwired!"))
    CASE ELSE
      Main.WriteLog(("Unknown error occured while uploading XML data to Bwired!"))
  END SELECT

  ' if debug is on print all http headers
  IF NOT Main.bBwiredMapDebug THEN RETURN
  FOR iCount = 0 TO hPost.Headers.Count - 1
    Main.WriteDebugLog("[Bwired] " & Left(hPost.Headers[iCount], Len(hPost.Headers[iCount]) - 1))
  NEXT

END
