' Gambas class file

' Description:
' COpenTherm.class
' Support for OpenTherm gateway interface

' Development Status: Work in progress

' Links:
' http://www.tclcode.com/opentherm/index.html#intro

' Supported device addresses:
'
' Thermostat      : value1 = Room Temperature set point (°C)      - OpenTherm MsgID=16
'                   value2 = Room Temperature sensor (°C)         - OpenTherm MsgID=24
'                   value3 = Room set point status                - n/a
'                   value4 = Status OpenTherm master:
'                            - "Idle"                             - OpenTherm MsgID=0, Lowerbyte: 0000 0000
'                            - "Heating up room"                  - OpenTherm MsgID=0, Lowerbyte: 0000 0010
'                            - "Heating up boiler"                - OpenTherm MsgID=0, Lowerbyte: 0000 0100
'                            - "Cooling down room"                - OpenTherm MsgID=0, Lowerbyte: 0001 0000
'                            - "Error"                            - OpenTherm MsgID=0, Lowerbyte: 0000 0001
'
' CentralHeating  : value1 = CH set point (°C)                    - OpenTherm MsgID=57
'                   value2 = CH temperature (°C)                  - OpenTherm MsgID=1
'                   value3 = Modulation level (%)                 - OpenTherm MsgID=17
'                   value4 = Water pressure (Bar)                 - OpenTherm MsgID=18
'
'
' Boiler          : value1 = Boiler set point (°C)                - OpenTherm MsgID=56
'                   value2 = Boiler water temperature (°C)        - OpenTherm MsgID=25
'                   value3 = Boiler water flow (l/min)            - OpenTherm MsgID=19  [Only when polling is disabled.]
'                   value4
'
' Burner          : value1 = Flame status  (On/Off)               - OpenTherm MsgID=0, Lowerbyte: 0000 1000
'                   value2 = Burner starts (counter)              - OpenTherm MsgID=116
'                   value3 = CH Burner operation (hours)          - OpenTherm MsgID=120
'                   value4 = DHW Burner operation (hours)         - OpenTherm MsgID=123
'
' Outside         : value1 = Outside Temperature Sensor (°C)      - OpenTherm MsgID=27
'                   value2
'                   value3
'                   value4

' Credits: BreFra (Frank van Breugel)

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PROPERTY Type AS String
PROPERTY TCPHost AS String
PROPERTY TCPPort AS String
PROPERTY SerPort AS String
PROPERTY OpenThermDebug AS Boolean
PROPERTY PollTime AS Integer
PROPERTY Thermostat AS String
PROPERTY TemperatureOverride AS String
PROPERTY OutsideSensor AS Boolean
PROPERTY SyncClock AS Boolean
PROPERTY RelayEnabled AS Boolean
PROPERTY RelayPort AS Integer

PRIVATE sType AS String
PRIVATE sTCPHost AS String
PRIVATE sTCPPort AS String
PRIVATE sSerPort AS String
PRIVATE bOpenThermDebug AS Boolean
PRIVATE iPollTime AS Integer
PRIVATE sThermostat AS String
PRIVATE sTemperatureOverride AS String
PRIVATE bOutsideSensor AS Boolean
PRIVATE bSyncClock AS Boolean
PRIVATE bRelayEnabled AS Boolean
PRIVATE iRelayPort AS Integer

PUBLIC tOpenTherm AS Timer
PUBLIC hOpenThermSer AS NEW SerialPort
PUBLIC hOpenThermTCP AS NEW Socket
PUBLIC hOpenThermRelay AS NEW ServerSocket
PUBLIC hRelayOTClient AS Object[]
PUBLIC hRelayOTSocket AS NEW Socket


PRIVATE sBuffer AS String
PRIVATE sBufferSocket AS String
PRIVATE sTemperatureOverrideStatus AS String = ""

' Device ID'S
PRIVATE iThermostatID AS Integer            'DeviceID of Thermostat device
PRIVATE iCHID AS Integer                    'DeviceID of Central Heating device
PRIVATE iBoilerID AS Integer                'DeviceID of Boiler device
PRIVATE iBurnerID AS Integer                'DeviceID of Burner device
PRIVATE iOutsideID AS Integer               'DeviceID of Outside temperature device

' local status variables
PRIVATE fRoomTemp AS Float = 0              'Room temperature
PRIVATE fRoomSetPoint AS Float = 0          'Room temperature set point
PRIVATE fOutsideTemp AS Float = 0           'Outside temperature
PRIVATE bFault AS Boolean = FALSE           'Fault indicator
PRIVATE bCH AS Boolean = FALSE              'Central Heating state (true = active)
PRIVATE bDHW AS Boolean = FALSE             'Domestic hot water state (true = active)
PRIVATE bCooling AS Boolean = FALSE         'Cooling state
PRIVATE bFlame AS Boolean = FALSE           'Flame status
PRIVATE fCHModLevel AS Float = 0            'Relative modulation level
PRIVATE fCHWaterPress AS Float = 0          'CH Water pressure
PRIVATE fBoilerTemp AS Float = 0            'Boler water temperature
PRIVATE fBoilerSetPoint AS Float = 0        'Boiler temperature set point
PRIVATE fBurnerStarts AS Float = 0          'Burner starts counter
PRIVATE fCHSetPoint AS Float = 0            'Central Heating temperature set point
PRIVATE fCHBurnerHours AS Float = 0         'Burning hours central heating
PRIVATE fHWBurnerHours AS Float = 0         'Burning hours boiler
PRIVATE fBoilerFlow AS Float = 0            'Hot water flow

PRIVATE bApplicationError AS Boolean        'indicates error state OpenTherm application
PRIVATE bIgnoreAbout AS Boolean             'indicates if about message is expected
PRIVATE bOverrideActive AS Boolean          'indicates if setpoint override is active
PRIVATE bOverrideSend AS Boolean            'indicates if override setpoint is send to gateway
PRIVATE fCurrentSetPoint AS Float           'setpoint received from gateway
PRIVATE fOverrideSetPoint AS Float          'setpoint to override existing thermostat setpoint
PRIVATE fOriginalSetPoint AS Float          'setpoint of thermostat before override
PRIVATE fOverrideBoilerSetPoint AS Float    'boiler setpoint
PRIVATE fCentralHeatingSetPoint AS Float    'central heating setpoint
PRIVATE bClockSyncedToday AS Boolean        'indicates if clock is already synced
PRIVATE iDayOfMonth AS Integer              '

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION ConnectSerial() AS Boolean

  ' try to close the port
  TRY hOpenThermSer.Close

  ' get a new one
  hOpenThermSer = NEW Serialport AS "OpenThermSer"

  WITH hOpenThermSer
    .PortName = sSerPort
    .Speed = 9600
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH

  bIgnoreAbout = TRUE
  TRY PRINT #hOpenThermSer, "PR=A\r" 'Get about information of opentherm gateway 
    IF ERROR THEN
      Main.WriteDebugLog(("[OpenTherm] Error writing data to serial port! -> ") & Error.Text)
      RETURN FALSE
    ELSE
      GetDeviceIDs()
      WAIT 1 ' wait to let the gateway respond to about request
      IF bIgnoreAbout THEN
        hOpenThermSer.Close
        Main.WriteDebugLog(("[OpenTherm] Gateway is not responding") & Error.Text)
        RETURN FALSE
      ELSE
        ' all ok
        RETURN TRUE
      ENDIF
    ENDIF

CATCH ' some errors
  Main.WriteLog(("OpenTherm Error: ") & ERROR.Text)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Receive data from serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OpenThermSer_Read()

  DIM sData AS String

  TRY READ #hOpenThermSer, sData, 1
  IF ERROR THEN Main.WriteDebugLog(("[OpenTherm] Error reading data from serial port! -> ") & Error.Text)
  IF sData = Chr(10) THEN ' buffer until linefeed then parse
    IF Len(sBuffer) > 1 THEN ParseLine(Left(sBuffer, Len(sBuffer) - 1))
    sBuffer = NULL
  ELSE
    sBuffer &= sData
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open TCP port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION ConnectTCP() AS Boolean

  ' try to close the connection
  TRY hOpenThermTCP.Close
  bIgnoreAbout = TRUE

  ' get a new one
  hOpenThermTCP = NEW Socket AS "OpenThermTCP"
  hOpenThermTCP.Connect(sTCPHost, sTCPPort)
  IF NOT ERROR THEN
    WAIT 1 ' wait to let the gateway responding to about request
    IF bIgnoreAbout THEN
      hOpenThermTCP.Close
      Main.WriteDebugLog(("[OpenTherm] Gateway is not responding."))
      RETURN FALSE
    ELSE
      ' all ok
      RETURN TRUE
    ENDIF
  ELSE
    RETURN FALSE
  ENDIF

CATCH ' some errors
  Main.WriteLog(("OpenTherm Error: ") & ERROR.Text & " at " & ERROR.Where)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is connected
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OpenThermTCP_Ready()

  GetDeviceIDs()
  Main.WriteLog(("OpenTherm TCP socket connected."))
  PRINT #hOpenThermTCP, "PR=A" 'Get about information of opentherm gateway

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is closed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OpenThermTCP_Closed()

  Main.WriteLog(("OpenTherm TCP socket closed."))

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' error while connected/connecting to host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OpenThermTCP_Error()

  ' handle error
  SELECT CASE hOpenThermTCP.Status
    CASE Net.CannotCreateSocket
      Main.WriteLog(("OpenTherm: The system does not allow to create a socket."))
    CASE Net.HostNotFound
      Main.WriteLog(("OpenTherm: Host '") & sTCPHost & ("' not found."))
    CASE Net.ConnectionRefused
      Main.WriteLog(("OpenTherm: Unable to connect to gateway. Connection refused."))
    CASE Net.CannotRead
      Main.WriteLog(("OpenTherm: Error reading data."))
    CASE Net.CannotWrite
      Main.WriteLog(("OpenTherm: Error writing data."))
  END SELECT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Receive data from network socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB OpenThermTCP_Read()

  DIM sData AS String

  TRY READ #hOpenThermTCP, sData, 1
  IF ERROR THEN Main.WriteDebugLog(("[OpenTherm] Error reading data from TCP socket! -> ") & Error.Text)
  IF sData = Chr(10) THEN ' buffer until linefeed then parse
    IF Len(sBuffer) > 1 THEN ParseLine(Left(sBuffer, Len(sBuffer) - 1))
    sBuffer = NULL
  ELSE
    sBuffer &= sData
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Start relay port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION StartRelay() AS Boolean

  hOpenThermRelay = NEW ServerSocket AS "OpenThermRelay"
  hOpenThermRelay.Type = Net.Internet
  hOpenThermRelay.Port = iRelayPort
  ' we start listening for max 1 connection
  hOpenThermRelay.Listen(1)

  IF hOpenThermRelay.Status = Net.Active THEN
    hRelayOTclient = NEW Object[]
    ' all ok
    RETURN TRUE
  ELSE
    RETURN FALSE
  END IF

END

PUBLIC SUB OpenThermRelay_Connection(sHost AS String)

  hRelayOTSocket = NEW Socket AS "RelayOTSocket"
  ' accept client
  IF hOpenThermRelay.Status <= Net.Inactive THEN RETURN
  IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm Relay] Connection request from client ") & sHost)

  hRelayOTSocket = hOpenThermRelay.Accept()
  hRelayOTclient.Add(hRelayOTSocket)
  IF Main.bOpenThermDebug THEN
    IF hRelayOTSocket.Status = Net.Connected THEN Main.WriteLog(("OpenTherm gateway relay client connection from ip address ") & hRelayOTSocket.RemoteHost & " (" & DNS.ResolveHost(hRelayOTSocket.RemoteHost) & ") accepted.")
  ENDIF

END

PUBLIC SUB OpenThermRelay_Error()

  ' handle error
  SELECT CASE hOpenThermRelay.Status
    CASE Net.CannotCreateSocket
      Main.WriteDebugLog(("[OpenTherm Relay] The system does not allow to create a socket."))
    CASE Net.CannotBindSocket
      Main.WriteDebugLog(("[OpenTherm Relay] Cannot bind socket."))
    CASE Net.CannotListen
      Main.WriteDebugLog(("[OpenTherm Relay] Cannot listen on port."))
  END SELECT

END

PUBLIC SUB OpenThermRelay_Closed()

  Main.WriteLog(("OpenTherm TCP relay socket closed."))

END

PUBLIC SUB Socket_Read()
  
  DIM sData AS String
  IF LAST.Status <> Net.Connected THEN RETURN 
  TRY READ #LAST, sData, 1
  
  IF sData = Chr(10) THEN ' buffer until linefeed then parse
    IF Len(sBufferSocket) > 1 THEN
      sBufferSocket = Left(sBufferSocket, Len(sBufferSocket) - 1)
      Main.WriteDebugLog(("[OpenTherm Relay] Received command from relay client: '") & sBufferSocket & "'")
      WriteCmd(sBufferSocket)
    ENDIF 
    sBufferSocket = NULL
  ELSE
    sBufferSocket &= sData
  ENDIF

END

PUBLIC SUB Socket_Closed()
  
  Main.WriteLog(("OpenTherm gateway relay client connection closed."))
  hRelayOTclient.Remove(hRelayOTclient.Find(LAST))
  
END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Get device id's, and store them as local variables. This will speedup the code
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB GetDeviceIDs()

  iThermostatID = Devices.Find("Thermostat", Devices.FindInterface("OpenTherm Gateway"), "OpenTherm Thermostat")
  IF iThermostatID = 0 THEN
    IF Main.bOpenThermDebug THEN
      Main.WriteDebugLog(("[OpenTherm] Thermostat device is missing and not automatically created. Thermostat updates will be ignored!"))
      Main.WriteDebugLog(("[OpenTherm] Check if the device exists and is not disabled or hidden"))
    ENDIF
  ENDIF
  iCHID = Devices.Find("CentralHeating", Devices.FindInterface("OpenTherm Gateway"), "OpenTherm Central Heating")
  IF iCHID = 0 THEN
    IF Main.bOpenThermDebug THEN
      Main.WriteDebugLog(("[OpenTherm] Central heating device is missing and not automatically created. Central heating updates will be ignored!"))
      Main.WriteDebugLog(("[OpenTherm] Check if the device exists and is not disabled or hidden"))
    ENDIF
  ENDIF
  iBoilerID = Devices.Find("Boiler", Devices.FindInterface("OpenTherm Gateway"), "OpenTherm Boiler")
  IF iBoilerID = 0 THEN
    IF Main.bOpenThermDebug THEN
      Main.WriteDebugLog(("[OpenTherm] Boiler device is missing and not automatically created. Boiler updates will be ignored!"))
      Main.WriteDebugLog(("[OpenTherm] Check if the device exists and is not disabled or hidden"))
    ENDIF
  ENDIF
  iBurnerID = Devices.Find("Burner", Devices.FindInterface("OpenTherm Gateway"), "OpenTherm Burner")
  IF iBurnerID = 0 THEN
    IF Main.bOpenThermDebug THEN
      Main.WriteDebugLog(("[OpenTherm] Burner device is missing and not automatically created. Burner updates will be ignored!"))
      Main.WriteDebugLog(("[OpenTherm] Check if the device exists and is not disabled or hidden"))
    ENDIF
  ENDIF
  iOutsideID = Devices.Find("Outside", Devices.FindInterface("OpenTherm Gateway"), "OpenTherm Outside sensor")
  IF iOutsideID = 0 THEN
    IF Main.bOpenThermDebug THEN
      Main.WriteDebugLog(("[OpenTherm] Outside sensor device is missing and not automatically created. Outside sensor updates will be ignored!"))
      Main.WriteDebugLog(("[OpenTherm] Check if the device exists and is not disabled or hidden"))
    ENDIF
  ENDIF

END
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Start polling
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Run()

  IF iPollTime > 0 THEN
    ' start poll timer
    tOpenTherm = NEW Timer AS "tOpenTherm"
    tOpenTherm.Delay = iPollTime * 1000 ' multiply for seconds
    tOpenTherm.Start
  ELSE
    'Don't turn on polling, just process all messages received via OpenTherm gateway connection
    WriteCmd("PS=0")
  ENDIF

END

PUBLIC SUB tOpenTherm_Timer()

  GetStatus()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' request latest status report of OpenTherm gateway
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB GetStatus()

  DIM rResult AS Result
  DIM sDay AS String

  ' request new status report
  WriteCmd("PS=1")
  IF bSyncClock THEN
    IF iDayOfMonth <> Day(Now) THEN
      bClockSyncedToday = FALSE
      iDayOfMonth = Day(Now)
    ENDIF
    IF NOT bClockSyncedToday THEN 
      ' TO DO: make TimeCron configurable from GUI
      IF Events.TimeCron("15 3 * * *") THEN ' only once a day at 3:15
        SLEEP 0.5 ' wait a little so reply of status report doesn't interfere
        bClockSyncedToday = TRUE
        sDay = CStr(WeekDay(Now))
        IF sDay = "0" THEN sDay = "7" ' correct sundays
        WriteCmd("SC=" & Format(Now, "h:n") & "/" & sDay)
      ENDIF
    ENDIF
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Parse received data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB ParseLine(sData AS String)

  DIM sLine, sResponse, sFlame, sThermostat AS String
  DIM sResponses AS String[]
  DIM iCount AS Integer
  DIM rResult AS Result
  DIM bParseLine AS Boolean

  DIM iMsgID AS Integer
  DIM fTemp AS Float
  DIM bTemp AS Byte
  DIM sMsgData, sMsgDirection, sErrorDescription AS String

  ' relay incoming data to relay port
  IF Main.bOpenThermRelayEnabled THEN TRY WRITE #hRelayOTSocket, sData & "\r"

  ' parse each line
  FOR EACH sLine IN Split(sData, "\n")
    bParseLine = TRUE

    SELECT CASE Right(sLine, 2)
      CASE "OK"
        'IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'OK' response: The command was accepted."))
        bParseLine = FALSE
      CASE "NG"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'NG' response: The command code is unknown."))
        bParseLine = FALSE
      CASE "SE"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'SE' response: The command contained an unexpected character or was incomplete."))
        bParseLine = FALSE
      CASE "BV"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'BV' response: The command contained a value that is not allowed"))
        bParseLine = FALSE
      CASE "OR"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'OR' response: A number was specified outside of the allowed range"))
        bParseLine = FALSE
      CASE "NS"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'NS' response: The alternative Data-ID could not be added because the table is full."))
        bParseLine = FALSE
      CASE "NF"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'NF' response: The specified alternative Data-ID could not be removed because it does not exist in the table."))
        bParseLine = FALSE
      CASE "IP"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'IP' response: The device cannot perform the action at this time, but will do so as soon as possible."))
        bParseLine = FALSE
      CASE "PF"
        'IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'PF' response: The requested information will be printed in a separate report."))
        bParseLine = FALSE
    END SELECT

    SELECT CASE Right(sLine, 8)
      CASE "Error 01"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'Error 01' response: A bit transistion happend at an unexpected time"))
        bParseLine = FALSE
      CASE "Error 02"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'Error 02' response: The stop bit was 0 while it should be 1"))
        bParseLine = FALSE
      CASE "Error 03"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'Error 03' response: A bit was not received when it was expected"))
        bParseLine = FALSE
      CASE "Error 04"
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'Error 04' response: A parity error was detected on a received opentherm message"))
        bParseLine = FALSE
    END SELECT

    IF Len(sLine) = 9 THEN 'AND NOT (bIgnoreAbout) THEN
      iMsgID = Val("&H" & Mid(sLine, 4, 2))
      sMsgData = Right(sLine, 4)
      fTemp = Val("&" & sMsgData)
      sMsgDirection = Left(sLine, 1)

      SELECT CASE iMsgID  'OpenTherm Message ID
        CASE 0
          IF sMsgDirection = "B" AND NOT (iThermostatID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received status message: " & sLine))
            bTemp = Val("&" & Right(sMsgData, 2)) 'Lower byte
            IF Main.bOpenThermDebug THEN 
              IF BTst(bTemp, 0) THEN Main.WriteDebugLog(("[OpenTherm]  - status = Error"))
              IF BTst(bTemp, 3) THEN
                Main.WriteDebugLog(("[OpenTherm]  - status = Flame on"))
              ELSE
                Main.WriteDebugLog(("[OpenTherm]  - status = Flame off"))
              ENDIF
              IF BTst(bTemp, 1) THEN Main.WriteDebugLog(("[OpenTherm]  - status = Heating up room"))
              IF BTst(bTemp, 2) THEN Main.WriteDebugLog(("[OpenTherm]  - status = Heating boiler"))
              IF BTst(bTemp, 4) THEN Main.WriteDebugLog(("[OpenTherm]  - status = Cooling down room"))
            ENDIF
            IF (BTst(bTemp, 1) <> bCH) THEN 'Central Heating state
              bCH = BTst(bTemp, 1)
              IF bCH THEN
                Devices.ValueUpdate(iThermostatID, "", "", "", "Heating up room")
              ELSE
                Devices.ValueUpdate(iThermostatID, "", "", "", "Idle")
              ENDIF
            ENDIF
            IF (BTst(bTemp, 2) <> bDHW) THEN 'Domestic hot water state
              bDHW = BTst(bTemp, 2)
              IF bDHW THEN
                Devices.ValueUpdate(iThermostatID, "", "", "", "Heating up boiler")
              ELSE
                Devices.ValueUpdate(iThermostatID, "", "", "", "Idle")
              ENDIF
            ENDIF
            IF (BTst(bTemp, 3) <> bFlame) THEN 'Flame status
              bFlame = BTst(bTemp, 3)
              IF bFlame THEN
                Devices.ValueUpdate(iBurnerID, "On", "", "", "")
              ELSE
                Devices.ValueUpdate(iBurnerID, "Off", "", "", "")
              ENDIF
            ENDIF
            IF (BTst(bTemp, 4) <> bCooling) THEN 'Cooling state
              bCooling = BTst(bTemp, 3)
              IF bCooling THEN
                Devices.ValueUpdate(iThermostatID, "", "", "", "Cooling down room")
              ELSE
                Devices.ValueUpdate(iThermostatID, "", "", "", "Idle")
              ENDIF
            ENDIF
            IF (BTst(bTemp, 0) <> bFault) THEN 'Fault indicator (details in message #5)
              bFault = BTst(bTemp, 0)
              IF bFault THEN
                IF NOT (bApplicationError) THEN Devices.ValueUpdate(iThermostatID, "", "", "", "Unknown error")
              ELSE
                Devices.ValueUpdate(iThermostatID, "", "", "", "Idle")
              ENDIF
            ENDIF
          ENDIF
        CASE 1  'Control set point
          IF sMsgDirection = "B" AND NOT (iCHID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received control set point message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - set point temperature = " & CStr(fTemp)))
            IF fCHSetPoint <> fTemp THEN
              fCHSetPoint = fTemp
              Devices.ValueUpdate(iCHID, Format(fCHSetPoint, "#0.0#"), "", "", "")
            ENDIF
          ENDIF
        CASE 5  'Application specific fault flags
          IF sMsgDirection = "B" THEN
            bTemp = Val("&" & Left(sMsgData, 2)) 'High byte
            IF bTemp > 0 THEN 'check if a fault bit is set
              bApplicationError = TRUE
              IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'application specific fault flags' message: " & sLine))
              sErrorDescription = ""
              IF BTst(bTemp, 0) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = service required"))
                sErrorDescription += ", Service required"
              ENDIF
              IF BTst(bTemp, 1) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = remote reset enabled"))
                sErrorDescription += ", remote reset enabled"
              ENDIF
              IF BTst(bTemp, 2) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = water pressure fault"))
                 sErrorDescription += ", water pressure fault"
              ENDIF
              IF BTst(bTemp, 3) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = gas/flame fault"))
                 sErrorDescription += ", gas/flame fault"
              ENDIF
              IF BTst(bTemp, 4) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = air pressure fault"))
                sErrorDescription += ", air pressure fault"
              ENDIF
              IF BTst(bTemp, 5) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = water over temperature fault"))
                sErrorDescription += ", water over temperature fault"
              ENDIF
              IF BTst(bTemp, 6) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = <unknown> (bit 7 set)"))
                sErrorDescription += ", unknown fault"
              ENDIF
              IF BTst(bTemp, 7) THEN
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - fault = <unknown> (bit 8 set)"))
                sErrorDescription += ", unknown fault"
              ENDIF
              IF Len(sErrorDescription) > 0 THEN 
                sErrorDescription = Right(sErrorDescription, Len(sErrorDescription) - 2)
                IF NOT (iThermostatID = 0) THEN Devices.ValueUpdate(iThermostatID, "", "", "", sErrorDescription)
              ENDIF
            ELSE
              IF Main.bOpenThermDebug THEN
                Main.WriteDebugLog(("[OpenTherm] Received 'application specific fault flags' message: " & sLine))
                Main.WriteDebugLog(("[OpenTherm]  - No fault flags set"))
              ENDIF
              bApplicationError = FALSE
            ENDIF
          ENDIF
        CASE 16 'Room set point
          IF sMsgDirection = "T" AND NOT (iThermostatID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received room set point message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - set point temperature = " & CStr(fTemp)))
            fCurrentSetPoint = fTemp
            IF fRoomSetPoint <> fTemp THEN
              fRoomSetPoint = fTemp
              Devices.ValueUpdate(iThermostatID, Format(fRoomSetPoint, "#0.0#"), "", CheckRemoteControl(), "")
            ENDIF
          ENDIF
        CASE 17 'Relative modulation level
          IF sMsgDirection = "B" AND NOT (iCHID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received relative modulation level message: " & sLine))
            fTemp = fTemp / 256
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - level = " & CStr(fTemp)))
            IF fCHModLevel <> fTemp THEN
              fCHModLevel = fTemp
              Devices.ValueUpdate(iCHID, "", "", fCHModLevel, "")
            ENDIF
          ENDIF
        CASE 18 'CH Water pressure
          IF sMsgDirection = "B" AND NOT (iCHID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received central heating water pressure message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - pressure = " & CStr(fTemp)))
            IF fCHWaterPress <> fTemp THEN
              fCHWaterPress = fTemp
              Devices.ValueUpdate(iCHID, "", "", "", Format(fCHWaterPress, "#.0#"))
            ENDIF
          ENDIF
        CASE 19 'Boiler hot water flow
          IF sMsgDirection = "B" AND NOT (iBoilerID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received hot water flow message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - flow rate = " & CStr(fTemp)))
            IF fBoilerFlow <> fTemp THEN
              fBoilerFlow = fTemp
              Devices.ValueUpdate(iBoilerID, "", "", Format(fBoilerFlow, "#.0#"), "")
            ENDIF
          ENDIF
        CASE 24 'Room temperature
          IF sMsgDirection = "T" AND NOT (iThermostatID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received room temperature message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - temperature = " & CStr(fTemp)))
            IF fRoomTemp <> fTemp THEN
              fRoomTemp = fTemp
              Devices.ValueUpdate(iThermostatID, "", Format(fRoomTemp, "#0.0#"), "", "")
            ENDIF
          ENDIF
        CASE 25 'Boiler water temperature
          IF sMsgDirection = "B" AND NOT (iBoilerID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received boiler temperature message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - temperature = " & CStr(fTemp)))
            IF fBoilerTemp <> fTemp THEN
              fBoilerTemp = fTemp
              Devices.ValueUpdate(iBoilerID, "", Format(fBoilerTemp, "#0.0#"), "", "")
            ENDIF
          ENDIF
        CASE 27 'Outside temperature
          IF sMsgDirection = "B" AND NOT (iOutsideID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received outside temperature message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - temperature = " & CStr(fTemp)))
            IF fOutsideTemp <> fTemp AND fTemp <> 0 THEN
              fOutsideTemp = fTemp
              Devices.ValueUpdate(iOutsideID, Format(fOutsideTemp, "#0.0#"), "", "", "")
            ENDIF
          ENDIF
        CASE 56 'Boiler temperature set point
          IF (sMsgDirection = "B" OR sMsgDirection = "A") AND NOT (iBoilerID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received boiler set point message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - set point temperature = " & CStr(fTemp)))
            IF fBoilerSetPoint <> fTemp THEN
              fBoilerSetPoint = fTemp
              Devices.ValueUpdate(iBoilerID, Format(fBoilerSetPoint, "#0.0#"), "", "", "")
            ENDIF
          ENDIF
        CASE 57 'CH temperature set point
          IF (sMsgDirection = "B" OR sMsgDirection = "A") AND NOT (iCHID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received maximum central heating set point message: " & sLine))
            fTemp = Round((fTemp / 256), -2)
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - set point temperature = " & CStr(fTemp)))
            IF fCHSetPoint <> fTemp THEN
              fCHSetPoint = fTemp
              Devices.ValueUpdate(iCHID, Format(fCHSetPoint, "#0.0#"), "", "", "")
            ENDIF
          ENDIF
        CASE 116 'Burner starts counter
          IF sMsgDirection = "B" AND NOT (iBurnerID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received burner starts counter message: " & sLine))
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - counter = " & CStr(fTemp)))
            IF fBurnerStarts <> fTemp THEN
              fBurnerStarts = fTemp
              Devices.ValueUpdate(iBurnerID, "", fBurnerStarts, "", "")
            ENDIF
          ENDIF
        CASE 120 'Burner hours central heating
          IF sMsgDirection = "B" AND NOT (iBurnerID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received burner running hours central heating message: " & sLine))
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - counter = " & CStr(fTemp)))
            IF fCHBurnerHours <> fTemp THEN
              fCHBurnerHours = fTemp
              Devices.ValueUpdate(iBurnerID, "", "", fCHBurnerHours, "")
            ENDIF
          ENDIF
        CASE 123 'Burner hours boiler
          IF sMsgDirection = "B" AND NOT (iBurnerID = 0) THEN
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received burner running hours boiler message: " & sLine))
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]  - counter = " & CStr(fTemp)))
            IF fHWBurnerHours <> fTemp THEN
              fHWBurnerHours = fTemp
              Devices.ValueUpdate(iBurnerID, "", "", "", fHWBurnerHours)
            ENDIF
          ENDIF
      END SELECT
      bParseLine = FALSE
    ENDIF

    IF InStr(sLine, "OpenTherm Gateway") THEN
      IF bIgnoreAbout THEN
        bIgnoreAbout = FALSE
        Main.WriteLog(("Connected to '" & sLine & "'"))
      ELSE
        IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Received 'OpenTherm Gateway' response, OpenTherm gateway device has been restarted unexpectedly?"))
        IF bOverrideActive THEN
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm]   resending thermostat override setpoint '" & CStr(fOverrideSetPoint) & "'"))
          SendCommand("THERMOSTAT", CStr(fOverrideSetPoint))
        ENDIF
      ENDIF
      bParseLine = FALSE
    ENDIF

    IF bParseLine THEN
      Main.WriteDebugLog(("[OpenTherm] Parsing received OpenTherm gateway report: ") & sLine)
      TRY sResponses = Split(sLine)
      IF ERROR THEN
        IF Main.bOpenThermDebug THEN
          Main.WriteDebugLog(("[OpenTherm] Failed to parse report: " & ERROR.Text & " at " & ERROR.Where))
        ENDIF
      ELSE
        ' Example of sResponses:
        '  0 - 00000011/00001010   Status (MsgID=0) - Printed as two 8-bit bitfields
        '  1 - 59.00               Control setpoint (MsgID=1) - Printed as a floating point value
        '  2 - 00000000/00000000   Remote parameter flags (MsgID= 6) - Printed as two 8-bit bitfields
        '  3 - 100.00              Maximum relative modulation level (MsgID=14) - Printed as a floating point value
        '  4 - 0/0                 Boiler capacity and modulation limits (MsgID=15) - Printed as two bytes
        '  5 - 19.50               Room Setpoint (MsgID=16) - Printed as a floating point value
        '  6 - 0.00                Relative modulation level (MsgID=17) - Printed as a floating point value
        '  7 - 1.80                CH water pressure (MsgID=18) - Printed as a floating point value
        '  8 - 19.57               Room temperature (MsgID=24) - Printed as a floating point value
        '  9 - 59.00               Boiler water temperature (MsgID=25) - Printed as a floating point value
        ' 10 - 0.00                DHW temperature (MsgID=26) - Printed as a floating point value
        ' 11 - 0.00                Outside temperature (MsgID=27) - Printed as a floating point value
        ' 12 - 0.00                Return water temperature (MsgID=28) - Printed as a floating point value
        ' 13 - 0/0                 DHW setpoint boundaries (MsgID=48) - Printed as two bytes
        ' 14 - 0/0                 Max CH setpoint boundaries (MsgID=49) - Printed as two bytes
        ' 15 - 60.00               DHW setpoint (MsgID=56) - Printed as a floating point value
        ' 16 - 90.00               Max CH water setpoint (MsgID=57) - Printed as a floating point value
        ' 17 - 31442               Burner starts (MsgID=116) - Printed as a decimal value
        ' 18 - 0                   CH pump starts (MsgID=117) - Printed as a decimal value
        ' 19 - 0                   DHW pump/valve starts (MsgID=118) - Printed as a decimal value
        ' 20 - 0                   DHW burner starts (MsgID=119) - Printed as a decimal value
        ' 21 - 3027                Burner operation hours (MsgID=120) - Printed as a decimal value
        ' 22 - 0                   CH pump operation hours (MsgID=121) - Printed as a decimal value
        ' 23 - 0                   DHW pump/valve operation hours (MsgID=122) - Printed as a decimal value
        ' 24 - 258                 DHW burner operation hours (MsgID=123) - Printed as a decimal value

        IF sResponses.count = 25 THEN
          ' remove : in any response
          FOR iCount = 0 TO 24
            sResponses[iCount] = Replace(sResponses[iCount], ":", "", 1)
          NEXT
          'Update current setpoint
          fCurrentSetPoint = Val(Format(CStr(sResponses[5]), "#.#"))

          'Update thermostat values
          IF NOT (iThermostatID = 0) THEN
            IF bOpenThermDebug THEN
              Main.WriteDebugLog(("[OpenTherm] Thermostat:"))
              Main.WriteDebugLog(("[OpenTherm]  - Room temperature set point = " & sResponses[8]))
              Main.WriteDebugLog(("[OpenTherm]  - Room temperature = " & sResponses[5]))
              Main.WriteDebugLog(("[OpenTherm]  - Status = " & sResponses[0]))
            ENDIF
            sTemperatureOverrideStatus = CheckRemoteControl() ' update thermostat override status
            sThermostat = "Idle" ' defauls to Idle
            IF (Mid(sResponses[0], 13, 1)) = "1" THEN sThermostat = "Cooling down room" ' OpenTherm MsgID=0, LWB, 0001 0000
            IF (Mid(sResponses[0], 15, 1)) = "1" THEN sThermostat = "Heating up boiler" ' OpenTherm MsgID=0, LWB, 0000 0100
            IF (Mid(sResponses[0], 16, 1)) = "1" THEN sThermostat = "Heating up room"   ' OpenTherm MsgID=0, LWB, 0000 0010
            IF (Mid(sResponses[0], 17, 1)) = "1" THEN sThermostat = "Error"             ' OpenTherm MsgID=0, LWB, 0000 0001
            Devices.ValueUpdate(iThermostatID, Format(sResponses[5], "#0.0#"), Format(sResponses[8], "#0.0#"), sTemperatureOverrideStatus, sThermostat)
          ENDIF

          'Update Central Heating values
          IF NOT (iCHID = 0) THEN
            IF bOpenThermDebug THEN
              Main.WriteDebugLog(("[OpenTherm] Central heating:"))
              Main.WriteDebugLog(("[OpenTherm]  - Set point = " & sResponses[16]))
              Main.WriteDebugLog(("[OpenTherm]  - Temperature = " & sResponses[1]))
              Main.WriteDebugLog(("[OpenTherm]  - Modulation level = " & sResponses[6]))
              Main.WriteDebugLog(("[OpenTherm]  - Water pressure = " & sResponses[7]))
            ENDIF
            Devices.ValueUpdate(iCHID, Format(sResponses[16], "##0.0#"), Format(sResponses[1], "##0.0#"), Format(sResponses[6], "##0"), Format(sResponses[7], "0.##"))
          ENDIF

          'Update hot water (boiler) values
          IF NOT (iBoilerID = 0) THEN
            IF bOpenThermDebug THEN
              Main.WriteDebugLog(("[OpenTherm] Hot water (boiler):"))
              Main.WriteDebugLog(("[OpenTherm]  - Set point = " & sResponses[15]))
              Main.WriteDebugLog(("[OpenTherm]  - Temperature = " & sResponses[9]))
            ENDIF
            Devices.ValueUpdate(iBoilerID, Format(sResponses[15], "##0.0#"), Format(sResponses[9], "##0.0#"), "", "")
          ENDIF

          'Update burner values
          IF NOT (iBurnerID = 0) THEN
            IF bOpenThermDebug THEN
              Main.WriteDebugLog(("[OpenTherm] Burner:"))
              Main.WriteDebugLog(("[OpenTherm]  - Flame = " & Mid(sResponses[0], 14, 1)))
              Main.WriteDebugLog(("[OpenTherm]  - starts = " & sResponses[17]))
              Main.WriteDebugLog(("[OpenTherm]  - operation hours central heating = " & sResponses[21]))
              Main.WriteDebugLog(("[OpenTherm]  - operation hours hot water boiler = " & sResponses[24]))
            ENDIF
            sFlame = IIf((Mid(sResponses[0], 14, 1) = "1"), "On", "Off")
            Devices.ValueUpdate(iBurnerID, sFlame, Format(CStr(sResponses[17]), "#####"), Format(CStr(sResponses[21]), "#####"), Format(CStr(sResponses[24]), "#####"))
          ENDIF

          'Update outside sensor
          IF NOT (iOutsideID = 0) THEN
            IF bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Outside temperature: " & sResponses[11]))
            IF sResponses[11] <> "0.00" THEN
              fOutsideTemp = CFloat(Format(CStr(sResponses[11]), "#0.0#"))
              Devices.ValueUpdate(iOutsideID, Format(CStr(fOutsideTemp), "#0.0#"), "", "", "")
            ENDIF
          ENDIF

        ELSE
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid response '") & sLine & "'")
        ENDIF
      ENDIF
    ENDIF
  NEXT

CATCH
  IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] ERROR: Parsing OpenTherm data packet: ") & sLine & " " & ERROR.Text & " at " & ERROR.Where)

END

PRIVATE FUNCTION CheckRemoteControl() AS String

  DIM sCheckRemoteControl AS String = ""

  ' update thermostat override status
  IF NOT bOverrideSend AND NOT bOverrideActive THEN sCheckRemoteControl = "Override inactive"
  IF bOverrideSend THEN
    IF fOverrideSetPoint = fCurrentSetPoint THEN
      Main.WriteDebugLog(("[OpenTherm]  - Room temperature set point is overruled by DomotiGa"))
      bOverrideActive = TRUE
      sCheckRemoteControl = "Override active"
      bOverrideSend = FALSE
    ELSE
      Main.WriteDebugLog(("[OpenTherm]  - New room temperature set point " & CStr(fOverrideSetPoint) & " not active yet"))
    ENDIF
  ENDIF

  IF bOverrideActive AND (sTemperatureOverride = "Temporarily") THEN
    IF fCurrentSetPoint <> fOverrideSetPoint THEN
      Main.WriteDebugLog(("[OpenTherm]  - Override room temperature disabled due to setpoint change"))
      bOverrideActive = FALSE
      sCheckRemoteControl = "Override inactive"
    ENDIF
  ENDIF
  RETURN sCheckRemoteControl

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' called from devices module
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB SendCommand(sAddress AS String, sCmd AS String)

  DIM iDeviceId AS Integer
  DIM sType, sDay, sCmdPrefix AS String

  iDeviceId = Devices.Find(sAddress, Devices.FindInterface("OpenTherm gateway"))
  IF iDeviceId THEN
    sType = Devices.FindTypeForDevice(iDeviceId)
  ELSE
    IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Device '") & sAddress & "' not found!")
    RETURN
  ENDIF

  IF Len(sCmd) > 0 THEN
    SELECT CASE UCase(sAddress)
      CASE "THERMOSTAT"  ' set new thermostat setpoint (override currently active setpoint)
        sCmdPrefix = ""
        fOriginalSetPoint = fCurrentSetPoint
        fOverrideSetPoint = CFloat(Fix(Val(sCmd)))
        SELECT CASE UCase(sTemperatureOverride)
          CASE "CONSTANT"
            sCmdPrefix = "TC="
            bOverrideSend = TRUE
          CASE "TEMPORARILY"
            ' set temporarily override for configured thermostat type
            SELECT CASE sThermostat
              CASE "Remeha Celcia 20"
                sCmdPrefix = "TR="
                bOverrideSend = TRUE
              CASE "Other"
                sCmdPrefix = "TT="
                bOverrideSend = TRUE
              CASE ELSE
                IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid Thermostat type: ") & sThermostat)
            END SELECT
          CASE ELSE
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid temperature override: ") & sTemperatureOverride)
        END SELECT
        IF Len(sCmdPrefix) = 3 THEN 
          IF fOverrideSetPoint >= 0 AND fOverrideSetPoint < 30 THEN 'validate new thermostat override setpoint
            IF fCurrentSetPoint = fOverrideSetPoint THEN 
              IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Thermostat setpoint already set to '" & sCmd & "'"))
            ELSE
              IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] send new thermostat setpoint command: '" & sCmdPrefix & sCmd & "'"))
              WriteCmd(sCmdPrefix & CStr(fOverrideSetPoint))
              IF sCmd = "0" OR sCmd = "0.0" THEN 
                sTemperatureOverrideStatus = ""
                fOriginalSetPoint = 0
                fOverrideSetPoint = 0
                bOverrideSend = FALSE
                bOverrideActive = FALSE
              ENDIF
              IF bOverrideSend THEN sTemperatureOverrideStatus = "Override send"
            ENDIF
          ELSE
            IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid thermostat override setpoint value '" & sCmd & "'. Valid values are between 0.0 and 30.0"))
            bOverrideSend = FALSE
          ENDIF
        ELSE
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid thermostat override prefix '") & sCmdPrefix * "'")
          bOverrideSend = FALSE
        ENDIF

      CASE "BOILER"           ' set the domestic hot water setpoint
        IF CFloat(Val(sCmd)) > 0 AND CFloat(Val(sCmd)) < 100 THEN
          fOverrideBoilerSetPoint = CFloat(Val(sCmd))
          WriteCmd("SW=" & CStr(fOverrideBoilerSetPoint))
        ELSE
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid boiler setpoint value '" & CStr(fOverrideBoilerSetPoint) & "'. Valid values are between 0.0 and 100.0"))
        ENDIF

      CASE "CENTRALHEATING"   ' set the maximum central heating setpoint
        IF CFloat(Val(sCmd)) > 0 AND CFloat(Val(sCmd)) < 100 THEN
          fCentralHeatingSetPoint = CFloat(Val(sCmd))
          WriteCmd("SH=" & CStr(fCentralHeatingSetPoint))
        ELSE
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid central heating setpoint value '" & CStr(fCentralHeatingSetPoint) & "'. Valid values are between 0.0 and 100.0"))
        ENDIF

      CASE "OUTSIDE"          ' send new outside temperature to thermostat
        IF CFloat(Val(sCmd)) > -40 AND CFloat(Val(sCmd)) < 127 THEN
          fOutsideTemp = CFloat(Val(sCmd))
          WriteCmd("OT=" & CStr(fOutsideTemp))
        ELSE
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid outside temperature '" & CStr(fOutsideTemp) & "'. Valid values are between -40.0 and +127.0"))
        ENDIF

      CASE "CLOCK"            ' synchonize clock with local clock by sending current time
        IF sCmd = "sync" THEN
          sDay = CStr(WeekDay(Now))
          IF sDay = "0" THEN sDay = "7" ' correct sundays
          WriteCmd("SC=" & Format(Now, "hh:nn") & "/" & sDay)
        ELSE
          IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid command for 'Clock' device: " & sCmd))
        ENDIF
    END SELECT
  ELSE
    IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid command '") & sCmd & "' for device " & sAddress)
  ENDIF

END

PRIVATE SUB WriteCmd(sCommand AS String)

  IF Main.bOpenThermDebug THEN
    IF sCommand <> "PS=1" THEN ' don't log default report request
      Main.WriteDebugLog(("[OpenTherm] write command: ") & sCommand)
    ENDIF
  ENDIF

  IF Len(sCommand) > 3 AND InStr(sCommand, "=") THEN 
    IF sType = "tcp" THEN
      TRY PRINT #hOpenThermTCP, sCommand & "\r"
      IF ERROR THEN
        Main.WriteDebugLog(("[OpenTherm] Error writing data to socket! -> ") & ERROR.Text)
        Main.WriteDebugLog(("[OpenTherm] Trying to reconnect..."))
        TRY hOpenThermTCP.Connect
      ENDIF
    ELSE
      TRY PRINT #hOpenThermSer, sCommand & "\r"
      IF ERROR THEN
        Main.WriteDebugLog(("[OpenTherm] Error writing data to serial port! -> ") & Error.Text)
      ENDIF
    ENDIF
  ELSE
    IF Main.bOpenThermDebug THEN Main.WriteDebugLog(("[OpenTherm] Invalid command: ") & sCommand)
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  IF sType = "tcp"
    TRY tOpenTherm.Stop
    TRY hOpenThermRelay.Close
    TRY hOpenThermTCP.Close
    Main.WriteLog(("OpenTherm serial port close."))
  ELSE
    TRY tOpenTherm.Stop
    TRY hOpenThermSer.Close
    Main.WriteLog(("OpenTherm serial port close."))
  ENDIF

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("OpenTherm Error: ") & ERROR.Text)
  RETURN FALSE

END

' implement properties
PRIVATE FUNCTION Type_Read() AS String

  RETURN sType

END

PRIVATE SUB Type_Write(Value AS String)

  sType = Value

END

PRIVATE FUNCTION TCPHost_Read() AS String

  RETURN sTCPHost

END

PRIVATE SUB TCPHost_Write(Value AS String)

  sTCPHost = Value

END

PRIVATE FUNCTION TCPPort_Read() AS String

  RETURN sTCPPort

END

PRIVATE SUB TCPPort_Write(Value AS String)

  sTCPPort = Value

END

PRIVATE FUNCTION SerPort_Read() AS String

  RETURN sSerPort

END

PRIVATE SUB SerPort_Write(Value AS String)

  sSerPort = Value

END

PRIVATE FUNCTION OpenThermDebug_Read() AS Boolean

  RETURN bOpenThermDebug

END

PRIVATE SUB OpenThermDebug_Write(Value AS Boolean)

  bOpenThermDebug = Value

END

PRIVATE SUB PollTime_Read() AS Integer

  RETURN iPollTime

END

PRIVATE FUNCTION PollTime_Write(Value AS Integer)

  iPollTime = Value

END

PRIVATE SUB Thermostat_Read() AS String

  RETURN sThermostat

END

PRIVATE FUNCTION Thermostat_Write(Value AS String)

  sThermostat = Value

END

PRIVATE SUB TemperatureOverride_Read() AS String

  RETURN sTemperatureOverride

END

PRIVATE FUNCTION TemperatureOverride_Write(Value AS String)

  sTemperatureOverride = Value

END

PRIVATE FUNCTION OutsideSensor_Read() AS Boolean

  RETURN bOutsideSensor

END

PRIVATE SUB OutsideSensor_Write(Value AS Boolean)

  bOutsideSensor = Value

END

PRIVATE FUNCTION SyncClock_Read() AS Boolean

  RETURN bSyncClock

END

PRIVATE SUB SyncClock_Write(Value AS Boolean)

  bSyncClock = Value

END

PRIVATE FUNCTION RelayEnabled_Read() AS Boolean

  RETURN bRelayEnabled

END

PRIVATE SUB RelayEnabled_Write(Value AS Boolean)

  bRelayEnabled = Value

END

PRIVATE FUNCTION RelayPort_Read() AS Integer

  RETURN iRelayPort

END

PRIVATE SUB RelayPort_Write(Value AS Integer)

  iRelayPort = Value

END
