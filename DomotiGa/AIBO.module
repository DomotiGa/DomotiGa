' Gambas module file

' Description:
' AIBO.module
' Support for Sony AIBO ERS-7 monitor if he/she is awake or not, and give him commands via e-mail.

' Development Status:
' Detecting AIBO and setting device status accordingly works, e-mail functionality needs to be implemented.

' Links:
' http://www.aiai.ed.ac.uk/project/aibo/aibo-mail.html
' http://littlegreengecko.org/aibomailref_mind3.htm

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC tAIBO AS Timer
PRIVATE hPing AS Process

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Run()

  ' start poll timer for AIBO
  tAIBO = NEW Timer AS "tAIBO"
  tAIBO.Delay = Main.iAIBOPollTime * 1000 ' multiply for seconds
  tAIBO.Start

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tAIBO_Timer()

  CheckAIBO()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find all devices with type AIBO and see if we can reach it's webserver
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB CheckAIBO()

  DIM rResult AS Result
  DIM iModule AS Integer

  ' get the id of devicetype "AIBO"
  iModule = Devices.FindDeviceType("AIBO")
  IF iModule = 0 THEN RETURN

  ' get all devices with this devicetype
  rResult = Main.hDB.Exec("SELECT * FROM devices WHERE module = &1 AND enabled is TRUE", iModule)
  IF rResult.Available THEN
    IF rResult.Count >= 1 THEN
      FOR EACH rResult
        IF Main.bAIBODebug THEN Main.WriteDebugLog(("[AIBO] Checking '") & rResult!name & ("' with address 'http://") & rResult!address & "'.")
        ' try to fetch their index page
        AiboPing(rResult!id, rResult!address)
      NEXT
    ELSE
      Main.WriteLog(("AIBO: No AIBO(s) found in device table!"))
    END IF
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' try to ping the AIBO to detect if it's awake or not
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB AiboPing(iId AS Integer, sAddress AS String)

  hPing = EXEC ["ping", "-c1", sAddress] FOR READ AS "Ping"
  hPing.Tag = iId

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' get id and return value, 0 means ok, anything else is not reached
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Ping_Kill()

  DIM iId, iRc AS Integer
  DIM sStatus AS String

  iId = LAST.Tag
  iRc = LAST.Value

  IF iRc = 0 THEN
    ' ping succesfull
    sStatus = ("Awake")
  ELSE
    ' not reachable, unknown host etc.
    sStatus = ("Sleeping")
  END IF

  ' debug output
  IF Main.bAIBODebug THEN Main.WriteDebugLog(("[AIBO] With id '") & iId & ("' is '") & sStatus & "'.")
  ' find and update device
  Devices.ValueUpdate(iId, sStatus, "", "", "")

END