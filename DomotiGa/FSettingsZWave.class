' Gambas class file

' Description:
' FSettingsZWave.class
' Settings form for Z-Wave support.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)

  txtSerialPort.Text = Main.sZWaveSerialPort
  cmbBaudrate.Text = Main.sZWaveBaudrate
  chkDebug.Value = Main.bZWaveDebug
  chkEnabled.Value = Main.bZWaveEnabled
  btnSave.Enabled = FALSE

  IF Main.bZWaveEnabled = FALSE THEN
    txtSerialPort.Enabled = FALSE
    cmbBaudrate.Enabled = FALSE
    chkDebug.Enabled = FALSE
  END IF

END

PUBLIC SUB btnCancel_Click()

  ME.Close

END

PUBLIC SUB txtSerialPort_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkDebug_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB btnSave_Click()

  DIM rResult AS Result

  ' save new Z-Wave settings
  rResult = Main.hDB.Exec("UPDATE settings_zwave SET debug = &1, serialport = &2, baudrate = &3, enabled = &4 WHERE id = 1", chkDebug.Value, txtSerialPort.Text, cmbBaudrate.Text, chkEnabled.Value)
  rResult = Main.GetSettingTable("zwave") ' reload settings
  IF rResult.Count THEN
    Main.bZWaveEnabled = rResult!enabled
    Main.sZWaveSerialPort = rResult!serialport
    Main.sZWaveBaudrate = rResult!baudrate
    Main.bZWaveDebug = rResult!debug
  END IF
  Main.Restart_ZWave()
  FMain.UpdateStatusPanel()
  ME.Close

END

PUBLIC SUB btnDefaults_Click()

  DIM rResult AS Result

  rResult = Main.GetSettingTable("zwave", TRUE) ' get defaults
  IF rResult.Count THEN
    chkEnabled.Value = rResult!enabled
    txtSerialPort.text = rResult!serialport
    cmbBaudrate.Text = rResult!baudrate
    chkDebug.Value = rResult!debug
  END IF
  btnSave.Enabled = TRUE

END

PUBLIC SUB chkEnabled_Click()

  txtSerialPort.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  cmbBaudrate.Enabled = chkEnabled.Value
  btnSave.Enabled = TRUE

END

PUBLIC SUB cmbBaudrate_Click()

  btnSave.Enabled = TRUE

END
