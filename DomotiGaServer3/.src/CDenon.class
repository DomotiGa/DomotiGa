' Gambas class file

' Description:
' CDenon.class
' Support for Denon receiver control.

' Development Status:
' Beta. Needs Testing.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module was written by Daniel Keller in 2010.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "Denon"
Public PluginType As Integer = Plugin.Type_Class
Public PluginFriendlyName As String = "Denon Receiver"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Byte = 1

Public KeyName As String
Public LogLabel As String = "[Denon] "
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $sTCPHost As String
Private $iTCPPort As Integer
Private $bDenonDebug As Boolean
Private $sInterface As String
Private $sSerPort As String
Private $sBaud As String

Public hDenon As New Socket
Public hDenonSer As New SerialPort

Public sBuffer As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)

  KeyName = cPl.KeyName
  Instance = cPl.Instance
  If Instance <> 1 Then LogLabel = Replace(LogLabel, "] ", "#" & Instance & "] ")

  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $sTCPHost = cPl.Settings["tcphost"]
  $iTCPPort = cPl.Settings["tcpport"]
  $sInterface = cPl.Settings["type"]
  $sSerPort = cPl.Settings["serialport"]
  $sBaud = cPl.Settings["baudrate"]
  $bDenonDebug = cPl.Settings["debug"]
 
  ' Connect/Initialize connection
  If InStr($sInterface, "tcp") Then
    ConnectTCP()
  Else
    ConnectSerial()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for each Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()

  Try Disconnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the tcp host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectTCP() As Boolean

  ' try to close the connection
  Try hDenon.Close

  ' get a new one
  hDenon = New Socket As "Denon"
  hDenon.Connect($sTCPHost, $iTCPPort)

  ' Write to main logfile we are trying to connect
  Main.WriteLog(LogLabel & PluginFriendlyName & " TCP interface connecting to " & $sTCPHost & ":" & $iTCPPort)

  ' Don't set IsRunning=True, this has to be done in _Ready 

  ' All went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " TCP interface FAILED to connect to " & $sTCPHost & ":" & $iTCPPort)
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectSerial() As Boolean

  ' try to close the connection
  Try hDenonSer.Close

  ' get a new one
  hDenonSer = New Serialport As "DenonSer"
  With hDenonSer
    .PortName = $sSerPort
    .Speed = $sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  End With


  ' Write to main logfile we connected successfully
  Main.WriteLog(LogLabel & PluginFriendlyName & " serial interface connected to port " & $sSerPort)
  IsRunning = True

  ' All went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " serial interface FAILED to connect to port " & $sSerPort)
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is connected
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Denon_Ready()

  Main.WriteLog(LogLabel & "TCP interface connected.")
  IsRunning = True

  ' send some commands to get the states of the device and write them back in variable.
  SendCmd("MV?")
  Sleep 0.5
  SendCmd("SV?")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is closed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Denon_Closed()

  Main.WriteLog(LogLabel & "ERROR: TCP socket closed by peer.")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the tcp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try hDenon.Close
  Try hDenonSer.Close

  If InStr($sInterface, "tcp") Then
    Main.WriteLog(LogLabel & PluginFriendlyName & " TCP connection closed.")
  Else
    Main.WriteLog(LogLabel & PluginFriendlyName & " Port closed.")
  Endif

  ' all ok
  Return True

Finally
  IsRunning = False
  ErrorText = ""
  ErrorWhere = ""

Catch
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' error while connected/connecting to host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Denon_Error()

  Dim sMsg As String

  sMsg = Log.Text_TCP_Error(Last.Status, $sTCPHost)
  Main.WriteLog(LogLabel & "ERROR: " & sMsg)

  IsRunning = False
  ErrorText = sMsg

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send command via serial port or tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCmd(sCmd As String)

  If $bDenonDebug Then Main.WriteDebugLog(LogLabel & "Sending '" & sCmd & "' to Receiver.")
  If $sInterface = "tcp" Then
    Try Write #hDenon, sCmd & Chr$(13), Len(sCmd) + 1
    If Error Then Main.WriteDebugLog(LogLabel & "Error writing data to the TCP port! -> " & Error.Text)
  Else
    Try Write #hDenonSer, sCmd & Chr$(13), Len(sCmd) + 1
    If Error Then Main.WriteDebugLog(LogLabel & "Error writing data to the serial port! -> " & Error.Text)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send command to device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(sAddress As String, sValue As String)

  ' ignore sAddress, only fixed unit 1
  Select Case LCase(sValue)
    Case "on"
      SendCmd("PWSTANDBY")
    Case "off"
      SendCmd("PWON")
    Case Else
      If $bDenonDebug Then Main.WriteDebugLog(LogLabel & "Unsupported command received: " & sValue)
  End Select

End

Public Sub SetCommand(sCmd As Variant, sValue As Variant, sAddress As String)

  ' ignore sAddress, only fixed unit 1
  Select Case LCase(sCmd)
    Case "power"
      ' ON or OFF
      If UCase(sValue) = "OFF" Then
        svalue = "STANDBY"
      Endif
      SendCmd("PW" & UCase(sValue))
    Case "z2power"
      ' ON or OFF
      SendCmd("Z2" & UCase(sValue))
    Case "mainpower"
      ' ON or OFF
      SendCmd("ZM" & UCase(sValue))
    Case "volume"
      ' 18(db) until -80 (db), up, down,
      ' maybe later on also +/- number (let's say to increase +5)
      Try sValue = Val(sValue)
      If TypeOf(sValue) = gb.Integer Then
        sValue = sValue + 80
      Else
        If UCase(sValue) = "OFF" Then sValue = 99
      Endif
      SendCmd("MV" & sValue)
    Case "z2volume"
      ' 18(db) until -80 (db), up, down,
      ' maybe later on also +/- number (let's say to increase +5)
      Try sValue = Val(sValue)
      If TypeOf(sValue) = gb.Integer Then
        sValue = sValue + 80
      Else
        If UCase(sValue) = "OFF" Then sValue = 99
      Endif
      SendCmd("Z2" & sValue)
    Case "mute"
      ' ON or OFF
      SendCmd("MU" & UCase(sValue))
    Case "z2mute"
      ' ON or OFF
      SendCmd("Z2MU" & UCase(sValue))
    Case "input"
      ' phone, cd, tuner, dvd, vdp, tv, dbs, vcr-1, vcr-2, vcr-3, v.aux, cdr/tape
      SendCmd("SI" & UCase(sValue))
    Case "z2input"
      ' phone, cd, tuner, dvd, vdp, tv, dbs, vcr-1, vcr-2, vcr-3, v.aux
      SendCmd("Z2" & UCase(sValue))
    Case "cli"
      ' send any correct command to receiver
      SendCmd(UCase(sValue))
    Case Else
      If $bDenonDebug Then Main.WriteDebugLog(LogLabel & "Unsupported command received: " & sCmd & " " & sValue)
  End Select
  ' TODO: has to be completed with others and zone 2

End

Public Sub GetCommand(sCmd As String, sAddress As String)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' data received on tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Denon_Read()

  Dim sData As String

  Try sData = Read #hDenon, 1
  If Error Then Main.WriteDebugLog(LogLabel & "Error reading data from the TCP port! -> " & Error.Text)
  If sData = Chr$(13) Then ' buffer until linefeed then parse
    If Len(sBuffer) > 1 Then ParseLine(sBuffer)
    sBuffer = Null
  Else
    sBuffer &= sData
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' data received on serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DenonSer_Read()

  Dim sData As String

  Try sData = Read #hDenonSer, 1
  If Error Then Main.WriteDebugLog(LogLabel & "Error reading data from the serial port! -> " & Error.Text)
  If sData = Chr$(13) Then ' buffer until linefeed then parse
    If Len(sBuffer) > 1 Then ParseLine(sBuffer)
    sBuffer = Null
  Else
    sBuffer &= sData
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse received data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ParseLine(sStr As String)

  Dim sMaincat, sZonecat, sItem, sValue, sTemp As String

  If $bDenonDebug Then Main.WriteDebugLog(LogLabel & "Received: " & sStr)

  sMaincat = Left(sStr, 2)
  sZonecat = Mid(sStr, 3)

  Select Case sMaincat

    Case "PW"
      ' main power
      sItem = "Power"
      If sStr = "PWON" Then sValue = "On"
      If sStr = "PWSTANDBY" Then sValue = "Off"
      DenonSetValue(sItem, sValue)

    Case "MV"
      ' master volume
      sTemp = Mid(sStr, 3)
      If Mid(sStr, 3, 3) = "MAX"
        'do nothing
      Else
        sItem = "Volume"
        If sTemp = "99"
          sValue = ""
        Else
          If Len(sTemp) = 1 Then sTemp = sTemp & "00"
          If Len(sTemp) = 2 Then sTemp = sTemp & "0"
          sValue = (Val(sTemp) / 10) - 80
          DenonSetValue(sItem, sValue)
        Endif
      Endif

    Case "MU"
      ' main mute
      sItem = "MainMute"
      If sStr = "MUON" Then sValue = "On"
      If sStr = "MUOFF" Then sValue = "Off"
      DenonSetValue(sItem, sValue)

    Case "ZM"
      ' power main zone
      sItem = "MainZonePower"
      If sStr = "ZMON" Then sValue = "On"
      If sStr = "ZMOFF" Then sValue = "Off"
      DenonSetValue(sItem, sValue)

    Case "SI"
      ' source input
      sItem = "MainInput"
      sValue = Left(sStr, 2)
      DenonSetValue(sItem, sValue)

    Case "SV"
      ' video input
      sItem = "VideoInput"
      sValue = Left(sStr, 2)
      DenonSetValue(sItem, sValue)

    Case "MS"
      sItem = "SurroundMode"
      sValue = Left(sStr, 2)
      DenonSetValue(sItem, sValue)

    Case "DC"
      sItem = "Digital Input Mode"
      sValue = Left(sStr, 2)
      DenonSetValue(sItem, sValue)

    Case "SD"
      sItem = "Input Mode"
      sValue = Left(sStr, 2)
      DenonSetValue(sItem, sValue)

    Case "SR"
      sItem = "Record Selection"
      sValue = Left(sStr, 2)
      DenonSetValue(sItem, sValue)

    Case "Z2"
      ' zone2 integration

      Select Case sZonecat

        Case "PHONO"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "CD"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "TUNER"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "DVD"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "HDP"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "TV/CBL"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "SAT"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "VCR"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "V.AUX"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "SOURCE"
          sItem = "Zone2Input"
          sValue = Left(sStr, 2)
          DenonSetValue(sItem, sValue)

        Case "MUON"
          sItem = "Zone2Mute"
          DenonSetValue(sItem, "On")

        Case "MUOFF"
          sItem = "Zone2Mute"
          DenonSetValue(sItem, "Off")

        Case "ON"
          sItem = "Zone2Power"
          DenonSetValue(sItem, "On")

        Case "OFF"
          sItem = "Zone2Power"
          DenonSetValue(sItem, "Off")
      End Select

      If IsNull(Val(sZonecat)) Then
        ' not a number, do nothing
      Else
        sTemp = Mid(sStr, 3)
        sItem = "Zone2Volume"
        If sTemp = "99"
          sValue = ""
        Else
          If Len(sTemp) = 1 Then sTemp = sTemp & "00"
          If Len(sTemp) = 2 Then sTemp = sTemp & "0"
          sValue = (Val(sTemp) / 10) - 80
          DenonSetValue(sItem, sValue)
        Endif
      Endif

    Case "Z3"
      ' not implemented yet

    Case Else
      If $bDenonDebug Then Main.WriteDebugLog(LogLabel & "Not implemented yet!")
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' save received values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub DenonSetValue(sItem As String, vValue As Variant)

  Main.SetGlobalVar("Denon_" & sItem, vValue)
  If $bDenonDebug Then Main.WriteDebugLog(LogLabel & "Received Item: " & sItem & " Value: " & vValue)

End

