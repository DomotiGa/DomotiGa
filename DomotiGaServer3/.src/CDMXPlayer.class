' Gambas class file

' Description:
' CDMXPlayer.class
' Support for DMXPlayer module control.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module was written by Ronald Kelleners in 2013.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "DMXPlayer"
Public PluginType As Integer = Plugin.Type_Class
Public PluginFriendlyName As String = "DMXPlayer"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ronald Kelleners"
Public PluginProtocols As String[]
Public PluginMaxInstances As Integer = 1

Public KeyName As String
Public LogLabel As String = "[DMXPlayer] "
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $sSerPort As String
Private $sBaud As String
Private $bDMXPlayerDebug As Boolean

Public hDMXPlayer As New SerialPort
Public sBuffer As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)

  KeyName = cPl.KeyName
  Instance = cPl.Instance
  If Instance <> 1 Then LogLabel = Replace(LogLabel, "] ", "#" & Instance & "] ")

  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $sSerport = cPl.Settings["serialport"]
  $sBaud = cPl.Settings["baudrate"]
  $bDMXPlayerDebug = cPl.Settings["debug"]
 
  ' Connect/Initialize connection
  Connect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for EACH Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()

  Try Disconnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the port
  Try hDMXPlayer.Close

  ' get a new one
  hDMXPlayer = New Serialport As "DMXPlayer" 

  With hDMXPlayer
    .PortName = $sSerPort
    .Speed = $sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .Open()
  End With

  ' Write to main logfile we connected successfully
  Main.WriteLog(LogLabel & PluginFriendlyName & " interface connected to Port " & $sSerPort)
  IsRunning = True

  ' All went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " interface FAILED to connect to Port " & $sSerPort)
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect the serialport
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try hDMXPlayer.Close

  Main.WriteLog(LogLabel & PluginFriendlyName & " Port closed.")

  ' all ok
  Return True

Finally
  IsRunning = False
  ErrorText = ""
  ErrorWhere = ""

Catch
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  Return False

End

Public Sub SendCommand(sAddress As String, sCmd As String)

  Dim iLevel As Integer
  Dim sValue As String

  sCmd = UCase(sCmd)

  If InStr(sCmd, "DIM") Then
    iLevel = Val(Replace(sCmd, "DIM ", ""))
    iLevel = (iLevel * 254) / 100
    sValue = Format$(iLevel, "000")
  Else If
    InStr(sCmd, "OFF") Then
    sValue = Format$(000, "000")
  Else If
    InStr(sCmd, "ON") Then
    sValue = Format$(255, "000")
  Else
    Main.WriteDebugLog(LogLabel & "Unknown command '" & sCmd & "' given!")
    Return
  Endif
  If $bDMXPlayerDebug Then Main.WriteDebugLog(LogLabel & "> C" & sAddress & "L" & sValue)
  Try Print #hDMXPlayer, ("C" & sAddress & "L" & sValue)

End

Public Sub DMXPlayer_Read()

  Dim sData As String

  Try sData = Read #hDMXPlayer, 1
  If Error Then Main.WriteDebugLog(LogLabel & "Error reading data from the serial port! -> " & Error.Text)
  If sData = Chr(10) Then ' buffer until linefeed then parse
    If Len(sBuffer) > 1 Then ParseLine(sBuffer)
    sBuffer = Null
  Else
    sBuffer &= sData
  Endif

End

Public Sub ParseLine(sStr As String)

  'Dim iDeviceId As Integer
  'Dim aScan As String[]

  If $bDMXPlayerDebug Then Main.WriteDebugLog(LogLabel & "< " & sStr)

  ' ' try to find device with address, and correct interface type.
  ' iDeviceId = Devices.Find(Instance, "<address>", Devices.FindInterface("DMX"))
  ' ' if found then update it's value
  ' If iDeviceId Then Devices.Update(iDeviceId, "<value>")

End

