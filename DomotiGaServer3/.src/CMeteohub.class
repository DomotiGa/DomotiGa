' Gambas class file

' Description:
' Meteohub.class
' Provide support for fetching logged data of a Meteohub.

' Development Status:
' Just started to build this one.

' Links:
' http://wiki.meteohub.de
' http://www.meteohub.de/files/HTTP-Data-Logging-Protocol-v1.4.pdf

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Property FetchURL As String
Property Polltime As Integer
Property MeteohubDebug As Boolean

Private sFetchURL As String
Private iPolltime As Integer
Private bMeteohubDebug As Boolean

Private hMeteohub As New HttpClient
Private tMeteohub As Timer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start the poll timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the connection
  Try hMeteohub.Close

  ' start poll timer for Meteohub
  tMeteohub = New Timer As "tMeteohub"
  tMeteohub.Delay = iPolltime * 1000 ' multiply for seconds
  tMeteohub.Start

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("Meteohub Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' stop timer and close handle
  Try tMeteohub.Stop
  Try hMeteohub.Close
  Main.WriteLog(("Meteohub closed."))

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("Meteohub Weather Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tMeteohub_Timer()

  hMeteohub = New HttpClient As "hMeteohub"
  hMeteohub.URL = sFetchURL
  hMeteohub.TimeOut = 8
  hMeteohub.Async = True
  hMeteohub.Get()
  If Main.bMeteohubDebug Then Main.WriteDebugLog("[Meteohub] Fetching '" & sFetchUrl & "'")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' meteohub data fetched
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hMeteohub_Finished()

  Dim sBuffer As String

  Select hMeteohub.Code
    Case 200
      If Lof(hMeteohub) Then sBuffer = Read #hMeteohub, Lof(hMeteohub)
      If Main.bMeteohubDebug Then Main.WriteDebugLog("[Meteohub] Received '" & sBuffer & "'")
      ParseData(sBuffer)
    Case Else
      Main.WriteDebugLog(("[Meteohub] Unknown error occured while trying to get Meteohub data!"))
  End Select
  Try hMeteohub.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse raw data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ParseData(sData As String)

  Dim sLine, sSensor, sValues, sForecast As String
  Dim aLines, aValues, aEachValue As String[]
  Dim aForecast As String[] = ["Rainy", "Cloudy", "Some Clouds", "Sunny", "Snowy", "Clouds at Night", "Clear Night"]
  Dim iDeviceId As Integer

  ' 20121108170502 rain0 0.0 329.7 0.0
  ' 20121108170502 th0 10.5 94 9.6
  ' 20121108170502 thb0 19.6 66 13.1 1015.5 1015.5 -1
  ' 20121108170502 wind0 135 2.0 0.3 9.8

  ' split per line
  aLines = Split(sData, "\n")
  For Each sLine In aLines
    ' parse each line
    aValues = Scan(sLine, "* * *")

    If aValues.Count = 3 Then
      sSensor = aValues[1]
      sValues = aValues[2]
        If Left(sSensor, 3) = "thb" Then ' thb0 - thb19
          aEachValue = Scan(sValues, "* * * * * *")
          If aEachValue.Count = 6 Then
            If aEachValue[5] = "-1" Then ' not available
              sForecast = ""
            Else
              sForecast = aForecast[Val(aEachValue[5])]
            Endif
            iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), UCase(Left(sSensor, 3)))
            If iDeviceId Then Devices.ValueUpdate(iDeviceId, aEachValue[0], aEachValue[1], aEachValue[3], sForecast)
          Endif
        Else If Left(sSensor, 2) = "th" Then ' th0 - th39
          aEachValue = Scan(sValues, "* * *")
          If aEachValue.Count = 3 Then
            iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), UCase(Left(sSensor, 2)))
            If iDeviceId Then Devices.ValueUpdate(iDeviceId, aEachValue[0], aEachValue[1], aEachValue[2], "")
          Endif
        Else If Left(sSensor, 1) = "t" Then ' t0 - t39
          iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), Left(sSensor, 1))
          If iDeviceId Then Devices.ValueUpdate(iDeviceId, sValues, "", "", "")
        Else If Left(sSensor, 4) = "wind" Then ' wind0 - wind19
          aEachValue = Scan(sValues, "* * * *")
          If aEachValue.Count = 4 Then
            iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), UCase(Left(sSensor, 4)))
            If iDeviceId Then Devices.ValueUpdate(iDeviceId, aEachValue[0], aEachValue[1], aEachValue[2], aEachValue[3])
          Endif
        Else If Left(sSensor, 4) = "rain" Then ' rain0 - rain19
          aEachValue = Scan(sValues, "* * *")
          If aEachValue.Count = 3 Then
            iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), UCase(Left(sSensor, 4)))
            If iDeviceId Then Devices.ValueUpdate(iDeviceId, aEachValue[0], aEachValue[1], aEachValue[2], "")
          Endif
        Else If Left(sSensor, 2) = "uv" Then ' uv0 - uv19
          iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), UCase(Left(sSensor, 2)))
          If iDeviceId Then Devices.ValueUpdate(iDeviceId, sValues, "", "", "")
        Else If Left(sSensor, 3) = "sol" Then ' sol0 - sol19
          iDeviceId = Devices.Find(sSensor, Devices.FindInterface("Meteohub Interface"), UCase(Left(sSensor, 3)))
          If iDeviceId Then Devices.ValueUpdate(iDeviceId, sValues, "", "", "")
        Else
          If Main.bMeteohubDebug Then Main.WriteDebugLog(("[Meteohub] Data from unsupported device type '") & sSensor & ("' received."))
        Endif
    Endif
  Next

Catch
  Main.WriteDebugLog("[Meteohub] Error parsing Meteohub data " & ERROR.Text & " at " & ERROR.Where)

End

Public Sub hMeteohub_Error()

  Main.WriteDebugLog(("[Meteohub] Error fetching meteohub data: ") & hMeteohub.Status)
  Try hMeteohub.Close

End

' implement properties
Private Function FetchURL_Read() As String

  Return sFetchURL

End

Private Sub FetchURL_Write(Value As String)

  sFetchURL = Value

End

Private Function Polltime_Read() As Integer

  Return iPolltime

End

Private Sub Polltime_Write(Value As Integer)

  iPolltime = Value

End

Private Function MeteohubDebug_Read() As Boolean

  Return bMeteohubDebug

End

Private Sub MeteohubDebug_Write(Value As Boolean)

  bMeteohubDebug = Value

End
