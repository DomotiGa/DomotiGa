' Gambas class file

' Description:
' CWebServer.class
' Built-in -very basic- webserver support.

' Development Status:
' Only some test pages available, not suited for high load/rich pages.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2010 Ron Klinkien

' Read file called COPYING for license details.

PROPERTY DocRoot AS String
PROPERTY HTTPPort AS String
PROPERTY WebServerDebug AS Boolean

PRIVATE sDocRoot AS String
PRIVATE sHTTPPort AS String
PRIVATE bWebServerDebug AS Boolean

PUBLIC hWebServer AS ServerSocket

PRIVATE oClients AS NEW Object[]

PUBLIC FUNCTION Connect() AS Boolean

  hWebServer = NEW ServerSocket AS "WebServer"
  hWebServer.Type = Net.Internet
  hWebServer.Port = sHTTPPort
  hWebserver.Listen(0)

  IF hWebServer.Status = Net.Active THEN
    RETURN TRUE
  END IF
  RETURN FALSE

END

' shutdown our web server
PUBLIC SUB Disconnect()

  hWebServer.Close()

END

PUBLIC SUB WebServer_Connection(RemoteHostIP AS String)

  DIM hSocket AS Socket

  IF hWebServer.Status <= Net.Active THEN RETURN
  hSocket = hWebServer.Accept()
  oClients.Add(hSocket)
  Main.WriteDebugLog(("[WebServer] HTTP connect from client with ip address ") & RemoteHostIP & " (" & DNS.ResolveHost(RemoteHostIP) & ")")

END

PUBLIC SUB WebServer_Error()

  Main.WriteLog(("HTTP Error: Unable to bind socket."))

END

PUBLIC SUB Socket_Read()

  DIM sBuffer, sReplyHeader, sHtmlPage AS String
  DIM i, j AS Integer

  IF LAST.Status <> Net.Connected THEN RETURN
  READ #LAST, sBuffer, Lof(LAST)
  sReplyHeader = "HTTP/1.1 200/OK\r\nContent-type:text/html\r\n\r\n"
  TRY WRITE #LAST, sReplyHeader, Len(sReplyHeader)
  IF NOT ERROR THEN
    i = String.InStr(sBuffer, "GET /")
    j = String.InStr(sBuffer, "HTTP")

    IF i > 0 AND j > (i + 5) THEN
      sBuffer = Trim(String.Mid(sBuffer, i + 5, j - i - 5))
      ' display default index page
      IF NOT sBuffer THEN sBuffer = "index.html"
      TRY sHtmlPage = File.Load(sDocRoot &/ sBuffer)
      sHtmlPage = Replace(sHtmlPage, "##APPLICATION##", Application.Name)
      sHtmlPage = Replace(sHtmlPage, "##VERSION##", Main.sProgramVersion)
      IF ERROR THEN
        Main.WriteDebugLog(("[WebServer] HTTP document not found: ") & sBuffer & " " & Error.Text)
      ENDIF
    ENDIF
  ENDIF

  IF NOT sHtmlPage THEN
    sHtmlPage = ("Web page not found.")
    Main.WriteDebugLog("[WebServer] " & sHtmlPage)
  END IF

  TRY WRITE #LAST, sHtmlPage, Len(sHtmlPage)
  LAST.Close
  oClients.Remove(oClients.Find(LAST))

END

PRIVATE SUB PrintPage(sPart AS String) AS String

  DIM sContent AS String

  TRY sContent = File.Load(sDocRoot &/ "page" &/ sPart)
  RETURN sContent

END

PUBLIC SUB Socket_Closed()

  oClients.Remove(oClients.Find(LAST))

END

PRIVATE FUNCTION DocRoot_Read() AS String

  RETURN sDocRoot

END

PRIVATE SUB DocRoot_Write(Value AS String)

  sDocRoot = Value

END

PRIVATE FUNCTION HTTPPort_Read() AS String

  RETURN sHTTPPort

END

PRIVATE SUB HTTPPort_Write(Value AS String)

  sHTTPPort = Value

END

PRIVATE FUNCTION WebserverDebug_Read() AS Boolean

  RETURN bWebserverDebug

END

PRIVATE SUB WebserverDebug_Write(Value AS Boolean)

  bWebserverDebug = Value

END
