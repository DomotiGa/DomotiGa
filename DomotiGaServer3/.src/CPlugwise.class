' Gambas class file

' Description:
' CPlugwise.class
' Use Plugwise to control devices.

' Development Status:
' Working fine, only need to add support for reading logbuffers from plugs.

' Links:
' http://www.plugwise.nl
' http://mindprod.com/jgloss/crc.html
' http://www.maartendamen.com/?p=359

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Property Port As String
Property PlugwiseDebug As Boolean
Property PollTime As Integer

Private sPort As String
Private bPlugwiseDebug As Boolean
Private iPollTime As Integer

Public hPlugwise As New SerialPort
Public tPlugwisePoll As Timer
Public tPlugwiseBusy As Timer

' calibration data
Private cGainA As New Collection
Private cGainB As New Collection
Private cOffNoise As New Collection
Private cOffTot As New Collection
Private cLogAddress As New Collection

' queue and command buffer stuff
Private cQueue As New Collection    ' command buffer
Private bStickBusy As Boolean       ' waiting for delay timer to finish
Private bHeaderFound As Boolean = False
Private bFooterFound As Boolean = False
Private sRecBuf As String

' CRC-16(CCITT) table with Poly = 0x10210
' [GB2:ARRD] PRIVATE aCRCTable AS Integer[256]
Private aCRCTable As New Integer[256]
Private bTableInit As Boolean

Const STICKINITCODE As String = "000A"
Const COMMANDRESPONSECODE As String = "0011"
Const ACKRESPONSECODE As String = "0000"
Const POWERCHANGECODE As String = "0017"
Const DEVICEINFOCODE As String = "0023"
Const DEVICEINFORESPONSECODE As String = "0024"
Const CALIBRATIONCODE As String = "0026"
Const CALIBRATIONRESPONSECODE As String = "0027"
Const POWERINFOCODE As String = "0012"
Const POWERINFORESPONSECODE As String = "0013"
Const LOGINFOCODE As String = "0048"
Const LOGADDRESSOFFSET As Integer = 278528
Const POWERLOGRESPONSECODE As String = "0049"

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the port
  Try hPlugwise.Close

  ' get a new one
  hPlugwise = New Serialport As "Plugwise"

  With hPlugwise
    .PortName = sPort
    .Speed = 115200
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  End With

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try stop running timers
  Try tPlugwisePoll.Stop
  Try tPlugwiseBusy.Stop

  ' try to close the connection
  hPlugwise.Close
  Main.WriteLog(("Plugwise serial port close."))

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

Public Sub Run()

  ' start poll timer for power usage check
  tPlugwisePoll = New Timer As "tPlugwisePoll"
  tPlugwisePoll.Delay = iPollTime * 1000 ' multiply for seconds
  tPlugwisePoll.Start

  ' define timer for Plugwise stick busy
  tPlugwiseBusy = New Timer As "tPlugwiseBusy"
  tPlugwiseBusy.Stop

  InitStick()
  CheckPlugs()

End

Public Sub tPlugwisePoll_Timer()

  CheckPlugs()

End

Private Function StoreCircle(sMAC As String, fGainA As Float, fGainB As Float, fOffTot As Float, fOffNoise As Float)

  ' simple try to add it
  Try cGainA.Add(fGainA, sMAC)
  Try cGainB.Add(fGainB, sMAC)
  Try cOffTot.Add(fOffTot, sMAC)
  Try cOffNoise.Add(fOffNoise, sMAC)

End

Private Function GetPackets(sRaw As String) As String[]

  Dim rReg As New Regexp
  Dim aMatches As New String[]
  Dim iCnt As Integer

  rReg.Compile("\x03\x03(\\w+)\r\n")
  rReg.Exec(sRaw)

  For iCnt = 0 To rReg.SubMatches.Count - 1
    aMatches.Add(rReg.SubMatches[iCnt].Text)
  Next
  Return aMatches

End

Private Function ProcessReceivedPacket(sInput As String)

  Dim sType, sPacket, sCRC As String
  Dim iBegin As Integer
  Dim aPackets As String[]

  ' reset for next data
  sRecBuf = ""
  bHeaderFound = False
  bFooterFound = False

  aPackets = GetPackets(sInput)

  For Each sPacket In aPackets

    ' find and store header position
    iBegin = InStr(sPacket, Chr(3) & Chr(3))

    ' get response type from packet
    sType = Mid(sPacket, iBegin + 2, 4)

    ' checksum check
    sCRC = Mid(sPacket, Len(sPacket) - 5, 4)
    If sCRC = CalculateCRC(Mid(sPacket, iBegin + 2, Len(sPacket) - iBegin - 7)) Then
      If bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] < " & Mid(sPacket, 5, Len(sPacket) - 6) & " (CRC OK)"))
    Else
      If bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] < " & Mid(sPacket, 5, Len(sPacket) - 6) & " (CRC Error, discarding data)"))
      Return
    Endif

    ' strip header and type
    sPacket = Mid(sPacket, iBegin + 6, Len(sPacket) - 8)
    Select sType
      Case CALIBRATIONRESPONSECODE
        ProcessCalibrationResponse(sPacket)

      Case POWERINFORESPONSECODE
        ProcessPowerInfoResponse(sPacket)

      Case DEVICEINFORESPONSECODE
        ProcessDeviceInfoResponse(sPacket)

      Case ACKRESPONSECODE
        ProcessAckResponse(sPacket)

      Case COMMANDRESPONSECODE
        ProcessCommandResponse(sPacket)

      Case POWERLOGRESPONSECODE
        ProcessPowerLogResponse(sPacket)
      Case Else
        If bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] Received unknown response type! (" & sType & ")"))
     End Select
  Next

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub ProcessReceivedChar(sTemp As String)

  If sTemp = Chr(5) Then bHeaderFound = True
  If sTemp = Chr(10) Then bFooterFound = True
  sRecBuf &= sTemp
  If bFooterFound = True And If bHeaderFound = True Then ProcessReceivedPacket(sRecbuf)

End

Private Sub CalculateLogHistory(sPulses As String, sMAC As String) As String

  Dim iPulses As Integer
  Dim fCorrectedPulses, fkWh As Float

  ' no valid data
  If sPulses = "FFFFFFFF" Then Return "?"

  ' calculate actual usage in Watt
  iPulses = Val("&H" & sPulses) / 1.0
  fCorrectedPulses = CorrectPulses(iPulses, sMAC)
  fkWh = (fCorrectedPulses / 3600) / 468.9385193

  Return Format(CStr(fkWh), "0.000")

End

Private Sub ProcessPowerLogResponse(sResult As String)

  Dim sMAC, sLogDate1, sLogDate2, sLogDate3, sLogDate4 As String
  Dim sLogPulse1, sLogPulse2, sLogPulse3, sLogPulse4 As String

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Received Power Log response!"))
    Main.WriteDebugLog(("[Plugwise] Sequence Number: ") & Mid(sResult, 1, 4))
    sMAC = Mid(sResult, 5, 16)
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    sLogDate1 = TimeSinceLastMonth(Mid(sResult, 21, 8))
    sLogPulse1 = CalculateLogHistory(Mid(sResult, 29, 8), sMAC)
    sLogDate2 = TimeSinceLastMonth(Mid(sResult, 37, 8))
    sLogPulse2 = CalculateLogHistory(Mid(sResult, 45, 8), sMAC)
    sLogDate3 = TimeSinceLastMonth(Mid(sResult, 53, 8))
    sLogPulse3 = CalculateLogHistory(Mid(sResult, 61, 8), sMAC)
    sLogDate4 = TimeSinceLastMonth(Mid(sResult, 69, 8))
    sLogPulse4 = CalculateLogHistory(Mid(sResult, 77, 8), sMAC)

    ' firstbuf=00004224  (First buffer - Hours passed since 1 jun 2007 02:00)
    Main.WriteDebugLog(("[Plugwise] Log1: ") & sLogPulse1 & " kWh at " & sLogDate1)
    Main.WriteDebugLog(("[Plugwise] Log2: ") & sLogPulse2 & " kWh at " & sLogDate2)
    Main.WriteDebugLog(("[Plugwise] Log3: ") & sLogPulse3 & " kWh at " & sLogDate3)
    Main.WriteDebugLog(("[Plugwise] Log4: ") & sLogPulse4 & " kWh at " & sLogDate4)
  Endif

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub ProcessDeviceInfoResponse(sResult As String)

  Dim sMAC, sRelay, sState, sLogAddress, sType, sHW, sTime As String
  Dim iLogAddress, iDeviceId As Integer

  sMAC = Mid(sResult, 5, 16)
  sRelay = Mid(sResult, 37, 2)
  sState = IIf(sRelay = "01", "On", "Off")
  sTime = Mid(sResult, 21, 8)
  sLogAddress = Mid(sResult, 29, 8)
  iLogAddress = (Val("&H" & sLogAddress & "&") - LOGADDRESSOFFSET) / 32
  sType = Mid(sResult, 61, 2)
  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Received Device Info response!"))
    Main.WriteDebugLog(("[Plugwise] Sequence Number: ") & Mid(sResult, 1, 4))
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] Relay State: ") & sRelay & " " & sState)
    Main.WriteDebugLog(("[Plugwise] Time in Circle: ") & TimeSinceLastMonth(sTime))
    Main.WriteDebugLog(("[Plugwise] Log Address: ") & iLogAddress & " Hex: " & sLogAddress)
    Main.WriteDebugLog(("[Plugwise] Frequency: ") & GetFreq(Mid(sResult, 39, 2)) & "Hz")
    Main.WriteDebugLog(("[Plugwise] Hardware version: ") & Mid(sResult, 41, 4) & "-" & Mid(sResult, 45, 4) & "-" & Mid(sResult, 49, 4))
    Main.WriteDebugLog(("[Plugwise] Firmware: ") & TimeSinceEpoch(Mid(sResult, 53, 8)))
    Select sType
      Case "00"
        sHW = "Stick"
      Case "01"
        sHW = "Circle+"
      Case "02"
        sHW = "Circle"
    End Select
    Main.WriteDebugLog(("[Plugwise] Hardware Type: ") & sHW)
  Endif

  ' store logaddress
  cLogAddress.Add(iLogAddress, sMAC)

  ' update device state
  iDeviceId = Devices.Find(sMAC, Devices.FindInterface("Plugwise Stick"), UCase("Plugwise Circle"))
  If iDeviceId Then Devices.ValueUpdate(iDeviceId, sState, "", "", "")

  ' now that the logaddress is known get the power logs too
  GetPowerLogs(sMAC)

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub ProcessCommandResponse(sResult As String)

  Dim sMAC, sOnline, sID, sSID As String

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Received Command response!"))
    sMAC = Mid(sResult, 5, 16)
    sOnline = Mid(sResult, 23, 2)
    sID = Mid(sResult, 25, 16)
    sSID = Mid(sResult, 41, 4)
    Main.WriteDebugLog(("[Plugwise] Sequence Number: ") & Mid(sResult, 1, 4))
    Main.WriteDebugLog(("[Plugwise] MAC Address Stick: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] Network is: ") & IIf(sOnline = "01", "Online", "Offline"))
    Main.WriteDebugLog(("[Plugwise] Unique Network ID: ") & sID)
    Main.WriteDebugLog(("[Plugwise] Short Network ID: ") & sSID)
  Endif

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub ProcessAckResponse(sResult As String)

  Dim sAckCode As String

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Received Ack response!"))
    sAckCode = Mid(sResult, 5, 4)
    Main.WriteDebugLog(("[Plugwise] Sequence Number: ") & Mid(sResult, 1, 4))
    Main.WriteDebugLog(("[Plugwise] Acknowledgement: ") & sAckCode & IIf(sAckCode = "00C1", " (OK)", " (ERROR)"))
  Endif

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub ProcessCalibrationResponse(sResult As String)

  Dim sMAC As String
  Dim fGainA, fGainB, fOffTot, fOffNoise As Float

  sMAC = Mid(sResult, 5, 16)
  fGainA = HexToFloat(Mid(sResult, 21, 8))
  fGainB = HexToFloat(Mid(sResult, 29, 8))
  fOffTot = HexToFloat(Mid(sResult, 37, 8))
  fOffNoise = HexToFloat(Mid(sResult, 45, 8))

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Received Calibration response!"))
    Main.WriteDebugLog(("[Plugwise] Sequence Number: ") & Mid(sResult, 1, 4))
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] GainA: ") & Str(fGainA))
    Main.WriteDebugLog(("[Plugwise] GainB: ") & Str(fGainB))
    Main.WriteDebugLog(("[Plugwise] OffTot: ") & Str(fOffTot))
    Main.WriteDebugLog(("[Plugwise] OffNoise: ") & Str(fOffNoise))
  Endif

  ' store calibration data
  StoreCircle(sMAC, fGainA, fGainB, fOffTot, fOffNoise)

  ' get power info now calibration data is known
  GetPowerInfo(sMAC)

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub ProcessPowerInfoResponse(sResult As String)

  Dim sMAC, sPulses, sPulses8s, sWatt, skW As String
  Dim iPulses, iDeviceId As Integer
  Dim fCorrectedPulses, fkW As Float

  sMAC = Mid(sResult, 5, 16)
  sPulses = Mid(sResult, 21, 4)
  sPulses8s = Mid(sResult, 25, 4)

  ' calculate actual usage in Watt
  iPulses = Val("&H" & sPulses) / 1.0
  fCorrectedPulses = CorrectPulses(iPulses, sMAC)
  fkW = (fCorrectedPulses / 1) / 468.9385193
  skW = Format(CStr(fkW), "0.00")
  sWatt = Format(CStr(fkW * 1000), "0.00")

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Received Power Info response!"))
    Main.WriteDebugLog(("[Plugwise] Sequence Number: ") & Mid(sResult, 1, 4))
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] Pulses: ") & iPulses & " Hex: " & sPulses & " (Corrected: " & fCorrectedPulses & ")")
    Main.WriteDebugLog(("[Plugwise] Usage: ") & sWatt & " Watt (" & skW & " kW)")
  Endif

  ' update device state
  iDeviceId = Devices.Find(sMAC, Devices.FindInterface("Plugwise Stick"), UCase("Plugwise Circle"))
  If iDeviceId Then Devices.ValueUpdate(iDeviceId, "", sWatt, skW, "")

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub CorrectPulses(iPulses As Integer, sMAC As String) As Float

  Dim fTemp, fCorrectedPulses As Float

  fTemp = iPulses + cOffNoise[sMAC]
  If iPulses Then fCorrectedPulses = 1.0 * ((((fTemp) ^ 2.0 * cGainB[sMAC]) + ((fTemp) * cGainA[sMAC])) + cOffTot[sMAC])

  Return fCorrectedPulses

End

Private Sub InitStick()

  Dim sPacket, sCmd, sCRC As String
  Dim sHeader As String = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  Dim sFooter As String = Chr(13) & Chr(10)

  If hPlugwise.Status <> Net.Active Then Return

  ' <ENQ><ENQ><ETX><ETX>000AB43C<CR><LF>
  sCmd = STICKINITCODE
  sCRC = CalculateCRC(sCmd)
  sPacket = sHeader & sCmd & sCRC & sFooter

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Initializing Stick"))
    Main.WriteDebugLog(("[Plugwise] Cmd: ") & sCmd & (" CRC: ") & sCRC)
  Endif
  QueueCommand(sPacket)

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' add command to queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub QueueCommand(sPacket As String)

  ' add command to queue
  cQueue.Add(sPacket, Rnd)

  ' if stick is not busy process it
  If bStickBusy = False Then DoQueue()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process items in queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub DoQueue()

  Dim sPacket As String

  For Each sPacket In cQueue
    WriteCommand(sPacket, cQueue.Key)
    Break ' only do first one
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send packet to plugwise stick
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub WriteCommand(sBuffer As String, sKey As String)

  If bPlugwiseDebug Then Main.WriteDebugLog("[Plugwise] > " & Mid(sBuffer, 5, Len(sBuffer) - 6))

  Try Print #hPlugwise, sBuffer
  If Error Then Main.WriteDebugLog(("[Plugwise] Error writing data to the serial port! ->") & ERROR.Text)

  ' remove sent command from queue
  cQueue.Remove(sKey)

  ' start timer to wait before sending next command
  tPlugwiseBusy.Delay = 50 ' 50mS (initial test value)
  tPlugwiseBusy.Start
  bStickBusy = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' stick busy reset
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tPlugwiseBusy_Timer()

  bStickBusy = False
  tPlugwiseBusy.Stop

  If cQueue.Count Then ' if there are more commands left in queue, send next one
    If bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] Sending command from Queue (count = " & cQueue.Count & ")"))
    DoQueue()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read data char for char
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Plugwise_Read()

  Dim sPacket As String

  Try Read #hPlugwise, sPacket, 1
  If Not Error Then
    ProcessReceivedChar(sPacket)
  Else
    Main.WriteDebugLog(("[Plugwise] Error reading data from the serial port! ->") & ERROR.Text)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' loop through plugwise devices in database and collect their data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CheckPlugs()

  Dim rResult As Result

  Try rResult = Devices.FindDevicesForInterface(Devices.FindInterface("Plugwise Stick"))
  If rResult.Count Then
    For Each rResult
      If Main.bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] Checking power usage of device '") & rResult!name & ("' with address ") & rResult!address)
      GetPowerInfo(rResult!address)
      If Main.bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] Checking relay state of device '") & rResult!name & ("' with address ") & rResult!address)
      GetDeviceInfo(rResult!address)
    Next
  Else
    Main.WriteDebugLog(("[Plugwise] No Circles found in the database, please add them."))
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' control plugs
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(sAddress As String, sCmd As String)

  Dim sPacket, sCommand, sFunc, sCRC, sMAC As String
  Dim sHeader As String = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  Dim sFooter As String = Chr(13) & Chr(10)

  sMAC = Replace(sAddress, ":", "")
  sFunc = IIf(UCase(sCmd) = "ON", "01", "00")

  ' <ENQ><ENQ><ETX><ETX>0017000A1100003111AB01AC92<CR><LF>
  sHeader = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  sCommand = POWERCHANGECODE & sMAC & sFunc
  sCRC = CalculateCRC(sCommand)
  sPacket = sHeader & sCommand & sCRC & sFooter

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Queue Power Change:"))
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] Cmd String: ") & sCommand & (" CRC: ") & sCRC)
  Endif
  QueueCommand(sPacket)

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' control plugs
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub GetPowerLogs(sAddress As String)

  Dim sPacket, sCmd, sCRC, sMAC, sLogAddress As String
  Dim sHeader As String = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  Dim sFooter As String = Chr(13) & Chr(10)
  Dim iLogAddress As Integer

  sMAC = Replace(sAddress, ":", "")

  If cLogAddress.Exist(sMAC) Then
    iLogAddress = LOGADDRESSOFFSET + (32 * cLogAddress[sMAC] - 1)
    sLogAddress = Hex(ilogaddress, 8)
    sCmd = LOGINFOCODE & sMAC & sLogAddress
    sCRC = CalculateCRC(sCmd)
    sPacket = sHeader & sCmd & sCRC & sFooter

    If bPlugwiseDebug Then
      Main.WriteDebugLog(("[Plugwise] Queue Power Logs:"))
      Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
      Main.WriteDebugLog(("[Plugwise] Log Address: ") & iLogAddress & " Hex: " & sLogAddress)
      Main.WriteDebugLog(("[Plugwise] Cmd String: ") & sCmd & (" CRC: ") & sCRC)
    Endif
    QueueCommand(sPacket)
  Else
    If bPlugwiseDebug Then
      Main.WriteDebugLog(("[Plugwise] Queue Power Logs:"))
      Main.WriteDebugLog(("[Plugwise] Log Address: Unknown, skipping."))
    Endif
  Endif

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub GetDeviceInfo(sAddress As String)

  Dim sPacket, sCmd, sCRC, sMAC As String
  Dim sHeader As String = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  Dim sFooter As String = Chr(13) & Chr(10)

  sMAC = Replace(sAddress, ":", "")

  sCmd = DEVICEINFOCODE & sMAC
  sCRC = CalculateCRC(sCmd)
  sPacket = sHeader & sCmd & sCRC & sFooter

  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] Queue Device Info:"))
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] Cmd String: ") & sCmd & (" CRC: ") & sCRC)
  Endif
  QueueCommand(sPacket)

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub GetPowerInfo(sAddress As String)

  Dim sPacket, sCmd, sCRC, sMAC As String
  Dim sHeader As String = Chr(5) & Chr(5) & Chr(3) & Chr(3)
  Dim sFooter As String = Chr(13) & Chr(10)

  sMac = Replace(sAddress, ":", "")

  If Not cGainA.Exist(sMAC) Then ' get calibration info first
    ' <ENQ><ENQ><ETX><ETX>0026000D6F00002366BB7071<CR><LF>
    sCmd = CALIBRATIONCODE & sMAC
    If bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] Queue Calibration Request:"))
  Else
    ' <ENQ><ENQ><ETX><ETX>0012000D6F00002366BB338B<CR><LF>
    sCmd = POWERINFOCODE & sMAC
    If bPlugwiseDebug Then Main.WriteDebugLog(("[Plugwise] Queue Power Info Request:"))
  Endif

  sCRC = CalculateCRC(sCmd)
  sPacket = sHeader & sCmd & sCRC & sFooter
  If bPlugwiseDebug Then
    Main.WriteDebugLog(("[Plugwise] MAC Address: ") & sMAC)
    Main.WriteDebugLog(("[Plugwise] Cmd String: ") & sCmd & (" CRC: ") & sCRC)
  Endif
  QueueCommand(sPacket)

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Function GetFreq(sByte As String) As String

  If sByte = "85" Then
    Return "50"
  Else If sByte = "C5" Then
    Return "60"
  Else
    Return "??"
  Endif

End

Private Function TimeSinceLastMonth(sTime As String) As String

  Dim gTime As Date
  Dim iYear, iMonth, iMinutes As Integer

  ' check for valid data
  If Len(sTime) <> 8 Then Return
  If sTime = "FFFFFFFF" Then Return "?"

  iYear = 2000 + Val("&H" & Left(sTime, 2))
  iMonth = Val("&H" & Mid(sTime, 3, 2))
  iMinutes = Val("&H" & Mid(sTime, 5, 4))

  ' add minutes to year/month values
  gTime = DateAdd(Date(iYear, iMonth, 1, 1, 0, 0), gb.Minute, iMinutes)

  Return Format$(gTime, "yyyy/mm/dd hh:nn:ss")

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Function TimeSinceEpoch(sTime As String) As String

  Dim gTime As Date

  If Len(sTime) <> 8 Then Return

  ' calculate time since epoch
  gTime = DateAdd(Date(1970, 1, 1), gb.Second, Val("&H" & sTime & "&"))

  Return Format$(gTime, "yyyy/mm/dd hh:nn:ss")

Catch ' some errors
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Function HexToFloat(sHex As String) As Float

  Dim iSign As Integer
  Dim fExponent, fMant As Float

  If sHex Then
    ' sign
    iSign = IIf((Val("&H" & Mid(sHex, 1, 2)) And &H80) = 128, -1, 1)
    ' exponent
    fExponent = (Val("&H" & Mid(sHex, 1, 3)) And &H7F8) / 2 ^ 3 - 127
    ' mantissa
    fMant = (Val("&H" & Mid(sHex, 3, 6)) And &H7FFFFF) / 2 ^ 23 + 1
  Endif
  Return iSign * fMant * 2 ^ fExponent

Catch
  Main.WriteDebugLog(("Plugwise Error: ") & ERROR.Text & " at " & ERROR.Where & " sHex = " & sHex)

End

Private Function CalculateCRC(sbuf As String) As String

  Dim iWork, iCnt As Integer

  If Not bTableInit Then InitCrcTable()

  iWork = &H0
  For iCnt = 1 To Len(sbuf)
    iWork = (aCRCTable[(Asc(Mid(sBuf, iCnt, 1)) Xor (Lsr(iWork, 8))) And &HFF] Xor (Lsl(iWork, 8))) And &HFFFF&
  Next
  Return Hex(iWork, 4)

End

Private Sub InitCRCTable()

  Dim iTableIndex, iIndex, iAccumulator, iMask As Integer

  For iTableIndex = 0 To 255
    iMask = Lsl(iTableIndex, 8)
    iAccumulator = 0
    For iIndex = 0 To 7
      If ((iMask Xor iAccumulator) And &H8000&) Then
        iAccumulator = Lsl(iAccumulator, 1) Xor &H1021&
      Else
        iAccumulator = Lsl(iAccumulator, 1)
      End If
      iMask = Lsl(iMask, 1)
    Next
    aCRCTable[iTableIndex] = iAccumulator And &HFFFF&
  Next
  bTableInit = True

End

' implement properties
Private Function Port_Read() As String

  Return sPort

End

Private Sub Port_Write(Value As String)

  sPort = Value

End

Private Function PlugwiseDebug_Read() As Boolean

  Return bPlugwiseDebug

End

Private Sub PlugwiseDebug_Write(Value As Boolean)

  bPlugwiseDebug = Value

End

Private Function PollTime_Read() As Integer

  Return iPollTime

End

Private Function PollTime_Write(Value As Integer)

  iPollTime = Value

End
