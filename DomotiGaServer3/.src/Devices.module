' Gambas module file

' Description:
' Devices.module
' Contains code which is device related.

' Development Status:
' Working, maybe needs more error checking.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2010 Ron Klinkien

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' control device via linked interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SetDevice(sDeviceName As String, sValue As String)

  Dim iInterface, iId As Integer
  Dim sText, sAddress As String

  iInterface = FindInterfaceForDevice(sDeviceName)
  sAddress = FindAddressForDevice(sDeviceName)

  Select Case iInterface
    Case 0
      Main.WriteDebugLog(("[Devices] Something is wrong, cannot query interface for this device!"))
    Case 1 ' RFXCom Receiver
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] RFXCom Receiver can't control anything!"))
    Case 2 ' Xanura CTX35
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Xanura CTX35: " & sAddress & " " & sValue)
      If Main.hCTX35 Then
        If Upper(sValue) <> "STOP" Then
          Main.hCTX35.sCommandToSend = sAddress & " " & sValue
          Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
          sText = ("Switched ") & sDeviceName & " " & sValue
        End If
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because CTX35 is disabled!"))
      End If
    Case 3, 4, 5, 6, 8
      Main.WriteDebugLog(("[Devices] Error, read-only interface used for switching!"))
    Case 7 ' Heyu X10
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Heyu X10")
      If Main.bHeyuEnabled Then
        sText = ("Switched ") & sDeviceName & " " & sValue
        Heyu.SendCommand(sAddress, sValue)
        Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because Heyu is disabled!"))
      End If
    Case 9 ' Weeder WTSSR
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Weeder WTSSR: " & sAddress & " " & sValue)
      iId = Find(sAddress, iInterface)
      If Main.hWeeder Then
        If iId Then
          If InStr(FindTypeNameForDevice(iId), "ITHO ECO Fan") Then
            If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] ITHO ECO Fan")
            Main.hWeeder.ControlITHO(sAddress, sValue)
            Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
          End If
        Else
          Main.WriteDebugLog(("[Devices] ITHO ECO Fan not enabled!"))
        End If
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because Weeder is disabled!"))
      End If
    Case 10 ' Plugwise
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Plugwise Stick: " & sAddress & " " & sValue)
      If Main.hPlugwise Then
        Main.hPlugwise.SendCommand(sAddress, sValue)
        Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
        sText = ("Switched ") & sDeviceName & " " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because Plugwise is disabled!"))
      End If
    Case 12 ' RFXCom Transmitter
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] RFXCom Transmitter: " & sAddress & " " & sValue)
      If Main.hRFXComTX Then
        If sValue <> "STOP" Then
          Main.hRFXComTX.SendCommand(sAddress, sValue)
          Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
          sText = ("Switched ") & sDeviceName & " " & sValue
        End If
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because RFXCom Transmitter is disabled!"))
      End If
    Case 13 ' KNX/EIB Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] KNX/EIB Interface: " & sAddress & " " & sValue)
      If Main.hEIB Then
        Main.hEIB.SendCommand(sAddress, sValue)
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because KNX/EIB is disabled!"))
      End If
    Case 15 ' Z-Wave controller
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Z-Wave Interface: " & sAddress & " " & sValue)
      If Main.hZWave Then
        If sValue <> "STOP" Then
          Main.hZWave.SendCommand(sAddress, sValue)
          Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
          sText = ("Switched ") & sDeviceName & " " & sValue
        End If
      Else
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because Z-Wave interface is disabled!"))
      End If
    CASE 16 ' PLCBUS Interface
      IF Main.bDevicesDebug THEN Main.WriteDebugLog("[Devices] PLCBUS Interface: " & sAddress & " " & sValue)
      IF Main.hPLCBUS THEN
        IF sValue <> "STOP" THEN
          Main.hPLCBUS.SendCommand(sAddress, sValue)
          Devices.ValueUpdate(Find(sAddress, iInterface), sValue, "", "", "")
          sText = ("Switched ") & sDeviceName & " " & sValue
        END IF
      ELSE
        Main.WriteDebugLog(("[Devices] Can't control device ") & sDeviceName & (" because PLCBUS interface is disabled!"))
      END IF
    Case -1
      Main.WriteDebugLog(("[Devices] Interface for device '") & sDeviceName & ("' does not support Write!"))
  Case Else
    Main.WriteDebugLog(("[Devices] Unknown Interface type for device '") & sDeviceName & "'!")
  End Select

  ' todo check for devices with log switched off
  If sText Then VoiceText.Speak(sText)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find interface with name sInterfaceName, return it's id.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindInterface(sInterfaceName As String) As Integer

  Dim rInterface As Result

  Try rInterface = Main.hDB.Exec("SELECT * FROM interfaces WHERE name = &1", sInterfaceName)
  If rInterface.Available Then
    If rInterface.Count = 1 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Interface with name '") & sInterfaceName & ("' has id '") & rInterface!id & "'")
      Return rInterface!id
    Else
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Interface with name '") & sInterfaceName & ("' not found!"))
      Return 0
    End If
  Else
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Cannot get Interface records!"))
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find devicetype with type, return it's id.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDeviceType(sDeviceType As String) As Integer

  Dim rDeviceType As Result

  Try rDeviceType = Main.hDB.Exec("SELECT * FROM devicetypes WHERE type LIKE &1", sDeviceType)
  If rDeviceType.Available Then
    If rDeviceType.Count = 1 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] DeviceType with type '") & sDeviceType & ("' has id '") & rDeviceType!id & "'")
      Return rDeviceType!id
    Else
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] DeviceType with type '") & sDeviceType & ("' not found!"))
      Return 0
    End If
  Else
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Cannot get DeviceType records!"))
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return module/devicetype for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindModuleForDevice(iDeviceId As Integer) As Integer

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT module FROM devices WHERE id = &1", iDeviceId)
  If rDevice.Available Then
    Return rDevice!module
  Else
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return type for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindTypeForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result
  Dim iDeviceType As Integer

  iDeviceType = FindModuleForDevice(iDeviceId)
  Try rDevice = Main.hDB.Exec("SELECT type FROM devicetypes WHERE id = &1", iDeviceType)
  Return rDevice!type

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return typename for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindTypeNameForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result
  Dim iDeviceType As Integer

  iDeviceType = FindModuleForDevice(iDeviceId)
  Try rDevice = Main.hDB.Exec("SELECT name FROM devicetypes WHERE id = &1", iDeviceType)
  Return rDevice!name

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return description for devicetype with iId
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDescrForDeviceType(iId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT description FROM devicetypes WHERE id = &1", iId)
  Return rDevice!description

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return name for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindNameForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT name FROM devices WHERE id = &1", iDeviceId)
  Return rDevice!name

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return device id for virtual device with address
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindVirtualDeviceID(sAddress As String) As Integer

  Dim iId As Integer

  iId = Find(sAddress, FindInterface("Virtual Interface"), "Virtual Device")
  Return iId

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return name for interface with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindNameForInterface(iInterfaceId As Integer) As String

  Dim rInterface As Result

  Try rInterface = Main.hDB.Exec("SELECT name FROM interfaces WHERE id = &1", iInterfaceId)
  Return rInterface!name

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return log boolean for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindLogForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT log FROM devices WHERE id = &1", iDeviceId)
  Return rDevice!log

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return hide boolean for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindHideForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT hide FROM devices WHERE id = &1", iDeviceId)
  Return rDevice!hide

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find interface for device with sdeviceName.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindInterfaceForDevice(sDeviceName As String) As Integer

  Dim rDevice, rResult As Result

  Try rDevice = Main.hDB.Exec("SELECT address, name, interface FROM devices WHERE name = &1", sDeviceName)
  ' found only one unique entry?
  If rDevice.Count > 1 Then
    Main.WriteDebugLog(("[Devices] The following devices have same name:"))
    For Each rDevice
      Main.WriteDebugLog(("[Devices] Device with address '") & rDevice!address & "' is '" & rDevice!name & "'")
    Next
    Return 0
  ' ok this one seems useful
  Else If rDevice.Count = 1 Then
    Try rResult = Main.hDB.Exec("SELECT mode FROM interfaces WHERE id = &1", rDevice!interface)
    If InStr(rResult!mode, "Write") Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Interface for device with name '") & sDeviceName & "' is '" & rDevice!interface & "'")
      Return rDevice!interface
    Else
      ' read-only
      Return -1
    End If
  ' no interface found, something is wrong
  Else
    Main.WriteDebugLog(("[Devices] Device with name '") & sDeviceName & ("' not found!"))
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find address for device with sDeviceName.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindAddressForDevice(sDeviceName As String) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT address FROM devices WHERE name = &1", sDeviceName)
  If rDevice.Count > 1 Then
    Main.WriteDebugLog(("[Devices] The following devices have same name:"))
    For Each rDevice
      Main.WriteDebugLog(("[Devices] Device with address '") & rDevice!address & ("' is '") & rDevice!name & "'")
    Next
    Return 0
  Else If rDevice.Count = 1 Then
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Address for device with name '") & sDeviceName & ("' is '") & rDevice!address & "'")
    Return rDevice!address
  Else
    Main.WriteDebugLog(("[Devices] Device with name '") & sDeviceName & ("' not found!"))
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with sName, return it's current value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetValueForDevice(sDeviceName As String) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value, address FROM devices WHERE name = &1", sDeviceName)
  If rDevice.Count > 1 Then
    Main.WriteDebugLog(("[Devices] The following devices have same name:"))
    For Each rDevice
      Main.WriteDebugLog(("[Devices] Device with address '") & rDevice!address & ("' is '") & rDevice!name & "'")
    Next
    Return 0
  Else If rDevice.Count = 1 Then
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Value of device with name '") & sDeviceName & ("' is ") & rDevice!value)
    Return rDevice!value
  Else
    Main.WriteDebugLog(("[Devices] Device with name '") & sDeviceName & ("' not found!"))
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentValueForDevice(iId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!value
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current value2
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentValue2ForDevice(iId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value2 FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!value2
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current value3
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentValue3ForDevice(iId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value3 FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!value3
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current value4
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentValue4ForDevice(iId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value4 FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!value4
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with sAddress, return id if found.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Find(sAddress As String, iInterface As Integer, Optional sDevice As String) As Integer

  Dim rDevice, rResult As Result

  Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE address LIKE &1 AND interface = &2 AND enabled is TRUE", sAddress, iInterface)
  If rDevice.Count > 1 Then
    Main.WriteDebugLog(("[Devices] The following devices have same address:"))
    For Each rDevice
      Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' is '") & rDevice!name & "'")
    Next
    Return 0
  Else If rDevice.Count = 1 Then
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' is '") & rDevice!name & "'")
    If rDevice!firstseen = "00:00:00" Or If rDevice!firstseen = "" Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and '") & rDevice!name & ("' is a new device"))
      rResult = Main.hDB.Exec("UPDATE devices SET firstseen = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), rDevice!id)
    End If
    ' if it's hidden we return 0, else we return it's id
    If rDevice!hide Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and name '") & rDevice!name & ("' is set to hide"))
      Return 0
    Else
      Return rDevice!id
    End If
  Else
    If Not IsBlacklisted(sAddress, iInterface) Then
      If sDevice Then
        Main.WriteDebugLog(("[Devices] Device '") & sDevice & ("' with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' not found!"))
      Else
        Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' not found!"))
      End If
      Return 0
    Else
      If Main.bDevicesDebug Then
        If sDevice Then
          Main.WriteDebugLog(("[Devices] Device '") & sDevice & ("' with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' is blacklisted!"))
        Else
          Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' is blacklisted!"))
        End If
      End If
    End If
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find out if an device has firstseen filled
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub CheckFirstSeen(iId As Integer)

  Dim rResult, rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    If rDevice!firstseen = "00:00:00" Or If rDevice!firstseen = "" Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & rDevice!address & ("' and '") & rDevice!name & ("' is a new device!"))
      rResult = Main.hDB.Exec("UPDATE devices SET firstseen = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), rDevice!id)
    End If
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find out if address is blacklisted and return true
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub IsBlacklisted(sAddress As String, iInterface As Integer) As Boolean

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT * FROM deviceblacklist WHERE address = &1", sAddress)
  If rDevice.Count Then
    If rDevice!id Then
      If iInterface = rDevice!id Then
        If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Address '") & sAddress & ("' with Interface '") & FindNameForInterface(iInterface) & ("' is blacklisted, so I ignore it."))
        Return True
      Else
        Return False
      End If
    End If
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Address '") & sAddress & ("' is blacklisted, so I ignore it."))
    Return True
  End If
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update device with iId with vValue, vValue2 etc
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ValueUpdate(iId As Integer, vValue As Variant, vValue2 As Variant, vValue3 As Variant, vValue4 As Variant) As Boolean

  Dim rResult, rDevice As Result
  Dim bUpdated As Boolean
  Dim sLabel, sLabel2, sLabel3, sLabel4, sValue, sValue2, sValue3, sValue4, sSql As String

  If vValue = "Dim 100" Then vValue = "On"
  If vValue = "Dim 0" Then vValue = "Off"

  rDevice = Main.hDB.Exec("SELECT name, label, label2, label3, label4, value, value2, value3, value4 FROM devices WHERE id = &1", iId)
  If rDevice.Count = 1 Then
    sLabel = rDevice!label
    sLabel2 = rDevice!label2
    sLabel3 = rDevice!label3
    sLabel4 = rDevice!label4
    sValue = rDevice!value
    sValue2 = rDevice!value2
    sValue3 = rDevice!value3
    sValue4 = rDevice!value4
  End If

  If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] ValueUpdate() called for device with id '") & iId & ("' and name '") & rDevice!name & "'")
  If vValue Then
    If Comp(vValue, sValue) <> 0 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device change triggered for device with id '") & iId & ("' and value1 '") & vValue & "'")
      EventLoop.DeviceChanged(iId, "Value", vValue, sLabel)
      bUpdated = True
      rResult = Main.hDB.Exec("UPDATE devices SET lastchanged = &1, lastseen = &2, value = &3 WHERE id = &4", Format(Now(), "yyyy-mm-dd hh:nn:ss"), Format(Now(), "yyyy-mm-dd hh:nn:ss"), vValue, iId)
    End If
  End If
  If vValue2 Then
    If Comp(vValue2, sValue2) <> 0 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device change triggered for device with id '") & iId & ("' and value2 '") & vValue2 & "'")
      EventLoop.DeviceChanged(iId, "Value2", vValue2, sLabel2)
      bUpdated = True
      rResult = Main.hDB.Exec("UPDATE devices SET lastchanged = &1, lastseen = &2, value2 = &3 WHERE id = &4", Format(Now(), "yyyy-mm-dd hh:nn:ss"), Format(Now(), "yyyy-mm-dd hh:nn:ss"), vValue2, iId)
    End If
  End If
  If vValue3 Then
    If Comp(vValue3, sValue3) <> 0 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device change triggered for device with id '") & iId & ("' and value3 '") & vValue3 & "'")
      EventLoop.DeviceChanged(iId, "Value3", vValue3, sLabel3)
      bUpdated = True
      rResult = Main.hDB.Exec("UPDATE devices SET lastchanged = &1, lastseen = &2, value3 = &3 WHERE id = &4", Format(Now(), "yyyy-mm-dd hh:nn:ss"), Format(Now(), "yyyy-mm-dd hh:nn:ss"), vValue3, iId)
    End If
  End If
  If vValue4 Then
    If Comp(vValue4, sValue4) <> 0 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device change triggered for device with id '") & iId & ("' and value4 '") & vValue4 & "'")
      EventLoop.DeviceChanged(iId, "Value4", vValue4, sLabel4)
      bUpdated = True
      rResult = Main.hDB.Exec("UPDATE devices SET lastchanged = &1, lastseen = &2, value4 = &3 WHERE id = &4", Format(Now(), "yyyy-mm-dd hh:nn:ss"), Format(Now(), "yyyy-mm-dd hh:nn:ss"), vValue4, iId)
    End If
  End If
  If Not bUpdated Then
    rResult = Main.hDB.Exec("UPDATE devices SET lastseen = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), iId)
  End If

  Main.RefreshDeviceList()

  If rResult Then
    Return True
  Else
    Return False
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return description for devicetype with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDescriptionForID(iID As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT description FROM devicetypes WHERE id = &1", iID)
  If rDevice.Available Then
    Return rDevice!description
  Else
    Return 0
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update battery status for device with iId
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Battery(iId As Integer, vValue As Variant) As Boolean

  Dim rResult As Result

  rResult = Main.hDB.Exec("UPDATE devices SET batterystatus = &1 WHERE id = &2", vValue, iId)

  If rResult.Count Then
    Return True
  Else
    Return False
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' mail battery status for device with battery status set
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function MailBatteryStatus()

  Dim rDevice As Result
  Dim sMessageBody As String
  Dim bFound As Boolean

  Try rDevice = Main.hDB.Exec("SELECT name, address, batterystatus FROM devices WHERE enabled is TRUE")
  If rDevice.Available Then
    If rDevice.Count Then
      For Each rDevice
        If rDevice!batterystatus Then 
          sMessageBody = sMessageBody & "\r" & rDevice!address & " - " & rDevice!name & "  " & rDevice!batterystatus
          bFound = True
        End If
      Next
      ' sent e-mail with lowbatt status
      If bFound Then Mail.SendMail(Application.Name & (": Battery Status"), ("These Devices have their Battery status set: \n") & sMessageBody, Main.sEmailToAddress)
    End If
  End If

End
