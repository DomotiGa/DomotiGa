' Gambas class file

' Description:
' YouLess.class
' Provide support for fetching data of a YouLess device.

' Development Status:
' Just started to build this one.

' Links:
' http://wiki.td-er.nl/index.php?title=YouLess

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Property FetchURL As String
Property Polltime As Integer
Property YouLessDebug As Boolean

Private sFetchURL As String
Private iPolltime As Integer
Private bYouLessDebug As Boolean

Private hYouLess As New HttpClient
Private tYouLess As Timer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start the poll timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the connection
  Try hYouLess.Close

  ' start poll timer for YouLess
  tYouLess = New Timer As "tYouLess"
  tYouLess.Delay = iPolltime * 1000 ' multiply for seconds
  tYouLess.Start

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("YouLess Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' stop timer and close handle
  Try tYouLess.Stop
  Try hYouLess.Close
  Main.WriteLog(("YouLess closed."))

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("YouLess Weather Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tYouLess_Timer()

  hYouLess = New HttpClient As "hYouLess"
  hYouLess.URL = sFetchURL
  hYouLess.TimeOut = 8
  hYouLess.Async = True
  hYouLess.Get()
  If Main.bYouLessDebug Then Main.WriteDebugLog("[YouLess] Fetching '" & sFetchUrl & "'")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' meteohub data fetched
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hYouLess_Finished()

  Dim sBuffer As String

  Select hYouLess.Code
    Case 200
      If Lof(hYouLess) Then Read #hYouLess, sBuffer, Lof(hYouLess)
      If Main.bYouLessDebug Then Main.WriteDebugLog("[YouLess] Received '" & Replace(sBuffer, "\n", "") & "'")
      ParseData(sBuffer)
    Case Else
      Main.WriteDebugLog(("[YouLess] Unknown error occured while trying to get YouLess data!"))
  End Select
  Try hYouLess.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse raw data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ParseData(sData As String)

  Dim iDeviceId As Integer
  Dim cData As Collection
  Dim sCon, sPwr, sCnt As String

  cData = JSON.Decode(sData)
  sCnt = Replace(Trim(cData["cnt"]), ",", ".")
  sPwr = cData["pwr"]
  sCon = cData["con"]
  If Main.bYouLessDebug Then
    Main.WriteDebugLog("[YouLess] Counter: " & sCnt & " kWh")
    Main.WriteDebugLog("[YouLess] Usage: " & sPwr & " Watt")
    Main.WriteDebugLog("[YouLess] Status: " & sCon)
  Endif

  If sCon = "OK" Then
    ' find device id
    iDeviceId = Devices.Find("1", Devices.FindInterface("YouLess Interface"), "YOULESS")
    ' update device
    If iDeviceId Then
      Devices.ValueUpdate(iDeviceId, sCnt, sPwr, sCon, "")
    Endif
  Else
    If Main.bYouLessDebug Then Main.WriteDebugLog(("[YouLess] Connection status not OK, skipping readings!"))
  Endif

Catch
  Main.WriteDebugLog("[YouLess] Error parsing YouLess data " & ERROR.Text & " at " & ERROR.Where)

End

Public Sub hYouLess_Error()

  Main.WriteDebugLog(("[YouLess] Error fetching YouLess data: ") & hYouLess.Status)
  Try hYouLess.Close

End

' implement properties
Private Function FetchURL_Read() As String

  Return sFetchURL

End

Private Sub FetchURL_Write(Value As String)

  sFetchURL = Value

End

Private Function Polltime_Read() As Integer

  Return iPolltime

End

Private Sub Polltime_Write(Value As Integer)

  iPolltime = Value

End

Private Function YouLessDebug_Read() As Boolean

  Return bYouLessDebug

End

Private Sub YouLessDebug_Write(Value As Boolean)

  bYouLessDebug = Value

End
