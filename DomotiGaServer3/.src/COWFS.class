' Gambas class file

' Description:
' OWFS.module
' Support for OWFS devices.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Property PollTime As Integer
Property Cached As Boolean
Property BaseDir As String
Property OWFSDebug As Boolean

Private iPollTime As Integer
Private bCached As Boolean
Private sBaseDir As String
Private bOWFSDebug As Boolean

Public tOWFS As Timer
Private hOWFS As Process
Private cOutput As New Collection

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Run()

  ' start poll timer for OWFS
  tOWFS = New Timer As "tOWFS"
  tOWFS.Delay = iPollTime * 1000 ' multiply for seconds
  tOWFS.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tOWFS_Timer()

  CheckOWFS()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find all devices with type OWFS and get their values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CheckOWFS()

  Dim rResult As Result
  Dim sAddress As String

  ' get all devices with this devicetype
'  rResult = Main.hDB.Exec("SELECT * FROM devices WHERE interface = &1 AND enabled is TRUE", Devices.FindInterface("OWFS Interface"))
  rResult = Devices.FindDevicesForInterface(Devices.FindInterface("OWFS Interface"))
  If rResult.Available Then
    If rResult.Count >= 1 Then
      For Each rResult
        If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Checking device '") & rResult!name & ("' with address '") & rResult!address & "'.")
        sAddress = rResult!address
        ' if address contains no "." add it.
        If InStr(sAddress, ".") = 0 Then sAddress = Left(sAddress, 2) & "." & Right(sAddress, Len(sAddress) - 2)
        ' choose cached/uncached
        sAddress = sBaseDir &/ IIf(bCached, "/", "/uncached") &/ sAddress
        ' if address contains a "/" already don't add anything
        If InStr(rResult!address, "/") = 0 Then
          Select Case Left(rResult!address, 2)
            Case "05" ' DS2405 - addressable switch
              sAddress &= "/PIO"
            Case "10", "28" ' DS18S20 and DS18B20 - temperature sensors
              sAddress &= "/temperature"
            Case "1D" ' DS2423 - dual counter
              sAddress &= "/counters.ALL"
            Case "26" ' DS2438 - humidity
              sAddress &= "/humidity"
            Case "12", "3A" ' DS2406 and DS2413 - dual I/O
              sAddress &= "/sensed.B"
            Case "20" ' DS2450 - quad A/D
              sAddress &= "/volt.A"
            Case Else
              If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Unsupported 1-Wire family '") & Left(rResult!address, 2) & "'")
          End Select
        End If
        GetOWFS(rResult!id, sAddress)
      Next
    Else
      Main.WriteLog(("OWFS: No 1-Wire devices found in device table!"))
    End If
  End If

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' to switch unit with device control
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(sAddress As String, sValue As String)

  Select Case UCase(sValue)
    Case "ON"
      SetOWFS(sAddress, "1")
    Case "OFF"
      SetOWFS(sAddress, "0")
    Case Else
      If Main.bLGTVDebug Then Main.WriteDebugLog(("[OWFS] Unsupported command received: ") & sValue)
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' try to set value of Device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub SetOWFS(sAddress As String, sValue As String)

  Dim sDevice As String

  If InStr(sAddress, ".") = 0 Then sAddress = Left(sAddress, 2) & "." & Right(sAddress, Len(sAddress) - 2)
  sDevice = sBaseDir &/ sAddress
  ' if address contains a "/" already don't add anything
  If InStr(sAddress, "/") = 0 Then
    Select Case Left(sAddress, 2)
      Case "05" ' DS2405 - addressable switch
        sDevice &= "/PIO"
      Case "12", "3A" ' DS2406 and DS2413
        sDevice &= "/PIO.BYTE"
      Case "20" ' DS2450- Quad A/D
        sDevice &= "/PIO.A"
      Case Else
        If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Unsupported 1-Wire family '") & Left(sAddress, 2) & "' for switching.")
    End Select
  Endif
  If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Settings contents of '") & sDevice & "' to '" & sValue & "'")
  Shell "echo \"" & sValue & "\" > " & sDevice

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' try to get value of Device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub GetOWFS(iId As Integer, sAddress As String)

  If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Checking contents of '") & sAddress & "'")
  hOWFS = Exec ["cat", sAddress] For Read As "OWFS"
  hOWFS.Tag = iId
  cOutput.Add("", iId)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read OWFS output and store it in collection
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub OWFS_Read()

  Dim sBuffer As String
  Dim iId As Integer

  sBuffer = Read #Last, -256
  iId = Last.Tag

  If cOutput.Exist(iId) Then cOutput[iId] &= sBuffer

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' get id and return value, 0 means ok, anything else is not found
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub OWFS_Kill()

  Dim iDeviceId, iRc As Integer
  Dim sOutput, sDeviceType As String

  iDeviceId = Last.Tag
  iRc = Last.Value

  If iDeviceId Then
    ' save ping output
    sOutput = Replace(cOutput[iDeviceId], " ", "")
    cOutput.Remove(iDeviceId)

    Select Case iRc
      Case 0

        sDeviceType = Devices.FindDescrForDeviceType(Devices.FindModuleForDevice(iDeviceId))
        Select sDeviceType
          Case "Addressable Switch", "Dual Addressable Switch", "Dual I/O"
            ' a switch device, convert values
            If sOutput = "0" Then sOutput = "Off"
            If sOutput = "1" Then sOutput = "On"
        End Select

        ' debug output
        If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Device with id '") & iDeviceId & ("' and type '") & sDeviceType & ("' has value '") & sOutput & "'.")
        ' find and update device
        Devices.CheckFirstSeen(iDeviceId)
        Devices.ValueUpdate(iDeviceId, sOutput, "", "", "")
      Case Else
        ' debug output
        If bOWFSDebug Then Main.WriteDebugLog(("[OWFS] Cannot open OWFS file for device with id '") & iDeviceId & "'.")
    End Select
  Else
    Main.WriteDebugLog(("[OWFS] Returned invalid device id!"))
  Endif

Catch
  Main.WriteDebugLog(("ERROR: Parsing OWFS Result: ") & Error.Text & " at " & Error.Where)

End

Private Function Cached_Read() As Boolean

  Return bCached

End

Private Sub Cached_Write(Value As Boolean)

  bCached = Value

End

Private Function BaseDir_Read() As String

  Return sBaseDir

End

Private Sub BaseDir_Write(Value As String)

  sBaseDir = Value

End

Private Function PollTime_Read() As Integer

  Return iPollTime

End

Private Sub PollTime_Write(Value As Integer)

  iPollTime = Value

End

Private Function OWFSDebug_Read() As Boolean

  Return bOWFSDebug

End

Private Sub OWFSDebug_Write(Value As Boolean)

  bOWFSDebug = Value

End
