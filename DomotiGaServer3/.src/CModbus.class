' Gambas class file

' Description:
' CModbus.class
' Read and write data from/to Modbus registers.

' Development Status:
' In Development.

' Links:

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "Modbus"
Public PluginFriendlyName As String = "Modbus"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Integer = 9
Public PluginRunTask As Boolean = False

Public KeyName As String
Public LogLabel As String = "[Modbus] "
Public InterfaceId As Integer
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $sTCPHost As String
Private $iTCPPort As Integer
Private $sInterface As String
Private $sModbusType As String
Private $sSerPort As String
Private $sBaud As String
Private $iDatabits As Integer
Private $iStopbits As Integer
Private $iParity As Integer
Private $iPollTime As Integer
Private $bDebug As Boolean

Public hMySocket As CSocket
Public hMySerial As CSerialPort

Public tModbusPoll As Timer
Public tModbusBusy As Timer

Private RecBuf As New Byte[200]
Private RecBytes As Byte

' CRC-16(CCITT) tables
Private aCRCTable1 As New Integer[256]
Private aCRCTable2 As New Integer[256]
Private bTableInit As Boolean

Private cQueue As New Collection    ' command buffer
Private bInterfaceBusy As Boolean   ' waiting for delay timer to finish

' Msg decode stuff
Private bExceptionRecv As Boolean
Private iMaxNumBytes As Integer
Private iCurrentRegister As Integer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)

  KeyName = cPl.KeyName
  Instance = cPl.Instance
  If Instance <> 1 Then LogLabel = Replace(LogLabel, "] ", "#" & Instance & "] ")

  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $sTCPHost = cPl.Settings["tcphost"]

  $iTCPPort = cPl.Settings["tcpport"]
  $sInterface = cPl.Settings["type"]
  $sSerPort = cPl.Settings["serialport"]
  $sBaud = cPl.Settings["baudrate"]
  $iDatabits = cPl.Settings["databits"]
  $iStopbits = cPl.Settings["stopbits"]
  $iParity = cPl.Settings["parity"]
  $sModbusType = cPl.Settings["modbustype"]
  $iPollTime = cPl.Settings["polltime"]
  $bDebug = cPl.Settings["debug"]

  InterfaceId = Devices.FindInterface("Modbus Interface")
  If InterfaceId = 0 Then
    ErrorText = "Required InterfaceId can't be retrieved from the database!"
    Main.WriteLog(LogLabel & "ERROR: " & ErrorText)
    IsRunning = False
    Return
  Endif

  ' Initialize
  InitCRCTable()

  ' Connect/Initialize connection
  If InStr($sInterface, "tcp") Then
    ConnectTCP()
  Else
    If ConnectSerial() Then
      ' Initialize
      Run()
    Endif
  Endif

  ' Initialize
  IsRunning = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for each Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()

  Try Disconnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectTCP() As Boolean

  Dim iReconnectRetry As Integer = 0

  ' try to close the connection
  Try hMySocket.Close
  Try iReconnectRetry = hMySocket.ReconnectRetry

  ' get a new one - but also pass on our previous reconnect counter
  hMySocket = New CSocket(iReconnectRetry) As "MySocket"

  hMySocket.DataType = &HFF + gb.Byte
  hMySocket.Connect($sTCPHost, $iTCPPort)

  ' write to main logfile we are trying to connect
  Main.WriteLog(LogLabel & PluginFriendlyName & " TCP interface connecting to " & $sTCPHost & ":" & $iTCPPort)

  ' don't set IsRunning=True, this has to be done in _Ready

  ' all went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " TCP interface FAILED to connect to " & $sTCPHost & ":" & $iTCPPort)
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectSerial() As Boolean

  Dim iReconnectRetry As Integer = 0

  ' try to close the port
  Try hMySerial.Close
  Try iReconnectRetry = hMySerial.ReconnectRetry

  ' get a new one - but store the retry counter between new instances
  hMySerial = New CSerialPort(iReconnectRetry) As "MySerial"

  With hMySerial
    .DataType = &HFF + gb.Byte ' Byte[]
    .PortName = $sSerPort
    .Speed = $sBaud
    .Parity = $iParity
    .DataBits = $iDatabits
    .StopBits = $iStopbits
    .FlowControl = 0
    .Open()
  End With

  ' define timer for Interface busy
  tModbusBusy = New Timer As "tModbusBusy"
  tModbusBusy.Stop

  ' log that we connected successfully
  Main.WriteLog(LogLabel & PluginFriendlyName & " serial interface connected to port " & $sSerPort)

  IsRunning = True

  ' all went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " serial interface FAILED to connect to port " & $sSerPort)
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text & ", at " & Error.Where)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try tModbusPoll.Stop
  Try tModbusBusy.Stop

  ' Try to close the connection
  Try hMySocket.Close
  Try hMySerial.Close

  Main.WriteLog(LogLabel & PluginFriendlyName & IIf($sInterface = "tcp", " TCP connection closed.", " Port closed."))

  ' all ok
  Return True

Finally
  IsRunning = False
  ErrorText = ""
  ErrorWhere = ""

Catch
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' peer closed tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Closed()

  Main.WriteDebugLog(LogLabel & "ERROR: TCP socket closed by peer.")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' error while connected/connecting to tcp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Error(sMsg As String)

  Main.WriteLog(LogLabel & "ERROR: " & sMsg)

  IsRunning = False
  ErrorText = sMsg

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' host ip address found
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Found()

  Log.Plugin_DNS_Found(LogLabel, $sTCPHost)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' received data from the tcp port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Read(vVar As Variant)

  Dim bData As Byte

  If vVar Then
    For Each bData In vVar
      ProcessReceivedChar(bData)
    Next
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' tcp socket is connected
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Ready()

  Main.WriteLog(LogLabel & "TCP interface connected.")
  IsRunning = True

 ' Initialize
  Run()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Called when we should reconnect to the tcp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Reconnect()

  ConnectTCP()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Handle errors
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySerial_Error(sMsg As String)

  Main.WriteDebugLog(LogLabel & "ERROR: " & sMsg)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' got data back from MySensors gateway and parse it when it's complete
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySerial_Read(vVar As Variant)

  Dim bData As Byte

  If vVar Then
    For Each bData In vVar
      ProcessReceivedChar(bData)
    Next
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Handle reconnect(s)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySerial_Reconnect()

  ' ReConnect/Initialize connection
  ConnectSerial()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer for polling
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Run()

  If $iPollTime = 0 Then Return

  ' start poll timer for modbus
  tModbusPoll = New Timer As "tModbusPoll"
  tModbusPoll.Delay = $iPollTime * 1000 ' multiply for seconds
  tModbusPoll.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find devices to query
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tModbusPoll_Timer()

  CheckModbus()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' scan for Modbus devices
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CheckModbus()

  Dim rResult As Result
  Dim iDeviceId, iDeviceTypeId As Integer

  ' get all enabled devices of type modbus
  rResult = Devices.FindDevicesForInterface(InterfaceId, Instance)
  If rResult And If rResult.Available Then
    ' loop through each modbus device
    For Each rResult
      iDeviceId = rResult!id
      If $bDebug Then Main.WriteDebugLog(LogLabel & "Checking device named '" & rResult!name & "' with address '" & rResult!address & "'.")

      ' check devicetype and query register
      iDeviceTypeId = Devices.FindDeviceTypeForDevice(rResult!id)
      Select Case iDeviceTypeId
        Case 675, 676, 677, 678 ' Read Holding Register 16-bit
          ReadHoldingRegisters(rResult!address, &H1)
        Case 679, 708, 709, 710 ' Read Holding Register 32-bit
          ReadHoldingRegisters(rResult!address, &H2)
        Case Else
          Main.WriteLog(LogLabel & "Unsupported devicetype for modbus found! (" & iDeviceTypeId & ")")
      End Select

    Next
  Else
    Main.WriteLog(LogLabel & "No enabled Modbus device(s) found in device table!")
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create 'read holding register' function 03 request
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ReadHoldingRegisters(sAddress As String, iNumRegisters As Integer)

  Dim bCmd As New Byte[8]
  Dim iCRC As New Integer[2]
  Dim iSlaveAddress, iAddressOffset As Integer
  Dim aAddr, aTemp As String[]

  aAddr = Split(sAddress, ":")
  If aAddr.Count = 2 Then
    iSlaveAddress = aAddr[0]
    If iSlaveAddress < 1 Or If iSlaveAddress > 247 Then
       Main.WriteDebugLog(LogLabel & "Slave address out of range! (1-247) -> " & iSlaveAddress)
       Return
    Endif
    iAddressOffset = Val("&H" & aAddr[1] & "&")
  Else
    Main.WriteDebugLog(LogLabel & "Device address has wrong format use slave:register! (eg 2:55) -> " & sAddress)
    Return
  Endif

  ' input checks
  If iAddressOffset < 0 Or If iAddressOffset > 65535 Then
    Main.WriteDebugLog(LogLabel & "Offset address out of range! -> " & iAddressOffset)
    Return
  Endif

  If iNumRegisters < 1 Or If iNumRegisters > 11 Then
    Main.WriteDebugLog(LogLabel & "Number of registers out of range! -> " & iNumRegisters)
    Return
  Endif

  bCmd[0] = iSlaveAddress  ' address of slave
  bCmd[1] = &H03 ' function to read holding registers
  bCmd[2] = iAddressOffset / 256 ' starting address hi
  bCmd[3] = iAddressOffset % 256 ' starting address lo
  bCmd[4] = iNumRegisters / 256 ' no of registers hi
  bCmd[5] = iNumRegisters % 256 ' no of registers lo

  ' calculate and add checksum
  iCRC = CRC(bCmd, 6)
  bCmd[6] = iCRC[0] ' CRC hi
  bCmd[7] = iCRC[1] ' CRC lo

  QueueCommand(bCmd)

Catch
  Main.WriteDebugLog(LogLabel & " Error decoding device address!")
  Main.WriteDebugLog(LogLabel & " " & Error.Text)
  Main.WriteDebugLog(LogLabel & " " & Error.Where)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process received data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ProcessReceivedChar(bTemp As Byte)

  Main.ControlLed("Modbus", "On")
  If RecBytes = 0 Then ' 1st char of a packet received
    If bTemp <> 0 Then
      If $bDebug Then Main.WriteDebugLogChars(LogLabel & "< ", 0)
    Else
      Return  ' ignore 1st char if 0
    Endif
  Endif

  Try RecBuf[RecBytes] = bTemp ' store received char
  If Error Then
    RecBytes = 0 ' temp hack (overflow)
    RecBuf = Null
    iMaxNumBytes = 255
    bExceptionRecv = False
    Return
  Endif
  Inc RecBytes  ' increment char counter

  ' detect exception response
  If RecBytes = 2 Then
    If BTst(RecBuf[1], 7) Then
      bExceptionRecv = True
      iMaxNumBytes = 5
    Endif
  Else If RecBytes = 3 Then
    If bExceptionRecv = False Then
      iMaxNumBytes = RecBuf[2] + 5
    Endif
  Endif

  If RecBytes = iMaxNumBytes Then ' all bytes of exception packet received
    If $bDebug Then Main.WriteDebugLogChars(Hex(bTemp, 2) & "\n", 1)
    If bExceptionRecv = False Then
      Decode_Message()  ' decode regular message
    Else
      Decode_Exception()  ' decode exception message
    Endif
    RecBytes = 0  ' set to zero to receive next message
    iMaxNumBytes = 255
    bExceptionRecv = False
    iCurrentRegister = 65535
  Else
    If $bDebug Then Main.WriteDebugLogChars(Hex(bTemp, 2), 1) ' write the output to the screen
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' decode response message
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub Decode_Message()

  Dim iDeviceId, iDeviceTypeId As Integer
  Dim vValue As Variant
  Dim iFunction As Integer = RecBuf[1]
  Dim iCRC As New Integer[2]
  Dim sAddress As String

  sAddress = Hex(RecBuf[0]) & ":" & Hex(iCurrentRegister)

  ' calc CRC
  iCRC = CRC(RecBuf, iMaxNumBytes - 2)

  If $bDebug Then
    Main.WriteDebugLog(LogLabel & "Received '" & Decode_Function() & "' response:")
    Main.WriteDebugLog(LogLabel & "Slave ID: " & Hex(RecBuf[0], 2))
    Main.WriteDebugLog(LogLabel & "Register: " & Hex(iCurrentRegister))
    Main.WriteDebugLog(LogLabel & "Function: " & Hex(iFunction, 2))
    Main.WriteDebugLog(LogLabel & "Address: " & sAddress)
    Main.WriteDebugLog(LogLabel & "Checksum Recv: " & Hex(RecBuf[iMaxNumBytes - 2], 2) & Hex(RecBuf[iMaxNumBytes - 1], 2))
    Main.WriteDebugLog(LogLabel & "Checksum Calc: " & Hex(iCRC[0], 2) & Hex(iCRC[1], 2))
  Endif

  ' check CRC
  If (iCRC[0] & iCRC[1]) <> (RecBuf[iMaxNumBytes - 2] & RecBuf[iMaxNumBytes - 1]) Then
    Main.WriteDebugLog(LogLabel & "Invalid Checksum, discarding.")
    Return
  Endif

  iDeviceId = Devices.Find(Instance, sAddress, InterfaceId)
  If iDeviceId Then
    iDeviceTypeId = Devices.FindDeviceTypeForDevice(iDeviceId)
    Select Case iDeviceTypeId
      Case 675, 676, 677, 678 ' Read Holding Register 16-bit
        If RecBuf[2] <> 2 Then
          Main.WritDebugLog(LogLabel & "Devicetype is using 16-bit, but received data is not!")
          Return
        Endif
        ' calculate value
        vValue = (RecBuf[3] * 256) + RecBuf[4]

        If $bDebug Then
          Main.WriteDebugLog(LogLabel & "Value Size: " & Hex(RecBuf[2]) & " Bytes, 16-bits integer.")
          Main.WriteDebugLog(LogLabel & "Value: " & vValue)
        Endif

        ' set device value
        If iDeviceId Then
          If $bDebug Then Main.WriteDebugLog(LogLabel & "Updating device with id: " & iDeviceId)
          Devices.ValueUpdate(iDeviceId, 1, vValue)
        Endif

      Case 679, 708, 709, 710 ' Read Holding Register 32-bit
        If RecBuf[2] <> 4 Then
          Main.WriteDebugLog(LogLabel & "Devicetype is using 32-bit, but received data is not!")
          Return
        Endif
        ' calculate value
        'vValue = Lsl(CLong(RecBuf[3]), 8) + CLong(RecBuf[4]) + Lsl(CLong(RecBuf[5]), 8) + CLong(RecBuf[5])
        vValue = RecBuf[4] & RecBuf[3] & RecBuf[6] & RecBuf[5]

        If $bDebug Then
          Main.WriteDebugLog(LogLabel & "Value Size: " & Hex(RecBuf[2]) & " Bytes, 32-bits integer.")
          Main.WriteDebugLog(LogLabel & "Value RAW: " & Hex(RecBuf[4], 2) & Hex(RecBuf[3], 2) & Hex(RecBuf[6], 2) & Hex(RecBuf[5], 2))
          Main.WriteDebugLog(LogLabel & "Value: " & vValue)
        Endif

        ' set device value
        If iDeviceId Then
          If $bDebug Then Main.WriteDebugLog(LogLabel & "Updating device with id: " & iDeviceId)
          Devices.ValueUpdate(iDeviceId, 1, vValue)
        Endif
      Case Else
        Main.WriteLog(LogLabel & "Unsupported devicetype for modbus found! (" & iDeviceTypeId & ")")
    End Select

  Endif

Catch
  Main.WriteDebugLog(LogLabel & " Error decoding message!")
  Main.WriteDebugLog(LogLabel & " " & Error.Text)
  Main.WriteDebugLog(LogLabel & " " & Error.Where)

End

Private Sub Decode_Function() As String

  Dim sMsg As String

  Select Case RecBuf[1]
    Case 1, 129
      sMsg = "Read Coil Status"
    Case 2, 130
      sMsg = "Read Input Status"
    Case 3, 131
      sMsg = "Read Holding Registers"
    Case 4, 132
      sMsg = "Read Input Registers"
    Case 5, 133
      sMsg = "Force Single Coil"
    Case 6, 134
      sMsg = "Preset Single Register"
    Case 15, 143
      sMsg = "Force Multiple Coils"
    Case 16, 144
      sMsg = "Preset Multiple Registers"
    Case Else
      sMsg = "Unknown"
  End Select
  Return sMsg

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' decode exception response message
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub Decode_Exception()

  Dim sMsg As String
  Dim iFunction As Integer = BClr(RecBuf[1], 7)
  Dim iCode As Integer = RecBuf[2]
  Dim iCRC As New Integer[2]

  Select Case iCode
    Case 1
      sMsg = "Illegal Function"
    Case 2
      sMsg = "Illegal Data Address"
    Case 3
      sMsg = "Illegal Data Value"
    Case 4
      sMsg = "Slave Device Failure"
    Case 5
      sMsg = "Acknowledge"
    Case 6
      sMsg = "Slave Device Busy"
    Case 7
      sMsg = "Negative Acknowledge"
    Case 8
      sMsg = "Memory Parity Error"
    Case 10
      sMsg = "Gateway Path Unavailable"
    Case 11
      sMsg = "Gateway Target Device Failed to Respond"
    Case Else
      sMsg = "Unknown"
  End Select
  If $bDebug Then
    Main.WriteDebugLog(LogLabel & "Received Response:")
    Main.WriteDebugLog(LogLabel & "Slave ID: " & Hex(RecBuf[0], 2))
    Main.WriteDebugLog(LogLabel & "Function: " & Hex(iFunction, 2))
    Main.WriteDebugLog(LogLabel & "Exception: " & sMsg & " (" & Hex(iCode, 2) & ")")
    Main.WriteDebugLog(LogLabel & "Checksum Recv: " & Hex(RecBuf[3], 2) & Hex(RecBuf[4], 2))
    iCRC = CRC(RecBuf, iMaxNumBytes - 2)
    Main.WriteDebugLog(LogLabel & "Checksum Calc: " & Hex(iCRC[0], 2) & Hex(iCRC[1], 2))
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send a command to the interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function WriteCommand(bBuffer As Byte[], sKey As String)

  Dim iCnt As Integer
  Dim sMessage As String

  For iCnt = 0 To bBuffer.Max
    sMessage &= Hex(bBuffer[iCnt], 2)
  Next
  If $bDebug Then Main.WriteDebugLog(LogLabel & "> " & sMessage)

  If $sInterface = "tcp" Then
    ' send the message to the tcp stream
    hMySocket.Write(bBuffer)
  Else
    ' send the message to the serial port
    hMySerial.Write(bBuffer)
  Endif

  ' store register
  iCurrentRegister = (bBuffer[2] * 256) + bBuffer[3]

  ' remove sent command from queue
  cQueue.Remove(sKey)

  tModbusBusy.Delay = 100 ' 50mS (initial test value)
  tModbusBusy.Start
  bInterfaceBusy = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' returns the CRC of the iLen no. of bytes in bBuf
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function CRC(bBuf As Byte[], iLen As Integer) As Integer[]

  Dim iCnt, iCar As Integer
  Dim iCRC As Byte[] = [CByte(&HFF), CByte(&HFF)]

  For iCnt = 0 To iLen - 1 ' for each byte
    iCar = bBuf[iCnt]
    iCar = iCar Xor iCRC[0]
    iCRC[0] = iCRC[1] Xor aCRCTable2[iCar]
    iCRC[1] = aCRCTable1[iCar]
  Next
  Return iCRC

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' init the CRC tables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub InitCRCTable()

  Dim iCRC, iMask, iBit, iMem As Integer

  For iMask = 0 To 255
    iCRC = iMask
    For iBit = 0 To 7
      iMem = iCRC And &H0001&
      iCRC /= 2
      If (iMem) Then iCRC = iCRC Xor &HA001&
    Next
    aCRCTable2[iMask] = iCRC And &HFF
    aCRCTable1[iMask] = Lsr(iCRC, 8)
  Next
  bTableInit = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' interface busy reset
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tModbusBusy_Timer()

  bInterfaceBusy = False
  tModbusBusy.Stop

  If cQueue.Count Then ' if there are more commands left in queue, send next one
    If $bDebug Then Main.WriteDebugLog(LogLabel & "Sending command from Queue (count = " & cQueue.Count & ")")
    DoQueue()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' add command to queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub QueueCommand(aPacket As Byte[])

  ' add command to queue
  cQueue.Add(aPacket, Rnd)

  ' if controller is not busy process it
  If bInterfaceBusy = False Then DoQueue()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process items in queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DoQueue()

  Dim aPacket As Byte[] = Null

  For Each aPacket In cQueue
    WriteCommand(aPacket, cQueue.Key)
    Break ' only do first one
  Next

End

Public Sub SendCommand(sAddress As String, sCmd As String)

  Dim iDeviceId As Integer
  Dim sType As String

  iDeviceId = Devices.Find(Instance, sAddress, InterfaceId)
  If iDeviceId Then
    sType = Devices.FindTypeForDevice(iDeviceId)
  Endif

  Select UCase(sType)
    ' Case "XXX"
    '   Coil(sType, sAddress, sCmd)
    Case Else
      Main.WriteDebugLog(LogLabel & " The '" & sType & "' function is not (yet) supported by this plugin!")
      Return
  End Select

End
