' Gambas class file

' Description:
' CCTX35.class
' Support for Xanura CTX35 and TI103 X10 interface.

' Development Status:
' Controlling devices works, with on/off, dim and bright, reading of on/off status works, reading of dim/bright needs to be implemented.

' Links:
' http://www.hometoys.com/htinews/dec02/articles/kingery16/kingery16.htm

' Credits:
' Thanks to Pieter from BWired.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.en

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables 
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "CTX35"
Public PluginFriendlyName As String = "Xanura CTX35 X10"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Integer = 1

Public KeyName As String
Public LogLabel As String = "[CTX35] "
Public InterfaceId As Integer
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $sSerPort As String
Private $sBaud As String
Private $iPollTime As Integer
Private $bGlobalX10 As Boolean
Private $bDebug As Boolean
Private $sTCPHost As String
Private $iTCPPort As Integer
Private $sInterface As String

Private $sMsgData As String

Public hMySocket As CSocket
Public hMySerial As CSerialPort

Public tCTX35Poll As Timer
Public tCTX35Led As Timer
Public tCTX35Busy As Timer

Private cQueue As New Collection    ' command buffer
Private bControllerBusy As Boolean  ' waiting for delay timer to finish
Private aCTX As New String[3, 7]    ' buffer received commands

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)

  KeyName = cPl.KeyName
  Instance = cPl.Instance
  If Instance <> 1 Then LogLabel = Replace(LogLabel, "] ", "#" & Instance & "] ")

  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $sSerPort = cPl.Settings["serialport"]
  $sBaud = cPl.Settings["baudrate"]
  $iPollTime = cPl.Settings["polltime"]
  $bGlobalX10 = cPl.Settings["globalx10"]
  $bDebug = cPl.Settings["debug"]
  $sTCPHost = cPl.Settings["tcphost"]
  $iTCPPort = cPl.Settings["tcpport"]
  $sInterface = cPl.Settings["type"]

  InterfaceId = Devices.FindInterface("Xanura CTX35")
  If InterfaceId = 0 Then
    ErrorText = "Required InterfaceId can't be retrieved from the database!"
    WriteLog("ERROR: " & ErrorText)
    IsRunning = False
    Return
  Endif

  ' Connect/Initialize connection
  If InStr($sInterface, "tcp") Then
    ConnectTCP()
  Else
    ConnectSerial()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for each Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()

  Try Disconnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Easy helper for WriteLog
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub WriteLog(sLog As String)

  Main.WriteLog(LogLabel & sLog)
  If $bDebug Then Main.WriteDebugLog(LogLabel & sLog)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Easy helper for WriteDebugLog
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub WriteDebugLog(sLog As String)

  If $bDebug Then Main.WriteDebugLog(LogLabel & sLog)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the tcp host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ConnectTCP() As Boolean

  Dim iReconnectRetry As Integer = 0

  ' try to close the connection
  Try hMySocket.Close
  Try iReconnectRetry = hMySocket.ReconnectRetry
  
  ' get a new one - but also pass on our previous reconnect counter
  hMySocket = New CSocket(iReconnectRetry) As "MySocket"

  hMySocket.DataType = gb.String
  hMySocket.Connect($sTCPHost, $iTCPPort)

  ' Write to main logfile we are trying to connect
  WriteLog(PluginFriendlyName & " TCP interface connecting to " & $sTCPHost & ":" & $iTCPPort)

  ' Don't set IsRunning=True, this has to be done in _Ready 

  ' All went ok
  Return True

Catch ' some errors
  WriteLog("ERROR: " & PluginFriendlyName & " TCP interface FAILED to connect to " & $sTCPHost & ":" & $iTCPPort)
  WriteLog("ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ConnectSerial() As Boolean

  Dim iReconnectRetry As Integer = 0

  ' try to close the port
  Try hMySerial.Close
  Try iReconnectRetry = hMySerial.ReconnectRetry

  ' get a new one - but store the retry counter between new instances
  hMySerial = New CSerialPort(iReconnectRetry) As "MySerial"

  With hMySerial
    .DataType = gb.String
    .PortName = $sSerPort
    .Speed = $sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  End With

  ' start poll timer for X10 status LED
  tCTX35Led = New Timer As "tCTX35LED"
  tCTX35Led.Delay = 100
  tCTX35Led.Stop

  ' define timer for CTX35 controller busy
  tCTX35Busy = New Timer As "tCTX35Busy"
  tCTX35Busy.Stop

  tCTX35Poll = New Timer As "tCTX35Poll"
  tCTX35Poll.Delay = $iPollTime
  tCTX35Poll.Start

  ' Write to main logfile we connected successfully
  WriteLog(PluginFriendlyName & " serial interface connected to port " & $sSerPort)
  IsRunning = True

  ' All went ok
  Return True

Catch ' some errors
  WriteLog("ERROR: " & PluginFriendlyName & " serial interface FAILED to connect to port " & $sSerPort)
  WriteLog("ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub Disconnect() As Boolean

  ' Try to close the connection
  Try hMySocket.Close
  Try hMySerial.Close

  ' Stop possible timers
  StopTimers()

  WriteLog(PluginFriendlyName & IIf($sInterface = "tcp", " TCP connection closed.", " Port closed."))

  ' all ok
  Return True

Finally
  IsRunning = False
  ErrorText = ""
  ErrorWhere = ""

Catch ' some errors
  WriteLog("ERROR: " & Error.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Stop all timers
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub StopTimers()

  Try tCTX35Poll.Stop
  Try tCTX35Busy.Stop
  Try tCTX35Led.Stop

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' peer closed tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Closed()

  WriteLog("ERROR: TCP socket closed by peer.")
  StopTimers()

  IsRunning = False
  ErrorText = "TCP socket closed by peer"

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' error while connected/connecting to tcp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Error(sMsg As String)

  IsRunning = False
  ErrorText = sMsg

  WriteLog("ERROR: " & sMsg)
  StopTimers()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' host ip address found
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Found()

  Log.Plugin_DNS_Found(LogLabel, $sTCPHost)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' received data from the tcp port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Read(vVar As Variant)

  Dim iCnt As Integer

  If vVar Then
    For iCnt = 1 To Len(vVar)
      ProcessReceivedChar(Mid(vVar, iCnt, 1))
    Next
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' tcp socket is connected
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Ready()

  WriteLog("TCP interface connected.")
  IsRunning = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Called when we should reconnect to the tcp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySocket_Reconnect()

  ConnectTCP()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Handle errors
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySerial_Error(sMsg As String)

  WriteLog("ERROR: " & sMsg)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' got data back from MySensors gateway and parse it when it's complete
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySerial_Read(vVar As Variant)

  Dim iCnt As Integer

  If vVar Then
    For iCnt = 1 To Len(vVar)
      ProcessReceivedChar(Mid(vVar, iCnt, 1))
    Next
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Handle reconnect(s)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub MySerial_Reconnect()

  ' ReConnect/Initialize connection
  ConnectSerial()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ProcessReceivedChar(sData As String)

  Main.ControlLed("X10", "On")
  If Main.bServer Then tCTX35Led.Start

  If sData = "$" Then ' begin of packet
    $sMsgData = sData
  Else
    $sMsgData &= sData
  Endif

  ' the CTX35 sometimes sends an incomplete packet when sending BOFFBOFF, this fixes the packet so it's valid
  If Right$($sMsgData, 9) = "BOFFBOFF8" Then $sMsgData = Left$($sMsgData, -1) & "85#"

  ' received complete packet
  If Right$($sMsgData, 1) = "#" Then
    ProcessX10($sMsgData)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ProcessX10(sStr As String)

  Dim iPos As Integer = 1
  Dim sChar As String

  $sMsgData = ""

  If $bDebug Then WriteDebugLog("< " & sStr)

  ' process special packets
  Select sStr
    Case "$<2800!4B#" ' ack packet
      If $bDebug Then WriteDebugLog("< Ack received!")
      Return
    Case "$<2800?69#" ' wrong checksum received
      If $bDebug Then WriteDebugLog("< Wrong checksum!")
      Return
    Case "$<2800!S0CE#" ' interface buffer full
      If $bDebug Then WriteDebugLog("< Buffer is full!")
      Return
  End Select

  ' check checksum first
  If Not TestCheckSum(sStr) Then
    If $bDebug Then WriteDebugLog("Checksum error!")
    Return
  Endif

  sStr = LTrim$(Mid$(sStr, 8, Len(sStr) - 10))

  Do
    If Mid$(sStr, iPos, 1) = " " Then Inc iPos
    sChar = Mid$(sStr, iPos, 3)
    If Right$(sChar, 1) Like "[0-9]" And Left$(sChar, 1) Like "[A-P]" Then ' code found
      iPos += 3
      ProcessCode("Code", sChar)
    Else If InStr(sChar, "ON") Then ' status found
      iPos += 3
      ProcessCode("Status", sChar)
    Else If InStr(sChar, "OF") Then ' status found
      sChar = Mid$(sStr, iPos, 3)
      iPos += 4
      ProcessCode("Status", sChar)
    Else If InStr(sChar, "SR") Then ' status request found
      iPos += 4
      ProcessCode("Status Request", sChar)
    Else If Right$(sChar, 2) = "SO" Then ' status found
      If InStr(sStr, "SON") Then
        ProcessCode("Status", Left$(sChar, 1) & "ON")
      Else If InStr(sStr, "SOF") Then
        ProcessCode("Status", Left$(sChar, 1) & "OF")
      Endif
      iPos += 4
    Else
      Inc iPos
    Endif
  Loop Until iPos >= Len(sStr)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ProcessCode(sType As String, sValue As String)

  Dim iCnt, iCnt2 As Integer
  Dim bCodefound As Boolean

  For iCnt = 0 To 2
    Select sType
      Case "Code"
        bCodefound = False
        If $bDebug Then WriteDebugLog("Code: " & sValue)
        For iCnt2 = 0 To 2
          If aCTX[iCnt, 0] = sValue And Not aCTX[iCnt, 5] Then bCodefound = True
        Next
        If aCTX[iCnt, 0] = "" And Not bCodefound Then
          aCTX[iCnt, 0] = sValue
          aCTX[iCnt, 4] = Time
          Return
        Else If aCTX[iCnt, 0] = sValue And aCTX[iCnt, 5] = "" And bCodefound Then
          aCTX[iCnt, 1] = sValue
          Return
        Else If aCTX[iCnt, 5] = "OK" And Not bCodefound Then ' different value but old one was complete so overwrite
          aCTX[iCnt, 0] = sValue
          aCTX[iCnt, 1] = ""
          aCTX[iCnt, 2] = ""
          aCTX[iCnt, 3] = ""
          aCTX[iCnt, 4] = Time
          aCTX[iCnt, 5] = ""
          aCTX[iCnt, 6] = ""
          Return
        Else If DateDiff(Time, aCTX[iCnt, 4], gb.Second) > 10 Then ' incomplete code/status, overwrite
          WriteDebugLog("Received Code without Status : " & aCTX[iCnt, 0] & " " & aCTX[iCnt, 1] & " " & aCTX[iCnt, 2] & " " & aCTX[iCnt, 3] & " " & aCTX[iCnt, 4] & " " & aCTX[iCnt, 5] & " " & aCTX[iCnt, 6])
          aCTX[iCnt, 0] = sValue
          aCTX[iCnt, 1] = ""
          aCTX[iCnt, 2] = ""
          aCTX[iCnt, 3] = ""
          aCTX[iCnt, 4] = Time
          aCTX[iCnt, 5] = ""
          aCTX[iCnt, 6] = ""
          Return
        Endif
      Case "Status Request"
        If $bDebug Then WriteDebugLog("Status Request " & sValue)
        If aCTX[iCnt, 0] = "" Then
          Return
        Else If Left$(aCTX[iCnt, 0], 1) = Left$(sValue, 1) And aCTX[iCnt, 5] = "" Then
          aCTX[iCnt, 6] = "SRQ" ' status request
          Return
        Endif
      Case "Status"
        If $bDebug Then WriteDebugLog(" Status: " & sValue)
        If aCTX[iCnt, 2] = "" And Left$(aCTX[iCnt, 0], 1) = Left$(sValue, 1) Then
          aCTX[iCnt, 5] = "OK"
          aCTX[iCnt, 4] = Time
          aCTX[iCnt, 2] = Right$(sValue, 2)
          UpdateX10(aCTX[iCnt, 0], Right$(sValue, 2))
          Return
        Else If aCTX[iCnt, 3] = "" And Left$(aCTX[iCnt, 0], 1) = Left$(sValue, 1) Then
          aCTX[iCnt, 3] = Right$(sValue, 2)
          Return
        Endif
      End Select
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub UpdateX10(sCode As String, sCommand As String)

  Dim iDeviceId As Integer

  If $bDebug Then WriteDebugLog("Received command '" & sCommand & "' for device '" & sCode & "'")

  If $bGlobalX10 Then
    iDeviceId = Devices.FindAll(Instance, sCode, 9999, "X10")
  Else
    iDeviceId = Devices.Find(Instance, sCode, InterfaceId, "X10")
  Endif
  If iDeviceId Then
    If sCommand = "ON" Then
      Devices.ValueUpdate(iDeviceId, 1, "On", Devices.ValueType_Switch)
    Else If sCommand = "OF" Then
      Devices.ValueUpdate(iDeviceId, 1, "Off", Devices.ValueType_Switch)
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' CTX35 controller busy reset
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tCTX35Busy_Timer()

  bControllerBusy = False
  tCTX35Busy.Stop

  If cQueue.Count Then ' if there are more commands left in queue, send next one
    If $bDebug Then WriteDebugLog("Sending next command from Queue (count = " & cQueue.Count & ")")
    DoQueue()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' ctx35 poll routine
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tCTX35Poll_Timer()

  ' sent poll command only if no command is already queued
  If Not bControllerBusy Then QueueCommand("$>2800008C#")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' add command to queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub QueueCommand(sCmd As String)

  ' add command to queue
  cQueue.Add(sCmd, Rnd)

  ' if controller is not busy process it
  If bControllerBusy = False Then DoQueue()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process items in queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub DoQueue()

  Dim sCmd As String

  For Each sCmd In cQueue
    WriteCommand(sCmd, cQueue.Key)
    Break ' only do first one
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send packet to CTX35 interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub WriteCommand(sBuffer As String, sKey As String)

  Main.ControlLed("X10", "On")
  If Main.bServer Then tCTX35Led.Start

  If $bDebug Then WriteDebugLog("> " & sBuffer)

  If InStr($sInterface, "tcp") Then
    ' send the message to the tcp stream
    hMySocket.Write(sBuffer)
  Else
    ' send the message to the serial port
    hMySerial.Write(sBuffer)
  Endif

  ' remove sent command from queue
  cQueue.Remove(sKey)

  ' start timer to wait before sending next command
  tCTX35Busy.Delay = 100 ' 100mS (initial test value)
  tCTX35Busy.Start
  If bControllerBusy = False Then bControllerBusy = True

End

Public Sub tCTX35Led_Timer()

  Main.ControlLed("X10", "Off")
  If Main.bServer Then tCTX35Led.Stop

End

Public Sub SendCommand(sAddress As String, sValue As String)

  Dim sHouseCode, sAddr, sStr As String

  ' check if it contains a valid address
  If Not (sAddress Like "[A-P][0-1][0-9]*") Then
    If $bDebug Then WriteDebugLog("Got invalid address " & sAddress & "!")
    Return
  Endif

  ' get house code from address
  sHouseCode = Left$(sAddress, 1)

  ' translate to CTX command
  sStr = X10toCTX(sValue)
  If Len(sStr) = 0 Then ' not valid
    If $bDebug Then WriteDebugLog("Got invalid command " & sValue & "!")
    Return
  Endif

  ' todo: check for repeated commands and support extended commands
  sStr = String$(2, sHouseCode & sStr)
  sAddr = String$(2, sAddress)

  QueueCommand(CreateWithChecksum((sAddr & " " & sStr)))

End

Private Sub TestCheckSum(sString As String) As Boolean

  Dim sHead, sTail As String
  Dim iLoop, iChecksum As Integer

  ' split string
  sTail = Right$(sString, 3)
  sHead = Left$(sString, -3)

  If Not sTail And sHead Then Return False

  For iLoop = 1 To Len(sHead)
    iCheckSum = iChecksum + Asc(Mid$(sHead, iLoop, 1)) And 255
  Next

  ' check result
  If sTail = Hex$(iChecksum, 2) & "#" Then Return True

  Return False

End

Private Sub CreateWithChecksum(sCommand As String) As String

  Dim iChecksum, iLoop As Integer

  sCommand = "$>28001" & sCommand

  For iLoop = 1 To Len(sCommand)
    iCheckSum += Asc(Mid$(sCommand, iLoop, 1))
  Next

  Return sCommand & Right$(Hex$(iChecksum), 2) & "#"

End

Private Sub X10toCTX(sCommand As String) As String

  Select UCase(sCommand)
    Case "ALL_UNITS_OFF"
      Return "AUF"
    Case "ALL_LIGHTS_ON"
      Return "ALN"
    Case "ALL_LIGHTS_OFF"
      Return "ALF"
    Case "ON"
      Return "ON"
    Case "OFF"
      Return "OFF"
    Case "DIM"
      Return "DIM"
    Case "BRIGHT"
      Return "BGT"
    Case "HAIL_REQ"
      Return "HRQ"
    Case "PREDIM1"
      Return "PR0"
    Case "PREDIM2"
      Return "PR1"
    Case "STATUS"
      Return "SRQ"
  End Select

  Return ""

End
