' Gambas module file

' Description:
' Pachube.module
' Support for Pachube (pronounce: patch-bay) sensors network.

' Development Status:
' Just build, so possible bugs around.

' Links:
' http://www.pachube.com

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public hPost As New HttpClient As "hPost"
Public tPachube As Timer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Run()

  ' start poll timer for Pachube
  tPachube = New Timer As "tPachube"
  tPachube.Delay = Main.iPachubePushTime * 1000 * 60 ' multiply for minutes
  tPachube.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tPachube_Timer()

  UploadPachubeData()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create xml data and upload it to the service
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub UploadPachubeData()

  Dim sContent As String

  ' if we are already uploading return
  If hPost.Status > 0 Then
    Main.WriteLog(("I'm already uploading sensor data to Pachube, skipping."))
    Return
  Endif

  ' create xml string
  sContent = CreatePachubeData()

  ' use httpclient to post xml to service
  hPost.URL = "https://www.pachube.com/api/feeds/" & Main.iPachubeFeed & ".xml?_method=put&key=" & Main.sPachubeAPIKey
  hPost.TimeOut = 10
  hPost.Async = True
  hPost.Post("text/xml", sContent)

  If Main.bPachubeDebug Then Main.WriteDebugLog("[Pachube] " & sContent)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' scan pachube devices table and create xml document
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function CreatePachubeData() As String

  Dim sXml, sValue, sTag As String
  Dim rResult As Result
  Dim aTags As String[]

  ' scan device table
  rResult = Main.hDB.Exec("SELECT * FROM devices_pachube")
  If Not rResult Then
    Main.WriteLog(("Error: table 'devices_pachube' not found!"))
    Return
  Endif

  ' build header
  sXml = "<?xml version='1.0' encoding='UTF-8'?>\n"
  sXml &= "<eeml xmlns='http://www.eeml.org/xsd/005' xmlns:xsi='http://www.w3.org/2001/XMLSchema-instance' xsi:schemaLocation='http://www.eeml.org/xsd/005 http://www.eeml.org/xsd/005/005.xsd'>\n"
  sXml &= "<environment>\n"

  ' create device entries
  If rResult.Count Then
    For Each rResult
      sXml &= "<data id='" & rResult!datastreamid & "'>\n"
      aTags = Split(rResult!tags, ",")
      If aTags.Count > 0 Then
        For Each sTag In aTags
          sXml &= "<tag>" & LTrim(sTag) & "</tag>\n"
        Next
      Else
        sXml &= "<tag>" & rResult!tags & "</tag>\n"
      Endif
      sValue = Devices.GetCurrentValueForDevice(rResult!deviceid, rResult!value)
      sXml &= "<value>" & sValue & "</value>\n"
      sXml &= "<unit symbol='" & rResult!devicelabel & "' type='" & rResult!unittype & "'>" & rResult!units & "</unit>\n"
      sXml &= "</data>\n"
    Next
  Endif
  ' close document
  sXml &= "</environment>\n"
  sXml &= "</eeml>\n"

  Return sXml

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' catch error
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hPost_Error()

  Main.WriteDebugLog(("[Pachube] Sensor data post error."))

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' check http return code
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hPost_Finished()

  Dim iCount As Integer

  ' select on http result code and display message
  Select hPost.Code
    Case 0
      If InStr(hPost.Headers[0], "HTTP/1.1 200") Or If InStr(hPost.Headers[0], "HTTP/1.1 100") Then
        Main.WriteLog(("Uploaded sensor data to Pachube."))
      Else
        Main.WriteLog(("Unknown error occured while uploading sensor data to Pachube!"))
      Endif
    Case 200
      Main.WriteLog(("Uploaded sensor data to Pachube."))
    Case 401
      Main.WriteLog(("Error authenticating while uploading sensor data to Pachube!"))
    Case 403
      Main.WriteLog(("Error forbidden to upload sensor data to Pachube!"))
    Case 404
      Main.WriteLog(("Error page not found while uploading sensor data to Pachube!"))
    Case 422
      Main.WriteLog(("Error sensor data is not valid after uploading to Pachube!"))
    Case 503
      Main.WriteLog(("Error rate limit exceeded while uploading sensor data to Pachube!"))
    Case Else
      Main.WriteLog(("Unknown error occured while uploading sensor data to Pachube! (") & hPost.Code & ") " & hPost.Reason)
  End Select

  ' if debug is on print all http headers
  If Not Main.bPachubeDebug Then Return
  For iCount = 0 To hPost.Headers.Count - 1
    Main.WriteDebugLog("[Pachube] " & Left(hPost.Headers[iCount], Len(hPost.Headers[iCount])))
  Next

End
