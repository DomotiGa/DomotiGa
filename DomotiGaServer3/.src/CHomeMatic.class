' Gambas class file

' Description:
' CHomeMatic.class
' Connect to HomeMatic LAN adapter via tcp socket.
' Version 29 April 2012.

' Development Status:
' Just started. Basic functionality. Bugs still possible, pls report.

' Credits: JvdK

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Property TCPHost As String
Property TCPPort As Integer
Property HMDebug As Boolean
Property HMLANid As String
Property Pairing As Boolean

Private sTCPHost As String
Private iTCPPort As Integer
Private bHMDebug As Boolean
Private sHMLANid As String

Public hHM As New Socket
Public tHMBusy As Timer
Public tHMConnect As Timer
Public bHMPairEnabled As Boolean
Public iStackCount As Integer               ' Use in Main form statusbar?
Public iStackCountUntransmitted As Integer  ' Use in Main form statusbar?
Public sHMLANReportedFW As String           ' HMLAN real properties
Public sHMLANReportedSerial As String       ' HMLAN real properties
Public sHMLANReportedAddress As String      ' HMLAN real properties

' logging
Public bHMRawLogEnabled As Boolean = False   ' log all port traffic
Public bHMRadioLogEnabled As Boolean = False ' log radio traffic
Public bHMInfoLogEnabled As Boolean = True   ' log all commands and responses
Public bHMErrorLogEnabled As Boolean = False ' log all unrecognized messages and errors

Private bFirstByte As Boolean = True
' [GB2:ARRD] PRIVATE RecBuf AS Byte[255]
Private RecBuf As New Byte[255]
Private bMessage As Boolean = False
Private iByteCount As Integer = 0
Private bHMBusy As Boolean  ' waiting for delay timer to finish
Private iConnectRetry As Integer = 5 ' retries
Private iConnectDelay As Integer = 60000 ' 1 minute

' stack management
Private cQueue As New Collection  ' command buffer
Private cAckstore As New Collection
Private cMsgnr As New Collection
Private cDestDevice_id As New Collection
Private iMessageCounter As Integer = 0
Private cPayload As New Collection
Private cTimeStamp As New Collection

' pairing
Private sPairing_lastMsg As Integer ' need to retain after exiting the sub
Private cDevice_state As New Collection
Private sNewDevice As New String[7]
Private sLastSent As String

' connection management
Private tKeepAlive As Timer
Private tAckTimer As Timer
Private iCommandRetry As Integer = 0

' timesync
Private iUTCoffset As Integer
Private iDelta As Long

' data collection
Private sConf5Dump As String

Public Sub HM_Ready()

  Main.WriteLog(("HomeMatic TCP socket connected."))
  Init()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reconnect routine
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tHMConnect_Timer()

  If Not ConnectTCP() Then
    Inc iConnectRetry
    iConnectDelay *= iConnectRetry
    tHMConnect.Delay = iConnectDelay
  Endif

End

Public Sub HM_Closed()

  Main.WriteDebugLog(("[HomeMatic] ERROR: TCP socket closed by peer."))
  If iConnectRetry < 6 Then
    Main.WriteDebugLog(("[HomeMatic] Retry to connect" & IIf(iConnectRetry, " in " & (iConnectDelay / 60000) & " minutes.", ".")))
    tHMConnect.Start
   Endif

End

Public Sub HM_Found()

  Main.WriteLog(("HomeMatic IP address resolved."))

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectTCP() As Boolean

  ' try to close the connection
  Try hHM.Close

  ' get a new one
  hHM = New Socket As "HM"
  Try hHM.Connect(sTCPHost, iTCPPort)
  If Error Then Print ERROR.Text

  ' start keeping the HMLAN Adapter alive
  SendHMLAN("K")
  tKeepAlive = New Timer As "tKeepAlive"
  tKeepAlive.Delay = 20000
  tKeepAlive.Start

  ' all ok
  Return True

Catch 'some errors

  Main.WriteLog(("HomeMatic Error: ") & ERROR.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try tHMBusy.Stop
  Try tHMConnect.Stop
  Try hHM.Close

  Main.WriteLog(("HomeMatic TCP socket close."))

  ' all ok
  Return True

Catch ' some errors

  Main.WriteLog(("HM Error: ") & ERROR.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' keep the HM LAN Adapter connection alive
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tKeepAlive_Timer()

  SendHMLAN("K")
  tKeepAlive.Stop
  tKeepAlive.Start
  StackStats()

End

Public Sub HM_Error()

  Dim sString As String = "HomeMatic: "

  ' handle error
  Select Case hHM.Status
    Case Net.CannotCreateSocket
      Main.WriteLog(sString & ("The system does not allow to create a socket."))
    Case Net.HostNotFound
      Main.WriteLog(sString & ("Host '") & sTCPHost & ("' not found."))
    Case Net.ConnectionRefused
      Main.WriteLog(sString & ("Unable to connect. Connection refused."))
    Case Net.CannotRead
      Main.WriteLog(sString & ("Error reading data."))
    Case Net.CannotWrite
      Main.WriteLog(sString & ("Error writing data."))
  End Select

End

Public Sub HM_Read()

  Dim bData As Byte

  Try bData = Read #hHM As Byte
  If Error Then Main.WriteDebugLog(("[HomeMatic] Error reading data from the TCP port! -> ") & Error.Text)
  ProcessReceivedChar(bData)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' control devices
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(iDeviceId As Integer, sValue As String)

  Dim iSpace As Integer
  Dim sAttribute, sCmd, sAddress, sModel As String

  ' find last space and split there to get command and value
  iSpace = RInStr(sValue, " ")
  If iSpace = 0 Then Return

  sCmd = Left(sValue, iSpace - 1)
  sAttribute = (Right$(sValue, (Len(sValue) - iSpace)))

  ' get address of Device
  Try sAddress = Devices.FindAddressForDevice(Devices.FindNameForDevice(iDeviceId))
  If Error Then Return

  ' get description of devicetype to use as model
  Try sModel = Devices.FindDescrForDeviceType(Devices.FindModuleForDevice(iDeviceId))
  If Error Then Return

  If sModel = "HM_CC_TC"
    Select sCmd
      Case "Temperature"
        HM_CC_TC_Temperature(sAddress, sAttribute)
      Case "Status Request"
        HM_CC_TC_Status(sAddress)
      Case "Parameter5"
      HM_CC_TC_GetconfigParam5(sAddress)
      Case "Parameter6"
      HM_CC_TC_GetconfigParam6(sAddress)
    End Select
    Return
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' pairing routines ---- can probably be run direct
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ProcessPair(sAddress As String, sStep As String)

  Select sStep
    Case "Pair1"
      Pair1(sAddress)
    Case "Pair2"
      Pair2(sAddress)
    Case "Pair3"
      Pair3(sAddress)
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' set the requested temperature (thermostat set temperature)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub HM_CC_TC_Temperature(sAddress As String, sValue As String)

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String
  Dim iValue As Integer

  iValue = Val(sValue) * 2
  sStatusbits = "A0"  ' wakeup, bidi, rpten
  sMsgtype = "11"
  sSrc = sHMLANid     ' address
  sDst = sAddress
  sData = "0202" & Hex$((iValue), 2)
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()
  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr(9) & sAddress & " Sending new SET Temperature " & (iValue / 2) & " Degrees C")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read the set temperature
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub HM_CC_TC_Status(sAddress As String)

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String

  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr(9) & sAddress & " Requested current SET Temperature")
  sStatusbits = "A1"  ' bidi, rpten
  sMsgtype = "01"
  sSrc = sHMLANid     ' address
  sDst = sAddress
  sData = "010E"
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()

End

Private Sub Pair1(sAddress As String)

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String

  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] " & sAddress & " Writing Register 5")
  sStatusbits = "A0"  ' bidi, rpten
  sMsgtype = "01"
  sSrc = sHMLANid     ' address
  sCmd = "+" & sSrc & ",00,00,"
  SendHMLAN(sCmd)
  sDst = sAddress
  sData = "00050000000000"
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()

End

Private Sub Pair2(sAddress As String)

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String
  
  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] " & sAddress & " Writing controller address ")
  sStatusbits = "A0"  ' bidi, rpten
  sMsgtype = "01"
  sSrc = sHMLANid     ' address
  sDst = sAddress
  sData = "000802010A" & Left$(sHMLANid, 2) & "0B" & Mid$(sHMLANid, 3, 2) & "0C" & Right$(sHMLANid, 2)
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()

End

Private Sub Pair3(sAddress As String)

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String

  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] " & sAddress & " Verifying controller address")
  sStatusbits = "A0"  ' bidi, rpten
  sMsgtype = "01"
  sSrc = sHMLANid     ' address
  sDst = sAddress
  sData = "0006"
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()

End

Private Sub HM_CC_TC_GetconfigParam5(sAddress As String) 'experimental

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String

  sStatusbits = "A0"  ' bidi, rpten
  sMsgtype = "01"
  sSrc = sHMLANid     ' address
  sDst = sAddress
  sCmd = "+" & sDst & ",00,00,"
  SendHMLAN(sCmd)
  SendHMLAN(sCmd)
  SendHMLAN(sCmd)
  sData = "02040000000005"
  'CONFIG CHANNEL 2
  'CONFIG PEER ADDRESS 000000
  'CONFIG PEER CHANNEL 0
  'CONFIG PARAM LIST 5
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()
  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] " & sAddress & " Requesting parameter list 5")

End

Private Sub HM_CC_TC_GetconfigParam6(sAddress As String) 'experimental

  Dim sStatusbits, sMsgtype, sSrc, sDst, sData, sCmd As String

  sStatusbits = "A0"  ' bidi, rpten
  sMsgtype = "01"
  sSrc = sHMLANid     ' address
  sDst = sAddress
  sCmd = "+" & sDst & ",00,00,"
  SendHMLAN(sCmd)
  SendHMLAN(sCmd)
  SendHMLAN(sCmd)
  sData = "02040000000006"
  'CONFIG CHANNEL 2
  'CONFIG PEER ADDRESS 000000
  'CONFIG PEER CHANNEL 0
  'CONFIG PARAM LIST 6
  sCmd = sStatusbits & sMsgtype & sSrc & sDst & sData
  StackCommand(sCmd)
  PushStack()
  If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] " & sAddress & " Requesting parameter list 6")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' message for HMLAN adapter
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendHMLAN(sBuffer As String)

  QueueAdd(sBuffer) ' & "\r\n")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' add command to stack
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StackCommand(sPayload As String)

  Dim sMsgnr, sDst, sHash As String

  sDst = Mid$(sPayload, 11, 6)
  sMsgnr = MessageCounter()

  ' populate with new commands
  sHash = sDst & sMsgnr
  cPayload.Add(sPayload, sHash)
  cAckstore.Add("STORED", sHash)
  cMsgnr.Add(sMsgnr, sHash)
  cDestDevice_id.Add(sDst, sHash)
  cTimestamp.Add(timestamp(), sHash) 'Time message went on the stack

  ' '- - - - - - - - - - - DEBUG - PRINT THE STACK - - - - - -
  ' FOR EACH sPayload IN cPayload
  '   PRINT "dst " & cPayload.Key & " NR " & cMsgnr[cPayload.key] & " Payload " & sPayload & " time " & cTimestamp[cPayload.Key] & " ACK " & cAckstore[cPayload.Key]
  ' NEXT
  ' PRINT "-------------------------------"

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find the oldest message on the stack that is in stored state
' and push it on the queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub PushStack()

  Dim sLowestTimestamp, sTimestamp, sOldestKey, sRaw, sSentHash As String

  sLowestTimestamp = "FFFFFFFF"
  For Each sTimestamp In cTimestamp
    If Val("&H" & sTimestamp & "&") < Val("&H" & sLowestTimestamp & "&") And cAckstore[cTimeStamp.key] = "STORED"
    sLowestTimestamp = sTimestamp
    sOldestKey = cTimestamp.Key
    Endif
  Next

  Try sRaw = "S" & timestamp() & "," & "00,00000000,01" & "," & Hex$((UNIXtime() - iDelta), 8) & "," & cMsgnr[sOldestkey] & cPayload[sOldestkey]
  If sRaw Then
    Try sSentHash = Mid$(cPayload[sOldestKey], 11, 6) & cMsgnr[sOldestKey]
    If sSentHash Then
      tAckTimer.Start
      QueueAdd(sRaw, sSentHash)
    Endif
  Endif

End

Public Sub QueueAdd(sCommand As String, Optional SHash As String) ' add command to queue

  cQueue.Add(sCommand, Rnd)
  If sHash Then cAckstore[sHash] = "QUEUED"

  ' process it
  If bHMBusy = False Then DoQueue()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process items in queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub DoQueue()

  Dim sPacket As String

  For Each sPacket In cQueue
    WriteCommand(sPacket, cQueue.Key)
    Break ' only do first one, or wait for echo and do next one ?
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Init()

  Dim rResult As Result

  ' define timer for interface busy
  tHMBusy = New Timer As "tHMBusy"
  tHMBusy.Stop

  ' define Timer for Ack/Nack timeouts
  tAckTimer = New Timer As "tAcktimer"
  tAckTimer.Delay = 4500                '10 x tHMBusy.Delay
  tAckTimer.Stop

  ' define timer for reconnect
  tHMConnect = New Timer As "tHMConnect"
  tHMConnect.Delay = iConnectRetry
  tHMConnect.Stop

  ' get offset from UTC for time calculations
  iUTCoffset = UTCoffset()

  ' set HMLAN address and time
  SendHMLAN("A" & sHMLANid)
  SendHMLAN("C")
  SendHMLAN("Y01,01,")
  SendHMLAN("Y02,01,")
  SendHMLAN("Y03,01,")
  SendHMLAN("Y03,01,")
  SendHMLAN("T" & systemtime() & ",04,00,00000000")
  SendHMLAN("A" & sHMLANid)

  ' setup ACK's for paired units
  rResult = Devices.FindDevicesForInterface(Devices.FindInterface("HomeMatic LAN Adapter"))
  For Each rResult
  SendHMLAN("+" & rResult!address & ",00,00")
  Next

  iCommandRetry = 0
  PushStack() ' kickstart

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send string to HMLAN interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub WriteCommand(sBuffer As String, sKey As String)

  Dim bByte As Byte
  Dim iCnt As Integer

  ' return if not connected yet
  If hHM.Status <> Net.Connected Then Return
  ' send the message to the tcp stream
  If bHMRawLogEnabled Then Main.WriteDebugLog("[HomeMatic] [RAW] " & Chr(9) & "TX " & sBuffer)

  Try Write #hHM, sBuffer, Len(sBuffer)
  If Error Then Main.WriteDebugLog(("[HomeMatic] Unable to write to TCP port! -> ") & Error.Text)

  ' remove sent command from queue
  cQueue.Remove(sKey)

  tHMBusy.Delay = 350 ' 350ms Value is critical, faster will cause the adapter to crash, if slower the thermostat falls asleep :(
  If bHMBusy = False Then bHMBusy = True
  tHMBusy.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' interface busy reset
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tHMBusy_Timer()

  bHMBusy = False
  tHMBusy.Stop

  If cQueue.Count Then ' if there are more commands left in queue, send next one
    DoQueue()
  Endif

End

Private Sub ProcessReceivedChar(bTemp As Byte)

  If bFirstByte = True Then
    bFirstByte = False
    iByteCount = 0
  Endif

  Main.ControlLed("HM", "On")

  Try RecBuf[iByteCount] = bTemp
  If Error Then Return

  If bTemp = &HA Then
    iByteCount -= 2 ' do not count \r\n
    bMessage = True
  Else
    Inc iByteCount
  Endif

  If bMessage Then Process_Raw_Message

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process the message received from the HM-LAN interface
' all messages are , separated text strings, closed with CR/LF
' so far 5 different types found
' with 1 segment: Response to D1 or D2 etc, don't know what it is
' with 3 segments: received when sending a D to the interface
' with 4 segments: the HM-LAN Ack I00, also received when sending a Y to the interface
' with 6 segments: Routine messages and pairing
' with 7 segments: Version information, response to "K"
' sending B turns on the red light and waits (for new FW ?)
' monitor (and store for transmission) received signal strength (later)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Private Sub Process_Raw_Message()

  Dim iCnt, iMaxSegments, iHMLANUptimecounter As Integer
  Dim sReceivedData As String
  Dim sMsgSegments As String[]

  bMessage = False
  bFirstByte = True

  For iCnt = 0 To iByteCount
    sReceivedData &= Chr(RecBuf[iCnt])
  Next
  sMsgSegments = Split(sReceivedData)
  iMaxSegments = sMsgSegments.Count
  If bHMRawLogEnabled Then Main.WriteDebugLog("[HomeMatic] [RAW] " & Chr(9) & "RX " & sReceivedData)

  Select Case iMaxSegments
    Case 1       ' Main.WriteDebugLog("[HomeMatic] Dx " & (sMsgSegments[0]))
    Case 3       ' Main.WriteDebugLog("[HomeMatic] D " & (sMsgSegments[0]) & " Seg1 " & (sMsgSegments[1]) & " T " & (sMsgSegments[2]))
    Case 4       ' Main.WriteDebugLog("[HomeMatic] Y " & (sMsgSegments[0]) & " Seg1 " & (sMsgSegments[1]) & " T " & (sMsgSegments[2]) & " Seg3 " & (sMsgSegments[3]))
    Case 6
      StackManager(sMsgSegments)
      If bHMRadioLogEnabled And If Left$(sMsgSegments[0]) = "E" Then Main.WriteDebugLog("[HomeMatic] [RADIO]" & Chr(9) & "RX " & Right$(sMsgSegments[0], 6) & " RSSI: " & Val("&H" & sMsgSegments[4]))
      ProcessRadioMessage(sMsgSegments)
    Case 7
       If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr(9) & (sMsgSegments[0]) & ", Uptime " & (Val("&H" & sMsgSegments[5]) Div 1000) & " Seconds, UNTRANSMITTED COMMANDS: " & iStackCountUntransmitted & ", OPEN COMMANDS: " & iStackCount & " for " & (Val("&H" & sMsgSegments[6] & "&")) & " DEVICE(S)")
       sHMLANReportedFW = sMsgSegments[1]
       sHMLANReportedSerial = sMsgSegments[2]
       sHMLANReportedAddress = sMsgSegments[4]
       iHMLANUptimecounter = Val("&H" & sMsgSegments[5])
       iDelta = UNIXtime() - iHMLANUptimecounter
    Case Else
       If bHMErrorLogEnabled Then Main.WriteDebugLog(("[HomeMatic] ERROR: " & Error.Text & " at " & Error.Where & " Parsing HM data packet: "))
       DisplayPacket(RecBuf)
  End Select

  ' do some errorchecking on the segmentdata, notify if something's not normal.
  ' idea: notify for low rssi values.

  'IF iMaxSegments > 0 THEN Process_Raw_Messagetypes(iMaxSegments, sMsgSegments)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process the message from the radio
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ProcessRadioMessage(sSegments As String[])

  Dim sSrc, sDst, sMsgnr, sMsgtype, sPayload, sCmd, sDummy, sHash As String
  Dim bAckRequested, bWakeup, bAsleep, bBroadcast, bPending, bCompleted As Boolean = False
  Dim iPayload_length, iCnt As Integer

  sMsgnr = Left(sSegments[5], 2)
  bWakeUp = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 0)       ' for later use and diagnostics
  bAsleep = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 1)       ' for later use and diagnostics
  bBroadcast = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 2)    ' for later use and diagnostics
  bAckRequested = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 5) ' for later use and diagnostics
  sMsgtype = (Mid(sSegments[5], 5, 2))                                 ' &00 &01 &02, &10, &58 &70
  sSrc = (Mid(sSegments[5], 7, 6))
  sDst = (Mid(sSegments[5], 13, 6))
  sPayload = (Right(sSegments[5], -18))
  iPayload_length = Len(Right(sSegments[5], -18))                      ' for later use and diagnostics

  '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  ' Messagetypes :
  ' &00 config request from Device
  ' &01 config message to Device
  ' &02 normally a plain Ack
  ' &10 status from device, sometimes with ack request
  ' &70 weather info
  ' &58 status from radiator valve
  '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  If bHMRadioLogEnabled Then Main.WriteDebugLog("[HomeMatic] [RADIO]" & Chr(9) & "RX " & sSegments[5] & IIf(bAckRequested, "    ACKREQ", "") & IIf(bWakeUp, " WAKEUP", "") & IIf(bBroadcast, " BROADCAST", "") & IIf(bAsleep, " SLEEPING", ""))
  If sMsgtype = "02" Then
    If bHMRadioLogEnabled And sSrc <> sHMLANid Then Main.WriteDebugLog("[HomeMatic] [RADIO]" & Chr(9) & "TX " & sSrc & CalcRSSI(Val("&H" & Right$(sPayload, 2) & "&")))
  Endif
  If Sdst = sHMLANid And bAckRequested = True And sMsgtype = "3F" Then
    sCmd = "+" & sSrc & ",02,00,"
    SendHMLAN(sCmd)
    sCmd = ("S" & Timestamp() & ",00,00000000,01," & Hex$((UNIXtime() - iDelta), 8) & "," & sMsgnr & "803F" & sDst & sSrc & "0204" & Systemtime())
    QueueAdd(sCmd)
  Endif
  If Sdst = sHMLANid And bAckRequested = True And sMsgtype <> "00" And sMsgtype <> "3F" And Left$(sSegments[0], 1) = "E" Then
    sCmd = "+" & sSrc & ",02,00,"
    SendHMLAN(sCmd)
    sCmd = ("S" & Timestamp() & ",00,00000000,01," & Hex$((UNIXtime() - iDelta), 8) & "," & sMsgnr & "8002" & sDst & sSrc & "00")
    QueueAdd(sCmd)
  Endif
  If bHMPairEnabled Then Pair_me(sMsgnr, sSrc, sPayload, sMsgtype)
  Display_Message(sSegments)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' respond to the pairing request if bHMPairEnabled
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub Pair_me(sMsgnr As String, sNewDevice_address As String, sPayload As String, sMsgtype As String)

  Dim sNewDevice_serH, sNewDevice_serA, sNewDevice_Id, sCmd, sDummy, sQuery, sValue, sType As String
  Dim sValues As String[]
  Dim iCnt, iModuleref, iDeviceId As Integer
  Dim rDeviceInfo, rResult As Result

  If sMsgtype = "00" ' received a pairing request while pairing is enabled
    sNewDevice[0] = sNewDevice_address      ' address
    sNewDevice[2] = Left(sPayload, 2)       ' firmware version
    sNewDevice[3] = Mid(sPayload, 3, 4)     ' device type
    sNewDevice[4] = Mid(sPayload, 7, 2)     ' device sub type
    sNewDevice[5] = Mid(sPayload, 15, 4)    ' device class (not used yet, but maybe later)
    sNewDevice[6] = Mid(sPayload, 26, 6)    ' device additional information (not used yet)
    sNewDevice_serH = Mid(sPayload, 9, 20)  ' device serial in hex char

    If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] Device serial in HEX '" & sNewDevice_serH & "'")
    For iCnt = 1 To 17 Step 2
      sNewDevice_serA &= Chr(Val("&" & (Mid(sNewDevice_serH, iCnt, 2))))
    Next
    sNewDevice[1] = sNewDevice_serA
    If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] Device serial in ASC '" & sNewDevice_serA & "'")

    If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr(9) & "Pairing Request from '" & sNewDevice[0] & "' of the type '" & sNewDevice[3] & "' and subtype '" & sNewDevice[4] & "'")
    rDeviceInfo = Main.hDB.Exec("SELECT * FROM devicetypes_homematic WHERE type_code = &1", sNewDevice[3])
    If rDeviceInfo!commands = "none supported"
      If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] [ERROR]" & Chr(9) & "This device is not (yet) supported " & sNewDevice[0] & ", " & sNewDevice[1] & ", " & sNewDevice[2] & ", " & sNewDevice[3] & ", " & sNewDevice[4] & ", " & sNewDevice[5])
    Else 'supported
      ' just clear any open commands for this device
      For Each sDummy In cDestDevice_id
        If sDummy = sNewDevice_address Then cDestDevice_id.Remove(cDestDevice_id.key)
      Next
      ProcessPair(sNewDevice_address, "Pair1")
      ProcessPair(sNewDevice_address, "Pair2")
      ProcessPair(sNewDevice_address, "Pair3")
      cDevice_state[sNewDevice_address] = "pairing complete"
      Wait 2
      For Each sDummy In cDestDevice_id
        If sDummy = sNewDevice_address Then cDevice_state[sNewDevice_address] = "pairing incomplete"
      Next
       ' insert device in devices table if pairing was successful
      If cDevice_state[sNewDevice_address] = "pairing complete" Then
        rDeviceInfo = Main.hDB.Exec("SELECT * FROM devicetypes_homematic WHERE type_code = &1", sNewDevice[3])
        sValue = rDeviceInfo!values
        sValues = Split(sValue)
        sType = rDeviceInfo!type
        ' check if device exists with this address.
        iDeviceId = Devices.Find(sNewDevice_address, Devices.FindInterface("HomeMatic LAN Adapter"), sType, sNewDevice_serA)
        ' If Not iDeviceId Then ' insert
        '   sNewDevice_Id = Devices.AutoCreateDevice(sNewDevice_address, Devices.FindInterface("HomeMatic LAN Adapter"), sType, sNewDevice_serA)
        '   rResult = Main.hDB.Exec("SELECT id FROM devices WHERE address = &1", sNewDevice_address)
        '   sQuery = "CREATE TABLE domotigalogs." & rResult!id & " (id int, LastSeen CHAR(64), lastChanged CHAR(6))"
        '   Main.hDB.Exec(sQuery)
        '   For Icnt = 0 To sValues.Count - 1
        '     sQuery = "ALTER TABLE domotigalogs." & rResult!id & " ADD COLUMN " & sValues[iCnt] & " FLOAT"
        '     Main.hDB.Exec(sQuery) ' create external log file
        '   Next
        ' Else ' it already exists
        '   If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] Device with address " & sNewDevice_address & " already exists. Not creating any new tables. Delete the device and logtables first to clear old device data")
        ' Endif ' autocreate
      Endif ' pairing
    Endif ' supported
  Endif ' msg 00

End

'----------------------------------------------
' display and log messages
'----------------------------------------------
Private Sub Display_Message(sSegments As String[])

  Dim sSrc, sDst, sMsgnr, sMsgtype, sPayload, sCmd As String
  Dim bAckRequested, bWakeup, bAsleep, bBroadcast As Boolean = False
  Dim iCnt, iDeviceId, iHumidity, iValvestate, iPayload_length, iIndex As Integer
  Dim fTemperature, fSetTemperature As Float
  Dim rResult As Result
  ' [GB2:ARRD] DIM sConfig AS String[20]
  Dim sConfig As New String[20]

  sMsgnr = Left(sSegments[5], 2)
  bWakeUp = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 0)
  bAsleep = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 1)
  bBroadcast = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 2)
  bAckRequested = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 5)
  sMsgtype = (Mid(sSegments[5], 5, 2))  ' %1 &2, &10, &58 &70 &00
  sSrc = (Mid(sSegments[5], 7, 6))      ' sending device id
  sDst = (Mid(sSegments[5], 13, 6))     ' receiving device id
  sPayload = (Right(sSegments[5], -18))
  iPayload_length = Len(Right(sSegments[5], -18))

  Try rResult = Main.hDB.Exec("SELECT id, module FROM devices WHERE address = &1", sSrc) ' don't bail out on unknown devices
  Try rResult = Main.hDB.Exec("SELECT description FROM devicetypes WHERE id = &1", rResult!module)
  If rResult.Available Then
    Select rResult!description
      Case "HM_CC_TC"
        Select sMsgtype
          Case "70"
            iHumidity = (ascii2hex(Mid(sSegments[5], 23, 2)))
            fTemperature = (ascii2hex(Mid(sSegments[5], 19, 4))) / 10
            iDeviceId = Devices.Find(sSrc, Devices.FindInterface("HomeMatic LAN Adapter"), "HM_CC_TC")
            If iDeviceId Then Devices.ValueUpdate(iDeviceId, "", fTemperature, iHumidity, "")
            If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr$(9) & sSrc & " Temperature " & fTemperature & " Humidity " & iHumidity)
          Case "58"
            iValvestate = (ascii2hex(Mid(sSegments[5], 21, 2))) / 2.55
            iDeviceId = Devices.Find(sSrc, Devices.FindInterface("HomeMatic LAN Adapter"), "HM_CC_TC")
            If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr$(9) & sSrc & " Requesting Valve " & sDst & " for " & iValvestate & " % open")
            If iDeviceId Then Devices.ValueUpdate(iDeviceId, "", "", "", iValvestate)
          Case "02"
            If bAsleep = False And bAckRequested = False And Mid$(sSegments[5], 19, 4) = "0102" Then
             fTemperature = (ascii2hex(Mid(sSegments[5], 23, 2))) / 2
             If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr$(9) & sSrc & " Set Temperature " & fTemperature)
             iDeviceId = Devices.Find(sSrc, Devices.FindInterface("HomeMatic LAN Adapter"), "HM_CC_TC")
             If iDeviceId Then Devices.ValueUpdate(iDeviceId, fTemperature, "", "", "")
            Else If bAsleep = False And bAckRequested = False And Mid$(sSegments[5], 19, 2) = "00" Then
              If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr$(9) & sSrc & " Awake ")
             Else
              If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] [ERROR]" & Chr$(9) & sSrc & " Message type " & sMsgtype & " with unsupported content " & Left$(sSegments[5], -2))
            Endif
          Case "10"
            If (bAsleep = False) And If (bAckRequested = True) And If (Mid$(sSegments[5], 19, 4) = "0602") Then
              fTemperature = (ascii2hex(Mid(sSegments[5], 23, 2))) / 2
              If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr$(9) & sSrc & " Manual Override Temperature " & fTemperature)
              iDeviceId = Devices.Find(sSrc, Devices.FindInterface("HomeMatic LAN Adapter"), "HM_CC_TC")
              If iDeviceId Then Devices.ValueUpdate(iDeviceId, fTemperature, "", "", "")
             Else If Left(sSegments[0]) = "E" And (bAsleep = False) And If (bAckRequested = True) And If (Mid$(sSegments[5], 19, 2) = "03") Then
              sConf5Dump &= Left(Right(sPayload, -4), -2) ' experimental, only through quickset after db modification (add the commands)
             Else
              If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] [ERROR]" & Chr$(9) & sSrc & "Message type " & sMsgtype & " with unsupported content " & Left$(sSegments[5], -2))
            Endif
          Case "3F"
            If bHMInfoLogEnabled Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr(9) & sSrc & " Timesync ") ' done in Ack message"
          Case "00"
            If bHMErrorLogEnabled And bHMPairEnabled = False Then Main.WriteDebugLog("[HomeMatic] [INFO]" & Chr(9) & "Received Pairing request from " & sSrc & " while not in Pairing mode")
          Case Else
            If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] [ERROR] Unknown message " & Left$(sSegments[5], -2) & " from address " & sSrc)
        End Select ' HM-CC-TC
      Case Else
        If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] [ERROR]" & Chr(9) & "Unknown Devicetype (Check Database)")
    End Select ' devices
  Else
    If bHMErrorLogEnabled Then
      If sSrc = sHMLANid Then
         'Main.WriteDebugLog("[HomeMatic] [RADIO]" & Chr(9) & "ECHO TX " & sSegments[5])
      Else
        Main.WriteDebugLog("[HomeMatic] [ERROR]" & Chr(9) & "Received message " & sSegments[5] & " from unknown device with address " & sSrc)
      Endif
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' if a command has not been confirmed by the HMLAN Adapter after 3.5 seconds
' assume that it was lost and stop waiting for it, send it again.
' after 25 retries stop retrying, the cable is probably disconnected
' During pairing 10 commands are going out in sequence, each adding a THMBusy delay of 350ms
' ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tAckTimer_Timer()

  tAckTimer.Stop
  Restack()
  If iCommandRetry < 25
    Inc iCommandRetry
    If iCommandRetry Mod 5 = 0 And bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] retries left: " & (25 - iCommandRetry))
    PushStack() ' retry and continue
   Else
    If bHMErrorLogEnabled Then Main.WriteDebugLog("[HomeMatic] Exceeded 25 retry attempts, retry mechanism disabled until HMLAN Adapter is reachable again")
  Endif

End

Private Sub Restack()

  Dim sSrc, sPayload As String

  For Each sSrc In cDestDevice_id
    If cAckstore[cDestDevice_id.Key] = "QUEUED"
      sPayload = cPayload[cDestDevice_id.Key]
      cPayload.Remove(cDestDevice_id.Key)
      cAckstore.Remove(cDestDevice_id.Key)
      cMsgnr.Remove(cDestDevice_id.Key)
      cTimestamp.Remove(cDestDevice_id.Key)
      cDestDevice_id.Remove(cDestDevice_id.Key)
      StackCommand(sPayload)
    Endif
  Next

End

Public Sub Timestamp() As String ' mS since 00:00

  ' [GB2:ARRD] DIM sDateUnits AS String[4]
  Dim sDateUnits As New String[4]
  Dim iMilliseconds, iSeconds, iMinutes As Integer

  sdateUnits = Split(Format$(Now, "hh:nn:ssu"), ":")
  iMilliseconds = Val(Right$(sDateUnits[2], 3))
  iseconds = Val(Left$(sDateUnits[2], 2))
  iMilliseconds = 3600000 * sDateUnits[0] + 60000 * sDateUnits[1] + 1000 * iSeconds + iMilliseconds
  Return Hex$(iMilliseconds, 8)

End

' seconds since 01/01/2000
Public Sub Systemtime() As String

  Dim fTimestamp As Float
  Dim iTimestamp As Integer
  Dim sHexTimestamp, sDate As String

  fTimestamp = DateDiff("01/01/2000 00:00:00", Now, gb.Second)
  iTimestamp = fTimestamp - iUTCoffset
  sHexTimestamp = Hex$(iTimestamp, 8)

  Return sHexTimestamp

End

'offset in seconds from UTC
Public Function UTCoffset() As Integer

  Dim iUTCtime, iLOCALtime As Integer

  Shell ("date -u +%s") To iUTCtime
  iLOCALtime = DateDiff("01/01/1970 00:00:00", Now, gb.Second)
  Return iLOCALtime - iUTCtime

End

' unix time in mS since epoch
Public Function UNIXtime() As Long

  Dim iReturnValue As Long
  Dim sReturnValue As String

  Exec ["date", "-u", "+%s%N"] To sReturnValue
  iReturnValue = (Val(sReturnValue)) / 1000000
  Return iReturnValue

End

Private Sub DisplayPacket(bBuf As Byte[]) As String

  Dim sMsg As String
  Dim iCnt As Integer

  For iCnt = 0 To iByteCount
    sMsg &= Chr(bBuf[iCnt])
  Next
  Return sMsg

End

Private Function MessageCounter() As String

  If iMessageCounter < 255 Then
    Inc iMessageCounter
  Else
    iMessageCounter = 0
  Endif
  Return Hex$(iMessagecounter, 2)

End

Private Function ascii2hex(sAsciiString As String) As Short

  Dim iDecResult As Short

  iDecResult = Val("&H" & sAsciiString & "&")
  If iDecResult > 16384 Then iDecResult = - (32768 - iDecResult)
  Return iDecResult

End

Private Sub StackManager(sSegments As String[])

  Dim sSrc, sDst, sMsgnr, sDummy, sCmd, sHash, sPayload As String
  'DIM sHMRssi AS String  ' signal strength from device to HM adapter, not yet implemented
  'DIM sDeviceRssi AS String  'signal strength from HM adapter to device, not yet implemented
  Dim bPending, bAckRequested, bBroadcast, bAsleep As Boolean = False

  sMsgnr = Left(sSegments[5], 2)
  bAsleep = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 1)       ' for later use and diagnostics
  bBroadcast = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 2)    ' for later use and diagnostics
  bAckRequested = BTst(Val("&H" & (Mid(sSegments[5], 3, 2)) & "&"), 5) ' for later use and diagnostics
  sSrc = (Mid(sSegments[5], 7, 6))
  sDst = (Mid(sSegments[5], 13, 6))

  ' process received ACKs AND NACKs

  ' check for NACK
  sHash = sDst & sMsgnr
  If (cMsgnr.exist(sHash) And sSegments[1] = "0008") Then
    cAckstore[sHash] = "NACK"
    sCmd = "+" & sDst & ",02,00,"
    SendHMLAN(sCmd)
    'DEBUG sDst & " " & sMsgnr & " NACK Received"
    tAckTimer.Stop
    PushStack() ' now run the next command on the queue
  Endif

  sHash = sSrc & sMsgnr

  ' check for ACK
  If (cMsgnr.Exist(sHash) And sSegments[1] = "0001") Then
    cPayload.Remove(sHash)
    cAckstore.Remove(sHash)
    cMsgnr.Remove(sHash)
    cDestDevice_id.Remove(sHash)
    cTimestamp.Remove(sHash)
    'DEBUG sSrc & " " & sMsgnr & " ACK Received"
    tAckTimer.Stop
    PushStack() ' now run the next command on the queue
  Endif

  ' check for open messages on the stack
  If sSegments[1] = "0081" Then
    For Each sDummy In cDestDevice_id
      If cAckstore[cDestDevice_id.Key] = "NACK" And Left$(cDestDevice_id.Key, 6) = sSrc
        sPayload = cPayload[cDestDevice_id.Key]
        cPayload.Remove(cDestDevice_id.Key)
        cAckstore.Remove(cDestDevice_id.Key)
        cMsgnr.Remove(cDestDevice_id.Key)
        cTimestamp.Remove(cDestDevice_id.Key)
        cDestDevice_id.Remove(cDestDevice_id.Key)
        'DEBUG sSrc & " " & sMsgnr & " Delayed NACK Received"
        'put all commands for this device back in the queue
        StackCommand(sPayload)
      Endif
    Next
    PushStack()
  Endif

  ' command completed is 8010 and 8002 (no ack requested)
  For Each sDummy In cDestDevice_id
   If sDummy = sSrc Then bPending = True ' if we still have commands in the stack, don't clear the status
  Next
  ' rule out all other messages 
  If bPending = False And bAckRequested = False And bBroadcast = False And bAsleep = False And sdst = sHMLANid Then
    SendHMLAN("-" & sSrc)
    SendHMLAN("+" & sSrc)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' analyze the stack.
' untransmitted: No response from the HMLAN adapter
' stack count: No response from remote device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub StackStats()

  Dim sDummy As String

  iStackCountUntransmitted = 0
  iStackCount = 0
  For Each sDummy In cAckstore
    If sDummy = "QUEUED" Then Inc iStackCountUntransmitted
    If sDummy = "NACK" Then Inc iStackCount
  Next

End

Private Sub CalcRSSI(fRSSI As Float) As String

  If fRSSI Then
    Return " RSSI: " & IIf(fRSSI >= 128, (fRSSI - 256) / 2 - 74, fRSSI / 2 - 74)
  Else
    Return ""
  Endif

End

' implement properties
Private Function TCPHost_Read() As String

  Return sTCPHost

End

Private Sub TCPHost_Write(Value As String)

  sTCPHost = Value

End

Private Function TCPPort_Read() As Integer

  Return iTCPPort

End

Private Sub TCPPort_Write(Value As Integer)

  iTCPPort = Value

End

Private Function HMDebug_Read() As Boolean

  Return bHMDebug

End

Private Sub HMDebug_Write(Value As Boolean)

  bHMDebug = Value

End

Private Function HMLANid_Read() As String

  Return sHMLANid

End

Private Sub HMLANid_Write(Value As String)

  sHMLANid = Value

End

Private Function Pairing_Read() As Boolean

  Return bHMPairEnabled

End

Private Sub Pairing_Write(Value As Boolean)

  bHMPairEnabled = Value

End

Private Function Rawlog_Read() As Boolean

  Return bHMRawlogEnabled

End

Private Sub Rawlog_Write(Value As Boolean)

  bHMRawLogEnabled = Value

End

Private Function Radiolog_Read() As Boolean

  Return bHMRadioLogEnabled

End

Private Sub Radiolog_Write(Value As Boolean)

  bHMRadioLogEnabled = Value

End

Private Function Infolog_Read() As Boolean

  Return bHMInfoLogEnabled

End

Private Sub Infolog_Write(Value As Boolean)

  bHMInfoLogEnabled = Value

End

Private Function Errorlog_Read() As Boolean

  Return bHMErrorLogEnabled

End

Private Sub Errorlog_Write(Value As Boolean)

  bHMErrorLogEnabled = Value

End
