' Gambas class file

' Description:
' Mochad.class
' Provide support for Mochad.

' Development Status:
' Just started developing this one.

' Links:
' http://sourceforge.net/apps/mediawiki/mochad/index.php?title=Main_Page

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module is written by and Copyright(C) 2012 Roland van Hulzen.
' This class only support standard X10 but the mochad interface can do much more.

' Read file called COPYING for license details.

Property TCPHost As String
Property TCPPort As String
Property GlobalX10 As Boolean
Property MochadDebug As Boolean

Private sTCPHost As String
Private iTCPPort As String
Private bMochadDebug As Boolean
Private bGlobalX10 As Boolean

Public hMochad As New Socket

Public sBuffer As String
Public tMochadConnect As Timer
Private iConnectRetry As Integer = 0 ' retries
Private iConnectDelay As Integer = 60000 ' 1 minute

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the connection
  Try hMochad.Close
Return
  ' get a new one
  hMochad = New Socket As "Mochad"
  hMochad.Connect(sTCPHost, iTCPPort)
  'ParseDump("/home/ron/Mochad.txt")
  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("Mochad Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is connected
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Mochad_Ready()

  Main.WriteLog(("Mochad TCP socket connected."))

  ' initialise the ASCII communication
  Try Write #hMochad, "ASCI", 4

  ' define timer for reconnect
  tMochadConnect = New Timer As "MochadConnect"
  tMochadConnect.Delay = iConnectRetry
  tMochadConnect.Stop

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is closed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Mochad_Closed()

  Main.WriteLog(("Mochad Error: TCP socket closed."))
  If iConnectRetry < 6 Then
    Main.WriteDebugLog(("[Mochad ]Retry to connect" & IIf(iConnectRetry, " in " & (iConnectDelay / 60) & " minutes.", ".")))
    tMochadConnect.Start
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reconnect routine
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tMochadConnect_Timer()

  If Not Connect() Then
    Inc iConnectRetry
    iConnectDelay *= iConnectRetry
    tMochadConnect.Delay = iConnectDelay
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try tMochadConnect.Stop
  Try hMochad.Close
  Main.WriteLog(("Mochad TCP socket close."))

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("Mochad Error: ") & ERROR.Text & (" at ") & ERROR.Where)
  Return False

End

Public Sub Mochad_Found()

  Main.WriteLog(("Mochad IP address resolved."))

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' error while connected/connecting to host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Mochad_Error()

  ' handle error
  Select Case hMochad.Status
    Case Net.CannotCreateSocket
      Main.WriteLog(("Mochad: The system does not allow to create a socket."))
    Case Net.HostNotFound
      Main.WriteLog(("Mochad: Host '") & sTCPHost & ("' not found."))
    Case Net.ConnectionRefused
      Main.WriteLog(("Mochad: Unable to connect to interface. Connection refused."))
    Case Net.CannotRead
      Main.WriteLog(("Mochad: Error reading data."))
    Case Net.CannotWrite
      Main.WriteLog(("Mochad: Error writing data."))
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send generic command via tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(sAddress As String, sValue As String)

  Dim sCmd As String

  sCmd = "PL " & Left(sAddress, 1) & Val(Right(sAddress, 2)) & " " & Upper(sValue) 
  If Main.bMochadDebug Then Main.WriteDebugLog(("[Mochad] Send command: ") & sCmd)
  Try Write #hMochad, sCmd & Chr$(10), Len(sCmd) + 1
  If Error Then Main.WriteDebugLog(("[Mochad] Error writing data to the TCP port! -> ") & Error.Text)

End

' just here for development/debugging
Public Sub ParseDump(sFile As String)

  Dim hFile As File
  Dim sLine As String

  Try hFile = Open sFile For Input
  If Error Then Return
  While Not Eof(hFile)
    Line Input #hFile, sLine
    If Len(sLine) > 5 Then ParseData(sLine)
  Wend

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' data received on tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Mochad_Read()

  Dim sData As String

  Try Read #hMochad, sData, 1
  If Error Then Main.WriteDebugLog(("[Mochad] Error reading data from the TCP port! -> ") & Error.Text)
  If sData = Chr(10) Then ' buffer until linefeed then parse
    ParseData(sBuffer)
  Else
    sBuffer &= sData
  Endif

End

Private Sub ParseData(sStr As String)

  Dim sTemp, sPL_RF, sAddress, sMochad, sType, sValue, sTamper As String
  Dim sBatt As String = "?"
  Dim aData, aType, aCmd As String[]
  Dim iDeviceId As Integer

  sBuffer = Null
  If InStr(sStr, "Raw data received:") = 0 Then
    If Main.bMochadDebug Then Main.WriteDebugLog(("[Mochad] Received string: ") & sStr)
    sTemp = Mid(sStr, 22, Len(sStr))
    sPL_RF = Mid(sStr, 19, 2)
    Select sPL_RF
      Case "PL"
        If InStr(sTemp, "HouseUnit:") Then 
          sMochad = Mid(sTemp, 12, 1) & Format$(Trim(Mid(sTemp, 13, 10)), "00")
        Else
          If InStr(sTemp, "Func:") Then 
            If (Left(sMochad, 1) <> " " And sMochad <> "") Then
              sMochad = sMochad & " " & Mid(sTemp, InStr(sTemp, "Func:") + 5, 10)
            Endif
          Endif
        Endif
        If Len(sMochad) > 4 And sMochad <> "Error" Then
          sAddress = Upper$(Trim(Left(sMochad, InStr(sMochad, " "))))
          sValue = Upper$(Trim(Right(sMochad, InStr(sMochad, " ") - 1)))
          UpdateX10(sAddress, sValue)
        Endif
      Case "RF"
        ' Motion_alert_MS10A
        ' Motion_normal_MS10A
        ' Contact_alert_min_DS10A
        ' Contact_normal_min_DS10A
        ' Contact_alert_max_DS10A
        ' Contact_normal_max_DS10A
        ' Contact_alert_min_low_DS10A
        ' Contact_normal_min_low_DS10A
        ' Contact_alert_max_low_DS10A
        ' Contact_normal_max_low_DS10A
        ' Arm_KR10A
        ' Disarm_KR10A
        ' Lights_On_KR10A
        ' Lights_Off_KR10A
        ' Panic_KR10A
        aData = Scan(sStr, "* RFSEC Addr: * Func: *")
        If aData.Count = 3 Then
          sAddress = "x10sec 0x" & Lower(Replace(aData[1], ":", ""))
          If aData[2] = "(null)" Then Return
          aType = Scan(aData[2], "*_*")
          If aType.Count = 2 Then
          Select aType[0]
            Case "Motion"
              sType = "MOTION"
              aCmd = Scan(aType[1], "*_*")
              If aCmd.Count = 2 Then
                sValue = IIf(aCmd[0] = "alert", "Motion", "No Motion")
              Endif
            Case "Contact"
              sType = "DWS"
              aCmd = Scan(aType[1], "*_*_*")
              If aCmd.Count = 3 Then
                sValue = IIf(aCmd[0] = "alert", "Alert", "Normal")
                sTamper = IIf(aCmd[1] = "min", "Secure", "Tamper")
                sBatt = "Low"
              Else
                aCmd = Scan(aType[1], "*_*")
                If aCmd.Count = 2 Then
                  sValue = IIf(aCmd[0] = "alert", "Alert", "Normal")
                  sTamper = IIf(aCmd[1] = "min", "Secure", "Tamper")
                  sBatt = "OK"
                Endif
              Endif
            Case "Arm", "Disarm", "Panic"
              sType = "REMOTE"
              sValue = aType[0]
            Case "Lights"
              sType = "REMOTE"
              aCmd = Scan(aType[1], "*_*")
              If aCmd.Count = 2 Then
                sValue = "Lights " & aCmd[0]
              Endif
            Case Else
              If Main.bMochadDebug Then Main.WriteDebugLog(("[Mochad] Unknown device type: ") & aData[1])
          End Select
        Else
          If Main.bMochadDebug Then Main.WriteDebugLog(("[Mochad] Invalid command data: ") & aData[2])
        Endif
      Else
        If Main.bMochadDebug Then Main.WriteDebugLog(("[Mochad] Invalid data: ") & sStr)
      Endif
      If Main.bMochadDebug Then
        Main.WriteDebugLog("Address = " & sAddress)
        Main.WriteDebugLog("Command = " & sValue)
        Main.WriteDebugLog("Type = " & sType)
        Main.WriteDebugLog("Battery level = " & sBatt)
      Endif
      iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Mochad Interface"), sType)
      ' update value & battery status
      If iDeviceId Then
        Devices.ValueUpdate(iDeviceId, sValue, sTamper, "", "")
        If Len(sBatt) Then Devices.Battery(iDeviceId, sBatt)
      Endif
    End Select
  Endif

Catch
   Main.WriteDebugLog(("[Mochad] Error while Parsing: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub UpdateX10(sCode As String, sCommand As String)

  Dim iDeviceId As Integer

  If Main.bMochadDebug Then Main.WriteDebugLog(("[Mochad] Received command '") & sCommand & ("' for device '") & sCode & "'")

  If bGlobalX10 Then
    iDeviceId = Devices.FindAll(sCode, 9999, "X10")
  Else
    iDeviceId = Devices.Find(sCode, Devices.FindInterface("Mochad Interface"), "X10")
  Endif
  If iDeviceId Then
    If sCommand = "ON" Then
      Devices.ValueUpdate(iDeviceId, "On", "", "", "")
    Else If sCommand = "OFF" Then
      Devices.ValueUpdate(iDeviceId, "Off", "", "", "")
    Endif
  Endif

End

' implement properties
Private Function TCPHost_Read() As String

  Return sTCPHost

End

Private Sub TCPHost_Write(Value As String)

  sTCPHost = Value

End

Private Function TCPPort_Read() As String

  Return iTCPPort

End

Private Sub TCPPort_Write(Value As String)

  iTCPPort = Value

End

Private Function MochadDebug_Read() As Boolean

  Return bMochadDebug

End

Private Sub MochadDebug_Write(Value As Boolean)

  bMochadDebug = Value

End

Private Function GlobalX10_Read() As Boolean

  Return bGlobalX10

End

Private Sub GlobalX10_Write(Value As Boolean)

  bGlobalX10 = Value

End
