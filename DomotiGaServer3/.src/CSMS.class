' Gambas class file

' Description:
' CSMS.class
' Support for SMS modem for outgoing SMS text messages
' Tested with Siemens MC35i and Nokia 6300

' Development Status:
' Modem init and sending SMS text messages works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Property Port As String
Property Baud As String
Property PIN As String
Property ServiceCentre As String
Property Contact As String
Property SMSDebug As Boolean

Private sPort As String
Private sBaud As String
Private sPIN As String
Private sServiceCentre As String
Private sContact As String
Private bSMSDebug As Boolean

Public hSMS As New SerialPort
Private sManufacturer As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the port
  Try hSMS.Close

  ' get a new one
  hSMS = New Serialport As "SMS"
  With hSMS
    .PortName = sPort
    .Speed = sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  End With

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("SMS Error: ") & ERROR.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize SMS modem, enter PIN if needed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub InitModem() As Boolean

  Dim sResult As String

  ' a few possible commands strings
  ' sCmd = "AT+CGMI" ' get manufacturer info
  ' sCmd = "AT+CPIN=0000" ' enter PIN
  ' sCmd = "AT+CPIN?" ' ask PIN status
  ' sCmd = "AT+CREG?" ' network status
  ' sCmd = "AT+COPS=?" ' show network(s)
  ' sCmd = "AT+CSQ" ' show network field strength
  ' sCmd = "AT+CGSN" ' show IMEI number
  ' init
  If bSMSDebug Then Main.WriteDebugLog(("[SMS] Send init AT:"))
  If InStr(SendCmd("AT"), "OK") Then
    sManufacturer = SendCmd("AT+GMI")
    ' ask PIN status
    If bSMSDebug Then Main.WriteDebugLog(("[SMS] Ask PIN status:"))
    sResult = SendCmd("AT+CPIN?")
    If InStr(sResult, "OK") Then
      ' no PIN needed
      If InStr(sResult, "READY") Then
        If SetServiceCenter() Then Return True
      End If
      ' modem ask for PIN
      If InStr(sResult, "SIM PIN") Then
        If bSMSDebug Then Main.WriteDebugLog(("[SMS] Send PIN:"))
        SendCmd("AT+CPIN=" & sPIN)
        ' ask for PIN status again
        If bSMSDebug Then Main.WriteDebugLog(("[SMS] Ask PIN status:"))
        ' PIN ok
        If InStr(SendCmd("AT+CPIN?"), "READY") Then
          If SetServiceCenter() Then Return True
        End If
      End If
    End If
  End If
  ' something went wrong
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' set or get SMS service center
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub SetServiceCenter() As Boolean

  Dim sResult, sNetwork As String

  ' check manufacturer
  If InStr(sManufacturer, "Nokia") Then
    ' get service center
    sResult = SendCmd("AT+CSCA?")
    If bSMSDebug Then Try Main.WriteLog("SMS ServiceCenter is " & Mid(sResult, InStr(sResult, Chr(34)), RInStr(sResult, Chr(34)) - InStr(sResult, Chr(34)) + 1))
  Else
    ' set service center
    If bSMSDebug Then Main.WriteDebugLog(("[SMS] Set ServiceCenter:"))
    sResult = SendCmd("AT+CSCA=" & sServiceCentre)
  End If
  If InStr(sResult, "OK") Then
    sResult = SendCmd("AT+COPS?")
    Try sNetwork = Mid(sResult, InStr(sResult, Chr(34)), RInStr(sResult, Chr(34)) - InStr(sResult, Chr(34)) + 1)
    If InStr(sManufacturer, "Nokia") Then
      Select Mid$(sNetwork, 2, 5)
        Case 20404
          sNetwork = "Vodafone NL" 'Libertel
        Case 20408
          sNetwork = "KPN NL"
        Case 20412
          sNetwork = "Telfort NL"
        Case 20416
          sNetwork = "T-Mobile NL" 'Ben
        Case 20420
          sNetwork = "T-Mobile NL" 'Orange, Dutchtone?
        Case Else
          sNetwork = "??"
      End Select
    End If
    Main.WriteLog(("SMS modem is initialized and registered at ") & sNetwork & (" network."))
    Return True
  End If
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create SMS message and send it to modem
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendSMS(sText As String, sTo As String) As Boolean

  Dim sTemp, sData, sResult As String
  Dim iTries As Integer = 32

  ' send message
  If bSMSDebug Then Main.WriteDebugLog(("[SMS] SendSMS:"))
  sResult = SendCmd("AT+CMGF=1")
  If InStr(sResult, "OK") Then
    If hSMS.Status <> Net.Active Then Return
    If bSMSDebug Then Main.WriteDebugLog(("[SMS] > AT+CMGS=") & Chr(34) & sTo & Chr(34))
    Print #hSMS, "AT+CMGS=" & Chr(34) & sTo & Chr(34) & Chr(13)

    While (iTries > 0)
      ' see if we got some data.
      Try sTemp = Read #hSMS, Lof(hSMS)
      ' wait a bit and read response.
      Sleep 0.01
      sData &= sTemp
      If InStr(sData, ">") Then
        If bSMSDebug Then Main.WriteDebugLog(("[SMS] got '>' at try #") & iTries)
        If bSMSDebug Then Main.WriteDebugLog(("[SMS] > ") & sText & "CTRL-Z")
        Print #hSMS, sText & Chr(26);
        Main.WriteLog(("I have just sent an SMS message to ") & sTo & ".")
        Break
      End If
      Dec iTries
    Wend
  End If
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read SMS messages
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ReadSMS() As String

  Dim sCmd, sResult As String

  sCmd = "AT+CMGL" ' read messages
  If bSMSDebug Then Main.WriteDebugLog(("[SMS] ReadSMS:"))
  sResult = SendCmd(sCmd, 1)

  Return sResult

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send AT command string to modem
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCmd(sCmd As String, Optional bFlag As Boolean) As String

  Dim sData, sTemp, sOutput As String
  Dim iTries As Integer = 80

  If hSMS.Status <> Net.Active Then Return

  If bFlag Then
    Print #hSMS, sCmd;
  Else
    Print #hSMS, sCmd; Chr$(13); Chr$(10);
  End If
  If bSMSDebug Then Main.WriteDebugLog("[SMS] > " & sCmd)

  While (iTries > 0)
    ' see if we got some data.
    Try sTemp = Read #hSMS, Lof(hSMS)
    ' wait a bit and read response.
    Sleep 0.01
    sData &= sTemp
    If Right(sData, 2) = Chr(13) & Chr(10) Then
      If bSMSDebug Then Main.WriteDebugLog(("[SMS] got response at try #") & iTries)
      Break
    End If
    Dec iTries
  Wend
  sOutput = Replace(sData, "\r", "")
  If bSMSDebug Then Main.WriteDebugLog("[SMS] < " & sOutput)

  Return sOutput

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try hSMS.Close
  Main.WriteLog(("SMS serial port close."))

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("SMS Error: ") & ERROR.Text)
  Return False

End

' implement the properties
Private Function Port_Read() As String

  Return sPort

End

Private Sub Port_Write(sValue As String)

  sPort = sValue

End

Private Function Baud_Read() As String

  Return sBaud

End

Private Sub Baud_Write(sValue As String)

  sBaud = sValue

End

Private Function PIN_Read() As String

  Return sPIN

End

Private Sub PIN_Write(sValue As String)

  sPIN = sValue

End

Private Function Contact_Read() As String

  Return sContact

End

Private Sub Contact_Write(sValue As String)

  sContact = sValue

End

Private Function ServiceCentre_Read() As String

  Return sServiceCentre

End

Private Sub ServiceCentre_Write(sValue As String)

  sServiceCentre = sValue

End

Private Function SMSDebug_Read() As Boolean

  Return bSMSDebug

End

Private Sub SMSDebug_Write(sValue As Boolean)

  bSMSDebug = sValue

End
