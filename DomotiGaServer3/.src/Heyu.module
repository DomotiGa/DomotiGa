' Gambas module file

' Description:
' Heyu.module
' Use Heyu 2.0 to control X10 devices.

' Development Status:
' Writing is working, maybe better error checking is needed.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2011 Ron Klinkien.

' Reading device status written by Renaud.

' Read file called COPYING for license details.

Private sOutput As String
Private sMonitorBuffer As String
Private pMonitor As Process

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send a command with heyu
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(sDevice As String, sValue As String)

  Dim sCommand As String

  Select Case LCase(sValue)
    Case "on", "off"
      sCommand = Main.sHeyuCommand & " " & LCase(sValue) & " " & sDevice
      Shell sCommand & " 2>&1" For Read As "Heyu"
      If Main.bHeyuDebug Then Main.WriteDebugLog(("[Heyu] Run: ") & sCommand)
    Case Else
      Main.WriteDebugLog(("[Heyu] Unsupported command ") & sValue)
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' got output, save it
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Heyu_Read()

  Dim sLine As String

  Read #Last, sLine, -256
  sOutput &= sLine

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' digitemp has finished, process it's output
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Heyu_Kill()

  If Main.bHeyuDebug Then Main.WriteDebugLog(("[Heyu] Result: ") & sOutput)
  sOutput = Null

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start heyu monitor
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Monitor()

  pMonitor = Exec [Main.sHeyuCommand, "monitor"] For Read As "HeyuMonitor"

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read monitor output
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub HeyuMonitor_Read()

  Dim sLine As String

  Try Read #Last, sLine, -256
  If Error Then Main.WriteDebugLog(("[Heyu] Error reading data from Monitor output-> ") & Error.Text)
  If Len(sLine) > 0 Then
    sMonitorBuffer &= sLine
    If InStr(sMonitorBuffer, "func") > 0 Then
      ParseMonitorMessage(sMonitorBuffer)
      sMonitorBuffer = Null
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse monitor output
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ParseMonitorMessage(sMessage As String)

  Dim iDeviceId As Integer
  Dim aFirstLine, aSecondLine As String[]
  Dim sAddress, sOrder As String

  If Main.bHeyuDebug Then Main.WriteDebugLog(("[Heyu] Monitor get: ") & sMessage)

  ' Message sample :
  ' 1 line : 09/19 18:44:53  rcvi addr unit       4 : hu I4  (_no_alias_)
  ' 2 line : 09/19 18:44:53  rcvi func          Off : hc I

  aFirstLine = Scan(sMessage, "*addr unit* : hu*(*")
  aSecondLine = Scan(sMessage, "*func*: hc*")

  If aFirstLine.Count = 4 And aSecondLine.Count = 3 Then
    sAddress = Trim(aFirstLine[2])

    If Len(sAddress) = 2 Then
      sAddress = Left(sAddress, 1) & "0" & Right(sAddress, 1)
    Endif
    sOrder = Trim(aSecondLine[1])

    If Main.bHeyuGlobalX10 Then
      iDeviceId = Devices.FindAll(sAddress, 9999, "X10")
    Else
      iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Heyu X10"), "X10")
    Endif

    ' if found then update it's value
    If iDeviceId Then Devices.ValueUpdate(iDeviceId, sOrder, "", "", "")
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' stop monitor
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Monitor_Stop()

  pMonitor.Kill

End
