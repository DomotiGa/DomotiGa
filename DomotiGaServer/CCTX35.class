' Gambas class file

' Description:
' CCTX35.class
' Support for Xanura CTX35 A10/X10 interface

' Development Status:
' Controlling devices works, with on/off, dim and bright, reading of on/off status works, reading of dim/bright needs to be implemented.

' Links:
' http://www.hometoys.com/htinews/dec02/articles/kingery16/kingery16.htm

' Credits:
' Thanks to Pieter from BWired.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PROPERTY Port AS String
PROPERTY Baud AS String
PROPERTY PollTime AS Integer
PROPERTY CTX35Debug AS Boolean

PRIVATE sPort AS String
PRIVATE sBaud AS String
PRIVATE iPollTime AS Integer
PRIVATE bCTX35Debug AS Boolean

PUBLIC hCTX35 AS NEW SerialPort
PUBLIC tCTX35 AS Timer

PUBLIC sCommandToSend AS String ' holds commands to send
PRIVATE aCTX AS NEW String[3, 7] ' buffer received commands

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Connect() AS Boolean

  ' try to close the port
  TRY hCTX35.Close

  ' get a new one
  hCTX35 = NEW Serialport AS "CTX35"

  WITH hCTX35
    .PortName = sPort
    .Speed = sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH


  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("CTX35 Error: ") & ERROR.Text)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  TRY hCTX35.Close
  Main.WriteLog(("CTX35 serial port close."))

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("CTX35 Error: ") & ERROR.Text)
  RETURN FALSE

END

PUBLIC SUB Run()

  tCTX35 = NEW Timer AS "tCTX35"
  tCTX35.Delay = iPollTime
  tCTX35.Start

END

PRIVATE SUB Received(sData AS String)

  IF Left$(sData, 1) = "$" AND Right$(sData, 1) = "#" THEN ' received complete packet
    IF sData = "$<2800!4B#" THEN ' ignore acks
      sData = ""
    ELSE
      sData = Replace(sData, "$<2800!4B#", "") ' remove ack packet
      IF Main.bCTX35Debug THEN Main.WriteDebugLog("[CTX35] " & sData)
      TranslateCTX(sData)
    END IF
    sData = ""
  END IF

END

PRIVATE SUB TranslateCTX(sStr AS String)

  DIM iPos AS Integer = 1
  DIM sChar AS String

  IF Main.bCTX35Debug THEN Main.WriteDebugLog("[CTX35] " & sStr)

  ' search for start and end
  IF Left$(sStr, 1) = "$" AND Right$(sStr, 1) = "#" THEN ' complete packet
    ' checksum
    IF NOT TestCheckSum(sStr) THEN
      IF Main.bCTX35Debug THEN Main.WriteDebugLog(("[CTX35] Checksum error!"))
      RETURN
    END IF
    sStr = LTrim$(Mid$(sStr, 8, Len(sStr) - 10))

    DO
      IF Mid$(sStr, iPos, 1) = " " THEN INC iPos
      sChar = Mid$(sStr, iPos, 3)
      IF Right$(sChar, 1) LIKE "[0-9]" AND Left$(sChar, 1) LIKE "[A-Z]" THEN ' code found
        iPos = iPos + 3
        ProcessCode("Code", sChar)
      ELSE IF InStr(sChar, "ON") THEN ' status found
        iPos = iPos + 3
        ProcessCode("Status", sChar)
      ELSE IF InStr(sChar, "OF") THEN ' status found
        sChar = Mid$(sStr, iPos, 3)
        iPos = iPos + 4
        ProcessCode("Status", sChar)
      ELSE IF InStr(sChar, "SR") THEN ' status request found
        iPos = iPos + 4
        ProcessCode("Status Request", sChar)
      ELSE IF Right$(sChar, 2) = "SO" THEN ' status found
        IF InStr(sStr, "SON") THEN
          ProcessCode("Status", Left$(sChar, 1) & "ON")
        ELSE IF InStr(sStr, "SOF") THEN
          ProcessCode("Status", Left$(sChar, 1) & "OF")
        END IF
        iPos = iPos + 4
      ELSE
        INC iPos
      END IF
    LOOP UNTIL iPos >= Len(sStr)
  ELSE
    IF Main.bCTX35Debug THEN Main.WriteDebugLog(("[CTX35] Wrong X10 data received! ") & sStr)
  END IF

END SUB

PRIVATE SUB ProcessCode(sType AS String, sValue AS String)

  DIM iCnt, iCnt2 AS Integer
  DIM bCodefound AS Boolean

  FOR iCnt = 0 TO 2
    IF sType = "Code" THEN
      bCodefound = FALSE
      IF Main.bCTX35Debug THEN Main.WriteDebugLog("[CTX35] Code: " & sValue)
      FOR iCnt2 = 0 TO 2
        IF aCTX[iCnt, 0] = sValue AND NOT aCTX[iCnt, 5] THEN
          bCodefound = TRUE
        END IF
      NEXT
      IF aCTX[iCnt, 0] = "" AND NOT bCodefound THEN
        aCTX[iCnt, 0] = sValue
        aCTX[iCnt, 4] = Time
        RETURN
      ELSE IF aCTX[iCnt, 0] = sValue AND aCTX[iCnt, 5] = "" AND bCodefound THEN
        aCTX[iCnt, 1] = sValue
        RETURN
      ELSE IF aCTX[iCnt, 5] = "OK" AND NOT bCodefound THEN ' different value but old one was complete so overwrite
        aCTX[iCnt, 0] = sValue
        aCTX[iCnt, 1] = ""
        aCTX[iCnt, 2] = ""
        aCTX[iCnt, 3] = ""
        aCTX[iCnt, 4] = Time
        aCTX[iCnt, 5] = ""
        aCTX[iCnt, 6] = ""
        RETURN
      ELSE IF DateDiff(Time, aCTX[iCnt, 4], gb.Second) > 10 THEN ' incomplete code/status, overwrite
        Main.WriteDebugLog(("[CTX35] Received Code without Status : ") & aCTX[iCnt, 0] & " " & aCTX[iCnt, 1] & " " & aCTX[iCnt, 2] & " " & aCTX[iCnt, 3] & " " & aCTX[iCnt, 4] & " " & aCTX[iCnt, 5] & " " & aCTX[iCnt, 6])
        aCTX[iCnt, 0] = sValue
        aCTX[iCnt, 1] = ""
        aCTX[iCnt, 2] = ""
        aCTX[iCnt, 3] = ""
        aCTX[iCnt, 4] = Time
        aCTX[iCnt, 5] = ""
        aCTX[iCnt, 6] = ""
        RETURN
      END IF
    ELSE IF sType = "Status Request" THEN
      IF Main.bCTX35Debug THEN Main.WriteDebugLog(("[CTX35] Status Request ") & sValue)
      IF aCTX[iCnt, 0] = "" THEN
        RETURN
      ELSE IF Left$(aCTX[iCnt, 0], 1) = Left$(sValue, 1) AND aCTX[iCnt, 5] = "" THEN
        aCTX[iCnt, 6] = "SRQ" ' status request
        RETURN
      END IF
    ELSE IF sType = "Status" THEN
      IF Main.bCTX35Debug THEN Main.WriteDebugLog(("[CTX35] Status: ") & sValue)
      IF aCTX[iCnt, 2] = "" AND Left$(aCTX[iCnt, 0], 1) = Left$(sValue, 1) THEN
        aCTX[iCnt, 5] = "OK"
        aCTX[iCnt, 4] = Time
        aCTX[iCnt, 2] = Right$(sValue, 2)
        UpdateX10(aCTX[iCnt, 0], Right$(sValue, 2))
        RETURN
      ELSE IF aCTX[iCnt, 3] = "" AND Left$(aCTX[iCnt, 0], 1) = Left$(sValue, 1) THEN
        aCTX[iCnt, 3] = Right$(sValue, 2)
        RETURN
      END IF
    END IF
  NEXT

END

PRIVATE SUB UpdateX10(sCode AS String, sCommand AS String)

  DIM iDeviceId AS Integer

  IF Main.bCTX35Debug THEN Main.WriteDebugLog(("[CTX35] Received command ") & sCommand & (" for device ") & sCode)

  iDeviceId = Devices.Find(sCode, Devices.FindInterface("Xanura CTX35"), "X10/A10")
  IF iDeviceId THEN
    IF sCommand = "ON" THEN
      Devices.ValueUpdate(iDeviceId, "On", "", "", "")
    ELSE IF sCommand = "OF" THEN
      Devices.ValueUpdate(iDeviceId, "Off", "", "", "")
    END IF
  END IF

END

' ctx35 poll routine
PUBLIC SUB tCTX35_Timer()

  DIM sResponse AS String
  DIM sCommand AS String

  ' sent poll command only if no command is requested
  IF NOT sCommandToSend THEN
    sResponse = SendPollCommand()
  ELSE
    sCommand = PrepareCommand(sCommandToSend)
    IF NOT sCommand THEN
      Main.WriteDebugLog(("[CTX35] Unparsable command given!"))
    ELSE 
      sResponse = SendX10Command(sCommand)
      IF InStr(sResponse, "?") THEN 
        Main.WriteDebugLog(("[CTX35] Device reported bad checksum!"))
      ELSE IF InStr(sResponse, "!S0") THEN 
        Main.WriteDebugLog(("[CTX35] Device reported buffer full!"))
      END IF
    END IF
    sCommandToSend = NULL
  END IF

END

PUBLIC SUB SendPollCommand() AS String

  DIM sBuf, sCommand AS String
  sCommand = "$>2800008C#"

  IF hCTX35.Status <> Net.Active THEN RETURN

  PRINT #hCTX35, sCommand
  READ #hCTX35, sBuf, Lof(hCTX35)
  Received(sBuf)

END

PRIVATE SUB SendX10Command(sString AS String) AS String

  DIM sBuf AS String

  ' create command
  sString = CreateWithChecksum(sString)

  IF Main.bCTX35Debug THEN Main.WriteDebugLog(sString)

  PRINT #hCTX35, sString
  TRY READ #hCTX35, sBuf, Lof(hCTX35)
  Received(sBuf)

END

PRIVATE SUB PrepareCommand(sString AS String) AS String

  DIM sLetter, sNumber, sAddr, sStr AS String

  sString = UCase$(sString)

  ' check if it contains a valid address
  IF NOT (sString LIKE "[A-P][0-1][0-9]*") THEN RETURN

  sLetter = Left$(sString, 1)
  sNumber = Mid$(sString, 2, 2)
  sStr = X10toCTX(Right$(sString, -4))
  ' todo check for repeated commands and support extended commands
  sStr = String$(2, sLetter & sStr)
  sAddr = String$(2, sLetter & sNumber)

  RETURN sAddr & " " & sStr

END

PRIVATE SUB TestCheckSum(sString AS String) AS Boolean

  DIM sHead, sTail AS String
  DIM iLoop, iChecksum AS Integer

  ' split string
  sTail = Right$(sString, 3)
  sHead = Left$(sString, -3)

  IF NOT sTail AND sHead THEN RETURN FALSE 

  FOR iLoop = 1 TO Len(sHead)
    iCheckSum = iChecksum + Asc(Mid$(sHead, iLoop, 1)) AND 255
  NEXT

  ' check result
  IF sTail = Hex$(iChecksum) & "#" THEN RETURN TRUE

  RETURN FALSE

END

PUBLIC SUB CreateWithChecksum(sCommand AS String) AS String

  DIM iChecksum, iLoop AS Integer

  sCommand = "$>28001" & sCommand

  FOR iLoop = 1 TO Len(sCommand)
    iCheckSum = iChecksum + Asc(Mid$(sCommand, iLoop, 1))
  NEXT

  RETURN sCommand & Right$(Hex$(iChecksum), 2) & "#"

END

PRIVATE SUB CTXtoX10(sCommand AS String) AS String

  SELECT sCommand
    CASE "AUF"
      RETURN "ALL_UNITS_OFF"
    CASE "ALN"
      RETURN "ALL_LIGHTS_ON"
    CASE "ALF"
      RETURN "ALL_LIGHTS_OFF"
    CASE "BGT"
      RETURN "BRIGHT"
    CASE "HRQ"
      RETURN "HAIL_REQ"
    CASE "HAK"
      RETURN "HAIL_ACK"
    CASE "PR0"
      RETURN "PREDIM1"
    CASE "PR1"
      RETURN "PREDIM2"
    CASE "SRQ"
      RETURN "STATUS"
    CASE "SON"
      RETURN "STATUS_ON"
    CASE "SOF"
      RETURN "STATUS_OFF"
    CASE "ON"
      RETURN "ON"
    CASE "OFF"
      RETURN "OFF"
    CASE "DIM"
      RETURN "DIM"
  END SELECT

  RETURN ""

END

PRIVATE SUB X10toCTX(sCommand AS String) AS String

  SELECT sCommand
    CASE "ALL_UNITS_OFF"
      RETURN "AUF"
    CASE "ALL_LIGHTS_ON"
      RETURN "ALN"
    CASE "ALL_LIGHTS_OFF"
      RETURN "ALF"
    CASE "ON"
      RETURN "ON"
    CASE "OFF"
      RETURN "OFF"
    CASE "DIM"
      RETURN "DIM"
    CASE "BRIGHT"
      RETURN "BGT"
    CASE "HAIL_REQ"
      RETURN "HRQ"
    CASE "PREDIM1"
      RETURN "PR0"
    CASE "PREDIM2"
      RETURN "PR1"
    CASE "STATUS"
      RETURN "SRQ"
  END SELECT

  RETURN ""

END

' implement properties
FUNCTION Port_Read() AS String

  RETURN sPort

END

SUB Port_Write(Value AS String)

  sPort = Value

END

PRIVATE FUNCTION Baud_Read() AS String

  RETURN sBaud

END

PRIVATE SUB Baud_Write(Value AS String)

  sBaud = Value

END

PRIVATE FUNCTION CTX35Debug_Read() AS Boolean

  RETURN bCTX35Debug

END

PRIVATE SUB CTX35Debug_Write(Value AS Boolean)

  bCTX35Debug = Value

END

PRIVATE FUNCTION PollTime_Read() AS Integer

  RETURN iPollTime

END

PRIVATE SUB PollTime_Write(Value AS Integer)

  iPollTime = Value

END
