' Gambas class file

' Description:
' CIViewer.class
' Native support for CommandFusion's iViewer protocol.

' Development Status:
' Not working yet.

' Links:
' http://www.commandfusion.com
' http://commandfusion.googlecode.com/svn/trunk/dotNet_control/iViewerControl

' DomotiGa - an open source home automation program.
' Copyright(C) 2010 Ron Klinkien

' Read file called COPYING for license details.

PROPERTY IViewerPort AS String
PROPERTY IViewerDebug AS Boolean
PROPERTY IViewerPassword AS String

PRIVATE sIViewerPort AS String
PRIVATE bIViewerDebug AS Boolean
PRIVATE sIViewerPassword AS String

PUBLIC hIViewer AS ServerSocket

PRIVATE oClients AS NEW Object[]
PRIVATE sBuffer AS String

' start our tcp server
PUBLIC FUNCTION Connect() AS Boolean

  hIViewer = NEW ServerSocket AS "IViewer"
  hIViewer.Type = Net.Internet
  hIViewer.Port = sIViewerPort
  hIViewer.Listen(0)

  IF hIViewer.Status = Net.Active THEN RETURN TRUE

  RETURN FALSE

END

' shutdown our tcp server
PUBLIC SUB Disconnect()

  ' DO WHILE hSocket.Count > 0
  '   RemoveSocket(hSocket[0])
  ' LOOP

  hIViewer.Close()

END

PUBLIC SUB IViewer_Connection(sRemoteHostIP AS String)

  DIM hSocket AS Socket

  IF hIViewer.Status <= Net.Active THEN RETURN
  hSocket = hIViewer.Accept()
  oClients.Add(hSocket)
  IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] Got connect from client with IP ") & hSocket.RemoteHost)

END

PUBLIC SUB IViewer_Error()

  Main.WriteLog(("CF iViewer Error: Unable to bind socket."))

END

PUBLIC SUB Socket_Read()

  DIM sData AS String

  IF LAST.Status <> Net.Connected THEN RETURN
  READ #LAST, sData, 1
  IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] Data from socket ") & oClients.Find(LAST))
  IF sData = "\x03" THEN ' buffer until \x03 then parse
    IF Len(sBuffer) > 1 THEN ParseLine(sBuffer)
    sBuffer = NULL
  ELSE
    sBuffer &= sData
  ENDIF

END

PUBLIC SUB ParseLine(sText AS String)

  DIM aScan AS String[]

  IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] ") & sText)
  aScan = Scan(sText, "*=*")
  IF aScan.Count = 2 THEN
    SELECT CASE Left(aScan[0], 1)
      CASE "a", "d"
        ' analog (eg. a2=4000) or digital join (eg. d10=1)
        Events.CheckIViewerRemoteEvents("iviewer", aScan[0], aScan[1])
      CASE "s"
        ' serial (eg. s4=this is text)
        IF aScan[0] = "s10002" THEN
          IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] < Client ID = ") & aScan[1])
        ELSE
          Events.CheckIViewerRemoteEvents("iviewer", aScan[0], aScan[1])
        ENDIF
      CASE "h"
        ' heartbeat message
        IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] < Heartbeat request."))
        TRY WRITE #LAST, "h=1\x03", 4
        IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] > Heartbeat reply."))
      CASE "p"
        IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] < Password request."))
        ' password
        IF aScan[1] = sIViewerPassword THEN
          TRY WRITE #LAST, "p=ok\x03", 5
          IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] > Password ok."))
        ELSE
          TRY WRITE #LAST, "p=bad\x03", 6
          IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] > Password bad."))
        ENDIF
      CASE "m"
        ' orientation, (eg. m=landscape)
      CASE "l"
        ' list event, (eg. l1:4:d1=1)
    END SELECT
  ENDIF

END

PUBLIC SUB Socket_Closed()

  IF Main.bIViewerDebug THEN Main.WriteDebugLog(("[iViewer] Connection closed by client ") & oClients.Find(LAST))
  oClients.Remove(oClients.Find(LAST))

END

PRIVATE FUNCTION IViewerPort_Read() AS String

  RETURN sIViewerPort

END

PRIVATE SUB IViewerPort_Write(Value AS String)

  sIViewerPort = Value

END

PRIVATE FUNCTION IViewerDebug_Read() AS Boolean

  RETURN bIViewerDebug

END

PRIVATE SUB IViewerDebug_Write(Value AS Boolean)

  bIViewerDebug = Value

END

PRIVATE FUNCTION IViewerPassword_Read() AS String

  RETURN sIViewerPassword

END

PRIVATE SUB IViewerPassword_Write(Value AS String)

  sIViewerPassword = Value

END
