' Gambas module file

' Description:
' Bwired.module
' Support for Bwired related functions.

' Development Status:
' Just build, so possible bugs around.

' Links:
' http://www.bwired.nl/domoticaworld.asp

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC hPost AS NEW HttpClient AS "hPost"

PRIVATE SUB CreateBwiredData() AS String

  DIM xXml AS XmlWriter
  DIM sXml, sValue, sLabel AS String
  DIM rResult AS Result
  DIM iId AS Integer

  xXml = NEW XmlWriter
  xXml.Open("", TRUE, "UTF-8")
  xXml.StartElement("BWired")
    xXml.StartElement("Init")
      xXml.Element("DateTime", Now())
      xXml.Element("UserName", Main.sBwiredMapUser)
      xXml.Element("Password", Main.sBwiredMapPassword)
      xXml.Element("ScreenName", Main.sBwiredMapScreenName)
      xXml.Element("Gpslat", Main.sBwiredMapGpsLat)
      xXml.Element("Gpslong", Main.sBwiredMapGpsLong)
      xXml.Element("City", Main.sBwiredMapCity)
      xXml.Element("Website", Main.sBwiredMapWebsite)
      xXml.Element("WebCamPicUrl", Main.sBwiredMapWebsitePicUrl)
      xXml.Element("Title", Main.sBwiredMapTitle)
    xXml.EndElement()
    rResult = Main.hDB.Exec("SELECT * FROM devices_bwired")

    IF NOT rResult THEN
      Main.WriteLog(("Error: table 'devices_bwired' not found!"))
      RETURN
    END IF

    IF rResult.Count THEN
      FOR EACH rResult
        xXml.StartElement("Entry")
        xXml.Element("Name", rResult!description)
        xXml.Element("ID", iId)
        xXml.Element("Units", rResult!devicelabel)
        SELECT rResult!value
          CASE "Value"
            sValue = Devices.GetCurrentValueForDevice(rResult!deviceid)
          CASE "Value2"
            sValue = Devices.GetCurrentValue2ForDevice(rResult!deviceid)
          CASE "Value3"
            sValue = Devices.GetCurrentValue3ForDevice(rResult!deviceid)
          CASE "Value4"
            sValue = Devices.GetCurrentValue3ForDevice(rResult!deviceid)
        END SELECT
        xXml.Element("Value", sValue)
        xXml.EndElement()
        INC iId
      NEXT
    END IF

  xXml.EndElement()
  RETURN xXml.EndDocument()

END

PUBLIC SUB UploadBwiredData()

  DIM sContent AS String

  IF hPost.Status > 0 THEN
    Main.WriteLog(("I'm already uploading XML data to Bwired, skipping."))
    RETURN
  END IF

  sContent = CreateBwiredData()

  hPost.URL = "http://www.bwired.nl/Bwiredservice/receive.asp"
  hPost.TimeOut = 10
  hPost.Async = TRUE
  hPost.Post("text/xml", sContent)

  IF Main.bBwiredMapDebug THEN Main.WriteDebugLog("[Bwired] " & sContent)

END

PUBLIC SUB hPost_Error()

  Main.WriteDebugLog(("[Bwired] XML data post error."))

END

PUBLIC SUB hPost_Finished()

  DIM iCount AS Integer

  SELECT hPost.Code
    CASE 0
      IF InStr(hPost.Headers[0], "HTTP/1.1 200") THEN
        Main.WriteLog(("Uploaded XML data to Bwired."))
      ELSE
        Main.WriteLog(("Unknown error occured while uploading XML data to Bwired!"))
      END IF
    CASE 401
      Main.WriteLog(("Error authenticating while uploading XML data to Bwired!"))
    CASE 404
      Main.WriteLog(("Error page not found while uploading XML data to Bwired!"))
    CASE ELSE
      Main.WriteLog(("Unknown error occured while uploading XML data to Bwired!"))
  END SELECT

  IF Main.bBwiredMapDebug THEN
    FOR iCount = 0 TO hPost.Headers.Count - 1
      Main.WriteDebugLog("[Bwired] " & Left(hPost.Headers[iCount], Len(hPost.Headers[iCount]) - 1))
    NEXT
  END IF

END
