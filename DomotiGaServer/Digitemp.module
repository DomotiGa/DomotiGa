' Gambas module file

' Description:
' Digitemp.module
' Support for digitemp 1-wire sensors (only temperature at this moment).
' DigiTemp supports DS18S20, DS1820, DS18B20, DS2438, DS2422, DS2423 and DS2409

' Development Status:
' Just build, possibly bugs around.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC tDigitemp AS NEW Timer
PUBLIC sOutput AS String

PRIVATE CONST DS18S20 AS String = "10"
PRIVATE CONST DS2423 AS String = "1D"
PRIVATE CONST DS2409 AS String = "1F"
PRIVATE CONST DS2438 AS String = "26"
PRIVATE CONST DS18B20 AS String = "28"
PRIVATE CONST DS2422 AS String = "41"

PUBLIC FUNCTION Run()

  ' start poll timer for digitemp
  tDigitemp = NEW Timer AS "tDigitemp"
  tDigitemp.Delay = Main.iDigitempPollTime * 1000 ' multiply for seconds
  tDigitemp.Start

  CheckDigitemp()

END

PUBLIC SUB CheckDigitemp()

  DIM sCommand AS String
  DIM iReadTime AS Integer

  IF Main.iDigitempReadtime < 1000 THEN
    iReadtime = 1500
  ELSE
    iReadtime = Main.iDigitempReadtime
  END IF
  sCommand = Main.sDigitempCommand & " -a -q -r " & iReadTime & " -c " & Main.sDigitempConfig
  SHELL sCommand & " 2>&1" FOR READ AS "Digitemp"
  IF Main.bDigitempDebug THEN Main.WriteDebugLog("[Digitemp] " & sCommand)

END

PUBLIC SUB Digitemp_Read()

  DIM sLine AS String

  READ #LAST, sLine, -256
  sOutput &= sLine

END

PUBLIC SUB Digitemp_Kill()

  DIM sLine AS String
  DIM aScan AS String[]
  DIM iDeviceId AS Integer

  FOR EACH sLine IN Split(sOutput, "\n")
    IF InStr(sLine, "Sensor") THEN
      SELECT CASE Left$(sLine, 2)
        CASE DS18S20
          ' 1049023A01080015 Nov 21 19:44:49 Sensor 0 C: 22.44 F: 72.39
          aScan = Scan(sLine, "* * * * * * * * * *")
          IF aScan.Count = 10 THEN
            IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ID: ") & aScan[0] & (" Type: DS18S20 Values: ") & aScan[7] & "°C " & aScan[9] & "°F")
            iDeviceId = Devices.Find(aScan[0], Devices.FindInterface("Digitemp"), "Dallas DS18S20")
            IF iDeviceId THEN
              IF Main.sTemperature = "°C" THEN
                Devices.ValueUpdate(iDeviceId, aScan[7], "", "", "")
              ELSE
                Devices.ValueUpdate(iDeviceId, aScan[9], "", "", "")
              END IF
            END IF
          ELSE
            Main.WriteDebugLog(("[Digitemp] Got parse error when scanning line: ") & sLine)
          END IF
        CASE DS2423
          aScan = Scan(sLine, "* * * * * * * *")
          IF aScan.Count = 8 THEN
            IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ID: ") & aScan[0] & (" Type: DS2423 Values: ") & aScan[6] & " " & aScan[7])
            iDeviceId = Devices.Find(aScan[0], Devices.FindInterface("Digitemp"), "Dallas DS2423")
            IF iDeviceId THEN
              SELECT aScan[6]
                CASE "#0"
                  Devices.ValueUpdate(iDeviceId, aScan[7], "", "", "")
                CASE "#1"
                  Devices.ValueUpdate(iDeviceId, "", aScan[7], "", "")
              END SELECT
            END IF
          ELSE
            Main.WriteDebugLog(("[Digitemp] Got parse error when scanning line: ") & sLine)
          END IF
        CASE DS2409
          IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ") & aScan[0] & (" Type: DS2409 ") & sLine)
        CASE DS2438
          ' 26D050E7000000FF Apr 25 18:15:45 Sensor 0 C: 22.22 F: 71.99 H: 20%
          aScan = Scan(sLine, "* * * * * * * * * * * *")
          IF aScan.Count = 12 THEN
            IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ID: ") & aScan[0] & (" Type: DS2438 Values: ") & aScan[7] & "°C " & aScan[9] & "°F" & aScan[11] & "%")
            iDeviceId = Devices.Find(aScan[0], Devices.FindInterface("Digitemp"), "Dallas DS2438")
            IF iDeviceId THEN
              IF Main.sTemperature = "°C" THEN
                Devices.ValueUpdate(iDeviceId, aScan[7], aScan[11], "", "")
              ELSE
                Devices.ValueUpdate(iDeviceId, aScan[9], aScan[11], "", "")
              END IF
            END IF
          ELSE
            Main.WriteDebugLog(("[Digitemp] Got parse error when scanning line: ") & sLine)
          END IF
        CASE DS18B20
          aScan = Scan(sLine, "* * * * * * * * * *")
          IF aScan.Count = 10 THEN
            IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ID: ") & aScan[0] & (" Type: DS18B20 Values: ") & aScan[7] & "°C " & aScan[9] & "°F")
            iDeviceId = Devices.Find(aScan[0], Devices.FindInterface("Digitemp"), "Dallas DS18B20")
            IF iDeviceId THEN
              IF Main.sTemperature = "°C" THEN
                Devices.ValueUpdate(iDeviceId, aScan[7], "", "", "")
              ELSE
                Devices.ValueUpdate(iDeviceId, aScan[9], "", "", "")
              END IF
            END IF
          ELSE
            Main.WriteDebugLog(("[Digitemp] Got parse error when scanning line: ") & sLine)
          END IF
        CASE DS2422
          IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ") & aScan[0] & (" Type: DS2422 ") & sLine)
        CASE ELSE
          IF Main.bDigitempDebug THEN Main.WriteDebugLog(("[Digitemp] Sensor ") & aScan[9] & (" Type: Unsupported! ") & sLine)
        END SELECT
    END IF
  NEXT
  sOutput = NULL

END

PUBLIC SUB tDigitemp_Timer()

  CheckDigitemp()

END
