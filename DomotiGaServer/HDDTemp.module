' Gambas module file

' Description:
' HDDTemp.module
' Support for HDDTemp harddisk temperature monitoring.

' Development Status:
' Working.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC tHDDTemp AS NEW Timer
PUBLIC hHDDTemp AS NEW Socket

PUBLIC FUNCTION Run()

  ' start poll timer for HDDTemp
  tHDDTemp = NEW Timer AS "tHDDTemp"
  tHDDTemp.Delay = Main.iHDDTempPollTime * 1000 ' multiply for seconds
  tHDDTemp.Start

  Poll()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open socket and read values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Poll() AS Boolean

  hHDDTemp = NEW Socket AS "HDDTemp"
  hHDDTemp.Connect(Main.sHDDTempTCPHost, Main.iHDDTempTCPPort)

END

PUBLIC SUB HDDTemp_Ready()

  IF Main.bHDDTempDebug THEN Main.WriteLog("[HDDTemp] Connected to socket " & Main.sHDDTempTCPHost & ":" & Main.iHDDTempTCPPort)

END

PUBLIC SUB HDDTemp_Read()

  DIM sDisks, sFields AS String[]
  DIM sBuffer, sDisk, sField, sTemp, sDevice AS String
  DIM iCount, iDeviceId AS Integer

  READ #hHDDTemp, sBuffer, Lof(hHDDTemp)
  TRY CLOSE #hHDDTemp

  IF Main.bHDDTempDebug THEN Main.WriteDebugLog("[HDDTemp] Got string: " & sBuffer)
  ' first change string so disks can be separated
  sBuffer = "|" & sBuffer & "|"
  sBuffer = Replace$(sBuffer, "||", "!")
  sDisks = Split(sBuffer, "!")

  FOR EACH sDisk IN sDisks
    sFields = Split(sDisk, "|")
    iCount = 0
    FOR EACH sField IN sFields
      IF iCount = 0 THEN sDevice = sField
      IF iCount = 2 THEN
        sTemp = sField
        IF Main.bHDDTempDebug THEN Main.WriteDebugLog("[HDDTemp] Device " & sDevice & " has temp " & sTemp & "°C")
        iDeviceId = Devices.Find(sDevice, Devices.FindInterface("HDDTemp Socket"), "HDDTemp disk")
        IF iDeviceId THEN
          Devices.ValueUpdate(iDeviceId, sTemp, "", "", "")
          IF Main.bHDDTempDebug THEN Main.WriteDebugLog("[HDDTemp] Device " & sDevice & " has id " & iDeviceId)
        ELSE
          IF Main.bHDDTempDebug THEN Main.WriteDebugLog("[HDDTemp] Device " & sDevice & " not found!")
        END IF
        ' send warning per e-mail if threshold is reached
        IF CInt(sTemp) > Main.iHDDTempThreshold THEN
          MailWarning("The temperature of disk " & sDevice & " is " & sTemp & "°C, so it's above the threshold of " & Main.iHDDTempThreshold & "°C!")
        END IF
      END IF
      iCount = iCount + 1
    NEXT
  NEXT

  CATCH ' some errors
    Main.WriteLog("HDDTemp Error: " & ERROR.Text)

END

PUBLIC SUB HDDTemp_Error()

  ' handle errors
  SELECT CASE hHDDTemp.Status
    CASE Net.CannotCreateSocket
      Main.WriteLog("HDDTemp: The system does not allow to create a socket.")
    CASE Net.HostNotFound
      Main.WriteLog("HDDTemp: Host " & Main.sHDDTempTCPHost & " not found.")
    CASE Net.ConnectionRefused
      Main.WriteLog("HDDTemp: Unable to connect. Connection refused.")
    CASE Net.CannotRead
      Main.WriteLog("HDDTemp: Error reading data.")
    CASE Net.CannotWrite
      Main.WriteLog("HDDTemp: Error writing data.")
  END SELECT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' generate e-mail when temp is above threshold
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION MailWarning(sMessage AS String) AS Boolean

  Mail.SendMail(Application.Name & ": HDDTemp", sMessage, Main.sEmailToAddress)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' poll for temperatures
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tHDDTemp_Timer()

  Poll()

END
