' Gambas class file

' Description:
' CJeeLabs.class
' Support for JeeLabs JeeLinks interface and Sensors

' Development Status:
' Working for JeeNodes.

' Links:
' http://jeelabs.org

' Credits:
' Thanks to Jean-Claude Wippler
' and Wouter Wolkers.

' DomotiGa - an open source home automation program.
' Copyright(C) 2009-2010 Ron Klinkien.

' Read file called COPYING for license details.

PROPERTY Port AS String
PROPERTY Baud AS String
PROPERTY JeeLabsDebug AS Boolean

PRIVATE sPort AS String
PRIVATE sBaud AS String
PRIVATE bJeeLabsDebug AS Boolean

PUBLIC hJeeLabs AS NEW SerialPort
PRIVATE sBuffer AS String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Connect() AS Boolean

  ' try to close the port
  TRY hJeeLabs.Close

  ' get a new one
  hJeeLabs = NEW Serialport AS "JeeLabs"

  WITH hJeeLabs
    .PortName = sPort
    .Speed = sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("JeeLabs Error: ") & ERROR.Text)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  TRY hJeeLabs.Close
  Main.WriteLog(("JeeLabs serial port close."))

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("JeeLabs Error: ") & ERROR.Text)
  RETURN FALSE

END

PUBLIC SUB JeeLabs_Read()

  DIM sData AS String

  TRY READ #hJeeLabs, sData, 1
  IF ERROR THEN Main.WriteDebugLog(("[JeeLabs] Error reading data from serial port! -> ") & Error.Text)
  IF sData = Chr(10) THEN ' buffer until linefeed then parse
    IF Len(sBuffer) > 1 AND IF Left(sBuffer, 2) = "OK" THEN ParseLine(Left(sBuffer, Len(sBuffer) - 1))
    sBuffer = NULL
  ELSE
    sBuffer &= sData
  END IF

END

PRIVATE SUB ParseLine(sData AS String)

  DIM sLine, sMotion, sLowBat AS String
  DIM aScan AS String[]
  DIM iDeviceId, iSensorId, iLDR, iHumi, iTemp, iLowBat AS Integer
  DIM bPIR AS Boolean

  IF Main.bJeeLabsDebug THEN Main.WriteDebugLog("[JeeLabs] " & sData)

  ' parse each line
  FOR EACH sLine IN Split(sData, "\n")
    ' parse sensor data
    aScan = Scan(sLine, "* * * * * *")
    IF aScan.Count = 6 THEN
      iSensorId = aScan[1]
      iSensorId = IIf(iSensorId > 31, iSensorId - 32, iSensorId)
      iLDR = aScan[2]
      bPIR = BTst(CInt(aScan[3]), 0)
      iHumi = Lsr(CInt(aScan[3]), 1)
      iTemp = (((256 * (CInt(aScan[5]) AND 3) + CInt(aScan[4])) XOR 512) - 512)
      iLowBat = Lsr(CInt(aScan[5]), 2) AND 1
      sLowBat = If(iLowBat, "Empty", "OK")
      sMotion = IIf(bPIR, "Motion", "No Motion")
      IF Main.bJeeLabsDebug THEN
        Main.WriteDebugLog("[JeeLabs] Sensor ID: " & iSensorId)
        Main.WriteDebugLog("[JeeLabs] LDR      : " & iLDR)
        Main.WriteDebugLog("[JeeLabs] PIR      : " & sMotion)
        Main.WriteDebugLog("[JeeLabs] Humi     : " & iHumi)
        Main.WriteDebugLog("[JeeLabs] Temp     : " & iTemp)
        Main.WriteDebugLog("[JeeLabs] LowBat   : " & iLowBat & " (" & sLowBat & ")")
      ENDIF
      ' try to find device with address, and correct interface type.
      iDeviceId = Devices.Find(iSensorId, Devices.FindInterface("JeeLabs Interface"))
      ' if found then update it's value
      IF iDeviceId THEN
        Devices.ValueUpdate(iDeviceId, iLDR, sMotion, iHumi, iTemp)
        Devices.Battery(iDeviceId, sLowBat)
      ENDIF
    ENDIF
  NEXT

END

' implement properties
FUNCTION Port_Read() AS String

  RETURN sPort

END

SUB Port_Write(Value AS String)

  sPort = Value

END

PRIVATE FUNCTION Baud_Read() AS String

  RETURN sBaud

END

PRIVATE SUB Baud_Write(Value AS String)

  sBaud = Value

END

PRIVATE FUNCTION JeeLabsDebug_Read() AS Boolean

  RETURN bJeeLabsDebug

END

PRIVATE SUB JeeLabsDebug_Write(Value AS Boolean)

  bJeeLabsDebug = Value

END
