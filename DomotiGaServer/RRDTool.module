' Gambas module file

' Description:
' RRDTool.module
' Support for RRDTool graphing.

' Development Status:
' Working, make hardcoded heartbeat and step values configurable, need better error checking.
' Maybe create separate graphs table in db.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2009 Ron Klinkien

' Read file called COPYING for license details.

PUBLIC tRRDTool AS Timer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' check for/create missing rrd files and start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB Run()

  ' check for missing rrd databases
  CreateRRDs()

  ' start poll timer for RRDTool
  tRRDTool = NEW Timer AS "tRRDTool"
  tRRDTool.Delay = Main.iRRDToolPollTime * 1000 ' multiply for seconds
  tRRDTool.Start

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB tRRDTool_Timer()

  UpdateRRDs() ' update rrd values every Polltime seconds

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find all devices with graph boolean enabled and
' check for existance of rrd file, create one if missing
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB CreateRRDs()

  DIM rDevice AS Result
  DIM sRRD, sRRDCmd, sRRDName AS String

  IF NOT Main.bRRDToolEnabled THEN RETURN

  TRY rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE graph is TRUE AND enabled is TRUE")
  IF (rDevice.Count > 0) THEN
    FOR EACH rDevice
      IF Main.bRRDToolDebug THEN Main.WriteDebugLog(("[RRDTool] Device with address '") & rDevice!address & ("' named '") & rDevice!name & ("' has graphing enabled."))
      sRRDName = LCase(Replace$(rDevice!name, " ", ""))
      sRRDName = Replace$(sRRDName, "/", "")
      sRRD = Main.sBaseDir &/ "rrd" &/ sRRDName & ".rrd"
      IF NOT Exist(sRRD) THEN
        sRRDCmd = "rrdtool create " & sRRD & " -s " & Main.iRRDToolPollTime & " "
        IF rDevice!valuerrddsname THEN sRRDCmd = sRRDCmd & "DS:" & rDevice!valuerrddsname & ":" & rDevice!valuerrdtype & ":600:U:U "
        IF rDevice!value2rrddsname THEN sRRDCmd = sRRDCmd & "DS:" & rDevice!value2rrddsname & ":" & rDevice!value2rrdtype & ":600:U:U "
        IF rDevice!value3rrddsname THEN sRRDCmd = sRRDCmd & "DS:" & rDevice!value3rrddsname & ":" & rDevice!value3rrdtype & ":600:U:U "
        IF rDevice!value4rrddsname THEN sRRDCmd = sRRDCmd & "DS:" & rDevice!value4rrddsname & ":" & rDevice!value4rrdtype & ":600:U:U "
        sRRDCmd = sRRDCmd & "RRA:AVERAGE:0.5:1:600 RRA:AVERAGE:0.5:2:600 RRA:AVERAGE:0.5:7:600 RRA:AVERAGE:0.5:30:600 RRA:AVERAGE:0.5:365:600"
        sRRDCmd &= " 2>&1 >/dev/null"
        IF Main.bRRDToolDebug THEN Main.WriteDebugLog("[RRDTool] " & sRRDCmd)
        SHELL sRRDCmd WAIT
      END IF
    NEXT
  ELSE
    IF Main.bRRDToolDebug THEN Main.WriteDebugLog(("[RRDTool] No device(s) with graphing enabled found!"))
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find devices with graph enabled, and update rrd files with device values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB UpdateRRDs()

  DIM rDevice AS Result
  DIM sRRDCmd, sRRDName AS String

  TRY rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE enabled is TRUE AND graph is TRUE")
  IF (rDevice.Count > 0) THEN
    FOR EACH rDevice
      sRRDName = LCase(Replace$(rDevice!name, " ", ""))
      sRRDName = Replace$(sRRDName, "/", "")
      sRRDCmd = "rrdtool update " & Main.sBaseDir &/ "rrd" &/ sRRDName & ".rrd" & " N"
      IF rDevice!valuerrddsname THEN sRRDCmd &= ":" & rDevice!value
      IF rDevice!value2rrddsname THEN sRRDCmd &= ":" & rDevice!value2
      IF rDevice!value3rrddsname THEN sRRDCmd &= ":" & rDevice!value3
      IF rDevice!value4rrddsname THEN sRRDCmd &= ":" & rDevice!value4
      sRRDCmd &= " 2>&1 >/dev/null"
      IF Main.bRRDToolDebug THEN Main.WriteDebugLog("[RRDTool] " & sRRDCmd)
      SHELL sRRDCmd WAIT
    NEXT
  ELSE
    IF Main.bRRDToolDebug THEN Main.WriteDebugLog(("[RRDTool] No device(s) with graphing enabled found!"))
  END IF

END