' Gambas class file

' Description:
' FScenarioEditor.class
' Support for creating and editing of all scenario.

' Development Status:
' Works for 99%.

' DomotiGa - an open source home automation program.
' Copyright(C) 2010 Ron Klinkien

' Read file called COPYING for license details.

PRIVATE rResult AS Result
PUBLIC bAddScenario AS Boolean

PUBLIC SUB Form_Open()

  IF bAddScenario THEN ME.Text = ("Create Scenario")
  ME.Move(FMain.X + 50, FMain.Y + 70)
  LoadScenario()

END

PUBLIC SUB LoadScenario()

  IF NOT bAddScenario THEN
    ' get selected event values
    TRY rResult = Main.hDB.Exec("SELECT * FROM thermostat_scenarii WHERE id = &1", FThermostat.txtCurTherm)
    IF NOT ERROR THEN
      IF rResult.Count >= 1 THEN
        tbName.Text = rResult!name
        IF rResult!name = Main.GlobalVar["Thermostat_Mode"] THEN chkEnabled.Value = TRUE
        txtDescription.Text = rResult!description
      ELSE
        Message(("Couldn't load scenario record!"))
        ME.Close
      ENDIF
    ENDIF
  ENDIF

END

PUBLIC SUB btnCancel_Click()

  bAddScenario = FALSE
  ME.Close

END

PUBLIC SUB btnDelete_Click()

  SELECT Message.Question(("Are you sure that you want to delete this scenario?"), ("Yes"), ("No"))
    CASE 1
      Main.hDB.Exec("DELETE FROM thermostat_scenarii WHERE id = &1", rResult!id)
      ME.Close
      FThermostat.GetThermList()
  END SELECT

END

PUBLIC SUB btnNew_Click()

  bAddScenario = TRUE
  btnSave_Click()

END

PUBLIC SUB btnSave_Click()

  DIM rResultUpdate AS Result
  DIM sSql AS String
  DIM iRet AS Integer

  IF NOT tbName.Text THEN
    Balloon(("Please enter a name for this scenario!"), tbName)
    RETURN
  ENDIF
  ' name has changed
  IF rResult!name <> tbName.Text THEN
    IF ScenarioNameExist(tbName.Text) THEN
      Balloon(("Please enter a unique name for this scenario!"), tbName)
      RETURN
    ENDIF
  ENDIF
  IF NOT (chkEnabled.Value) AND rResult!name = Main.GlobalVar["Thermostat_Mode"] THEN
    iRet = Message.Question(("There will be no more enabled scenario, are you sure you want to continue ?"), ("Yes"), ("No"))
      IF iret = 1 THEN
        Main.GlobalVar["Thermostat_Mode"] = ""
      ENDIF
      IF iRet = 2 THEN
        chkEnabled.Value = TRUE
        RETURN
      ENDIF
  ENDIF
  IF bAddScenario THEN
    ' create new scenario row
    sSql = "INSERT INTO thermostat_scenarii SET  name = &1, description= &2"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, txtDescription.Text)
    IF chkEnabled.Value THEN 
      IF Main.GlobalVar["Thermostat_Mode"] <> tbName.Text THEN
        Main.GlobalVar["Thermostat_Mode"] = tbName.Text
        Thermostat.DeleteAllDerogateHeating()
      ENDIF
    ENDIF
  ELSE
    ' update new event details
    sSql = "UPDATE thermostat_scenarii SET name = &1, description= &2 WHERE id = &3"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, txtDescription.Text, rResult!id)
    IF chkEnabled.Value THEN
      IF Main.GlobalVar["Thermostat_Mode"] <> tbName.Text THEN
        Main.GlobalVar["Thermostat_Mode"] = tbName.Text
        Thermostat.DeleteAllDerogateHeating()
      ENDIF
    ENDIF
  ENDIF

  FThermostat.GetThermList()
  bAddScenario = FALSE
  ME.Close

END

PRIVATE SUB ScenarioNameExist(sName AS String) AS Boolean

  DIM rResultName AS Result

  rResultName = Main.hDB.Exec("SELECT id FROM thermostat_scenarii WHERE name = &1", sName)
  IF rResultName THEN
    IF rResultName.Count THEN
      RETURN TRUE
    ENDIF
  ENDIF
  RETURN FALSE

END

PUBLIC SUB tbName_KeyPress()

  btnSave.Enabled = TRUE

END

PUBLIC SUB chkEnabled_Click()

  btnSave.Enabled = TRUE

END

PUBLIC SUB txtDescription_KeyPress()

  btnSave.Enabled = TRUE

END