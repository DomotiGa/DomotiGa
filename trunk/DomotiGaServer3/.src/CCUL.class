' Gambas class file

' Description:
' CCUL.class
' Connect to CULFW interface via tcp socket or serial port.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright(C) 2008-2011 Ron Klinkien

' Read file called COPYING for license details.

Property TCPHost As String
Property TCPPort As Integer
Property Interface As String
Property SerPort As String
Property Baud As String
Property CULDebug As Boolean
Property Model As Integer
Property FHTid As String
Property AutoCreate As Boolean

Private sTCPHost As String
Private iTCPPort As Integer
Private sInterface As String
Private sSerPort As String
Private sBaud As String
Private bCULDebug As Boolean
Private iModel As Integer
Private sFHTid As String
Private bAutoCreate As Boolean

Public hCUL As New Socket
Public hCULSer As New SerialPort
Public bSimulate As Boolean = False
Public tCULBusy As Timer
Public tCULLed As Timer
Public tCULConnect As Timer
Public tCULPoll As Timer

Private bFirstByte As Boolean = True
' [GB2:ARRD] Private RecBuf As Byte[255]
Private RecBuf As New Byte[255]
Private bMessage As Boolean = False
Private iByteCount As Integer = 0
Private cQueue As New Collection  ' command buffer
Private bCULBusy As Boolean  ' waiting for delay timer to finish
Private iConnectRetry As Integer = 0 ' retries
Private iConnectDelay As Integer = 60000 ' 1 minute
Private cLowTemps As New Collection ' to store FHT low temp byte

' FS20
Private sFS20Values As String[] = ["Off", "Dim 6", "Dim 12", "Dim 18", "Dim 25", "Dim 31", "Dim 37", "Dim 43", "Dim 50", "Dim 56", "Dim 62", "Dim 68", "Dim 75", "Dim 81", "Dim 87", "Dim 93", "Dim 100", "On", "Toggle", "DimUp", "DimDown", "DimUpDown", "Timer", "SendState", "Off-For-Timer", "On-For-Timer", "On-Old-For-Timer", "Reset", "Ramp-On-Time", "Ramp-Of-Time", "On-Old-For-Timer-Prev", "On-100-For-Timer-Prev"]

' FHT not used yet
' CONST FHT_SYNCTIME AS Byte = &H2C
' CONST FHT_MODE AS Byte = &H3D
' CONST FHT_DESIREDTEMP AS Byte = &H41
' CONST FHT_STATUS AS Byte = &H44
' CONST FHT_DAYTEMP AS Byte = &H82
' CONST FHT_NIGHTTEMP AS Byte = &H84
' CONST FHT_WINDOWOPENTEMP AS Byte = &H8A

Public Sub CUL_Ready()

  Main.WriteLog(("CUL TCP socket connected."))
  Init()
  tCULPoll.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' temp poll routine to see if CUL socket stays active
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tCULPoll_Timer()

 ' get version
  QueueCommand("V")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reconnect routine
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tCULConnect_Timer()

  If Not ConnectTCP() Then
    Inc iConnectRetry
    iConnectDelay *= iConnectRetry
    tCULConnect.Delay = iConnectDelay
  Endif

End

Public Sub CUL_Closed()

  Main.WriteDebugLog(("[CUL] ERROR: TCP socket closed by peer."))
  If iConnectRetry < 6 Then
    Main.WriteDebugLog(("[CUL] Retry to connect" & IIf(iConnectRetry, " in " & (iConnectDelay / 60) & " minutes.", ".")))
    tCULConnect.Start
  Endif

End

Public Sub CUL_Found()

  Main.WriteLog(("CUL IP address resolved."))

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectTCP() As Boolean

  ' try to close the connection
  Try hCUL.Close

  ' get a new one
  hCUL = New Socket As "CUL"
  hCUL.Connect(sTCPHost, iTCPPort)

  tCULPoll = New Timer As "tCULPoll"
  tCULPoll.Delay = 60 * 1000

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("CUL Error: ") & ERROR.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function ConnectSerial() As Boolean

  ' try to close the connection
  Try hCULSer.Close

  ' get a new one
  hCULSer = New Serialport As "CULSer"
  With hCULSer
    .PortName = sSerPort
    .Speed = sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  End With

  Init()

  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("CUL Error: ") & ERROR.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try tCULBusy.Stop
  Try tCULLed.Stop
  Try tCULPoll.Stop
  Try tCULConnect.Stop
  Try hCUL.Close
  Try hCULSer.Close

  If InStr(sInterface, "tcp") Then
    Main.WriteLog(("CUL TCP socket close."))
  Else
    Main.WriteLog(("CUL serial port close."))
  End If
  ' all ok
  Return True

Catch ' some errors
  Main.WriteLog(("CUL Error: ") & ERROR.Text)
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reset led status in main status bar
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tCULLED_Timer()

  Main.ControlLed("CUL", "Off")
  tCULLED.Stop

End

Public Sub CUL_Error()

  Dim sString As String = "CUL: "

  ' handle error
  Select Case hCUL.Status
    Case Net.CannotCreateSocket
      Main.WriteLog(sString & ("The system does not allow to create a socket."))
    Case Net.HostNotFound
      Main.WriteLog(sString & ("Host '") & sTCPHost & ("' not found."))
    Case Net.ConnectionRefused
      Main.WriteLog(sString & ("Unable to connect. Connection refused."))
    Case Net.CannotRead
      Main.WriteLog(sString & ("Error reading data."))
    Case Net.CannotWrite
      Main.WriteLog(sString & ("Error writing data."))
  End Select

End

Public Sub CUL_Read()

  Dim bData As Byte

  Try Read #hCUL, bData
  If Error Then Main.WriteDebugLog(("[CUL] Error reading data from the TCP port! -> ") & Error.Text)
  ProcessReceivedChar(bData)

End

Public Sub CULSer_Read()

  Dim bData As Byte

  Try Read #hCULSer, bData
  If Error Then Main.WriteDebugLog(("[CUL] Error reading data from the serial port! -> ") & Error.Text)
  ProcessReceivedChar(bData)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' control devices
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCommand(sAddress As String, sCmd As String)

  Dim iDeviceId As Integer
  Dim sType As String

  ' find out what kind of device we are dealing with
  iDeviceId = Devices.Find(sAddress, Devices.FindInterface("CUL Interface"))
  If iDeviceId Then
    sType = Devices.FindTypeForDevice(iDeviceId)
  Else
    Return
  Endif

  ' select which routine to use
  Select sType
    Case "FS20"
      PrepareFS20Command(sAddress, sCmd)
    Default
      Main.WriteDebugLog(("[CUL] Controlling '" & sType & "' devices is not supported in this version!"))
      Return
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' prepare packet to send to FS20 device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub PrepareFS20Command(sAddress As String, sCmd As String)

  Dim sPacket, sCommand As String
  Dim aParams As String[]

  ' F <Hex>
  ' <Hex> is a Hex String of the following form:
  ' hhhhaacc or hhhhaaccee, where
  ' hhhh is the FS20 housecode,
  ' aa is the FS20 device address,
  ' cc is the FS20 command
  ' ee is the FS20 timespec. Note that cc must have the extension bit set.
  ' Example: F12340111

  ' check if address has 3 bytes
  If (Len(sAddress) <> 6) Then
    Main.WriteDebugLog(("[CUL] FS20 address has invalid format '" & sAddress & "' we need 3 bytes!"))
    Return
  Endif

  aParams = Split(sCmd, " ")
  '--- for example ON, OFF
  If aParams.Count = 1 Then
    sCommand = CommandtoFS20(aParams[0])
    If Len(sCommand) = 0 Then
      Main.WriteDebugLog(("[CUL] Only ON, OFF and DIM are supported in this version!"))
      Return
    Endif
  '--- for example DIM 1
  Else If aParams.Count = 2 Then
    If UCase(aParams[0]) = "DIM" Then
      sCommand = ConvertFS20Dim(aParams[1])
    Else
      Main.WriteDebugLog(("[CUL] Invalid command format!"))
      Return
    Endif
  Endif

  ' --- set extended bit ---
  ' IF bAck THEN bCommand = bCommand OR &H20

  sPacket = "F" & sAddress & sCommand
  QueueCommand(sPacket)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' add command to queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub QueueCommand(sPacket As String)

  ' add command to queue
  cQueue.Add(sPacket & "\r\n", Rnd)

  ' if interface is not busy process it
  If bCULBusy = False Then DoQueue()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' process items in queue
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DoQueue()

  Dim sPacket As String

  For Each sPacket In cQueue
    WriteCommand(sPacket, cQueue.Key)
    Break ' only do first one
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Init()

  ' define timer for CUL interface busy
  tCULBusy = New Timer As "tCULBusy"
  tCULBusy.Stop

  ' define timer for CUL status LED
  tCULLed = New Timer As "tCULLED"
  tCULLed.Delay = 500
  tCULLed.Stop

  ' define timer for CUL reconnect
  tCULConnect = New Timer As "tCULConnect"
  tCULConnect.Delay = iConnectRetry
  tCULConnect.Stop

  ' init CUL
  QueueCommand("X21")

  ' set FHTid
  If Len(sFHTid) And If sFHTid <> "0000" Then
    QueueCommand("T01" & sFHTid)
    If bCULDebug Then
      Main.WriteRFXDebugLog("[CUL] Setting FHTid to '" & sFHTid & "' (" & HexToDec(sFHTid) & ")\n")
    Endif
  Else
    If bCULDebug Then Main.WriteRFXDebugLog("[CUL] No FHTid set, value '" & sFHTid & "' is not valid!\n")
  Endif

  ' get version
  QueueCommand("V")

  ParseDump()

End

' just here for development/debugging
Public Sub ParseDump()

  Dim hFile As File
  Dim sLine As String

  Try hFile = Open "/home/ron/FHT.txt" For Input
  If Error Then Return

  While Not Eof(hFile)
    Line Input #hFile, sLine
    If Len(sLine) > 5 Then Simulate(sLine)
  Wend

End

Private Sub ConvertFS20Dim(iLevel As Integer) As String

  Return Hex(CInt((iLevel / 6.5) + 1), 2) ' cut 1-99 down to 16 steps

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send string to CUL interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub WriteCommand(sBuffer As String, sKey As String)

  Dim bByte As Byte
  Dim iCnt As Integer

  Main.ControlLed("CUL", "On")
  If Main.bServer Then tCULLed.Start

  If sInterface = "tcp" Then
    ' send the message to the tcp stream
    Try Write #hCUL, sBuffer, Len(sBuffer)
    If Error Then Main.WriteRFXDebugLog(("[CUL] Unable to write to TCP port! -> ") & Error.Text)
  Else
    ' send the message to the serial port
    Try Write #hCULSer, sBuffer, Len(sBuffer)
    If Error Then Main.WriteRFXDebugLog(("[CUL] Unable to write to serial port! -> ") & Error.Text)
  Endif
  If bCULDebug Then Main.WriteRFXDebugLog("[CUL] > " & Mid(sBuffer, 1, Len(sBuffer) - 2) & "\n")

  ' remove sent command from queue
  cQueue.Remove(sKey)

  tCULBusy.Delay = 100 ' 100mS
  If bCULBusy = False Then bCULBusy = True
  tCULBusy.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' cul interface busy reset
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tCULBusy_Timer()

  bCULBusy = False
  tCULBusy.Stop

  If cQueue.Count Then ' if there are more commands left in queue, send next one
    If bCULDebug Then Main.WriteDebugLog(("[CUL] Sending next command from Queue (count = " & cQueue.Count & ")"))
    DoQueue()
  Endif

End

Private Sub CommandtoFS20(sCmd As String) As String

  Select UCase(sCmd)
    Case "OFF"
      Return "00"
    Case "ON"
      Return "11"
    Default
      Return ""
  End Select

  ' CONST FS20_TOGGLE AS Byte = &H12
  ' CONST FS20_DIMUP AS Byte = &H13
  ' CONST FS20_DIMDOWN AS Byte = &H14
  ' CONST FS20_DIMSWITCH AS Byte = &H15
  ' CONST FS20_TIMER AS Byte = &H16
  ' CONST FS20_SENDSTATE AS Byte = &H17
  ' CONST FS20_OFFFORTIMER AS Byte = &H18
  ' CONST FS20_ONFORTIMER AS Byte = &H19
  ' CONST FS20_ONOLDFORTIMER AS Byte = &H1A
  ' CONST FS20_RESET AS Byte = &H1B
  ' CONST FS20_RAMPONTIME AS Byte = &H1C
  ' CONST FS20_RAMPOFFTIME AS Byte = &H1D

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' inject packet into parser.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Simulate(sPacket As String)

  Dim iCnt As Integer

  bSimulate = True
  bFirstByte = True
  If bCULDebug Then Main.WriteRFXDebugLog(("[CUL] CUL receiver Simulate String: ") & sPacket & "\n")

  For iCnt = 1 To Len(sPacket)
    ProcessReceivedChar(Asc(Mid(sPacket, iCnt, 1)))
  Next
  ProcessReceivedChar(&HD)
  ProcessReceivedChar(&HA)
  bSimulate = False

End Sub

Private Sub ProcessReceivedChar(bTemp As Byte)

  If bFirstByte = True Then
    bFirstByte = False
    iByteCount = 0
    If bCULDebug Then Main.WriteRFXDebugLog("[CUL] < ", 0)
  Endif

  Main.ControlLed("CUL", "On")

  Try RecBuf[iByteCount] = bTemp
  If Error Then Return

  If bTemp = &HA Then
    iByteCount -= 2 ' do not count \r\n
    bMessage = True
  Else
    If bCULDebug And If (bTemp <> &HD) Then Main.WriteRFXDebugLog(Chr(bTemp), 1)
    Inc iByteCount
  Endif

  If bMessage Then Display_Message()

End

Private Sub Display_Message()

  Dim iDeviceId, iCnt, iCmd, iDur, iI, iJ, iRSSI As Integer
  Dim sHouseCode, sAddress, sValue, sValue2, sCmd, sEMType, sEMCumulValue, sEMPeakValue, sMsgCounter, sBatt, sVal, sId, sSval, sFv As String
  Dim sWarn, sLowTemp, sSensor, sWindow As String
  Dim aEMType As String[] = ["EM1000", "EM1000-S", "EM1000-EM", "EM1000-GZ"]
  Dim aFHTc2m As String[] = ["Auto", "Manual", "Holiday", "Holiday-Short"]
  Dim bConfirm As Boolean

  bMessage = False
  bFirstByte = True

  If bCULDebug Then Main.WriteRFXDebugLog("\n", 1)

  Select Case Chr(RecBuf[0])
    Case "F" ' FS20 message
      ' F <Hex>
      ' <Hex> is a hex string with the following format:
      ' hhhhaacc or hhhhaaccee, where
      ' hhhh is the FS20 housecode
      ' aa is the FS20 device address
      ' cc is the FS20 command
      ' ee is the FS20 timespec. Note that cc must have the extension bit set.
      ' Example: F12340111

      ' check for valid packet size
      If iByteCount < 9 Or If iByteCount > 12 Then
        If bCULDebug Then Main.WriteDebugLog(("[CUL] FS20 message received with invalid length of " & iByteCount & " bytes!"))
        Return
      Endif

      sHouseCode = Chr(RecBuf[1]) & Chr(RecBuf[2]) & Chr(RecBuf[3]) & Chr(RecBuf[4])
      sAddress = Chr(RecBuf[5]) & Chr(RecBuf[6])
      iCmd = Val("&H" & Chr(RecBuf[7]) & Chr(RecBuf[8]))

      ' signal
      If iByteCount > 11 Then
        iRSSI = Val("&H" & Chr(RecBuf[11]) & Chr(RecBuf[12]))
      Else
        iRSSI = Val("&H" & Chr(RecBuf[9]) & Chr(RecBuf[10]))
      Endif

      ' calculate duration if extension bit is set
      If (iCmd And &H20) Then
        iDur = Val("&H" & Chr(RecBuf[9]) & Chr(RecBuf[10]))
        iI = (iDur And &HF0) / 16
        iJ = (iDur And &HF)
        iDur = (iI ^ 2) * iJ * 0.25

        ' calculate cmd
        iCmd = iCmd Xor &H20
      Endif

      ' test if cmd value is known
      Try sVal = sFS20Values[iCmd]
      If Not Error Then
        If bCULDebug Then Main.WriteDebugLog(("[CUL] FS20 message received with address '") & sHouseCode & sAddress & "' (" & Hex2Four(sHouseCode & sAddress) & ") and command '" & sVal & "'" & IIf(iDur, " duration " & iDur, "") & IIf(iRSSI, " RSSI: " & iRSSI, ""))
        ' move some values around if needed
        If iCmd < 18 Then ' Off, Dim, On
          sValue = sVal
          sValue2 = " "
        Else If iCmd = 24 ' Off-For-Timer
          sValue = "Off"
          sValue2 = sVal
        Else If iCmd = 25 ' On-For-Timer
          sValue = "On"
          sValue2 = sVal
        Else
          sValue = ""
          sValue2 = sVal
        Endif
        ' find device id
        iDeviceId = Devices.Find(sHouseCode & sAddress, Devices.FindInterface("CUL Interface"), "FS20")
        If iDeviceId Then Devices.ValueUpdate(iDeviceId, sValue, sValue2, "", "")
      Else
        If bCULDebug Then Main.WriteDebugLog(("[CUL] FS20 message received with address '") & sHouseCode & sAddress & "' (" & Hex2Four(sHouseCode & sAddress) & ") and unknown/invalid command '" & Hex(iCmd) & "'")
      Endif
    Case "T" ' FHT messages
      ' check for valid packet size
      If iByteCount < 9 Or If iByteCount > 13 Then
        If bCULDebug Then Main.WriteDebugLog(("[CUL] FHT message received with invalid length of " & iByteCount & " bytes!"))
        Return
      Endif

      sAddress = Chr(RecBuf[1]) & Chr(RecBuf[2]) & Chr(RecBuf[3]) & Chr(RecBuf[4]) & Chr(RecBuf[5]) & Chr(RecBuf[6])
      iCmd = Val("&H" & Chr(RecBuf[7]) & Chr(RecBuf[8]))
      If iByteCount = 10 Then ' must be FHT FTK sensor
        ' T <Hex>
        ' <Hex> is a hex string with the following format:
        ' ccccccxx
        ' where cccccc being the id of the sensor in hex
        ' xx being the current status like 'open,close,sync,test,battlow'
        Select Case iCmd
          Case &H02, &H82
            sValue = "Closed"
            sBatt = "Ok"
          Case &H01, &H81
            sValue = "Open"
            sBatt = "Ok"
          Case &H11, &H91
            sValue = "Open"
            sBatt = "Low"
          Case &H12, &H92
            sValue = "Closed"
            sBatt = "Low"
          Case &H0C
            sValue2 = "Sync:Syncing"
          Case &H0F
            sValue2 = "Test:Success"
        End Select

        If bCULDebug Then Main.WriteDebugLog(("[CUL] FHT TFK message received with address '") & sHouseCode & sAddress & "', status '" & IIf(Len(sValue2), sValue2, sValue) & "' and battery level '" & sBatt & "'")
        ' find device id
        iDeviceId = Devices.Find(sHouseCode & sAddress, Devices.FindInterface("CUL Interface"), "FHTTFK")
        If iDeviceId Then
          ' update values
          Devices.ValueUpdate(iDeviceId, sValue, sValue2, "", "")
          ' store battery status
          If (Len(sBatt) > 1) Then Devices.Battery(iDeviceId, sBatt)
        Endif
      Else ' FHT80 message
        ' T15157D671216
        ' T15177E7712FA
        ' T5A334369001A
        ' T <Hex>
        ' <Hex> is a hex string with the following format:
        ' Thhhhccnnvvxx
        ' hhhh = address
        ' cc = command
        ' nn = report value / id
        ' vv = value
        ' xx = rssi
        sAddress = Chr(RecBuf[1]) & Chr(RecBuf[2]) & Chr(RecBuf[3]) & Chr(RecBuf[4])
        iCmd = Val("&H" & Chr(RecBuf[5]) & Chr(RecBuf[6]))
        sCmd = FHTCode(iCmd)

        If iByteCount > 9 Then
          sVal = Val("&H" & (Chr(RecBuf[9]) & Chr(RecBuf[10])))
        Else If iByteCount < 9 Then
          sCmd = "FHT short message"
          sVal = ""
        Endif

        If iByteCount > 11 Then
          ' rssi
          iRSSI = Val("&H" & Chr(RecBuf[11]) & Chr(RecBuf[12]))
        Endif

        If sCmd = "Report1" Or If sCmd = "Report2" Then
          sVal = Val("&H" & (Chr(RecBuf[7]) & Chr(RecBuf[8])))
          bConfirm = True
        Endif

        If InStr(sCmd, "-From") Or If InStr(sCmd, "-To") Then ' time format
          sVal = Format(Val(sVal) / 6, "0#") & ":" & Format((Val(sVal) Mod 6) * 10, "0#")
        Else If sCmd = "Mode" ' auto, manual, holiday, holiday-short
          sVal = aFHTc2m[sVal]
        Else If InStr(sCmd, "-Temp")
          sVal = sVal / 2
          sVal = Format(sVal, "#.0")
        Else If sCmd = "Lowtemp-Offset"
          sVal = Format(sVal, "#.0")
        Else If InStr(sCmd, "Actuator")
          sSval = Chr(RecBuf[7]) & Chr(RecBuf[8])
          sFv = Format((100 * Val(sVal) / 255), "#")
          sId = Chr(RecBuf[8])
          If sSval = "A0" Or If sSval = "B0" Then
            sVal = sFv ' sync in the summer
          Else If Right(sSval, 1) = "0" Then
            sVal = "SyncNow"
          Else If Right(sSval, 1) = "1" Then
            sVal = "99%" ' FHT set to 30.5, FHT80B="ON"
          Else If Right(sSval, 1) = "2" Then
            sVal = "0%" ' FHT set to 5.5
          Else If Right(sSval, 1) = "6" Then
            sVal = sFv
          Else If Right(sSval, 1) = "8" Then
            sVal = "Offset: " & sFv
          Else If sSval = "2A" Or If sSval = "3A" Then
            sVal = "Lime-Protection"
          Else If sSval = "AA" Or If sSval = "BA" Then
            sVal = sFv ' lime protection bug
          Else If Right(sSval, 1) = "C" Then
            sVal = Val(sVal) / 2 - 1.5
            sVal = "SyncTime: " & Str(sVal)
          Else If Right(sSval, 1) = "E" Then
            sVal = "Test"
          Else If Right(sSval, 1) = "F" Then
            sVal = "Pair"
          Else
            sVal = "Unknown"
          Endif

          If InStr(sVal, "%") And If sId = 0 Then
            If bCULDebug Then Main.WriteDebugLog(("[CUL] Position " & sId & " " & sVal))
          Endif
        Else If sCmd = "Measured-Low"
          cLowTemps[sAddress] = sVal
        Else If sCmd = "Measured-High"
          Try sLowTemp = cLowTemps[sAddress]
          If Not Error Then
            sVal = (sVal * 256) + sLowTemp
            sVal /= 10
            sCmd = "Measured-Temp"
          Endif
        Else If sCmd = "Warnings"
          If (sVal And 1) Then
            sWarn = "Battery low"
            sBatt = "Low"
          Endif
          If (sVal And 2) Then
            If sWarn Then sWarn &= "; "
            sWarn &= "Temp too low"
            sLowTemp = "Warn"
          Endif
          If (sVal And 32) Then
            If sWarn Then sWarn &= "; "
            sWarn &= "Window open"
            sWindow = "Open"
          Endif
          If (sVal And 16) Then
            If sWarn Then sWarn &= "; "
            sWarn &= "Window sensor fault"
            sSensor = "Fault"
          Endif
          ' default value if not set
          If Not Len(sBatt) Then sBatt = "Ok"
          If Not Len(sLowTemp) Then sLowTemp = "Ok"
          If Not Len(sWindow) Then sWindow = "Closed"
          If Not Len(sSensor) Then sSensor = "Ok"
          If Not Len(sWarn) Then sWarn = "None"
        Endif
        If bCULDebug Then
          Main.WriteDebugLog(("[CUL] FHT message received with address '") & sAddress & "' (" & HexToDec(sAddress) & ") command '" & sCmd & "' (" & Hex(iCmd) & ") value '" & sVal & "'" & IIf(iRSSI, " RSSI: " & iRSSI, ""))
          If sCmd = "Warnings" Then Main.WriteDebugLog(("[CUL] Warnings: " & sWarn & ", Battery: " & sBatt & " LowTemp: " & sLowTemp & " Sensor: " & sSensor & " Window: " & sWindow))
        Endif
        ' find device id
        iDeviceId = Devices.Find(sAddress, Devices.FindInterface("CUL Interface"), "FHT80")
        If iDeviceId Then
          If sCmd = "Measured-Temp" Then
            ' update values
            Devices.ValueUpdate(iDeviceId, sVal, "", "", "")
          Else If sCmd = "Desired-Temp" Then
            Devices.ValueUpdate(iDeviceId, "", sVal, "", "")
          Else If InStr(sCmd, "Actuator") Then
            Devices.ValueUpdate(iDeviceId, "", "", sVal, "")
          ' ELSE
          '   Devices.ValueUpdate(iDeviceId, "", "", "", sVal)
          Endif
          ' store battery status
          If (Len(sBatt) > 1) Then Devices.Battery(iDeviceId, sBatt)
        Endif
      Endif
    Case "E"
      ' E <Hex>
      ' EM message. <Hex> is a hex string with the following format:
      ' Ettaacc111122223333
      ' tt: type 01 = EM-1000S, 02 = EM-1000EM, 03 = EM-1000GZ
      ' aa: address, depending on the type above 01: 01-04, 02: 05-08, 03: 09-12
      ' cc: counter, will be incremented by one for each message
      ' 1111: cumulated value
      ' 2222: last value(not set for type 2)
      ' 3333: top value(not set for type 2)
      sEMType = Chr(RecBuf[1]) & Chr(RecBuf[2])
      sAddress = Chr(RecBuf[3]) & Chr(RecBuf[4])
      sEMCumulValue = Chr(RecBuf[7]) & Chr(RecBuf[8]) & Chr(RecBuf[9]) & Chr(RecBuf[10])
      sMsgCounter = Chr(RecBuf[5]) & Chr(RecBuf[6])
      Try sValue = Chr(RecBuf[11]) & Chr(RecBuf[12]) & Chr(RecBuf[13]) & Chr(RecBuf[14])
      Try sEMPeakValue = Chr(RecBuf[15]) & Chr(RecBuf[16]) & Chr(RecBuf[17]) & Chr(RecBuf[18])

      If bCULDebug Then Main.WriteDebugLog(("[CUL] EM message received sensor '") & aEMType[Val(sEMType)] & "' [" & sEMType & "]" & " with address '" & sAddress & "' cumulated value: " & sEMCumulValue & (" last: ") & sValue & (" top: ") & sEMPeakValue & (" msg count: ") & Val("&H" & sMsgCounter))
      iDeviceId = Devices.Find(sAddress, Devices.FindInterface("CUL Interface"), aEMType[Val(sEMType)])
      ' update value
      If iDeviceId Then Devices.ValueUpdate(iDeviceId, sEMCumulValue, sValue, sEMPeakValue, "")
    Case "V"
      If bCULDebug Then Main.WriteDebugLog(("[CUL] Version string received."))
    Case Else
      Main.WriteDebugLog(("[CUL] Unknown/unsupported message format received!"))
  End Select
  Main.ControlLed("CUL", "Off")

Catch
   Main.WriteDebugLog(("[CUL] ERROR: " & Error.Text & " at " & Error.Where & " Parsing CUL data packet: ") & DisplayPacket(RecBuf))

End

' convert doubles bytes hex to decimal
Private Sub HexToDec(sHex As String) As String

  Return Val("&H" & Mid(sHex, 1, 2)) & Val("&H" & Mid(sHex, 3, 2))

End

' converting CUL Hex IDs into ELV-4-Ids
Private Sub Hex2Four(sHex As String) As String

  Dim iCnt As Integer
  Dim sELV As String

  For iCnt = 1 To Len(sHex)
    sELV &= Left(Str((Val("&H" & Mid(sHex, iCnt, 1)) / 4) + 1), 1)
    sELV &= (Val("&H" & Mid(sHex, iCnt, 1)) Mod 4) + 1
  Next
  Return sELV

End

Private Sub FHTCode(iCode As Byte) As String

  Select Case iCode
    Case &H00
      Return "Actuator"
    Case &H01
      Return "Actuator1"
    Case &H02
      Return "Actuator2"
    Case &H03
      Return "Actuator3"
    Case &H04
      Return "Actuator4"
    Case &H05
      Return "Actuator5"
    Case &H06
      Return "Actuator6"
    Case &H07
      Return "Actuator7"
    Case &H08
      Return "Actuator8"
    Case &H14
      Return "Mon-From1"
    Case &H15
      Return "Mon-To1"
    Case &H16
      Return "Mon-From2"
    Case &H17
      Return "Mon-To2"
    Case &H18
      Return "Tue-From1"
    Case &H19
      Return "Tue-To1"
    Case &H1A
      Return "Tue-From2"
    Case &H1B
      Return "Tue-To2"
    Case &H1C
      Return "Wed-From1"
    Case &H1D
      Return "Wed-To1"
    Case &H1E
      Return "Wed-From2"
    Case &H1F
      Return "Wed-To2"
    Case &H20
      Return "Thu-From1"
    Case &H21
      Return "Thu-To1"
    Case &H22
      Return "Thu-From2"
    Case &H23
      Return "Thu-To2"
    Case &H24
      Return "Fri-From1"
    Case &H25
      Return "Fri-To1"
    Case &H26
      Return "Fri-From2"
    Case &H27
      Return "Fri-To2"
    Case &H28
      Return "Sat-From1"
    Case &H29
      Return "Sat-To1"
    Case &H2A
      Return "Sat-From2"
    Case &H2B
      Return "Sat-To2"
    Case &H2C
      Return "Sun-From1"
    Case &H2D
      Return "Sun-To1"
    Case &H2E
      Return "Sun-From2"
    Case &H2F
      Return "Sun-To2"
    Case &H3E
      Return "Mode"
    Case &H3F
      Return "Holiday1"
    Case &H40
      Return "Holiday2"
    Case &H41
      Return "Desired-Temp"
    Case &H42
      Return "Measured-Low"
    Case &H43
      Return "Measured-High"
    Case &H44
      Return "Warnings"
    Case &H45
      Return "Manu-Temp"
    Case &H4B
      Return "Ack"
    Case &H53
      Return "Can-Xmit"
    Case &H54
      Return "Can-Rcv"
    Case &H60
      Return "Year"
    Case &H61
      Return "Month"
    Case &H62
      Return "Day"
    Case &H63
      Return "Hour"
    Case &H64
      Return "Minute"
    Case &H65
      Return "Report1"
    Case &H66
      Return "Report2"
    Case &H69
      Return "Ack2"
    Case &H7D
      Return "Start-Xmit"
    Case &H7E
      Return "End-Xmit"
    Case &H82
      Return "Day-Temp"
    Case &H84
      Return "Night-Temp"
    Case &H85
      Return "Lowtemp-Offset"
    Case &H8A
      Return "WindowOpen-Temp"
    Case Else
      Return "Unknown"
    End Select

End

Private Sub DisplayPacket(bBuf As Byte[]) As String

  Dim sMsg As String
  Dim iCnt As Integer

  For iCnt = 0 To iByteCount
    sMsg &= Chr(bBuf[iCnt])
  Next
  Return sMsg

End

' implement properties
Private Function TCPHost_Read() As String

  Return sTCPHost

End

Private Sub TCPHost_Write(Value As String)

  sTCPHost = Value

End

Private Function TCPPort_Read() As Integer

  Return iTCPPort

End

Private Sub TCPPort_Write(Value As Integer)

  iTCPPort = Value

End

Private Function Interface_Read() As String

  Return sInterface

End

Private Sub Interface_Write(Value As String)

  sInterface = Value

End

Private Function Baud_Read() As String

  Return sBaud

End

Private Sub Baud_Write(Value As String)

  sBaud = Value

End

Private Function SerPort_Read() As String

  Return sSerPort

End

Private Sub SerPort_Write(Value As String)

  sSerPort = Value

End

Private Function CULDebug_Read() As Boolean

  Return bCULDebug

End

Private Sub CULDebug_Write(Value As Boolean)

  bCULDebug = Value

End

Private Function Model_Read() As Integer

  Return iModel

End

Private Sub Model_Write(Value As Integer)

  iModel = Value

End

Private Function AutoCreate_Read() As Boolean

  Return bAutoCreate

End

Private Sub AutoCreate_Write(Value As Boolean)

  bAutoCreate = Value

End

Private Function FHTid_Read() As String

  Return sFHTid

End

Private Sub FHTid_Write(Value As String)

  sFHTid = Value

End
