' Gambas class file

' Description:
' FThermostatProfileEditor.class
' Thermostat profile editor.

' Development Status:
' Beta.

' Credits: This module is build by Francois.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private Profiles As New Collection
Private barWidth As Integer
Private graphHeight As Integer
Private graphWidth As Integer
Private graphTop As Integer
Private graphBottom As Integer
Private graphLeft As Integer
Private graphRight As Integer
Private barVStepSize As Integer
Private tempMax As Integer
Private selectedProfile As String
Private picGraph As Picture

Private IconBarWidth As Integer
Private IconGraphHeight As Integer
Private IconGraphWidth As Integer
Private IconGraphTop As Integer
Private IconGraphBottom As Integer
Private IconGraphLeft As Integer
Private IconGraphRight As Integer
Private IconBarVStepSize As Integer

Public Sub _new(...)

  InitVar()
  InitProfile()

End

Public Sub InitVar()

  graphHeight = graph.Height - 50 - 24
  graphWidth = graph.Width - 50
  graphTop = 25
  graphLeft = 45
  graphBottom = graphTop + graphHeight
  graphRight = graphLeft + graphWidth
  barWidth = (graphWidth / 24)
  barVStepSize = graphHeight / 25
  tempMax = 25

  IconGraphHeight = 27 - 2
  IconGraphWidth = 50 - 2
  IconGraphTop = 1
  IconGraphLeft = 1
  IconGraphBottom = IconGraphTop + IconGraphHeight
  IconGraphRight = IconGraphLeft + IconGraphWidth
  IconBarWidth = (IconGraphWidth / 24)
  IconBarVStepSize = IconGraphHeight / 25

End

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  InitVar()
  InitProfile()
  InitAxis()
  FillProfileList()

End

Private Sub InitProfile()

  Dim rResult As Result
  Dim aTemperatures As New String[24]
  Dim sVal As String
  Dim iCnt As Integer

  Try rResult = Main.hDB.Exec("SELECT * FROM temperature_profile")
  If rResult Then
    If rResult.Count Then
      For Each rResult
        iCnt = 0
        Profiles[rResult!name] = New Integer[24]
        aTemperatures = Split(Trim(rResult!values), " ")
        For Each sVal In aTemperatures
          Profiles[rResult!name][iCnt] = CInt(sVal)
          Inc iCnt
        Next
      Next
      If Main.bMainDebug Then Main.WriteDebugLog(("[Main] Temperature profile restored."))
    Endif
  Endif

End

Private Sub InitAxis()

  Dim iHour, iCnt As Integer
  Dim s As Separator
  Dim c As CheckBox
  Dim l As Label
  Dim iCheckBoxW As Integer = 13

  Graph.Clear
  Draw.Begin(Graph)

  For iHour = 0 To 23
    Draw.Text(iHour, iHour * (barWidth) + graphLeft, graphBottom)
  Next

  For iCnt = 0 To tempMax - 1
    Draw.Text(tempMax - iCnt, 8, iCnt * barVStepSize + graphTop)
  Next

  Draw.Line(graphLeft, 15 * barVStepSize + graphTop, graphRight, 15 * barVStepSize + graphTop)
  Draw.Line(graphLeft, 5 * barVStepSize + graphTop, graphRight, 5 * barVStepSize + graphTop)
  Draw.End
  l = New Label(HBoxLocal)
  l.AutoResize = True
  l.Text = "Local"
  HBoxLocal.X = graphLeft - (barWidth - iCheckBoxW) / 2 - l.Width
  HBoxLocal.Width = graph.Width - HBoxLocal.X - 1
  s = New Separator(HBoxLocal)
  s.Width = barWidth - iCheckBoxW
  s.Height = 21
  For iHour = 0 To 23
    c = New CheckBox(HBoxLocal)
    c.Width = iCheckBoxW
    c.Action = CStr(iHour)
    s = New Separator(HBoxLocal)
    s.Width = barWidth - iCheckBoxW
    s.Height = 21
  Next

End

Private Sub FillProfileList()

  For Each Profiles
    lvwProfile.Add(Profiles.key, Profiles.key, GetProfilePicture(Profiles.key))
  Next

  If Profiles.Count Then
    lvwProfile.MoveFirst()
    lvwProfile.Item.Selected = True
    selectedProfile = lvwProfile.Item.key
    DrawProfile(selectedProfile)
  Endif

End

Private Sub DrawBar(iHour As Integer, temperature As Integer)

  Dim c As CheckBox
  Dim s As String
  Dim box As Integer

  c = HBoxLocal.Children[iHour * 2 + 2]
  s = c.Action
  c.Action = Null ' prevent action sending
  c.Value = If(temperature > 0, False, True)
  c.Action = s

  If temperature > 0 Then
    Draw.FillColor = Color.White
    Draw.Rect(iHour * (barWidth) + graphLeft, graphHeight + graphTop, barWidth - 1, - graphHeight)
    For box = 1 To temperature
      Draw.FillColor = Color.RGB((box - 10) * 10 + 105, 0, box * 15 + 100)
      Draw.Rect(iHour * (barWidth) + graphLeft, (tempMax - box) * barVStepSize + graphTop, barWidth - 1, barVStepSize - 1)
    Next
  Else
    Draw.FillColor = Color.LightGray
    Draw.Rect(iHour * (barWidth) + graphLeft, graphHeight + graphTop, barWidth - 1, - graphHeight)
  Endif
  Draw.Line(graphLeft, 15 * barVStepSize + graphTop, graphRight, 15 * barVStepSize + graphTop)
  Draw.Line(graphLeft, 5 * barVStepSize + graphTop, graphRight, 5 * barVStepSize + graphTop)

End

Private Sub DrawProfile(profileName As String)

  Dim iHour As Integer

  Draw.Begin(graph)
  Draw.FillStyle = True
  For iHour = 0 To 23
    DrawBar(iHour, Profiles[profileName][iHour])
  Next
  Draw.End

End

Public Sub GetProfilePicture(profileName As String) As Picture

  Dim iHour As Integer

  picGraph = New Picture(50, 27)
  picGraph.Fill(Color.white)
  If Profiles.Exist(profileName)
    Draw.Begin(picGraph)
    Draw.FillStyle = True
    For iHour = 0 To 23
      DrawBarPicture(iHour, Profiles[profileName][iHour])
    Next
    Draw.End
  Endif
  Return picGraph

End

Private Sub DrawBarPicture(iHour As Integer, iTemp As Integer)

  If iTemp > 0 Then
    Draw.FillColor = Color.Red
    Draw.Foreground = Color.Red
    Draw.Rect(iHour * IconBarWidth + IconGraphLeft, IconGraphHeight + IconGraphTop, IconBarWidth, (tempMax - iTemp) * IconBarVStepSize - IconGraphHeight)
  Else
    Draw.FillColor = Color.LightGray
    Draw.Foreground = Color.LightGray
    Draw.Rect(iHour * IconBarWidth + IconGraphLeft, IconGraphHeight + IconGraphTop, IconBarWidth, - IconGraphHeight)
  Endif

End

Public Sub Graph_MouseMove()

  Dim iHour, iStartHour, iTemp As Integer

  If lvwProfile.Current Then
    iHour = (Mouse.X - graphLeft) / (barWidth)
    iStartHour = (Mouse.StartX - graphLeft) / (barWidth)
    iTemp = tempMax - (Mouse.Y - graphTop) / (barVStepSize) + 1
    Draw.Begin(graph)
    Draw.FillStyle = True
    If iHour >= 0 And iHour < 24 And iTemp > 0 And iTemp < 26 Then
      DrawBar(iHour, iTemp)
      If iTemp < 25 Then Draw.Text(iTemp, iHour * (barWidth) + graphLeft + 2, (tempMax - iTemp) * barVStepSize + graphTop - 20)
      Profiles[selectedProfile][iHour] = iTemp
    Endif
    Draw.End
  Endif

End

Public Sub Graph_MouseUp()

  Dim iHour, iTemp As Integer

  If lvwProfile.Current Then
    iHour = (Mouse.X - graphLeft) / (barWidth)
    iTemp = tempMax - (Mouse.Y - graphTop) / (barVStepSize) + 1
    Draw.Begin(graph)
    Draw.FillStyle = True
    If iHour >= 0 And iHour < 24 And iTemp > 0 And iTemp < 26 Then
      DrawBar(iHour, iTemp)
      Profiles[selectedProfile][iHour] = iTemp
      lvwProfile.Current.Picture = GetProfilePicture(selectedProfile)
    Endif
    Draw.End
  Endif

End

Public Sub btnAdd_Click()

  Dim sProfileName As String
  Dim iCnt As Integer

  Do
    sProfileName = "profile " & (lvwProfile.Count + iCnt)
    Inc iCnt
    Try lvwProfile.Add(sProfileName, sProfileName)
  Loop Until Error = False

  lvwProfile.Item.Selected = True
  selectedProfile = sProfileName
  If Not Profiles[selectedProfile] Then Profiles[selectedProfile] = New Integer[24]
  Profiles[selectedProfile].Fill(1)
  DrawProfile(selectedProfile)
  lvwProfile.Current.Picture = GetProfilePicture(selectedProfile)

End

Public Sub lvwProfile_Click()

  selectedProfile = lvwProfile.Current.Key
  If Not Profiles[selectedProfile] Then Profiles[selectedProfile] = New Integer[24]
  DrawProfile(selectedProfile)

End

Public Sub btnDelete_Click()

  If lvwProfile.Current Then
    Profiles.Remove(lvwProfile.Current.Key)
    lvwProfile.Current.Delete()
    If lvwProfile.Current Then
      selectedProfile = lvwProfile.Current.Key
      lvwProfile.Current.Selected = True
      DrawProfile(selectedProfile)
    Else
      selectedProfile = ""
      Graph.Clear
      InitAxis()
    Endif
  Endif

End

Public Sub btnSave_Click()

  Dim sProfile As String
  Dim iProfile As Integer[]
  Dim rResult As Result
  Dim iVal As Integer
  Dim tableProfiles As Table

  ' delete complete history
  Try Main.hDB.Tables.Remove("temperature_profile")
  ' recreate empty one
  tableProfiles = Main.hDB.Tables.Add("temperature_profile")
  tableProfiles.Fields.Add("id", db.Serial)
  tableProfiles.Fields.Add("name", db.String, 64)
  tableProfiles.Fields.Add("values", db.String, 0)
  tableProfiles.PrimaryKey = ["id"]
  tableProfiles.Update()

  For Each iProfile In Profiles
    sProfile = ""
    Main.hDB.Begin()
    rResult = Main.hDB.Create("temperature_profile")
    rResult!name = lvwProfile[Profiles.Key].Text
    For Each iVal In iProfile
      If iVal < 10 And iVal > 0 Then
        sProfile = sProfile & "0" & iVal & " "
      Else
        sProfile = sProfile & iVal & " "
      Endif
    Next
    rResult!values = Trim(sProfile)
    rResult.Update()
    Main.hDB.Commit()
  Next
  Me.Close()

End

Public Sub btnCancel_Click()

  Me.Close()

End

Public Sub Action_Activate(sKey As String) As Boolean

  If Profiles[selectedProfile][CInt(sKey)] > 0 Then
    Profiles[selectedProfile][CInt(sKey)] = -1
  Else
    Profiles[selectedProfile][CInt(sKey)] = 1
  Endif

  Draw.Begin(Graph)
  Draw.FillStyle = True
  DrawBar(CInt(sKey), Profiles[selectedProfile][CInt(sKey)])
  Draw.End()
  lvwProfile.Current.Picture = GetProfilePicture(selectedProfile)

Catch

End
