' Gambas class file

' Description:
' FMenuEditor.class
' Edit program menu's.

' Development Status:
' Just build, possibly bugs ahead.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public hToolButton As ToolButton
Public iId As Integer

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  FillCombo()
  RefreshMenu()

End

Public Sub RefreshMenu()

  Dim rResult, rResultMenu, rResultLocation As Result
  Dim iCount, iPanel As Integer

  VBoxAvailMenu1.Height = 0
  VBoxAvailMenu2.Height = 0
  VBoxAvailMenu3.Height = 0
  VBoxAvailMenu4.Height = 0

  ' fill combo with available locations
  rResultLocation = Main.hDB.Exec("SELECT * FROM locations")
  If rResultLocation Then
    For Each rResultLocation
      cmbLocation.Add(rResultLocation!name)
    Next
  Endif
  ' fill combo with available menu names
  rResultMenu = Main.hDB.Exec("SELECT * FROM menu_names")
  If rResultMenu Then
    If (rResultMenu.Count >= 1) Then
      For Each rResultMenu
        ToolPanelAvail[iPanel].Text = rResultMenu!name
        Select iPanel
          Case 0
            txtMenu1.Text = rResultMenu!name
          Case 1
            txtMenu2.Text = rResultMenu!name
          Case 2
            txtMenu3.Text = rResultMenu!name
          Case 3
            txtMenu4.Text = rResultMenu!name
        End Select
        rResult = Main.hDB.Exec("SELECT * FROM menu WHERE menu = &1 ORDER BY position", rResultMenu!id)
        If Not rResult Then
          Message.Info(("Error: table 'menu' not found!"))
          Return
        Endif
        For iCount = 0 To rResult.Max
          If rResult!menu = 1 Then
            hToolButton = New ToolButton(VBoxAvailMenu1) As "AvailToolButton"
            VBoxAvailMenu1.Height = VBoxAvailMenu1.Height + 42
          Else If rResult!menu = 2 Then
            hToolButton = New ToolButton(VBoxAvailMenu2) As "AvailToolButton"
            VBoxAvailMenu2.Height = VBoxAvailMenu2.Height + 42
          Else If rResult!menu = 3 Then
            hToolButton = New ToolButton(VBoxAvailMenu3) As "AvailToolButton"
            VBoxAvailMenu3.Height = VBoxAvailMenu3.Height + 42
          Else
            hToolButton = New ToolButton(VBoxAvailMenu4) As "AvailToolButton"
            VBoxAvailMenu4.Height = VBoxAvailMenu4.Height + 42
          Endif
          With hToolButton
            .Name = rResult!id
            .Text = rResult!name
            .Background = Color.TextBackground
            If Not rResult!enabled Then .Foreground = Color.LightGray
            .Font = Font["-1"]
            .Height = 42
            .Width = 111
            .Picture = Picture["images" &/ rResult!icon]
            .Tag = rResult!id
          End With
          rResult.MoveNext
        Next
        Inc iPanel
      Next
    Else
      Message(("No menu names found!"))
    Endif
  Else
    Message.Info(("Error: table 'menu_names' not found!"))
  Endif

End

Public Sub FillCombo()

  Dim rResultMenu As Result

  rResultMenu = Main.hDB.Exec("SELECT * FROM menu_names")
  If rResultMenu Then
    If (rResultMenu.Count >= 1) Then
      For Each rResultMenu
        cmbMenus.Add(rResultMenu!name)
      Next
    Endif
  Endif

End

Public Sub AvailToolbutton_Click()

  Dim rResult, rResultMenu As Result

  iId = Last.tag
  ' get values
  rResult = Main.hDB.Exec("SELECT * FROM menu WHERE id = &1", iId)
  If rResult.Count >= 1 Then
    txtName.Text = rResult!name
    Try rResultMenu = Main.hDB.Exec("SELECT name FROM menu_names WHERE id = &1", rResult!menu)
    If Not Error Then cmbMenus.Text = rResultMenu!name
    txtPosition.Text = rResult!position
    cmbVisible.Text = Main.DisplayBool(rResult!enabled)
  Endif

End

Public Sub AvailToolbutton_DblClick()

  Dim rResult As Result

  iId = Last.tag
  ' get values
  rResult = Main.hDB.Exec("SELECT * FROM menu WHERE id = &1", iId)
  If rResult.Count >= 1 Then
    FMain.OpenMenu(rResult!item)
  Endif

End

Public Sub SaveMenuItem()

  Dim rResultUpdate As Result

  ' save new menu settings
  rResultUpdate = Main.hDB.Exec("UPDATE menu_names SET name = &1 WHERE id = 1", txtMenu1.Text)
  rResultUpdate = Main.hDB.Exec("UPDATE menu_names SET name = &1 WHERE id = 2", txtMenu2.Text)
  rResultUpdate = Main.hDB.Exec("UPDATE menu_names SET name = &1 WHERE id = 3", txtMenu3.Text)
  rResultUpdate = Main.hDB.Exec("UPDATE menu_names SET name = &1 WHERE id = 4", txtMenu4.Text)
  rResultUpdate = Main.hDB.Exec("UPDATE menu SET menu = &1, position = &2, enabled = &3, name = &4 WHERE id = &5", GetMenuId(cmbMenus.Text), txtPosition.Text, GetVisible(), txtName.Text, iId)

End

Public Sub GetMenuId(sMenu As String) As Integer

  Dim rResultMenu As Result

  Try rResultMenu = Main.hDB.Exec("SELECT id FROM menu_names WHERE name = &1", sMenu)
  If Not Error Then Return rResultMenu!id

End

Public Sub GetVisible() As Boolean

  If cmbVisible.Text = "True" Then
    Return True
  Else
    Return False
  Endif

End

Public Sub btnSave_Click()

  Dim oObject As Object

  SaveMenuItem()

  ' delete previous menu items first
  For Each oObject In VBoxAvailMenu1.Children
    oObject.Delete()
  Next
  For Each oObject In VBoxAvailMenu2.Children
    oObject.Delete()
  Next
  For Each oObject In VBoxAvailMenu3.Children
    oObject.Delete()
  Next
  For Each oObject In VBoxAvailMenu4.Children
    oObject.Delete()
  Next
  RefreshMenu()

  ' delete previous menu items first
  For Each oObject In FMain.VBoxMenu1.Children
    oObject.Delete()
  Next
  For Each oObject In FMain.VBoxMenu2.Children
    oObject.Delete()
  Next
  For Each oObject In FMain.VBoxMenu3.Children
    oObject.Delete()
  Next
  For Each oObject In FMain.VBoxMenu4.Children
    oObject.Delete()
  Next
  FMain.RefreshMenu()

End

Public Sub btnClose_Click()

  Me.Close

End

Public Sub cmbMenus_Change()

  btnSave.Enabled = True

End

Public Sub txtPosition_Change()

  btnSave.Enabled = True

End

Public Sub cmbVisible_Change()

  btnSave.Enabled = True

End

Public Sub txtMenu1_Change()

  btnSave.Enabled = True

End

Public Sub txtMenu2_Change()

  btnSave.Enabled = True

End

Public Sub txtMenu3_Change()

  btnSave.Enabled = True

End

Public Sub txtMenu4_Change()

  btnSave.Enabled = True

End
