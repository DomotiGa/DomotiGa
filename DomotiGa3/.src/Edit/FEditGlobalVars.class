' Gambas class file

' Description:
' FEditGlobalVars.class
' Add a way to edit/delete globalvars.

' Development Status:
' Finished

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  GetVarList()

End

Public Sub btnClose_Click()

  Me.Close

End

Public Sub GetVarList()

  Dim iCount As Integer
  Dim cGV As CGlobalVarEntry
  Dim vValue As Variant

  ' Lets refresh the globalvar list first - don't continue on error
  If Not Main.bServer Then
    If Not Client.GetGlobalVars() Then
      Message.Error(("Received an empty or invalid response from the JSON-RPC server. Check if the server is running."))
      Me.Close
      Return
    Endif
  Endif

  With tbvGlobalVar
    .Columns.Count = 3
    .Rows.Count = Main.GlobalVar.Count
    .Columns[0].Title = ("Name")
    .Columns[0].Width = 150
    .Columns[1].Title = ("Value")
    .Columns[1].Width = 350
    .Columns[2].Width = 0 ' automatic
    .Columns[2].Title = "Datatype"
  End With

  For Each cGV In Main.GlobalVar
    ' Get the vValue via the GetGlobalVar, else empty values are incorrectly displayed
    tbvGlobalVar[iCount, 0].Text = Main.GlobalVar.Key
    vValue = Main.GetGlobalVar(Main.GlobalVar.Key)
    If gb.Boolean = TypeOf(vValue) Then
      tbvGlobalVar[iCount, 1].Text = Main.DisplayBool(vValue)
    Else
      tbvGlobalVar[iCount, 1].Text = vValue
    Endif

    Select TypeOf(vValue)
      Case gb.Boolean
        tbvGlobalVar[iCount, 2].Text = "Boolean"
      Case gb.Byte, gb.Short, gb.Integer, gb.Long, gb.Single, gb.Float
        tbvGlobalVar[iCount, 2].Text = "Number"
      Default
        tbvGlobalVar[iCount, 2].Text = "String"
    End Select

    iCount = iCount + 1
  Next

End

Public Sub tbvGlobalVar_ColumnClick(Column As Integer)

  FMain.SortTableView(Me.tbvGlobalVar, Column, True)

End

Public Sub btnRefresh_Click()

  GetVarList()

End

Public Sub btnDelete_Click()

  ' Don't crash on a not existing row
  If tbvGlobalVar.Row = -1 Then Return

  If Main.GlobalVar.Exist(tbvGlobalVar[tbvGlobalVar.Row, 0].Text) Then
    If Main.GlobalVar[tbvGlobalVar[tbvGlobalVar.Row, 0].Text].System Then
      Message.Info(("Can't delete a system variable"))
      Return
    Endif
  Endif

  If Message.Question("Are you sure you want to delete var '" & tbvGlobalVar[tbvGlobalVar.Row, 0].Text & "'?", "Yes", "No") = 1 Then
    If Main.bServer Then
      Main.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
    Else
      Main.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
      Client.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
    Endif
    tbvGlobalVar.Rows.Remove(tbvGlobalVar.Row)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Edit the column. System variables and readonly are checked
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvGlobalVar_Activate()

  Dim aDataType As New String[]
  Dim bSystem As Boolean
  Dim bReadOnly As Boolean

  If Main.GlobalVar.Exist(tbvGlobalVar[tbvGlobalVar.Row, 0].Text) Then
    If Main.GlobalVar[tbvGlobalVar[tbvGlobalVar.Row, 0].Text].System Then bSystem = True
    If Main.GlobalVar[tbvGlobalVar[tbvGlobalVar.Row, 0].Text].ReadOnly Then bReadOnly = True
  Endif

  Select tbvGlobalVar.Column
    Case 0 ' Name
      If bSystem Then Return
      tbvGlobalVar.Edit

    Case 1 ' Value
      If bReadOnly Then Return
      tbvGlobalVar.Edit

    Case 2 ' Datatype
      If bSystem Or If bReadOnly Then Return

      aDataType.Clear
      aDataType.Add("Boolean")
      aDataType.Add("Number")
      aDataType.Add("String")
      tbvGlobalVar.Edit(aDataType, True)

  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Save the modified value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvGlobalVar_Save(Row As Integer, Column As Integer, Value As String)

  Dim sOldGlobalVar As String

  If tbvGlobalVar.Column = 0 Then
    sOldGlobalVar = tbvGlobalVar[Row, 0].Text
    tbvGlobalVar[Row, Column].Text = Value
    Main.DelGlobalVar(sOldGlobalVar)
    Main.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)

    If Not Main.bServer Then
      Client.DelGlobalVar(sOldGlobalVar)
      Client.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)
    Endif
  Else If tbvGlobalVar.Column = 1 Then
    tbvGlobalVar[Row, Column].Text = Value
    Main.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)

    If Not Main.bServer Then
      Client.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)
    Endif
  Endif

End

Public Sub tbvGlobalVar_Insert()

   tbvGlobalVar.Cancel
   
End

Public Sub btnAdd_Click()

  If Len(txtName.Text) > 0 And If Len(txtValue.Text) > 0 Then
    If Main.bServer Then
      Main.SetGlobalVar(txtName.Text, txtValue.Text)
    Else
      Main.SetGlobalVar(txtName.Text, txtValue.Text)
      Client.SetGlobalVar(txtName.Text, txtValue.Text)
    Endif
    GetVarList()
    txtName.Clear
    txtValue.Clear
  Else
    Balloon("Fill in a variable name and a value first!", txtName)
  Endif

End
