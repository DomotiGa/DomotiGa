' Gambas class file

' Description:
' FEditBwiredDevices.class
' Support for creating and edit bwired devices.

' Development Status:
' Finished and working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public rResult As Result
Public bAddDevice As Boolean

Private $cDevices As New Collection

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Code which will be executed when the form opens
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  ' give focus to first field
  txtDescription.SetFocus

  Me.Move(FMain.X + 50, FMain.Y + 70)
  LoadBwiredDevice()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load the existing devices in memory
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub LoadBwiredDevice()

  Dim iId As Integer
  Dim rDevices As Result
  Dim cCol As Collection
  Dim cValue As Integer[]

  $cDevices.Clear()

  rDevices = Devices.GetDevicesAndValues(["sortOrder": "name", "enabled": True, "inc_device_values": True])
  If rDevices Then
    If rDevices.Count Then
      For Each rDevices
        If rDevices!device_name Then
          If $cDevices.Exist(rDevices!device_name) Then
            cCol = $cDevices[rDevices!device_name]
          Else
            ' Store a new device and its id
            cCol = New Collection
            cValue = New Integer[]
            cCol["device_id"] = rDevices!device_id
            cCol["valuenum"] = cValue
            $cDevices[rDevices!device_name] = cCol
          Endif

          ' Store all valuenum of this device. We only show used valuenum (dynamic)
          If Not cCol["valuenum"].Exist(rDevices!device_value_valuenum) Then
            cCol["valuenum"].Add(rDevices!device_value_valuenum)
            cCol["valuenum"].Sort()
          Endif
        Endif
      Next
    Endif
  Else
    Message.Info(("ERROR: table 'devices' not found!"))
    Return
  Endif

  cmbDevices.Clear()
  cmbDevices.Add("")

  For Each $cDevices
    cmbDevices.Add($cDevices.Key)
  Next

  Try iId = FSettingsBwiredMap.tbvBwiredDevices[FSettingsBwiredMap.iCurRow, 0].Text
  If Not Error Then
    ' get values
    rResult = Main.hDB.Exec("SELECT * FROM devices_bwired WHERE id = &1", iId)
    If rResult And If rResult.Available Then
      cmbDevices.Text = rResult!devicename
      txtDescription.Text = rResult!description

      If rResult!value >= 1 Then
        cmbValue.Text = "value" & rResult!value
      Else
        cmbValue.Text = ""
      Endif

      cmbLabel.Text = rResult!devicelabel
    Endif
  Else
    bAddDevice = True
  Endif
  FillExample()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDelete_Click()

  If Not rResult Then Return

  Main.hDB.Exec("DELETE FROM devices_bwired WHERE id = &1", rResult!id)

  Me.Close
  FSettingsBwiredMap.FillBwiredDevices()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim iId As Integer

  If Not ValidInput() Then Return

  iId = Devices.FindIdForDevice(cmbDevices.Text)

  If bAddDevice Then
    Main.hDB.Begin()
    rResult = Main.hDB.Create("devices_bwired")
    rResult!description = txtDescription.Text
    rResult!devicename = cmbDevices.Text
    rResult!deviceid = iId
    rResult!devicelabel = cmbLabel.Text
    rResult!value = Replace(cmbValue.Text, "value", "")
    rResult.Update()
    Main.hDB.Commit()
    bAddDevice = False
  Else
    ' save new bwired device settings
    rResultUpdate = Main.hDB.Exec("UPDATE devices_bwired SET description = &1, devicename = &2, deviceid = &3, devicelabel = &4, value = &5 WHERE id = &6", txtDescription.Text, cmbDevices.Text, iId, cmbLabel.Text, Replace(cmbValue.Text, "value", ""), rResult!id)
  Endif
  Me.Close
  FSettingsBwiredMap.FillBwiredDevices()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub txtDescription_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbDevices_Click()

  Dim iValueNum As Integer

  cmbValue.Clear()
  If cmbDevices.Text Then
    If $cDevices.Exist(cmbDevices.Text) Then
      For Each iValueNum In $cDevices[cmbDevices.Text]["valuenum"]
        cmbValue.Add("value" & iValueNum)
      Next
    Endif
  Endif

  btnSave.Enabled = True
  FillExample()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbValue_Click()

  btnSave.Enabled = True
  FillExample()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ValidInput() As Boolean

  If Not txtDescription.Text Then
    Balloon(("Please enter a description!"), txtDescription)
    Return False
  Endif

  If Not cmbDevices.Text Then
    Balloon(("Please select a device!"), cmbDevices)
    Return False
  Endif

  If Not cmbValue.Text Then
    Balloon(("Please select a value field!"), cmbValue)
    Return False
  Endif

  Return True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnAdd_Click()

  bAddDevice = True
  btnSave_Click()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FillExample()

  Dim iId As Integer

  If cmbValue.Text And cmbDevices.Text Then
    iId = Devices.FindIdForDevice(cmbDevices.Text)
    If iId Then
      Try txtDeviceValue.Text = Devices.GetCurrentValueForDevice(iId, Replace(cmbValue.Text, "value", ""))
      If Error Then txtDeviceValue.Text = ""
      If Len(txtDeviceValue.Text) Then txtDeviceValue.Text &= " " & cmbLabel.Text
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbLabel_Click()

  btnSave.Enabled = True
  FillExample()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbLabel_Change()

  btnSave.Enabled = True
  FillExample()

End
