' Gambas class file

' Description:
' FEditGraphs.class
' Support for creating and editing custom rrdtool graphs.

' Development Status:
' Needs Testing.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module was written by Minus in 2010.

' Read file called COPYING for license details.

Public rGraph As Result

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  Reset_graph()
  Graph_list()
  PopulateGroups()

End

Public Sub ComboBoxGraph_name_Change()

  If Len(ComboBoxGraph_name.Text) > 1 Then
    Try rGraph = Main.hDB.Exec("SELECT name FROM graphs WHERE name = &1", ComboBoxGraph_name.Text)
    If rGraph.Count <> 0 Then
      Load_graph(ComboBoxGraph_name.Text)
      FrameGraph_data.Enabled = True
      FramePreview.Enabled = True
      Load_graph_data(ComboBoxGraph_name.Tag, ComboBoxGraphList.Text)
      AssignGroups()
    Endif
  Else
    Populate_device_list()
    Reset_graph()
    Reset_graph_data()
  Endif

End

Public Sub ComboBoxGraph_device_Change() ' select device -> update ds

  Dim rDevice As Result

  ComboBoxGraph_ds.Clear
  Try rDevice = Devices.GetDevicesAndValues(["device_id": Devices.FindIdForDevice(ComboBoxGraph_device.Text)])
  If rDevice Then
    For Each rDevice
      If rDevice!device_value_valuerrddsname Then
        ComboBoxGraph_ds.Add(rDevice!device_value_valuerrddsname)
      Endif
    Next
  Endif

End

Public Sub Reset_graph()

  ComboBoxGraph_name.Tag = ""
  TGraphComment.Text = ""

  ' checkbox min/max/last/avg
  CheckBoxGraph_min.Value = 0
  CheckBoxGraph_max.Value = 0
  CheckBoxGraph_avg.Value = 0
  CheckBoxGraph_last.Value = 0

  ' title
  Graph_title.Text = ""
  Graph_unit.Text = ""

  ' grid
  ComboBoxGraph_gridtype.Text = "Default"

  ' general color
  ColorButtonGraph_background.Value = 16777215
  ColorButtonGraph_canvas.Value = 16777215
  ColorButtonGraph_shadea.Value = 16777215
  ColorButtonGraph_shadeb.Value = 16777215
  ColorButtonGraph_font.Value = 0
  ColorButtonGraph_grid.Value = 0
  ColorButtonGraph_majorgrid.Value = 0
  ColorButtonGraph_frame.Value = 0
  ColorButtonGraph_axis.Value = 0
  ColorButtonGraph_arrow.Value = 0

  ' size
  ComboBoxGraph_width.Text = "400"
  ComboBoxGraph_height.Text = "100"

  CheckBoxLogarithmicscale.Value = 0
  ComboBoxGraphList.Text = 1

End

Public Sub Reset_graph_data()

  TextBoxGraph_name.Clear
  ColorButtonGraph.Value = 65280

  ComboBoxGraph_device.Clear
  ComboBoxGraph_ds.Clear

  CheckBoxGraph_graphvalue_min.Value = 0
  CheckBoxGraph_graphvalue_max.Value = 0
  CheckBoxGraph_graphvalue_avg.Value = 0
  ColorButtonGraph_graphvalue_min_color.Value = 0
  ColorButtonGraph_graphvalue_max_color.Value = 0
  ColorButtonGraph_graphvalue_avg_color.Value = 0

  CheckBoxGraph_min.Value = 0
  CheckBoxGraph_max.Value = 0
  CheckBoxGraph_avg.Value = 0
  CheckBoxGraph_last.Value = 0

  ComboBoxGraphList.Tag = ""

  CheckBoxShift.Value = 0
  SpinBoxShift.Value = 0
  CheckBoxShift.Value = 0
  SpinBoxTrend.Value = 0

  CheckBoxOperation.Value = 0
  SpinBoxOperation.Value = 0
  ComboBoxOperation.Text = "+"

  FrameGraph_data.Enabled = False
  FramePreview.Enabled = False
  Populate_device_list()

End

Public Sub Graph_list()

  ComboBoxGraph_name.Clear
  ComboBoxGraph_name.Add("")
  Try rGraph = Main.hDB.Exec("SELECT name, groups FROM graphs")
  If rGraph Then
    For Each rGraph
      ComboBoxGraph_name.Add(rGraph!name)
    Next
  Endif

End

Public Sub Grid_type(grid_type_txt As String) As String

  Dim grid_type_rrdCmd As String

  Select Case grid_type_txt
    Case "Default"
      grid_type_rrdCmd = "--grid-dash '1:1'"
      Return grid_type_rrdCmd
    Case "Dot Grid"
      grid_type_rrdCmd = "--grid-dash '1:3'"
      Return grid_type_rrdCmd
    Case "Full Grid"
      grid_type_rrdCmd = "--grid-dash '1:0'"
      Return grid_type_rrdCmd
  End Select

End

Public Sub Load_graph(Graph_name As String) As Boolean

  Dim Graph_id As Integer

  Try rGraph = Main.hDB.Exec("SELECT * FROM graphs WHERE name = &1", Graph_name)
  If rGraph.Count <> 0 Then
    Graph_id = rGraph!id
    ComboBoxGraph_name.Tag = rGraph!id

    ColorButtonGraph_background.Value = rGraph!color_background
    ColorButtonGraph_canvas.Value = rGraph!color_canvas
    ColorButtonGraph_shadea.Value = rGraph!color_shadea
    ColorButtonGraph_shadeb.Value = rGraph!color_shadeb
    ColorButtonGraph_font.Value = rGraph!color_font
    ColorButtonGraph_grid.Value = rGraph!color_grid
    ColorButtonGraph_majorgrid.Value = rGraph!color_majorgrid
    ColorButtonGraph_frame.Value = rGraph!color_frame
    ColorButtonGraph_axis.Value = rGraph!color_axis
    ColorButtonGraph_arrow.Value = rGraph!color_arrow

    Graph_title.Text = rGraph!graph_title
    Graph_unit.Text = rGraph!unit
    ComboBoxGraph_gridtype.Text = rGraph!grid_type
    TGraphComment.Text = rGraph!comments

    CheckBoxDate.Value = rGraph!date

    ' size
    ComboBoxGraph_width.Text = rGraph!graph_width
    ComboBoxGraph_height.Text = rGraph!graph_height

    CheckBoxLogarithmicscale.Value = rGraph!logarithmic_scale
    PopulateGroups()

  Endif

End

Public Sub Load_graph_data(graph_id As Integer, ds_id As Integer) As Boolean

  Dim rGraph_data As Result

  Try rGraph_data = Main.hDB.Exec("SELECT * FROM graph_data WHERE graph_id = &1 AND ds_id = &2", graph_id, ds_id)

  If rGraph_data.Count <> 0 Then
    ComboBoxGraphList.Tag = rGraph_data!id

    ' checkbox min/max/last/avg
    TextBoxGraph_name.Text = rGraph_data!graph_name
    ComboBoxGraph_device.Text = rGraph_data!graph_devicename
    ComboBoxGraph_ds.Text = rGraph_data!graph_rrddsname
    CheckBoxGraph_graphvalue_min.Value = rGraph_data!graph_value_min
    CheckBoxGraph_graphvalue_max.Value = rGraph_data!graph_value_max
    CheckBoxGraph_graphvalue_avg.Value = rGraph_data!graph_value_avg
    ColorButtonGraph_graphvalue_min_color.Value = rGraph_data!graph_value_min_color
    ColorButtonGraph_graphvalue_max_color.Value = rGraph_data!graph_value_max_color
    ColorButtonGraph_graphvalue_avg_color.Value = rGraph_data!graph_value_avg_color
    ComboBoxGraph_type.Text = rGraph_data!graph_line_type
    CheckBoxGraph_min.Value = rGraph_data!graph_min
    CheckBoxGraph_max.Value = rGraph_data!graph_max
    CheckBoxGraph_avg.Value = rGraph_data!graph_avg
    CheckBoxGraph_last.Value = rGraph_data!graph_last
    ColorButtonGraph.Value = rGraph_data!graph_color
    CheckBoxTrend.Value = rGraph_data!graph_trend
    SpinBoxShift.Value = rGraph_data!graph_timeshift_value
    CheckBoxShift.Value = rGraph_data!graph_timeshift
    SpinBoxTrend.Value = rGraph_data!graph_trend_value
    ' operation
    CheckBoxOperation.Value = rGraph_data!graph_operation
    SpinBoxOperation.Value = rGraph_data!graph_operation_value
    ComboBoxOperation.Text = rGraph_data!graph_operation_operator
  Else
    Reset_graph_data()
    FrameGraph_data.Enabled = True
    FramePreview.Enabled = True
  Endif

End

Public Sub ButtonGraphClose_Click()

  Me.Close

End

Public Sub CheckMissingGraphName() As Boolean

  If Len(ComboBoxGraph_name.Text) = 0 Then
    ComboBoxGraph_name.SetFocus
    Balloon("Graph name missing!", ComboBoxGraph_name,, 50, 10)
    Return False
  Else
    Return True
  Endif

End

Public Sub Populate_device_list()

  Dim rDevice As Result

  ComboBoxGraph_device.Clear
  ComboBoxGraph_device.Add("")
  Try rDevice = Devices.GetDevicesAndValues(["rrd": True, "sortOrder": "name"])
  If rDevice Then
    For Each rDevice
      If ComboBoxGraph_device.Find(rDevice!device_name) = -1 Then
        ComboBoxGraph_device.Add(rDevice!device_name)
      Endif
    Next
  Endif

End

' PUBLIC SUB ComboBoxGraph_device_GotFocus()
' 
'   IF ComboBoxGraph_device.Count = 0 AND CheckMissingGraphName() = TRUE THEN
'     Populate_device_list()
'   ENDIF
' 
' END

Public Sub ComboBoxGraphList_Change()

  If ComboBoxGraph_name.Tag <> "" Then
    Load_graph_data(ComboBoxGraph_name.Tag, ComboBoxGraphList.Text)
  Endif

End


Private Sub LoadPicture(iGraph_id As Integer, sRange As String) As Picture
  
  Dim sGraphPic, sPNG, sGraphUrl As String
  Dim pGraph As Picture
  
  If Main.bServer Then
    sGraphPic = RRDTool.GenerateGraph(iGraph_id, sRange)
    pGraph = Picture.Load(sGraphPic)
  Else
    sPNG = Client.GenerateRRDGraph(iGraph_id, sRange)
    If Len(sPNG) = 0 Then
      Error.Raise("Problem generating graphs on the server, check log")
    Endif
    sGraphUrl = Main.sGraphsUrl &/ sPNG
    sGraphPic = FMain.GetGraph(sGraphUrl)
    If Len(sGraphPic) Then
      If Main.bRRDToolDebug Then Main.WriteDebugLog("[RRDTool] Downloaded graph '" & sGraphUrl & "'")
      pGraph = Picture.Load(sGraphPic)
    Else
      Error.Raise("Failed to download graph '" & sGraphUrl & "'")
    Endif
  Endif
  
  Return pGraph
  
  Catch
    Message.Error(Error.Text)
    Return Null
  
End


Public Sub ButtonGraphPreview_Click()

  Dim pGraph As Picture
  
  pGraph = LoadPicture(ComboBoxGraph_name.Tag, ComboBoxRange_Preview.Text)
  If pGraph Then
    Display_Graph_Preview(pGraph)
  Endif

End

Public Sub ButtonGraphPreviewUpdate(sRange As String)

  Dim pGraph As Picture

  pGraph = LoadPicture(ComboBoxGraph_name.Tag, sRange)
  If pGraph Then
    Display_Graph_Preview(pGraph, True)
  Endif

End

Public Sub Display_Graph_Preview(pGraph As Picture, Optional bUpdate As Boolean)

  FGraphsPreview.PictureBoxGraphPreview.Picture = pGraph
  If bUpdate Then Return
  FGraphsPreview.Height = pGraph.Height + 80
  If pGraph.Width >= 415 Then
    FGraphsPreview.Width = pGraph.Width + 15
  Else
    FGraphsPreview.Width = 415
  Endif
  FGraphsPreview.X = (Desktop.W / 2) - (pGraph.Width / 2)
  FGraphsPreview.Y = (Desktop.H / 2) - (pGraph.Height / 2)
  FGraphsPreview.PictureBoxGraphPreview.H = pGraph.Height - 1
  FGraphsPreview.PictureBoxGraphPreview.W = pGraph.Width - 1
  FGraphsPreview.PictureBoxGraphPreview.Visible = True
  FGraphsPreview.Show

End

Public Sub DeleteGraphData_Button_Click()

  Dim rGraph As Result

  Try rGraph = Main.hDB.Exec("DELETE FROM graph_data WHERE id = &1", ComboBoxGraphList.Tag)
  If rGraph Then
    ComboBoxGraphList.Tag = ""
    Main.WriteDebugLog("[Graph Editor] DS deleted")
    Load_graph_data(ComboBoxGraph_name.Tag, ComboBoxGraphList.Text)
  Endif

End

Public Sub SaveGraphData_Button_Click()

  Dim rGraph As Result

  If Len(TextBoxGraph_name.Text) = 0 Then
    Balloon.Warning("Please specify a name!", TextBoxGraph_name)
    Return
  Endif
  If Len(ComboBoxGraph_device.Text) > 1 And Len(ComboBoxGraph_ds.Text) > 1 Then
    If ComboBoxGraphList.Tag Then
      Try rGraph = Main.hDB.Exec("UPDATE graph_data SET graph_name = &1, graph_max = &2, graph_min = &3, graph_last = &4, graph_avg = &5, graph_value_max = &6, graph_value_avg = &7, graph_value_min = &8, graph_value_max_color = &9, graph_value_avg_color = &{10}, graph_value_min_color = &{11}, graph_line_type = &{12}, graph_rrddsname = &{13}, graph_color = &{14}, graph_trend = &{15}, graph_trend_value = &{16}, graph_timeshift = &{17}, graph_timeshift_value = &{18}, graph_operation = &{19}, graph_operation_operator = &{20}, graph_operation_value = &{21}, graph_devicename = &{22} WHERE id = &{23}", TextBoxGraph_name.Text, Abs(CheckBoxGraph_max.Value), Abs(CheckBoxGraph_min.Value), Abs(CheckBoxGraph_last.Value), Abs(CheckBoxGraph_avg.Value), Abs(CheckBoxGraph_graphvalue_max.Value), Abs(CheckBoxGraph_graphvalue_avg.Value), Abs(CheckBoxGraph_graphvalue_min.Value), ColorButtonGraph_graphvalue_max_color.Value, ColorButtonGraph_graphvalue_avg_color.Value, ColorButtonGraph_graphvalue_min_color.Value, ComboBoxGraph_type.Text, ComboBoxGraph_ds.Text, ColorButtonGraph.Value, Abs(CheckBoxTrend.Value), SpinBoxTrend.Value, Abs(CheckBoxShift.Value), SpinBoxShift.Value, Abs(CheckBoxOperation.Value), ComboBoxOperation.Text, SpinBoxOperation.Value, ComboBoxGraph_device.Text, ComboBoxGraphList.Tag)
    Else
      Try rGraph = Main.hDB.Exec("INSERT INTO graph_data (graph_id, graph_name, graph_max, graph_min, graph_last, graph_avg, graph_value_max, graph_value_avg, graph_value_min, graph_value_max_color, graph_value_avg_color, graph_value_min_color, graph_line_type, graph_rrddsname, graph_color, graph_trend, graph_trend_value, graph_timeshift, graph_timeshift_value, graph_operation, graph_operation_operator, graph_operation_value, graph_devicename, ds_id) values (&1,&2,&3,&4,&5,&6,&7,&8,&9,&{10},&{11},&{12},&{13},&{14},&{15},&{16},&{17},&{18},&{19},&{20},&{21},&{22},&{23},&{24})", ComboBoxGraph_name.Tag, TextBoxGraph_name.Text, Abs(CheckBoxGraph_max.Value), Abs(CheckBoxGraph_min.Value), Abs(CheckBoxGraph_last.Value), Abs(CheckBoxGraph_avg.Value), Abs(CheckBoxGraph_graphvalue_max.Value), Abs(CheckBoxGraph_graphvalue_avg.Value), Abs(CheckBoxGraph_graphvalue_min.Value), ColorButtonGraph_graphvalue_max_color.Value, ColorButtonGraph_graphvalue_avg_color.Value, ColorButtonGraph_graphvalue_min_color.Value, ComboBoxGraph_type.Text, ComboBoxGraph_ds.Text, ColorButtonGraph.Value, Abs(CheckBoxTrend.Value), SpinBoxTrend.Value, Abs(CheckBoxShift.Value), SpinBoxShift.Value, Abs(CheckBoxOperation.Value), ComboBoxOperation.Text, SpinBoxOperation.Value, ComboBoxGraph_device.Text, ComboBoxGraphList.Text)
      Load_graph_data(ComboBoxGraph_name.Tag, ComboBoxGraphList.Text)
    Endif
  Else
    ComboBoxGraph_device.SetFocus
    Balloon.Warning("Device/DS Missing", ComboBoxGraph_device)
  Endif

End

Public Sub ButtonGraphSave2_Click()

  If Len(TextBoxGraph_name.Text) > 1 Then SaveGraphData_Button_Click()
  ButtonGraphSave_Click()

End

Public Sub ButtonGraphSave_Click()

  Dim rGraph As Result
  Dim sValues As String

  If Main.bRRDToolEnabled = False Then
    Error.Raise("You need to enable the RRDTool module!")
  Endif
  If Len(ComboBoxGraph_name.Text) = 0 Then
    Error.Raise("Name field empty, either select an existing tag or type a new name")
  Endif

  sValues = "name = '" & ComboBoxGraph_name.Text & "'"
  sValues &= ", enabled = " & Abs(CheckBoxGraph_enabled.Value)
  sValues &= ", comments = '" & TGraphComment.Text & "'"
  sValues &= ", graph_title = '" & Graph_title.Text & "'"
  sValues &= ", graph_height = " & ComboBoxGraph_height.Text
  sValues &= ", graph_width = " & ComboBoxGraph_width.Text
  sValues &= ", color_background = " & ColorButtonGraph_background.Value
  sValues &= ", color_canvas = " & ColorButtonGraph_canvas.Value
  sValues &= ", color_shadea = " & ColorButtonGraph_shadea.Value
  sValues &= ", color_shadeb = " & ColorButtonGraph_shadeb.Value
  sValues &= ", color_font = " & ColorButtonGraph_font.Value
  sValues &= ", color_grid = " & ColorButtonGraph_grid.Value
  sValues &= ", color_majorgrid = " & ColorButtonGraph_majorgrid.Value
  sValues &= ", color_frame = " & ColorButtonGraph_frame.Value
  sValues &= ", color_axis = " & ColorButtonGraph_axis.Value
  sValues &= ", color_arrow = " & ColorButtonGraph_arrow.Value
  sValues &= ", unit = '" & Graph_unit.Text & "'"
  sValues &= ", logarithmic_scale = " & Abs(CheckBoxLogarithmicscale.Value)
  sValues &= ", Date = " & CheckBoxDate.Value
  sValues &= ", grid_type = '" & ComboBoxGraph_gridtype.Text & "'"
  sValues &= ", groups = '" & GetGroups() & "'"

  If ComboBoxGraph_name.Tag Then
    rGraph = Main.hDB.Exec("UPDATE graphs SET " & sValues & " WHERE id = " & ComboBoxGraph_name.Tag)
  Else
    rGraph = Main.hDB.Exec("INSERT INTO graphs SET " & sValues)
    Graph_list()
    ComboBoxGraph_name.Select(ComboBoxGraph_name.Count, 1)
    Load_graph(ComboBoxGraph_name.Text)
  Endif

  Catch
    Message.Error(Error.Text)

End

Public Sub ButtonGraphDelete_Click()

  Dim rGraph As Result

  If ComboBoxGraph_name.Tag Then
    Try rGraph = Main.hDB.Exec("DELETE from graph_data where graph_id = &1", ComboBoxGraph_name.Tag)
    Try rGraph = Main.hDB.Exec("DELETE from graphs where id = &1", ComboBoxGraph_name.Tag)
    Reset_graph()
    Reset_graph_data()
    Graph_list()
  Endif

End

Public Sub PopulateGroups()

  Dim rResultGroups As Result

  lvGroupAvail.Clear
  ' fill available groups list
  rResultGroups = Main.hDB.Exec("SELECT * FROM groups ORDER BY name")
  If rResultGroups Then
    If rResultGroups.Count Then
      For Each rResultGroups
        lvGroupAvail.Add(rResultGroups!name, rResultGroups!name)
      Next
    Else
      Message(("No groups found!"))
    Endif
  Else
    Message(("No groups database table found!"))
  Endif

End

Public Sub AssignGroups()

  Dim rResultGroups As Result

  lvGroupActive.Clear
  ' fill available groups list
  rResultGroups = Main.hDB.Exec("SELECT * FROM groups")
  If (rResultGroups.Count >= 1) Then
    For Each rResultGroups
      If InStr("|" & rGraph!groups & "|", "|" & rResultGroups!name & "|") Then
        lvGroupActive.Add(rResultGroups!name, rResultGroups!name)
        lvGroupAvail.Remove(rResultGroups!name)
      Endif
    Next
  Else
    Message(("No groups found!"))
  Endif

End

Public Sub lvGroupAvail_Click()

  lvGroupActive.Add(lvGroupAvail.Key, lvGroupAvail.Key)
  lvGroupAvail.Remove(lvGroupAvail.Key)

End

Public Sub lvGroupActive_Click()

  lvGroupAvail.Add(lvGroupActive.Key, lvGroupActive.Key)
  lvGroupActive.Remove(lvGroupActive.Key)

End

Public Sub GetGroups() As String

  Dim iCount As Integer
  Dim sGroups As String = "|"

  lvGroupActive.MoveFirst

  For iCount = 1 To lvGroupActive.Count
    sGroups = sGroups & lvGroupActive.Item.Text & "|"
    lvGroupActive.MoveNext
  Next

  Return sGroups

End
