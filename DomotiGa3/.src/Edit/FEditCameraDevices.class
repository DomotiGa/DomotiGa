' Gambas class file

' Description:
' FEditCameraDevices.class
' Support for creating and edit camera devices.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public rResult As Result
Public rResultDevices As Result
Public bAddDevice As Boolean

Public Sub Form_Open()

  'give focus to field
  txtName.SetFocus

  Me.Move(FMain.X + 50, FMain.Y + 70)
  LoadCameraDevice()

End

Public Sub LoadCameraDevice()

  Dim iId As Integer

  ' fill camera types
  cmbType.Add("")
  cmbType.Add("Video4Linux")
  cmbType.Add("Fetch Image")
  cmbType.Add("Stream MJPEG")
  ' fill ptz types
  cmbPTZType.Add("")
  cmbPTZType.Add("Sony VISCA")
  cmbPTZType.Add("AXIS API")
  cmbPTZType.Add("Watchbot API")

  Try iId = FSettingsCameras.tbvCameraDevices[FSettingsCameras.iCurRow, 0].Text
  If Not Error Then
    ' get values
    rResult = Main.hDB.Exec("SELECT * FROM devices_camera WHERE id = &1", iId)
    If rResult.Count >= 1 Then
      cmbType.Text = rResult!type
      cmbType_Click()
      txtName.Text = rResult!name
      txtDescription.Text = rResult!description
      cmbPTZType.Text = rResult!ptztype
      cmbPTZType_Click()
      txtView.Text = rResult!viewstring
      txtGrab.Text = rResult!grabstring
      chkEnabled.Value = rResult!enabled
      txtPTZBaseUrl.Text = rResult!ptzbaseurl
      spVISCAAddress.Value = rResult!viscaaddress
      txtUser.Text = rResult!username
      txtPasswd.Text = rResult!passwd
      txtOptions.Text = rResult!cmdoptions
    End If
  Else
    bAddDevice = True
  End If

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnDelete_Click()

  Main.hDB.Exec("DELETE FROM devices_camera WHERE id = &1", rResult!id)
  Me.Close
  FSettingsCameras.FillCameraDevices()

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim sSql As String

  If Not ValidInput() Then Return

  If bAddDevice Then
    Main.hDB.Begin()
    rResult = Main.hDB.Create("devices_camera")
    rResult!description = txtDescription.Text
    rResult!name = txtName.Text
    rResult!type = cmbType.Text
    rResult!ptztype = cmbPTZType.Text
    rResult!viewstring = txtView.Text
    rResult!grabstring = txtGrab.Text
    rResult!enabled = chkEnabled.Value
    rResult!ptzbaseurl = txtPTZBaseUrl.Text
    rResult!viscaaddress = spVISCAAddress.Value
    rResult!username = txtUser.Text
    rResult!passwd = txtPasswd.Text
    rResult!cmdoptions = txtOptions.Text
    rResult.Update()
    Main.hDB.Commit()
    bAddDevice = False
  Else
    ' save new camera device settings
    sSql = "UPDATE devices_camera SET description = &1, name = &2, type = &3,  enabled = &4, ptzbaseurl = &5, ptztype = &6, viewstring = &7, viscaaddress = &8, username = &9, passwd = &{10}, cmdoptions = &{11}, grabstring = &{12} WHERE id = &{13}"
    rResultUpdate = Main.hDB.Exec(sSql, txtDescription.Text, txtName.Text, cmbType.Text, chkEnabled.Value, txtPTZBaseUrl.Text, cmbPTZType.Text, txtView.Text, spVISCAAddress.Value, txtUser.Text, txtPasswd.Text, txtOptions.Text, txtGrab.Text, rResult!id)
  End If
  Me.Close
  FSettingsCameras.FillCameraDevices()

End

Public Sub txtDescription_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtName_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtOptions_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtUser_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtPasswd_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbPTZType_Click()

  spVISCAAddress.Enabled = False
  Select Case cmbPTZType.Index
    Case 0
      txtPTZBaseUrl.Enabled = False
    Case 1
      spVISCAAddress.Enabled = True
      txtPTZBaseUrl.Enabled = False
    Case 2, 3
      txtPTZBaseUrl.Enabled = True
  End Select
  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not txtName.Text Then
    Balloon(("Please enter a name for this camera!"), txtName)
    Return False
  End If
  If Not cmbType.Text Then
    Balloon(("Please select a camera type!"), cmbType)
    Return False
  End If
  If cmbType.Index > 1 Then
    If Not txtView.Text Then
      Balloon(("Please give some input here!"), txtView)
      Return False
    End If
  End If
  If cmbType.Index = 1 Then
    If Not txtOptions.Text Then
      Balloon(("Please give some input here!"), txtOptions)
      Return False
    End If
  End If
  If cmbPTZType.Index = 2 Then
    If Not txtPTZBaseUrl.Text Then
      Balloon(("Please provide a base PTZ url!"), txtPTZBaseUrl)
      Return False
    End If
  End If
  Return True

End

Public Sub btnAdd_Click()

  bAddDevice = True
  btnSave_Click()

End

Public Sub cmbType_Click()

  Select Case cmbType.Index
    Case 1
      textOptions.Text = ("VLC Options")
      textView.Text = ("")
      txtUser.Enabled = False
      txtPasswd.Enabled = False
      txtView.Enabled = False
      txtGrab.Enabled = False
    Case 2
      textOptions.Text = ("Wget Options")
      textView.Text = ("Image URL")
      txtUser.Enabled = True
      txtPasswd.Enabled = True
      txtView.Enabled = True
      txtGrab.Enabled = False
    Case 3
      textOptions.Text = ("VLC Options")
      textView.Text = ("Stream URL")
      txtUser.Enabled = True
      txtPasswd.Enabled = True
      txtView.Enabled = True
      txtGrab.Enabled = False
  End Select
  btnSave.Enabled = True

End
