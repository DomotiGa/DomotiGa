' Gambas class file

' Description:
' FEditUsers.class
' Manage users, needed for future authentication and bluetooth proximity support.

' Development Status:
' Adding, deleting and editing user information is working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private sPasswd As String

Public Sub Form_Open()

  Dim hTextBoxPassword As TextBox = dcPassword.Children[0]
  Dim hTextBoxLastLogin As TextBox = dcLastLogin.Children[0]

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' allows users to maintain their own private data
  If Main.bUserisAdmin Then
    dsUsers.Filter = ""
  Else
    dsUsers.Filter = Subst("username = '&1'", Main.sActiveUser)
    dcUserName.Enabled = False
    dcAdmin.Enabled = False
    Me.Title = "Edit User Account"
  Endif

  With dbUsers.View
    .Columns[0].Text = ("Id")
    .Columns[0].Width = 28
    .Columns[1].Text = ("User Name")
    .Columns[1].Width = 80
    .Columns[2].Text = ("Full Name")
    .Columns[2].Width = 110
    .Columns[3].Text = ("Admin")
    .Columns[3].Width = 50
    .Columns[4].Text = ("Last Login")
    .Columns[4].Width = 120
    .Columns[5].Text = ("E-mail")
    .Columns[5].Width = 120
    .Columns[6].Text = ("Comments")
  End With
  hTextBoxPassword.Password = True
  hTextBoxLastLogin.ReadOnly = True

End

Public Sub dcPassword_GotFocus()

  sPasswd = dcPassword.Value
  dcPassword.Value = ""

End

Public Sub dcPassword_Validate(Value As Variant)

  Dim sSalt, sMD5 As String
  Dim rResult As Result

  ' need to check if user has admin power or user's name is same as DataControl1.Value
  If Comp(Main.sActiveUser, dcUserName.Value) <> 0 Then
    If Not Main.bUserisAdmin Then dcPassword.Value = ""
  Endif
  If Comp(sPasswd, dcPassword.Value) And Comp(dcPassword.Value, "") <> 0 Then
    sMD5 = Crypt.MD5(dcPassword.Value)
    sSalt = Right$(Left$(sMD5, 11), 8)
    sMD5 = Right$(sMD5, 22)
    If Comp(Main.sActiveUser, dcUserName.Value) <> 0 Then
      Message(("User '") & dcUserName.Value & ("' has to reset other private info (email password, etc.) when logging in."))
      Try Main.hDB.Exec("UPDATE users SET cookie = '' WHERE username = &1", dcUserName.Value)
    Else If Len(Main.sActiveUserCookie) > 0 Then ' put old cookie with new crypt key in DB
        Main.hDB.Exec("UPDATE users SET cookie = AES_ENCRYPT(&1,&2) WHERE username = &3", Main.sActiveUserCookie, Crypt.MD5(dcPassword.Value, Right(sMD5, 8)), dcUserName.Value)
    Endif
    dcPassword.Value = "MD5" & sSalt & sMD5
  Endif
  If Comp(dcPassword.Value, "") == 0 Then dcPassword.Value = sPasswd
  sPasswd = ""

End

Public Sub btnClose_Click()

  Me.Close

End
