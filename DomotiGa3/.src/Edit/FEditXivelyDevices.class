' Gambas class file

' Description:
' FEditXivelyDevices.class
' Support for creating and edit xively devices.

' Development Status:
' Finished and working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public rResult As Result
Public rResultDevices As Result
Public bAddDevice As Boolean

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  LoadXivelyDevice()

End

Public Sub LoadXivelyDevice()

  Dim iId As Integer

  ' fill combo with available devices
  cmbDevices.Add("")
  rResultDevices = Devices.FindAllEnabledDevices()
  If rResultDevices.Available Then
    If rResultDevices.Count Then
      For Each rResultDevices
        cmbDevices.Add(rResultDevices!name)
      Next
    Endif
  Else
    Message.Info(("Error: couldn't find devices details!"))
    Return
  Endif

  Try iId = FSettingsXively.tbvXivelyDevices[FSettingsXively.iCurRow, 0].Text
  If Not Error Then
    ' get values
    rResult = Main.hDB.Exec("SELECT * FROM devices_xively WHERE id = &1", iId)
    If rResult.Count >= 1 Then
      txtID.Text = rResult!datastreamid
      cmbDevices.Text = rResult!devicename
      txtTags.Text = rResult!tags
      txtUnits.Text = rResult!units
      cmbValue.Index = rResult!value
      cmbUnitSymbol.Text = rResult!devicelabel
      cmbUnitType.Text = rResult!unittype
    Endif
  Else
    bAddDevice = True
  Endif
  FillExample()

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnDelete_Click()

  Main.hDB.Exec("DELETE FROM devices_xively WHERE id = &1", rResult!id)
  Me.Close
  FSettingsXively.FillXivelyDevices()

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim iId As Integer

  If Not ValidInput() Then Return

  iId = Devices.FindIdForDevice(cmbDevices.Text)

  If bAddDevice Then
    Main.hDB.Begin()
    rResult = Main.hDB.Create("devices_xively")
    rResult!datastreamid = txtID.Text
    rResult!tags = txtTags.Text
    rResult!devicename = cmbDevices.Text
    rResult!deviceid = iId
    rResult!devicelabel = cmbUnitSymbol.Text
    rResult!value = cmbValue.Index
    rResult!units = txtUnits.Text
    rResult!unittype = cmbUnitType.Text
    rResult.Update()
    Main.hDB.Commit()
    bAddDevice = False
  Else
    ' save new bwired device settings
    rResultUpdate = Main.hDB.Exec("UPDATE devices_xively SET tags = &1, devicename = &2, deviceid = &3, devicelabel = &4, value = &5, datastreamid = &6, units = &7, unittype = &8 WHERE id = &9", txtTags.Text, cmbDevices.Text, iId, cmbUnitSymbol.Text, cmbValue.Index, txtID.Text, txtUnits.Text, cmbUnitType.Text, rResult!id)
  Endif
  Me.Close
  FSettingsXively.FillXivelyDevices()

End

Public Sub txtTags_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbDevices_Click()

  btnSave.Enabled = True
  FillExample()

End

Public Sub cmbValue_Click()

  btnSave.Enabled = True
  FillExample()

End

Private Sub ValidInput() As Boolean

  If Not txtID.Text Then
    Balloon(("Please enter a channel or datastream name!"), txtID)
    Return False
  Endif
  If Not txtTags.Text Then
    Balloon(("Please specify some tags!"), txtTags)
    Return False
  Endif
  If Not cmbDevices.Text Then
    Balloon(("Please select a device!"), cmbDevices)
    Return False
  Endif
  If Not cmbValue.Text Then
    Balloon(("Please select a value field!"), cmbValue)
    Return False
  Endif
  Return True

End

Public Sub btnAdd_Click()

  bAddDevice = True
  btnSave_Click()

End

Public Sub FillExample()

  Dim iId As Integer

  If cmbValue.Text And cmbDevices.Text Then
    iId = Devices.FindIdForDevice(cmbDevices.Text)
    If iId Then
      txtDeviceValue.Text = Devices.GetCurrentValueForDevice(iId, cmbValue.Index)
      If Len(txtDeviceValue.Text) Then txtDeviceValue.Text &= " " & cmbUnitSymbol.Text
    Endif
  Endif

End

Public Sub cmbUnitSymbol_Click()

  btnSave.Enabled = True
  FillExample()

End

Public Sub cmbUnitSymbol_Change()

  btnSave.Enabled = True
  FillExample()

End

Public Sub txtUnits_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbUnitType_Change()

  btnSave.Enabled = True

End
