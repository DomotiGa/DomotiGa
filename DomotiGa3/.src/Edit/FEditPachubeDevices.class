' Gambas class file

' Description:
' FEditPachubeDevices.class
' Support for creating and edit pachube devices.

' Development Status:
' Finished and working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public rResult As Result
Public rResultDevices As Result
Public bAddDevice As Boolean

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  LoadPachubeDevice()

End

Public Sub LoadPachubeDevice()

  Dim iId As Integer

  ' fill combo with available devices
  cmbDevices.Add("")
  rResultDevices = Devices.FindAllEnabledDevices()
  If rResultDevices.Available Then
    If rResultDevices.Count Then
      For Each rResultDevices
        cmbDevices.Add(rResultDevices!name)
      Next
    End If
  Else
    Message.Info(("Error: couldn't find devices details!"))
    Return
  End If

  Try iId = FSettingsPachube.tbvPachubeDevices[FSettingsPachube.iCurRow, 0].Text
  If Not Error Then
    ' get values
    rResult = Main.hDB.Exec("SELECT * FROM devices_pachube WHERE id = &1", iId)
    If rResult.Count >= 1 Then
      txtID.Text = rResult!datastreamid
      cmbDevices.Text = rResult!devicename
      txtTags.Text = rResult!tags
      txtUnits.Text = rResult!units
      cmbValue.Index = rResult!value
      cmbLabel.Text = rResult!devicelabel
      cmbUnitType.Text = rResult!unittype
    End If
  Else
    bAddDevice = True
  End If
  FillExample()

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnDelete_Click()

  Main.hDB.Exec("DELETE FROM devices_pachube WHERE id = &1", rResult!id)
  Me.Close
  FSettingsPachube.FillPachubeDevices()

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim iId As Integer

  If Not ValidInput() Then Return

  iId = Devices.FindIdForDevice(cmbDevices.Text)

  If bAddDevice Then
    Main.hDB.Begin()
    rResult = Main.hDB.Create("devices_pachube")
    rResult!datastreamid = txtID.Text
    rResult!tags = txtTags.Text
    rResult!devicename = cmbDevices.Text
    rResult!deviceid = iId
    rResult!devicelabel = cmbLabel.Text
    rResult!value = cmbValue.Index
    rResult!units = txtUnits.Text
    rResult!unittype = cmbUnitType.Text
    rResult.Update()
    Main.hDB.Commit()
    bAddDevice = False
  Else
    ' save new bwired device settings
    rResultUpdate = Main.hDB.Exec("UPDATE devices_pachube SET tags = &1, devicename = &2, deviceid = &3, devicelabel = &4, value = &5, datastreamid = &6, units = &7, unittype = &8 WHERE id = &9", txtTags.Text, cmbDevices.Text, iId, cmbLabel.Text, cmbValue.Index, txtID.Text, txtUnits.Text, cmbUnitType.Text, rResult!id)
  End If
  Me.Close
  FSettingsPachube.FillPachubeDevices()

End

Public Sub txtTags_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbDevices_Click()

  btnSave.Enabled = True
  FillExample()

End

Public Sub cmbValue_Click()

  btnSave.Enabled = True
  FillExample()

End

Private Sub ValidInput() As Boolean

  If Not txtID.Text Then
    Balloon(("Please enter a datastream ID!"), txtID)
    Return False
  End If
  If Not txtTags.Text Then
    Balloon(("Please enter a description!"), txtID)
    Return False
  End If
  If Not cmbDevices.Text Then
    Balloon(("Please select a device!"), cmbDevices)
    Return False
  End If
  If Not cmbValue.Text Then
    Balloon(("Please select a value field!"), cmbValue)
    Return False
  End If
  Return True

End

Public Sub btnAdd_Click()

  bAddDevice = True
  btnSave_Click()

End

Public Sub FillExample()

  Dim iId As Integer

  If cmbValue.Text And cmbDevices.Text Then
    iId = Devices.FindIdForDevice(cmbDevices.Text)
    If iId Then
      txtDeviceValue.Text = Devices.GetCurrentValueForDevice(iId, cmbValue.Index)
      If Len(txtDeviceValue.Text) Then txtDeviceValue.Text &= " " & cmbLabel.Text
    Endif
  End If

End

Public Sub cmbLabel_Click()

  btnSave.Enabled = True
  FillExample()

End

Public Sub cmbLabel_Change()

  btnSave.Enabled = True
  FillExample()

End

Public Sub txtUnits_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbUnitType_Change()

  btnSave.Enabled = True

End
