' Gambas class file

' Description:
' FLocations.class
' Display devices locations.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private cLocations As Collection
Private sLocation As String
Private tRefresh As Timer

Public Sub Form_Open()

  PopulateButtons()
  RefreshPage()

  ' create refresh timer
  tRefresh = New Timer As "tRefresh"
  tRefresh.Delay = 30000 ' 30 seconds
  tRefresh.Start

End

Public Sub Form_Close()

  tRefresh.Stop

End

Public Sub Form_Resize()

  vbLocations.Width = 86
  vbLocations.Height = Me.Height - 10
  svLocation.Move(100, 5, Me.ClientWidth, Me.ClientHeight - 18)

End

Public Sub PopulateButtons()

  Dim rDevice, rLocation As Result
  Dim hButton As Button
  Dim sLoc As String

  cLocations = New Collection

  Try rDevice = Main.hDB.Exec("SELECT * FROM devices")
  If rDevice.Count Then
    For Each rDevice
      If rDevice!location = 0 Then Continue
      Try rLocation = Main.hDB.Exec("SELECT * FROM locations WHERE id='" & rDevice!location & "'")
      If rLocation.Count Then
        If Not rLocation!name Then Continue
        ' get unique list of used location
        cLocations.Add(rLocation!name, rLocation!id)
      Endif
    Next
  Endif

  For Each sLoc In cLocations
    hButton = New Button(vbLocations) As "LocationButton"
     With hButton
    .Text = sLoc
    .Height = 20
    .Width = 84
    .Tag = sLoc
    .Font = Font["Sans Serif, 7, Normal"]
    End With
  Next

End

Public Sub LocationButton_Click()

  sLocation = Last.Tag
  FMain.DisplayLocation(sLocation, FLocations.vpnlLocation)

End

Public Sub RefreshPage()

  If Not sLocation Then Return

  FMain.DisplayLocation(sLocation, FLocations.vpnlLocation)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' refresh contents
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tRefresh_Timer()

  RefreshPage()

End

Public Sub btnRefresh_Click()

  RefreshPage()

End
