' Gambas class file

' Description:
' FFloorplanEditor.class
' Place devices on floorplan image to determine correct X and Y coordinates.

' Development Status:
' Just started, must be worked on.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public imgFloorPlanImage As Image
Public hPictureBox As PictureBox

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  FFloorPlans.DisplayFloorplan(FDeviceEditor.GetFloorplan(), svFloorplanPict, FDevices.tbvDevices[FDevices.iCurRow, 0].Text)

  If txtX.Text = "" Then txtX.Text = "0"
  If txtY.Text = "" Then txtY.Text = "0"

  If Not IsNull(pbIcon.Picture) And txtX.Text <> "0" And txtY.Text <> "0" Then
    AddToFloorPlan(pbIcon.Picture, txtX.Text, txtY.Text)
  Endif

End

Public Sub Form_Resize()

  svFloorplanPict.Move(1, 1, Me.ClientWidth - 100, Me.ClientHeight - 2)
  frameIcon.Move(Me.ClientWidth - 94, 0)
  lblX.Move(Me.ClientWidth - 90, 84)
  lblY.Move(Me.ClientWidth - 90, 109)
  txtX.Move(Me.ClientWidth - 70, 84)
  txtY.Move(Me.ClientWidth - 70, 109)
  btnSave.Move(Me.ClientWidth - 94, 159)
  btnDelete.Move(Me.ClientWidth - 94, 195)
  btnCancel.Move(Me.ClientWidth - 94, 231)

End

' provide drag/drop for deviceicon image
Public Sub DeviceIcon_MouseDrag()

  If Mouse.Left Then
    Drag.IconX = 0
    Drag.IconY = 0
    Drag.Icon = Last.Picture

    ' Only try if we have a valid image
    If Not IsNull(Last.Picture) Then
      Last.Drag(Last.Picture.Image)
    Endif
  Endif

End

Public Sub PlanIcon_MouseDrag()

  If Mouse.Left Then
    Drag.IconX = 0
    Drag.IconY = 0
    Drag.Icon = Last.Picture
    Last.Drag(Last.Picture.Image)
  Endif

End

Public Sub Plan_DragMove()

  Drag.Show(Last)

End

Public Sub Plan_Drop()

  txtX.Text = Drag.X + svFloorplanPict.ScrollX
  txtY.Text = Drag.Y + svFloorplanPict.ScrollY
  AddToFloorPlan(Drag.Icon, Drag.X + svFloorplanPict.ScrollX, Drag.Y + svFloorplanPict.ScrollY)

End

Public Sub AddToFloorplan(imgIcon As Picture, X As Integer, Y As Integer)

  ' If the icon already exist, we need to remove it first
  If Not IsNull(hPictureBox) Then
    hPictureBox.Picture = Null
  Endif

  ' recreate floorplan image
  hPictureBox = New PictureBox(svFloorplanPict) As "FloorIcon"
  With hPictureBox
    .Picture = imgIcon
    .Height = imgIcon.Height
    .Width = imgIcon.Width
    .Drop = True
    .Tag = "PlanIcon"
    .X = X
    .Y = Y
  End With

End

Public Sub btnSave_Click()

  FDeviceEditor.tbX.Text = txtX.Text
  FDeviceEditor.tbY.Text = txtY.Text
  Me.Close

End

Public Sub FloorIcon_MouseDrag()

  If Mouse.Left Then
    Drag.IconX = 0
    Drag.IconY = 0
    Drag.Icon = Last.Picture

    If Not IsNull(Last.Picture) Then
      Last.Drag(Last.Picture.Image)
    Endif
  Endif

End

Public Sub FloorIcon_DragMove()

  Drag.Show(Last)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnDelete_Click()

  txtX.Text = "0"
  txtY.Text = "0"

  ' If the icon already exist, we need to remove it first
  If Not IsNull(hPictureBox) Then
    hPictureBox.Picture = Null
  Endif


End
