' Gambas class file

' Description:
' FDatabaseAdmin.class
' Support to backup database or delete tables, use with care!

' Development Status:
' Finished, make backup async/background job.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public sOutput As String
Public sBackupPath As String = User.Home

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

End

Public Sub btnClose_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' ask user for confirmation twice
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub YesNo(sText As String) As Boolean

  Select Message.Question(sText, ("Yes"), ("No"))
    Case 1
    Select Message.Question(("Last Time: ") & sText, ("Yes"), ("No"))
      Case 1
      Return True
    End Select
    Return False
  End Select
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' delete selected database records
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDelete_Click()

  If YesNo(("Are you very sure that you want to Erase all selected Data?")) Then
    If chkDevices.Value Then Main.hDB.Exec("TRUNCATE TABLE devices")
    If chkDevicesLog.Value Then Main.hDB.Exec("TRUNCATE TABLE device_values_log")
    If chkDevicesBwired.Value Then Main.hDB.Exec("TRUNCATE TABLE devices_bwired")
    If chkDevicesXively.Value Then Main.hDB.Exec("TRUNCATE TABLE devices_xively")
    If chkPowerUsage.Value Then Main.hDB.Exec("TRUNCATE TABLE power_usage")
    If chkWaterUsage.Value Then Main.hDB.Exec("TRUNCATE TABLE water_usage")
    If chkGasUsage.Value Then Main.hDB.Exec("TRUNCATE TABLE gas_usage")
    If chkPhoneCalls.Value Then Main.hDB.Exec("TRUNCATE TABLE cdr")
    If chkEvents.Value Then Main.hDB.Exec("TRUNCATE TABLE events")
    If chkConditions.Value Then Main.hDB.Exec("TRUNCATE TABLE conditions")
    If chkTriggers.Value Then Main.hDB.Exec("TRUNCATE TABLE triggers")
    If chkActions.Value Then Main.hDB.Exec("TRUNCATE TABLE actions")
    If chkStock.Value Then Main.hDB.Exec("TRUNCATE TABLE stock")
    If chkContacts.Value Then Main.hDB.Exec("TRUNCATE TABLE contacts")
    If chkCaptures.Value Then
      Main.hDB.Exec("TRUNCATE TABLE capture_camera0")
      Main.hDB.Exec("TRUNCATE TABLE capture_camera1")
      Main.hDB.Exec("TRUNCATE TABLE capture_camera2")
      Main.hDB.Exec("TRUNCATE TABLE capture_camera3")
    Endif
    If chkTVChannels.Value Then Main.hDB.Exec("TRUNCATE TABLE tv_channels")
    If chkTVPrograms.Value Then Main.hDB.Exec("TRUNCATE TABLE tv_programs")
    If chkTVCategories.Value Then Main.hDB.Exec("TRUNCATE TABLE tv_categories")
    If chkTVSubCategories.Value Then Main.hDB.Exec("TRUNCATE TABLE tv_subcategories")
  Endif
  Message.Info("Selected data is deleted!")
  chkDevices.Value = False
  chkDevicesLog.Value = False
  chkDevicesBwired.Value = False
  chkDevicesXively.Value = False
  chkPowerUsage.Value = False
  chkWaterUsage.Value = False
  chkGasUsage.Value = False
  chkPhoneCalls.Value = False
  chkEvents.Value = False
  chkConditions.Value = False
  chkTriggers.Value = False
  chkActions.Value = False
  chkStock.Value = False
  chkContacts.Value = False
  chkCaptures.Value = False
  chkTVChannels.Value = False
  chkTVPrograms.Value = False
  chkTVCategories.Value = False
  chkTVSubCategories.Value = False

End

Public Sub btnBackupDB_Click()

  BackupDatabase()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' backup database to sql file and gzip it
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub BackupDatabase()

  Dim sExt As String[]

  ' only show sql or compressed files
  Dialog.Filter = ["*.tar.gz", ("MySQL Backup Files Compressed"), "*.sql", ("MySQL Backup Files")]
  Dialog.Title = ("Backup Database")
  Dialog.Path = sBackupPath &/ Lower(Application.Name) & "-" & Format$(Now(), "yyyy-mm-dd-hh-nn-ss") & ".sql.tar.gz"

  If Not (Dialog.SaveFile() = True) Then
    ' save backup path
    sBackupPath = Mid$(Dialog.Path, 1, (RInStr(Dialog.Path, "/") - 1))
    ' set program status to busy
    Application.Busy = True
    ' save backup to temp file first
    Shell "mysqldump -u " & Main.sSQLUser & " --password=" & Main.sSQLPass & " --host=" & Main.sSQLHost & " --databases " & Main.sSQLDatabase & " > " & "/tmp" &/ Lower(Application.Name) & ".sql" Wait

    ' check selected extension
    sExt = Split(Dialog.Path, ".")
    If (Lower$(sExt[sExt.count - 1]) = "gz") Then
      ' compress file
      Shell "tar -cvzf " & Dialog.Path & " -C /tmp " & Lower(Application.Name) & ".sql >/dev/null" Wait
    Else
      ' copy file
      Shell "cp " & "/tmp" &/ Lower(Application.Name) & ".sql " & Dialog.Path
    Endif
    Shell "rm " & "/tmp" &/ Lower(Application.Name) & ".sql"
    ' set program status to free
    Application.Busy = False
    Message.Info(("Database Backup Finished!"))
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' delete all RRD database files after confirmation
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDelRRDs_Click()

  If YesNo(("Are you very sure that you want to Erase all RRD Data?")) Then
    If Main.bServer Then
      RRDTool.DeleteRRD(-1)
      RRDTool.CreateRRDs()
    Else
      Client.DeleteRRD(-1)
      Client.CreateRRDs()
    Endif
    
    ServerStats.CreateRRDs()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' delete all RRD graph images files after confirmation
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDelGraphs_Click()

  If YesNo(("Are you very sure that you want to Erase all RRD graph Images?")) Then
    RRDTool.DeleteAllGraphs()
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' repair mysql database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRepairDB_Click()

  If YesNo(("Are you very sure that you want to repair your db?\nOnly use it when there are problems, because device id's may change and relations lost!")) Then
    Shell "mysqlcheck --repair --host " & Main.sSQLHost & " --databases " & Main.sSQLDatabase & " -u " & Main.sSQLUser & " -p" & Main.sSQLPass & " 2>&1" For Read As "DatabaseRepair"
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' got output, save it
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DatabaseRepair_Read()

  Dim sBuffer As String

  sBuffer = Read #Last, -256
  sOutput &= sBuffer

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' database repair finished, display result
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DatabaseRepair_Kill()

  FTextViewer.sTitle = ("Database Repair Finished!")
  FTextViewer.sText = sOutput
  FTextViewer.Show
  sOutput = ""

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' optimize mysql database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnOptimizeDB_Click()

  If YesNo(("Are you very sure that you want to optimize your db?\nOnly use it when there are problems, because device id's may change and relations lost!")) Then
    Shell "mysqlcheck --optimize --host " & Main.sSQLHost & " --databases " & Main.sSQLDatabase & " -u " & Main.sSQLUser & " -p" & Main.sSQLPass & " 2>&1" For Read As "DatabaseOptimize"
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' got output, save it
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DatabaseOptimize_Read()

  Dim sBuffer As String

  sBuffer = Read #Last, -256
  sOutput &= sBuffer

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' database optimize finished, display result
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DatabaseOptimize_Kill()

  FTextViewer.sTitle = ("Database Optimize Finished!")
  FTextViewer.sText = sOutput
  FTextViewer.Show
  sOutput = ""

End
