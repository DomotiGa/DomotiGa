' Gambas class file

' Description:
' FToolsSerialMon.class
' Serial Terminal

' Development Status:
' Works, but needs better error checking.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Close()

  If Sport.Status = Net.Active Then Close Sport

End

Public Sub Check_Status()

  ChkDSR.Value = Sport.DSR
  ChkDTR.Value = Sport.DTR
  ChkCTS.Value = Sport.CTS
  ChkRTS.Value = Sport.RTS
  ChkDCD.Value = Sport.DCD
  ChkRNG.Value = Sport.RNG

End

Public Sub btnOpen_Click()

  If Sport.Status = Net.Active Then
    Close Sport
    btnOpen.Text = ("Open")
    Timer1.Stop
  Else
    ' line parameters
    Sport.PortName = TxtPort.Text
    Sport.Speed = CmbSpeed.Text
    Sport.Parity = CmbParity.Index
    Sport.DataBits = CmbData.Text
    Sport.StopBits = CmbStop.Text

    ' keep DTR on
    Try Sport.FlowControl = ComboBox1.Index
    Try Sport.Open()
    If Error Then
      Message(("Unable to open serial port"))
    Else
      Check_Status()
      txtData.Text = ("Port Opened : ") & Sport.PortName & (" Settings : ") &
      Sport.Speed & "," & Sport.Parity & "," & Sport.DataBits & "," &
      Sport.StopBits & Chr(13) & Chr(10)
      btnOpen.Text = ("Close")
      Timer1.Start
    Endif
  Endif

End

Public Sub SPort_Read()

  Dim sString As String

  sString = Read #Sport, Lof(Sport)

  txtData.Text = txtData.Text & sString
  txtData.Pos = Len(txtData.Text)

End

Public Sub SPort_RNGChange(iVal As Boolean)

  ChkRng.Value = iVal

End

Public Sub SPort_DTRChange(iVal As Boolean)

  ChkDTR.Value = iVal

End

Public Sub SPort_DSRChange(iVal As Boolean)

  ChkDSR.Value = iVal

End

Public Sub SPort_CTSChange(iVal As Boolean)

  ChkCTS.Value = iVal

End

Public Sub SPort_DCDChange(iVal As Boolean)

  ChkDCD.Value = iVal

End

Public Sub SPort_RTSChange(iVal As Boolean)

  ChkRTS.Value = iVal

End

Public Sub btnCR_Click()

  If Sport.Status = Net.Inactive Then
    Message(("Open port first!"))
  Else
    Print #Sport, txtSend.Text
  Endif

End

Public Sub ChkDTR_Click()

  If Sport.Status = Net.Active Then
    Sport.DTR = ChkDTR.Value
    Check_Status
  Endif

End

Public Sub ChkRTS_Click()

  If Sport.Status = Net.Active Then
    Sport.RTS = ChkRTS.Value
    Check_Status
  Endif

End

Public Sub ComboBox1_Click()

  Sport.Close()
  Sport.FlowControl = ComboBox1.Index

End

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  cmbSpeed.Index = cmbSpeed.Find("19200")

End

Public Sub btnClose_Click()

  Try Timer1.Stop
  Me.Close

End

Public Sub btnSerial_Click()

  Dim sSerialPort As String

  sSerialPort = FToolsSerialPorts.Run()
  If sSerialPort Then TxtPort.Text = sSerialPort

End
