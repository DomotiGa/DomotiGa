' Gambas class file

' Description:
' FToolsZWaveCmdr.class
' Test some basic Z-Wave functionality.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private iSelectedNode As Integer
Private iConfigNode As Integer
Private iAssocNode As Integer
Private iUserCodeNode As Integer
Private tbText As TextBox

' Define timers for Z-Wave Commander messages, the "real" timers are defined in CZWave.class
' because the Commander doesn't have a garantueed lifetime during the Z-Wave lifetime
Private tControllerCommandMessage As Timer
Private tUserCodeEnrollMessage As Timer

Private $Error As String
Private $InstanceList As New Integer[]
Private $cControllerList As Collection
Private $cControllerInfo As Collection
Private $iHomeId As Integer
Private $bIsRunning As Boolean

' Only used in server mode
Private hOpenZWave As Object

Public tRefreshTabInfo As Timer
Public iRefreshTabInfo As Integer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Dim jCnt As Integer
  Dim iInstance As Integer

  Me.Move(FMain.X + 50, FMain.Y + 70)

  If Main.bServer Then
    If Not Plugin.IsPluginEnabled("OpenZWave", 0) Then
      Message.Info(("OpenZWave Z-Wave interface is not enabled"))
      Try Me.Close
      Return
    Endif
  Endif

  ' We are running in client/server mode - we only support JSON-RPC
  If Not Main.bServer Then
    If Main.sRPCType <> "JSONRPC" Then
      Message.Info(("Open Z-Wave Commander will only work in Client/Server mode if the JSON-RPC is configured in the GUI"))
      Try Me.Close
      Return
    Endif
  Endif

  ' Create/define the Open Z-Wave node table
  With tbvNodes
    .Columns.Count = 12
    .Columns[0].Title = ("Node")
    .Columns[1].Title = ("Basic Type")
    .Columns[2].Title = ("Generic Type")
    .Columns[3].Title = ("Value1")
    .Columns[4].Title = ("Value2")
    .Columns[5].Title = ("Value3")
    .Columns[6].Title = ("Value4")
    .Columns[7].Title = ("Status")
    .Columns[8].Title = ("Manufacturer")
    .Columns[9].Title = ("Product")
    .Columns[10].Title = ("Neighbors")
    .Columns[11].Title = ("Supported Classes")

    For jCnt = 0 To 11
      .Columns[jCnt].Width = tbvNodes.Font.TextWidth(.Columns[jCnt].Title) + .Font.Size
    Next
  End With

  ' Create/define the configuration table
  With tbvConfig
    .Columns.Count = 4
    .Columns[0].Title = ("Number") ' This column will NOT be shown, is for internal use
    .Columns[0].Width = 0 'This column will NOT be shown, is for internal use
    .Columns[0].Resizable = False ' This column will NOT be shown, is for internal use
    .Columns[1].Title = ("Saved")
    .Columns[1].Width = 0 'This column will NOT be shown, is for internal use
    .Columns[1].Resizable = False ' This column will NOT be shown, is for internal use
    .Columns[2].Title = ("Configuration")
    .Columns[3].Title = ("Value")
  End With

  ' Create/define the association table
  With tbvAssociation
    .Columns.Count = 3
    .Columns[0].Title = ("Number") ' This column will NOT be shown, is for internal use
    .Columns[0].Width = 0 'This column will NOT be shown, is for internal use
    .Columns[0].Resizable = False ' This column will NOT be shown, is for internal use
    .Columns[1].Title = ("Group")
    .Columns[2].Title = ("Assigned Node(s)")
  End With

  ' Create/define the usercode table
  With tbvUserCode
    .Columns.Count = 4
    .Columns[0].Title = ("Number")
    .Columns[0].Width = 0 'This column will NOT be shown, is for internal use
    .Columns[0].Resizable = False ' This column will NOT be shown, is for internal use
    .Columns[1].Title = ("Code")
    .Columns[2].Title = ("Active")
    .Columns[3].Title = ("UserCode")
  End With

  ' Server will get the information locally, else we need to do a JSON-RPC call
  If Main.bServer Then
    ' fill the combo list and auto-enable the first instance
    ' First get the list of enabled instances
    $InstanceList = Plugin.GetPluginEnabledList("OpenZWave")

  Else
    $cControllerList = Client.ToolsOpenZWaveGet("list")

    ' Don't continue if we get an empty response
    If $cControllerList = Null Then
      Message.Error(("Received an empty or invalid response from the JSON-RPC server.\nPlease consult the server logfile for more information"))
      Try Me.Close
      Return
    Endif

    ' Check if we received the valid fields and handle errors
    If Not $cControllerList.Exist("instance_id") Then
      Message.Error(("Missing controller list in response from the JSON-RPC server.\nPlease consult the server logfile for more information"))
      Try Me.Close
      Return
    Endif

    If TypeOf($cControllerList["instance_id"]) <> gb.Object Then
      Message.Error(("Controller list is invalid in response from the JSON-RPC server.\nPlease consult the server logfile for more information"))
      Try Me.Close
      Return
    Endif
 
    $InstanceList = $cControllerList["instance_id"]
  Endif

  ' loop through the list and fill the combo list
  cmbList.Clear()
  For Each iInstance In $InstanceList
    cmbList.Add("instance #" & iInstance)
  Next

  ' Auto-enable the first instance
  cmbList.Index = 0

  ' Set controller tab as default
  TabStripZWaveCmdr.Index = 0

  ' Auto-hide UserCode and Wake-Up tab
  For jCnt = 0 To (TabStripZWaveCmdr.Count - 1)
    If TabStripZWaveCmdr[jCnt].Text = "User Code" Then
      TabStripZWaveCmdr[jCnt].Visible = False
    Endif

    If TabStripZWaveCmdr[jCnt].Text = "Wake-Up" Then
      TabStripZWaveCmdr[jCnt].Visible = False
    Endif
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' When clicked or change node
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvNodes_Change()

  EnableControls(tbvNodes.Row)
  UpdateTabStrip()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub TabStripZWaveCmdr_Click()

  UpdateTabStrip()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub EnableControls(iRow As Integer)

  Dim iCount As Integer

  iSelectedNode = tbvNodes[iRow, 0].Text

  ' Don't allow anything if the homeid is empty
  If $iHomeId = 0 Then
    Return
  Endif

  Select tbvNodes[iRow, 2].Text
    Case "Generic Controller", "Static Controller"
      btnNodeOn.Enabled = False
      btnNodeOff.Enabled = False
      slDim.Value = 0
      slDim.Enabled = False
      btnRequestBasicReport.Enabled = False
      btnRequestDynamicReport.Enabled = False
      btnRequestNodeNeighborUpdate.Enabled = True
      btnRefreshNodeInfo.Enabled = True
      btnRequestVersionReport.Enabled = False
      btnRequestMeterReport.Enabled = False
      spInstance.Enabled = False

    Case "Multilevel Switch"

      If Main.bServer Then
        Try slDim.Value = hOpenZWave.GetCurrentValueNode(iSelectedNode)
      Else
        ' todo
      Endif

      btnNodeOn.Enabled = True
      btnNodeOff.Enabled = True
      slDim.Enabled = True
      btnRequestBasicReport.Enabled = True
      btnRequestDynamicReport.Enabled = True
      btnRequestNodeNeighborUpdate.Enabled = True
      btnRefreshNodeInfo.Enabled = True
      btnRequestVersionReport.Enabled = True
      btnRequestMeterReport.Enabled = True
      spInstance.Enabled = True

    Case "Binary Switch"
      btnNodeOff.Enabled = True
      btnNodeOn.Enabled = True
      slDim.Value = 0
      slDim.Enabled = False
      btnRequestBasicReport.Enabled = True
      btnRequestDynamicReport.Enabled = True
      btnRequestNodeNeighborUpdate.Enabled = True
      btnRefreshNodeInfo.Enabled = True
      btnRequestVersionReport.Enabled = True
      btnRequestMeterReport.Enabled = True
      spInstance.Enabled = True

    Case "Semi Interoperable", "Non Interoperable"
      btnNodeOn.Enabled = False
      btnNodeOff.Enabled = False
      slDim.Value = 0
      slDim.Enabled = False
      btnRequestBasicReport.Enabled = False
      btnRequestDynamicReport.Enabled = False
      btnRequestNodeNeighborUpdate.Enabled = False
      btnRefreshNodeInfo.Enabled = False
      btnRequestVersionReport.Enabled = False
      btnRequestMeterReport.Enabled = False
      spInstance.Enabled = False

    Default
      btnNodeOn.Enabled = False
      btnNodeOff.Enabled = False
      slDim.Value = 0
      slDim.Enabled = False
      btnRequestBasicReport.Enabled = True
      btnRequestDynamicReport.Enabled = True
      btnRequestNodeNeighborUpdate.Enabled = True
      btnRefreshNodeInfo.Enabled = True
      btnRequestVersionReport.Enabled = True
      btnRequestMeterReport.Enabled = True
      spInstance.Enabled = False

  End Select

  btnRemoveFailedNode.Enabled = True
  btnReplaceFailedNode.Enabled = True

  ' Only show UserCode & Wake-Up tab if the commandclass is available
  For iCount = 0 To (TabStripZWaveCmdr.Count - 1)
    If TabStripZWaveCmdr[iCount].Text = "User Code" Then
      If InStr(tbvNodes[iRow, 11].Text, "USER_CODE") Then
        TabStripZWaveCmdr[iCount].Visible = True 
      Else
        TabStripZWaveCmdr[iCount].Visible = False
      Endif
    Endif

    If TabStripZWaveCmdr[iCount].Text = "Wake-Up" Then
      If InStr(tbvNodes[iRow, 11].Text, "WAKE_UP") Then
        TabStripZWaveCmdr[iCount].Visible = True 
      Else
        TabStripZWaveCmdr[iCount].Visible = False
      Endif
    Endif
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Get the nodeinfo collection if in client mode
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub GetNodeInfo(iNodeId As Integer) As Variant

  Dim cNodeInfo As Collection
  Dim iTemp As Integer

  ' Find nodeinfo in the $cNodeInfo list
  For Each cNodeInfo In $cControllerInfo["nodeinfo"]
    If cNodeInfo.Exist("node_id") Then
      Try iTemp = CInt(cNodeInfo["node_id"])
      If Not Error Then
        If iTemp = iNodeId Then Break
      Endif
    Endif
  Next

  ' Return null if nodeinfo can't be found
  If iTemp = iNodeId Then
    Return cNodeInfo
  Else
    ' shown error message
    Return Null
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Update Tab, controller, config, group, usercode, etc
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub UpdateTabStrip()

  Dim sElt As String[]
  Dim sStr As String
  Dim sLp As String
  Dim iGroup As Integer
  Dim iUserCode As Integer
  Dim iCnt As Integer
  Dim jCnt As Integer
  Dim bConfig As Boolean
  Dim bGroup As Boolean
  Dim bUserCode As Boolean
  Dim cNodeInfo As Collection
  Dim cConfig As Collection
  Dim cGroup As Collection
  Dim cUserCode As Collection
  Dim iNodeId As Integer

  ' We only update the Config/Association/UserCode/Wake-up if it is visible

  If iSelectedNode >= 1 Then

    ' Find the nodeinfo, if we are in client mode
    If Not Main.bServer Then
      cNodeInfo = GetNodeInfo(iSelectedNode)
      If cNodeInfo = Null Then Return
    Endif

    Select TabStripZWaveCmdr.Index
      Case 2 ' Configuration
        If iSelectedNode <> iConfigNode Then

          tbvConfig.Clear
          tbvConfig.Rows.Count = 0
          FrConfigEmptyInfo.Visible = False
          txtConfigHelp.Text = ""

          For jCnt = 2 To 3
            tbvConfig.Columns[jCnt].Width = tbvConfig.Font.TextWidth(tbvConfig.Columns[jCnt].Title) + tbvConfig.Font.Size
          Next

          bConfig = False

          If Main.bServer Then
            ' If we don't have a configuration list, change the tab with usefull information how to continue
            sStr = hOpenZWave.GetNodeConfigList(iSelectedNode)
            If sStr Then
              bConfig = True

              ' We got a list, fill the table now
              sElt = Split(sStr, "|")
              For Each sLp In sElt

                Inc tbvConfig.Rows.Count
                tbvConfig[tbvConfig.Rows.Count - 1, 0].Text = sLp
                tbvConfig[tbvConfig.Rows.Count - 1, 1].Text = hOpenZWave.GetNodeConfigValue(iSelectedNode, CInt(sLp))
                tbvConfig[tbvConfig.Rows.Count - 1, 2].Text = hOpenZWave.GetNodeConfigLabel(iSelectedNode, CInt(sLp))
                tbvConfig[tbvConfig.Rows.Count - 1, 3].Text = tbvConfig[tbvConfig.Rows.Count - 1, 1].Text
              Next

            Endif
          Else

            ' The config is received in the nodeinfo request
            If cNodeInfo.Exist("config") Then
              For Each cConfig In cNodeInfo["config"]
                bConfig = True

                Inc tbvConfig.Rows.Count
                tbvConfig[tbvConfig.Rows.Count - 1, 0].Text = cConfig["index"]
                tbvConfig[tbvConfig.Rows.Count - 1, 1].Text = cConfig["value"]
                tbvConfig[tbvConfig.Rows.Count - 1, 2].Text = cConfig["label"]
                tbvConfig[tbvConfig.Rows.Count - 1, 3].Text = tbvConfig[tbvConfig.Rows.Count - 1, 1].Text
              Next
            Endif

          Endif

          ' We don't have a config list, hide the table & save button
          '  Just show the old config stuff + warning
          tbvConfig.Visible = bConfig
          txtConfigHelp.Visible = bConfig
          frConfigHelp.Visible = bConfig
          btnConfigSave.Visible = bConfig
          lbConfigParam.Visible = Not bConfig
          lbConfigValue.Visible = Not bConfig
          lbConfigSize.Visible = Not bConfig
          vbConfigParam.Visible = Not bConfig
          vbConfigValue.Visible = Not bConfig
          cmbConfigSize.Visible = Not bConfig
          btnConfigSet.Visible = Not bConfig
          txtConfigInfo.Visible = Not bConfig
          frConfigInfo.Visible = Not bConfig

          ' Make columns widther if needed, skip column 0
          For iCnt = 0 To (tbvConfig.Rows.Count - 1)

            For jCnt = 2 To 3
              If (tbvConfig.Font.TextWidth(tbvConfig[iCnt, jCnt].Text) + tbvConfig.Font.Size) > tbvConfig.Columns[jCnt].Width Then
                tbvConfig.Columns[jCnt].Width = tbvConfig.Font.TextWidth(tbvConfig[iCnt, jCnt].Text) + tbvNodes.Font.Size
              Endif
            Next
          Next

          iConfigNode = iSelectedNode
        Endif
      Case 3 ' Association
        If iSelectedNode <> iAssocNode Then

          tbvAssociation.Clear
          tbvAssociation.Rows.Count = 0

          For jCnt = 1 To 2
            tbvAssociation.Columns[jCnt].Width = tbvAssociation.Font.TextWidth(tbvAssociation.Columns[jCnt].Title) + tbvAssociation.Font.Size
          Next

          bGroup = False

          If Main.bServer Then
            ' Retrieve the group count for this device
            iGroup = hOpenZWave.GetNodeGroupCount(iSelectedNode)
            If iGroup >= 1 Then

              ' We got a list, fill the table now
              bGroup = True
              For iCnt = 1 To iGroup
                Inc tbvAssociation.Rows.Count
                tbvAssociation[tbvAssociation.Rows.Count - 1, 0].Text = iCnt
                tbvAssociation[tbvAssociation.Rows.Count - 1, 1].Text = hOpenZWave.GetNodeGroupLabel(iSelectedNode, iCnt)
                tbvAssociation[tbvAssociation.Rows.Count - 1, 2].Text = hOpenZWave.GetNodeGroupList(iSelectedNode, iCnt)
              Next

            Endif
          Else

            ' The groups are received in the nodeinfo request
            If cNodeInfo.Exist("groups") Then
              For Each cGroup In cNodeInfo["groups"]
                bGroup = True

                Inc tbvAssociation.Rows.Count
                tbvAssociation[tbvAssociation.Rows.Count - 1, 0].Text = cGroup["count"]
                tbvAssociation[tbvAssociation.Rows.Count - 1, 1].Text = cGroup["label"]
                tbvAssociation[tbvAssociation.Rows.Count - 1, 2].Text = cGroup["list"]
              Next
            Endif
          Endif

          tbvAssociation.Visible = bGroup
          lbAssociationGroup.Visible = bGroup
          vbAssociationGroup.Visible = bGroup
          lbAssociationNode.Visible = bGroup
          vbAssociationNode.Visible = bGroup
          btnAddAssociation.Visible = bGroup
          btnRemoveAssociation.Visible = bGroup
          FrAssociationEmptyInfo.Visible = Not bGroup

          ' Make columns widther if needed, skip column 0
          For iCnt = 0 To (tbvAssociation.Rows.Count - 1)
            For jCnt = 1 To 2
              If (tbvAssociation.Font.TextWidth(tbvAssociation[iCnt, jCnt].Text) + tbvAssociation.Font.Size) > tbvAssociation.Columns[jCnt].Width Then
                tbvAssociation.Columns[jCnt].Width = tbvAssociation.Font.TextWidth(tbvAssociation[iCnt, jCnt].Text) + tbvAssociation.Font.Size
              Endif
            Next
          Next

          iAssocNode = iSelectedNode

        Endif

      Case 4 ' Wake-up
        If iSelectedNode <> iUserCodeNode Then

          If Main.bServer Then
            vbWakeUpInterval.Value = hOpenZWave.GetNodeWakeUpInterval(iSelectedNode)
          Else
            Try vbWakeUpInterval.Value = CInt(cNodeInfo["wakeup"])
          Endif

          If vbWakeUpInterval.Value = -1 Then
            vbWakeUpInterval.Enabled = False
            vbWakeUpInterval.Value = 0
          Else
            vbWakeUpInterval.Enabled = True
          Endif
        Endif

      Case 5 ' UserCode
        If iSelectedNode <> iUserCodeNode Then

          tbvUserCode.Clear
          tbvUserCode.Rows.Count = 0

          For jCnt = 1 To 3
            tbvUserCode.Columns[jCnt].Width = tbvUserCode.Font.TextWidth(tbvUserCode.Columns[jCnt].Title) + tbvUserCode.Font.Size
          Next

          bUserCode = False

          If Main.bServer Then
            ' Retrieve the group count for this device
            iUserCode = hOpenZWave.GetNodeUserCodeCount(iSelectedNode)
            If iUserCode >= 1 Then
  
              ' We got a list, fill the table now
              bUserCode = True
              For iCnt = 1 To iUserCode

                Inc tbvUserCode.Rows.Count
                tbvUserCode[tbvUserCode.Rows.Count - 1, 0].Text = iCnt
                tbvUserCode[tbvUserCode.Rows.Count - 1, 1].Text = hOpenZWave.GetNodeUserCodeLabel(iSelectedNode, iCnt)
                tbvUserCode[tbvUserCode.Rows.Count - 1, 3].Text = hOpenZWave.GetNodeUserCodeValue(iSelectedNode, iCnt)
                If (Left(tbvUserCode[tbvUserCode.Rows.Count - 1, 3].Text, 4)) = "0x00" Then
                  tbvUserCode[tbvUserCode.Rows.Count - 1, 2].Text = "Free"
                Else
                  tbvUserCode[tbvUserCode.Rows.Count - 1, 2].Text = "Enrolled"
                Endif
              Next

            Endif
          Else

            ' The usercodes are received in the nodeinfo request
            If cNodeInfo.Exist("usercode") Then
              For Each cUserCode In cNodeInfo["usercode"]
                bUserCode = True

                Inc tbvUserCode.Rows.Count
                tbvUserCode[tbvUserCode.Rows.Count - 1, 0].Text = cUserCode["usercode"]
                tbvUserCode[tbvUserCode.Rows.Count - 1, 1].Text = cUserCode["label"]
                tbvUserCode[tbvUserCode.Rows.Count - 1, 3].Text = cUserCode["value"]
                tbvUserCode[tbvUserCode.Rows.Count - 1, 2].Text = cUserCode["text"]
              Next
            Endif

          Endif

          tbvUserCode.Visible = bUserCode
          btnUserCodeEnroll.Visible = bUserCode
          btnUserCodeRemove.Visible = bUserCode
          btnUserCodeRemove.Enabled = Not bUserCode
          FrUserCodeEmptyInfo.Visible = Not bUserCode

          ' Make columns widther if needed, skip column 0
          For iCnt = 0 To (tbvUserCode.Rows.Count - 1)
            For jCnt = 1 To 3
              If (tbvUserCode.Font.TextWidth(tbvUserCode[iCnt, jCnt].Text) + tbvUserCode.Font.Size) > tbvUserCode.Columns[jCnt].Width Then
                tbvUserCode.Columns[jCnt].Width = tbvUserCode.Font.TextWidth(tbvUserCode[iCnt, jCnt].Text) + tbvUserCode.Font.Size
              Endif
            Next
          Next

          iUserCodeNode = iSelectedNode

        Endif
    End Select

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvConfig_Click()

  Dim sElt As String[]
  Dim Sb As String
  Dim sStr As String
  Dim cNodeInfo As Collection
  Dim cConfig As Collection

  ' Refresh the Help information
  txtConfigHelp.Text = ""
  sStr = ""

  If Main.bServer Then
    sElt = Split(hOpenZWave.GetNodeConfigHelp(iSelectedNode, CInt(tbvConfig[tbvConfig.Row, 0].Text)), " ")
  Else

    ' Find the nodeinfo, if we are in client mode
    If Not Main.bServer Then
      cNodeInfo = GetNodeInfo(iSelectedNode)
      If cNodeInfo = Null Then Return
    Endif

    If cNodeInfo.Exist("config") Then
      For Each cConfig In cNodeInfo["config"]
        If cConfig["index"] = CInt(tbvConfig[tbvConfig.Row, 0].Text) Then
          sElt = Split(cConfig["help"], " ")
        Endif
      Next
    Endif
 
  Endif

  For Each Sb In sElt
     If txtConfigHelp.Font.TextWidth(sStr & " " & Sb) < txtConfigHelp.Width Then
       If sStr = "" Then
         sStr = Sb
       Else
         sStr = sStr & " " & Sb
       Endif
     Else
       If txtConfigHelp.Text = "" Then
         txtConfigHelp.Text = sStr
         sStr = ""
       Else
         txtConfigHelp.Text = "\n" & sStr
         sStr = ""
       Endif
     Endif

  Next

  If sStr <> "" Then
    If txtConfigHelp.Text = "" Then
      txtConfigHelp.Text = sStr
    Else
      txtConfigHelp.Text &= "\n" & sStr
    Endif
  Endif

  sStr = ""
  sElt.Clear()

  ' Only edit the value column
  If tbvConfig.Column = 3 Then

    If Main.bServer Then
      ' Stop edit if the value is read-only
      If hOpenZWave.GetNodeConfigValueReadOnly(iSelectedNode, CInt(tbvConfig[tbvConfig.Row, 0].Text)) Then
        Return
      Endif

      If hOpenZWave.GetNodeConfigValueType(iSelectedNode, tbvConfig[tbvConfig.Row, 0].Text) = "List" Then
        sStr = hOpenZWave.GetNodeConfigValueList(iSelectedNode, CInt(tbvConfig[tbvConfig.Row, 0].Text))
        sElt = Split(sStr, "|")
        tbvConfig.Edit(sElt, True)
      Else
        ' We define a ValueBox, to only allow numeric values. The Z-Wave configuration has numeric values
        ' We also align to the left, else it is unreadable
        'vbValue = New ValueBox(tbvConfig) As "vbValue"
        'vbValue.Type = 0 ' Number
        'tbText = vbValue.Children[0]
        tbtext = New TextBox(tbvconfig) As "tbText"
        tbText.Alignment = Align.Left
        tbText.MaxLength = 10
        tbvConfig.EditWith(tbText)
        'tbvConfig.EditWith(vbValue)
        'tbvConfig.Edit
      Endif
    Else
    Endif

  Endif
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Change configuration value, not saved yet
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvConfig_Save(Row As Integer, Column As Integer, Value As String)

  ' Save in Table & sending to the node will only be done if pressing "Save"
  tbvConfig[Row, Column].Text = Value

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbtext_KeyPress()
  
  ' We only allow 0 .. 9 and none alphanumeric keys
  If Not ((Key.Code >= 48 And Key.Code <= 57) Or Key.Code < 0 Or Key.Code > 255) Then
    Stop Event
  Endif
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvConfig_Insert()

  ' We need to send a cancel, else at the last cell, it will stay in edit mode
  tbvConfig.Cancel

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Save the change configuration
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnConfigSave_Click()

  Dim jCnt As Integer
  Dim bChanged As Boolean
  Dim cData As New Collection

  ' Save made settings
  For jCnt = 0 To (tbvConfig.Rows.Count - 1)

    ' Check if the value really has changed, then we sync it to the device
    If tbvConfig[jCnt, 1].Text <> tbvConfig[jCnt, 3].Text Then

      If Main.bServer Then
        ' If it is a list type, we need to send a string - else a numeric value
        If hOpenZWave.GetNodeConfigValueType(iSelectedNode, CInt(tbvConfig[jCnt, 0].Text)) = "List" Then
          hOpenZWave.SetConfigParamList(iSelectedNode, CInt(tbvConfig[jCnt, 0].Text), tbvConfig[jCnt, 3].Text)
        Else
          hOpenZWave.SetConfigParam(iSelectedNode, CInt(tbvConfig[jCnt, 0].Text), CInt(tbvConfig[jCnt, 3].Text))
        Endif
      Else
        cData["command"] = "setconfig"
        cData["instance_id"] = $InstanceList[cmbList.Index]
        cData["node_id"] = iSelectedNode
        cData["index"] = CInt(tbvConfig[jCnt, 0].Text)
        cData["value"] = CInt(tbvConfig[jCnt, 3].Text)
        Client.ToolsOpenZWaveSet(cData)
      Endif

      ' Save the set value now
      tbvConfig[jCnt, 1].Text = tbvConfig[jCnt, 3].Text

      bChanged = True
    Endif

  Next

  ' Start timer for refresh if a value has changed
  If bChanged Then
    tRefreshTabInfo = New Timer As "tRefreshTabInfo"
    tRefreshTabInfo.Delay = 1000
    tRefreshTabInfo.Start
    iRefreshTabInfo = 1
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvUserCode_Click()

  ' If the sStr is empty, then no UserCode registered
  If (Left(tbvUserCode[tbvUserCode.Row, 3].Text, 4)) = "0x00" Then
    btnUserCodeRemove.Enabled = False
  Else
    btnUserCodeRemove.Enabled = True
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnUserCodeEnroll_Click()
  
  ' Send the command to open-zwave wrapper + start 30 sec timer
  ' If the button is pressed twice, the timer restarts
  hOpenZWave.SetNodeUserCodeStart(iSelectedNode)

  ' Start timer, we will cancel this command after 30 seconds
  Try tUserCodeEnrollMessage.Stop
  tUserCodeEnrollMessage = New Timer As "tUserCodeEnrollMessage"
  tUserCodeEnrollMessage.Delay = 30000
  tUserCodeEnrollMessage.Start
  txtMessage.Text = "Start enrollment mode, hold the tag in front of the device or enter code"

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tUserCodeEnrollMessage_Timer()

  hOpenZWave.SetNodeUserCodeStop()

  tUserCodeEnrollMessage.Stop
  txtMessage.Text = "30 seconds has expired, enrollment mode cancelled"

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Remove UserCode from the node
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnUserCodeRemove_Click()

  If (Left(tbvUserCode[tbvUserCode.Row, 3].Text, 4)) = "0x00" Then
    txtMessage.Text = "UserCode can't be removed, because it isn't enrolled"
  Else
    ' Send the Remove UserCode to the Z-Wave device
    txtMessage.Text = " Removal command send, it will be applied when the device wakes up"

    If Main.bServer Then
      hOpenZWave.SetNodeUserCodeRemove(iSelectedNode, CInt(tbvUserCode[tbvUserCode.Row, 0].Text))
    Else
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnClose_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Turn node/instance On
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnNodeOn_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "On command send to node " & iSelectedNode & ", instance " & spInstance.Value

    If Main.bServer Then
      hOpenZWave.SetValue(iSelectedNode, spInstance.Value, &HFF)
    Else
      cData["command"] = "setvalue"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      cData["index"] = spInstance.Value
      cData["value"] = &HFF
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Turn node/instance Off
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnNodeOff_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "Off command send to node " & iSelectedNode & ", instance " & spInstance.Value

    If Main.bServer Then
      hOpenZWave.SetValue(iSelectedNode, spInstance.Value, &H0)
    Else
      cData["command"] = "setvalue"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      cData["index"] = spInstance.Value
      cData["value"] = &H0
      Client.ToolsOpenZWaveSet(cData)
    Endif
  
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub slDim_MouseUp()

  Dim cData As New Collection

  If slDim.Enabled And iSelectedNode >= 1 Then
    txtMessage.Text = "Dim=" & slDim.Value & " request send to node " & iSelectedNode

    If Main.bServer Then
      hOpenZWave.SetValue(iSelectedNode, spInstance.Value, slDim.Value)
    Else
      cData["command"] = "setvalue"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      cData["index"] = spInstance.Value
      cData["value"] = slDim.Value
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request basic report from node
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestBasicReport_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "Requested basic report for node " & iSelectedNode

    If Main.bServer Then
      hOpenZWave.RequestBasicReport(iSelectedNode)
    Else
      cData["command"] = "basicreport"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request node capabilities
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestCapabilities_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "Requested capabilities for node " & iSelectedNode

    If Main.bServer Then
      hOpenZWave.RequestNodeCapabilities(iSelectedNode)
    Else
      cData["command"] = "nodecapability"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRefresh_Click()

  cmbList_Click()
  'FillNodeTable()
  'TabStripZWaveCmdr.Index = 0

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnCancelControllerCommand_Click()

  ' Try to stop the timer, if configured
  Try tControllerCommandMessage.Stop

  If hOpenZWave.CancelControllerCommand() Then
    txtMessage.Text = "Cancelled controller command"
  Else
    txtMessage.Text = "Controller was already in normal operation mode"
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Start inclusion of a device, the button needs to be pressed on the device to complete
' Will be automatic cancelled after 30 seconds
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnIncludeDevice_Click()

  Dim cData As New Collection

  If Main.bServer Then
    If hOpenZWave.IncludeDevice() Then
      txtMessage.Text = "Press button on device to add!"

      ' Start timer, we will cancel this command after 30 seconds
      tControllerCommandMessage = New Timer As "tControllerCommandMessage"
      tControllerCommandMessage.Delay = 30000
      tControllerCommandMessage.Start
    Else
      txtMessage.Text = "Controller already in add/remove mode, press cancel first"
    Endif
  Else
    cData["command"] = "includedevice"
    cData["instance_id"] = $InstanceList[cmbList.Index]
    Client.ToolsOpenZWaveSet(cData)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Start exclusion of a device, the button needs to be pressed on the device to complete
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnExcludeDevice_Click()

  Dim cData As New Collection

  If Main.bServer Then
    If hOpenZWave.ExcludeDevice() Then
      txtMessage.Text = "Press button on device to remove!"

      ' Start timer, we will cancel this command after 30 seconds
      tControllerCommandMessage = New Timer As "tControllerCommandMessage"
      tControllerCommandMessage.Delay = 30000
      tControllerCommandMessage.Start
    Else
      txtMessage.Text = "Controller already in add/remove mode, press cancel first"
    Endif
  Else
    cData["command"] = "excludedevice"
    cData["instance_id"] = $InstanceList[cmbList.Index]
    Client.ToolsOpenZWaveSet(cData)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request the node to update its neighborhood
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestNodeNeighborUpdate_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then

    If Main.bServer Then
      txtMessage.Text = "Requesting neighborhood update for node " & iSelectedNode
      hOpenZWave.RequestNodeNeighborUpdate(iSelectedNode)
    Else
      cData["command"] = "updateneighbor"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Add association/device to a group
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnAddAssociation_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    If vbAssociationGroup.Value >= 1 And vbAssociationGroup.Value <= 9 Then

      If vbAssociationNode.Value >= 1 And vbAssociationNode.Value <= 254 Then
        txtMessage.Text = "Adding association for node " & iSelectedNode & " - group " & vbAssociationGroup.Value & " and node " & vbAssociationNode.Value

        If Main.bServer Then
          hOpenZWave.AddAssociation(iSelectedNode, vbAssociationGroup.Value, vbAssociationNode.Value)
        Else
          cData["command"] = "addassociation"
          cData["instance_id"] = $InstanceList[cmbList.Index]
          cData["node_id"] = iSelectedNode
          cData["group"] = CInt(vbAssociationGroup.Value)
          cData["addnode"] = CInt(vbAssociationNode.Value)
          Client.ToolsOpenZWaveSet(cData)
        Endif

      Else
        txtMessage.Text = "ERROR: Requested to associate node is not between 1-254"
      Endif

    Else
      txtMessage.Text = "ERROR: Requested association group is not between 1-9"
    Endif
  Endif

  ' Start timer for refresh
  tRefreshTabInfo = New Timer As "tRefreshTabInfo"
  tRefreshTabInfo.Delay = 1000
  tRefreshTabInfo.Start
  iRefreshTabInfo = 1

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Remove association from group
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRemoveAssociation_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    If vbAssociationGroup.Value >= 1 And vbAssociationGroup.Value <= 9 Then

      If vbAssociationNode.Value >= 1 And vbAssociationNode.Value <= 254 Then
        txtMessage.Text = "Removing association for node " & iSelectedNode & " - group " & vbAssociationGroup.Value & " and node " & vbAssociationNode.Value

        If Main.bServer Then
          hOpenZWave.RemoveAssociation(iSelectedNode, vbAssociationGroup.Value, vbAssociationNode.Value)
        Else
          cData["command"] = "removeassociation"
          cData["instance_id"] = $InstanceList[cmbList.Index]
          cData["node_id"] = iSelectedNode
          cData["group"] = CInt(vbAssociationGroup.Value)
          cData["removenode"] = CInt(vbAssociationNode.Value)
          Client.ToolsOpenZWaveSet(cData)
        Endif

      Else
        txtMessage.Text = "ERROR: Requested to be removed associate node is not between 1-254"
      Endif

    Else
      txtMessage.Text = "ERROR: Requested association group is not between 1-9"
    Endif
  Endif

  ' Start timer for refresh
  tRefreshTabInfo = New Timer As "tRefreshTabInfo"
  tRefreshTabInfo.Delay = 1000
  tRefreshTabInfo.Start
  iRefreshTabInfo = 1

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tRefreshTabInfo_Timer()

  iAssocNode = 0
  iConfigNode = 0

  UpdateTabStrip()
  Me.Refresh()

  iRefreshTabInfo = iRefreshTabInfo + 1
  If iRefreshTabInfo >= 5 Then
    tRefreshTabInfo.Stop
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request Dynamic report (all values, including meter)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestDynamicReport_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then

    If Main.bServer Then
      txtMessage.Text = "Requesting dynamic update for node " & iSelectedNode
      hOpenZWave.RequestNodeDynamicReport(iSelectedNode)
    Else
      cData["command"] = "dynamicreport"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Refresh the node information, rediscover it
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRefreshNodeInfo_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then

    If Main.bServer Then
      txtMessage.Text = "Requesting RefreshInfo for node " & iSelectedNode
      hOpenZWave.RefreshNodeInfo(iSelectedNode)
    Else
      cData["command"] = "nodeinfo"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request version information, like library/application of the device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestVersionReport_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then

    If Main.bServer Then
      txtMessage.Text = "Requesting version report for node " & iSelectedNode
      hOpenZWave.RequestNodeVersionReport(iSelectedNode)
    Else
      cData["command"] = "versionreport"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Request meter e.g. kWh values from the device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestMeterReport_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then

    If Main.bServer Then
      txtMessage.Text = "Requesting meter report for node " & iSelectedNode
      hOpenZWave.RequestNodeMeterReport(iSelectedNode)
    Else
      cData["command"] = "meterreport"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Soft reset controller, data isn't lost
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSoftResetController_Click()

  Dim cData As New Collection

  If Message.Question("Are you sure that you want to SOFT Reset the Z-Wave controller?", (" Yes "), (" No ")) = 1 Then
    txtMessage.Text = "Controller is beeing Soft Reset (Reboot)"

    If Main.bServer Then
      hOpenZWave.ControllerSoftReset()
    Else
      cData["command"] = "softreset"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Hard reset controller, ALL data will be lost
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnHardResetController_Click()

  Dim cData As New Collection

  If Message.Question("Are you sure that you want to HARD Reset the Z-Wave controller?\n*** *ALL* the data will be erased from the controller ***", ("Yes"), ("No")) = 1 Then
    txtMessage.Text = "Controller is beeing HARD Reset (*ALL* data erased)"

    If Main.bServer Then
      hOpenZWave.ControllerHardReset()
    Else
      cData["command"] = "hardreset"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestBasicReportAll_Click()

  Dim cData As New Collection

  txtMessage.Text = "Requested basic report for all nodes"

  If Main.bServer Then
    hOpenZWave.RequestBasicReport(0)
  Else
      cData["command"] = "basicreport"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = 0
      Client.ToolsOpenZWaveSet(cData)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestDynamicReportAll_Click()

  Dim cData As New Collection

  txtMessage.Text = "Requested dynamic report for all nodes"

  If Main.bServer Then
    hOpenZWave.RequestNodeDynamicReport(0)
  Else
      cData["command"] = "dynamicreport"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = 0
      Client.ToolsOpenZWaveSet(cData)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tControllerCommandMessage_Timer()

  txtMessage.Text = "30 seconds have expired, cancelling controller command"
  tControllerCommandMessage.Stop

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Remove a failed node, it *needs* to be on the controller failed list, else it will not work
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRemoveFailedNode_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "Trying to remove failed node " & iSelectedNode

    If Main.bServer Then
      hOpenZWave.RemoveFailedNode(iSelectedNode)
    Else
      cData["command"] = "removenode"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Replace a failed node with the same device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnReplaceFailedNode_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "Trying to replace failed node " & iSelectedNode

    If Main.bServer Then
      hOpenZWave.ReplaceFailedNode(iSelectedNode)
    Else
      cData["command"] = "replacenode"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Heal the Z-Wave network, requesting a neighborhood update of all nodes
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRequestHealNetwork_Click()

  Dim cData As New Collection

  txtMessage.Text = "Healing the Z-Wave network"

  If Main.bServer Then
    hOpenZWave.HealNetwork()
  Else
    cData["command"] = "healnetwork"
    cData["instance_id"] = $InstanceList[cmbList.Index]
    Client.ToolsOpenZWaveSet(cData)
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Set/change config item
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnConfigSet_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then
    txtMessage.Text = "Setting config parameter " & vbConfigParam.Value & " for node " & iSelectedNode & " to " & vbConfigParam.Value

    If Main.bServer Then
      hOpenZWave.SetConfigParam(iSelectedNode, vbConfigParam.Value, vbConfigValue.Value, cmbConfigSize.Text)
    Else
      cData["command"] = "setvalue"
      cData["instance_id"] = $InstanceList[cmbList.Index]
      cData["node_id"] = iSelectedNode
      cData["index"] = vbConfigParam.Value
      cData["value"] = vbConfigValue.Value
      cData["size"] = CInt(cmbConfigSize.Text)
      Client.ToolsOpenZWaveSet(cData)
    Endif

  Endif


End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Set/change wakeup interval of the device, normally it takes a while before it is picked up
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSetWakeUpInterval_Click()

  Dim cData As New Collection

  If iSelectedNode >= 1 Then

    If vbWakeUpInterval.Value > 0 Then
      txtMessage.Text = "Setting Wake-Up interval " & vbWakeUpInterval.Value & " seconds for node " & iSelectedNode

      If Main.bServer Then
        hOpenZWave.SetNodeWakeUpInterval(iSelectedNode, vbWakeUpInterval.Value)
      Else
        cData["command"] = "wakeupinterval"
        cData["instance_id"] = $InstanceList[cmbList.Index]
        cData["node_id"] = iSelectedNode
        cData["value"] = CInt(vbWakeUpInterval.Value)
        Client.ToolsOpenZWaveSet(cData)
      Endif

    Else
      txtMessage.Text = "Wake-Up interval has to be 1 or greater"
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Change the OpenZWave instance (list)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbList_Click()

  Dim aNodes As New Integer[]
  Dim cNodeInfo As Object
  Dim iCnt As Integer
  Dim jCnt As Integer
  Dim bError As Boolean

  If Main.bServer Then
    Try hOpenZWave = Plugin.GetPluginObject("OpenZWave", $InstanceList[cmbList.Index]).Interface
    bError = Error
  Else
    $cControllerInfo = Client.ToolsOpenZWaveGet("controller", $InstanceList[cmbList.Index])

    ' Better error handling required
    If $cControllerInfo = Null Then
      bError = True
    Else
      bError = Not $cControllerInfo.Exist("instance_id")
    Endif
  Endif

  If bError Then
    txtStatus.Text = ("Not Connected")
    txtStatus.Foreground = &HFF0000
    btnShowError.Visible = True
    $Error = "OpenZWave Object Instance doesn't exist, please report this as a bug"

    'EnableDisableTX(False)
  Else

    'EnableDisableTX(hOpenZWave.IsRunning)
    If Main.bServer Then
      $bIsRunning = hOpenZWave.IsRunning
    Else
      Try $bIsRunning = $cControllerInfo["running"]
    Endif

    If $bIsRunning Then
      txtStatus.Text = ("Connected")
      txtStatus.Foreground = &H00FF00
      btnShowError.Visible = False
      $Error = ""
    Else
      txtStatus.Text = ("Not Connected")
      txtStatus.Foreground = &HFF0000
      btnShowError.Visible = True

      If Main.bServer Then
        $Error = hOpenZWave.ErrorText
      Else
        Try $Error = $cControllerInfo["errortext"]
      Endif

    Endif
  Endif

  ' Set On/Off/Slide off as default
  tbvNodes.Enabled = False
  btnNodeOn.Enabled = False
  btnNodeOff.Enabled = False
  slDim.Enabled = False

  ' Disable all config tab items
  tbvConfig.Visible = False
  FrConfigEmptyInfo.Visible = True
  txtConfigHelp.Visible = False
  frConfigHelp.Visible = False
  btnConfigSave.Visible = False
  lbConfigParam.Visible = False
  lbConfigValue.Visible = False
  lbConfigSize.Visible = False
  vbConfigParam.Visible = False
  vbConfigValue.Visible = False
  cmbConfigSize.Visible = False
  btnConfigSet.Visible = False
  txtConfigInfo.Visible = False
  frConfigInfo.Visible = False

  ' We don't have a group list, remove all buttons
  tbvAssociation.Visible = False
  lbAssociationGroup.Visible = False
  vbAssociationGroup.Visible = False
  lbAssociationNode.Visible = False
  vbAssociationNode.Visible = False
  btnAddAssociation.Visible = False
  btnRemoveAssociation.Visible = False
  FrAssociationEmptyInfo.Visible = True

  ' Disable UserCode buttons
  tbvUserCode.Visible = False
  btnUserCodeEnroll.Visible = False
  btnUserCodeRemove.Visible = False
  FrUserCodeEmptyInfo.Visible = True

  ' Disable all nodes, because we don't know the node YET
  btnRequestBasicReport.Enabled = False
  btnRequestDynamicReport.Enabled = False
  btnRequestNodeNeighborUpdate.Enabled = False
  btnRefreshNodeInfo.Enabled = False
  btnRequestVersionReport.Enabled = False
  btnRequestMeterReport.Enabled = False
  spInstance.Enabled = False
  btnRemoveFailedNode.Enabled = False
  btnReplaceFailedNode.Enabled = False

  If Main.bServer Then
    $iHomeId = hOpenZWave.GetHomeId()
  Else
    Try $iHomeId = $cControllerInfo["homeid"]
  Endif

  ' Disallow the controller commands if we don't have a homeid
  If $iHomeId = 0 Then
    txtHomeID.Text = "UNKNOWN"
    btnIncludeDevice.Enabled = False
    btnExcludeDevice.Enabled = False
    btnCancelControllerCommand.Enabled = False
    btnSoftResetController.Enabled = False
    btnHardResetController.Enabled = False
    btnRequestBasicReportAll.Enabled = False
    btnRequestDynamicReportAll.Enabled = False
    btnRequestHealNetwork.Enabled = False
  Else
    txtHomeID.Text = "0x" & Right(Hex($iHomeId), 8)
    btnIncludeDevice.Enabled = True
    btnExcludeDevice.Enabled = True
    btnCancelControllerCommand.Enabled = True
    btnSoftResetController.Enabled = True
    btnHardResetController.Enabled = True
    btnRequestBasicReportAll.Enabled = True
    btnRequestDynamicReportAll.Enabled = True
    btnRequestHealNetwork.Enabled = True
  Endif

  If Main.bServer Then
    txtControllerID.Text = hOpenZWave.GetControllerId()
    txtControllerType.text = hOpenZWave.GetControllerType()
    txtAPIVersion.Text = hOpenZWave.GetAPIVersion()
    txtSDKVersion.Text = hOpenZWave.GetSDKVersion()

    aNodes = hOpenZWave.GetNodeList()

    If aNodes.Count >= 1 Then
      tbvNodes.Enabled = True
      tbvNodes.Rows.Count = aNodes.Count
      For iCnt = 0 To (aNodes.Count - 1)
        cNodeInfo = hOpenZWave.GetNodeInfo(aNodes[iCnt])
        If cNodeInfo <> Null Then 
          tbvNodes[iCnt, 0].Text = cNodeInfo.NodeId
          tbvNodes[iCnt, 1].Text = hOpenZWave.BasicTypeName(cNodeInfo.Basic)
          tbvNodes[iCnt, 2].Text = hOpenZWave.GenericTypeName(cNodeInfo.Generic)
          tbvNodes[iCnt, 3].Text = cNodeInfo.Values["1"]
          tbvNodes[iCnt, 4].Text = cNodeInfo.Values["2"]
          tbvNodes[iCnt, 5].Text = cNodeInfo.Values["3"]
          tbvNodes[iCnt, 6].Text = cNodeInfo.Values["4"]
          tbvNodes[iCnt, 7].Text = IIf(cNodeInfo.Listening, "Listening", "Sleeping")
          tbvNodes[iCnt, 8].Text = hOpenZWave.GetNodeManufacturerName(cNodeInfo.NodeId)
          tbvNodes[iCnt, 9].Text = hOpenZWave.GetNodeProductName(cNodeInfo.NodeId)
          tbvNodes[iCnt, 10].Text = hOpenZWave.GetNodeNeighborsList(cNodeInfo.NodeId)
          tbvNodes[iCnt, 11].Text = hOpenZWave.GetNodeCommandClassList(cNodeInfo.NodeId)

          ' Make column widther if needed
          For jCnt = 0 To 11
            If (tbvNodes.Font.TextWidth(tbvNodes[iCnt, jCnt].Text) + tbvNodes.Font.Size) > tbvNodes.Columns[jCnt].Width Then
              tbvNodes.Columns[jCnt].Width = tbvNodes.Font.TextWidth(tbvNodes[iCnt, jCnt].Text) + tbvNodes.Font.Size
            Endif
          Next
        Endif

      Next
    Else
      tbvNodes.Enabled = False
      tbvNodes.Rows.Count = 0
    Endif

  Else
    txtControllerID.Text = $cControllerInfo["controllerid"]
    txtControllerType.text = $cControllerInfo["controllertype"]
    txtAPIVersion.Text = $cControllerInfo["apiversion"]
    txtSDKVersion.Text = $cControllerInfo["sdkversion"]

    If $cControllerInfo <> Null And If $cControllerInfo["count"] > 0 Then

      tbvNodes.Enabled = True
      tbvNodes.Rows.Count = $cControllerInfo["count"]
      iCnt = 0
      For Each cNodeInfo In $cControllerInfo["nodeinfo"]
        tbvNodes[iCnt, 0].Text = cNodeInfo["node_id"]
        tbvNodes[iCnt, 1].Text = cNodeInfo["basic"]
        tbvNodes[iCnt, 2].Text = cNodeInfo["generic"]
        'tbvNodes[iCnt, 3].Text = cNodeInfo.Values["1"]
        'tbvNodes[iCnt, 4].Text = cNodeInfo.Values["2"]
        'tbvNodes[iCnt, 5].Text = cNodeInfo.Values["3"]
        'tbvNodes[iCnt, 6].Text = cNodeInfo.Values["4"]
        tbvNodes[iCnt, 7].Text = IIf(cNodeInfo["listening"], "Listening", "Sleeping")
        tbvNodes[iCnt, 8].Text = cNodeInfo["manufacturername"]
        tbvNodes[iCnt, 9].Text = cNodeInfo["productname"]
        tbvNodes[iCnt, 10].Text = cNodeInfo["neighbors"]
        tbvNodes[iCnt, 11].Text = cNodeInfo["commandclass"]

          ' Make column widther if needed
        For jCnt = 0 To 11
          If (tbvNodes.Font.TextWidth(tbvNodes[iCnt, jCnt].Text) + tbvNodes.Font.Size) > tbvNodes.Columns[jCnt].Width Then
            tbvNodes.Columns[jCnt].Width = tbvNodes.Font.TextWidth(tbvNodes[iCnt, jCnt].Text) + tbvNodes.Font.Size
          Endif
        Next

        iCnt += 1
      Next

    Else
      tbvNodes.Enabled = False
      tbvNodes.Rows.Count = 0
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnShowError_Click()

  Message.Error($Error)

End
