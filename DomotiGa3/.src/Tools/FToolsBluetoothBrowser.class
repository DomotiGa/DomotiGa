' Gambas class file

' Description:
' FToolsBluetoothMon.class
' Bluetooth network browser.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private sOutput As String
Private pBluetooth As Process

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' tweak tableview
  With tbvBTDevices
    .Columns.Count = 3
    .Columns[0].Title = ("MAC Address")
    .Columns[0].Width = 130
    .Columns[1].Title = ("Device Name")
    .Columns[1].Width = 131
    .Columns[2].Title = ("RSSI")
    .Columns[2].Width = 50
  End With
  ScanDevices()

End

Public Sub btnClose_Click()

  Try pBluetooth.Kill

  Me.Close

End

Public Sub ScanDevices()

  Dim sDevice As String

  ' clear history
  sOutput = Null

  Try sDevice = Plugin.GetPluginObject("Bluetooth", 1).Settings["device"]
  If Error Then
    txtStatus.Text = ("Plugin error ...")
    Return
  Endif

  If sDevice Then
    ' run tool to find devices
    pBluetooth = Exec ["hcitool", "-i", sDevice, "scan"] For Read As "Bluetooth"
    txtStatus.Text = ("Scanning, please wait ...")
    btnRescan.Enabled = False
  Else
    txtStatus.Text = ("Device is empty")
  Endif

End

Public Sub Bluetooth_Read()

  Dim sLine As String

  sLine = Read #Last, -256
  sOutput &= sLine

End

Public Sub Bluetooth_Error(sString As String)

  Dim bDebug As Boolean

  txtStatus.Text = sString
  Try bDebug = Plugin.GetPluginObject("Bluetooth", 1).Settings["debug"]
  If bDebug Then Main.WriteDebugLog("[Bluetooth] " & sString)
  btnRescan.Enabled = True

End

Public Sub Bluetooth_Kill()

  Dim aText As String[]
  Dim aStr As String
  Dim iCount As Integer
  Dim bDebug As Boolean
  Dim hBluetooth As New CBluetooth

  aText = Split(sOutput, "\n")
  Try bDebug = Plugin.GetPluginObject("Bluetooth", 1).Settings["debug"]
  If bDebug Then Main.WriteDebugLog(sOutput)
  If aText.Count = 2 Then
    txtStatus.Text = ("No devices found ...")
  Else
    If aText.Count = 3 Then
      txtStatus.Text = ("Found a device ...")
    Else If aText.Count > 3 Then
      txtStatus.Text = ("Found devices ...")
    Endif
    For Each aStr In aText
      If InStr(LTrim$(aStr), ":") = 3 Then
        tbvBTDevices.Rows.Count = aText.Count - 2
        tbvBTDevices[iCount, 0].Text = LTrim$(Left$(aStr, 18))
        tbvBTDevices[iCount, 1].Text = LTrim$(Right$(aStr, -18))
        tbvBTDevices[iCount, 2].Text = hBluetooth.GetRSSI(LTrim$(Left$(aStr, 18)))
        Inc icount
      Endif
    Next
  Endif
  btnRescan.Enabled = True

End

Public Sub btnRescan_Click()

  ScanDevices()

End

Public Sub tbvBTDevices_DblClick()

  Clipboard.Copy(tbvBTDevices[tbvBTDevices.Row, 0].Text, "text/plain")

End