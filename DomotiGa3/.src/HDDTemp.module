' Gambas module file

' Description:
' HDDTemp.module
' Support for HDDTemp harddisk temperature monitoring.

' Development Status:
' Working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public tHDDTemp As Timer
Public hHDDTemp As Socket
Private dTimeHighTemp As Date

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Run()

  dTimeHighTemp = Date(1970, 1, 1, 0, 0, 0)
  ' start poll timer for HDDTemp
  tHDDTemp = New Timer As "tHDDTemp"
  tHDDTemp.Delay = Main.iHDDTempPollTime * 1000 ' multiply for seconds
  tHDDTemp.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tHDDTemp_Timer()

  CheckHDDTemp()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open socket and connect to hddtemp daemon
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CheckHDDTemp()

  hHDDTemp = New Socket As "hHDDTemp"
  hHDDTemp.Connect(Main.sHDDTempTCPHost, Main.iHDDTempTCPPort)

Catch
  Main.WriteLog(("HDDTemp Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' print debug info
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hHDDTemp_Ready()

  If Main.bHDDTempDebug Then Main.WriteLog(("[HDDTemp] Connected to socket ") & Main.sHDDTempTCPHost & ":" & Main.iHDDTempTCPPort)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read and parse response string from daemon
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hHDDTemp_Read()

  Dim sDisks, sFields As String[]
  Dim sBuffer, sDisk, sField, sTemp, sDevice As String
  Dim iCount, iDeviceId As Integer

  ' read string and close the connection
  sBuffer = Read #hHDDTemp, Lof(hHDDTemp)
  Try Close #hHDDTemp

  If Main.bHDDTempDebug Then Main.WriteDebugLog(("[HDDTemp] Got string: ") & sBuffer)

  ' first change string so disks can be separated
  sBuffer = "|" & sBuffer & "|"
  sBuffer = Replace$(sBuffer, "||", "!")
  sDisks = Split(sBuffer, "!")

  ' iterate over each disk
  For Each sDisk In sDisks
    sFields = Split(sDisk, "|")
    iCount = 0
    For Each sField In sFields
      If iCount = 0 Then sDevice = sField
      If iCount = 2 Then
        If sField <> "NA" Then
          Try sTemp = CFloat(sField)
          If Error Then Continue
          If Main.bHDDTempDebug Then Main.WriteDebugLog(("[HDDTemp] Device '") & sDevice & ("' has a temperature of ") & sTemp & "°C")
          ' find device in database
          iDeviceId = Devices.Find(sDevice, Devices.FindInterface("HDDTemp Socket"), "HDDTEMP")
          If iDeviceId Then Devices.ValueUpdate(iDeviceId, sTemp, "", "", "")
          ' send warning message per e-mail if threshold is reached
          ' limit the amount of emails to one per 30 minutes
          If (CFloat(sTemp) > Main.iHDDTempThreshold) And (DateDiff(dTimeHighTemp, Now(), gb.Minute) > 30) Then
            MailWarning(("The temperature of disk '") & sDevice & ("' is '") & sTemp & ("'°C/°F, so it's above the threshold of '") & Main.iHDDTempThreshold & "'°C/°F!")
            dTimeHighTemp = Now()
          Endif
          If (CFloat(sTemp) < (Main.iHDDTempThreshold - 0.5)) And (DateDiff(dTimeHighTemp, Now(), gb.Minute) < 100) Then
            ' mail OK temp for now
            MailWarning(("The temperature of disk '") & sDevice & ("' is '") & sTemp & ("'°C/°F, so it's normal, below the threshold of '") & Main.iHDDTempThreshold & "'°C/°F!")
            dTimeHighTemp = Date(1970, 1, 1, 0, 0, 0)
          Endif
        Else
          If Main.bHDDTempDebug Then Main.WriteDebugLog(("[HDDTemp] Device '") & sDevice & ("' doesn't support temp measurement!"))
        Endif
      Endif
      Inc iCount
    Next
  Next

Catch ' some errors
  Main.WriteLog(("HDDTemp Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' catch errors
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub hHDDTemp_Error()

  Dim sString As String = "HDDTemp: "

  ' handle errors
  Select Case hHDDTemp.Status
    Case Net.CannotCreateSocket
      Main.WriteLog(sString & ("The system does not allow to create a socket."))
    Case Net.HostNotFound
      Main.WriteLog(sString & ("Host '") & Main.sHDDTempTCPHost & ("' not found."))
    Case Net.ConnectionRefused
      Main.WriteLog(sString & ("Unable to connect. Connection refused."))
    Case Net.CannotRead
      Main.WriteLog(sString & ("Error reading data."))
    Case Net.CannotWrite
      Main.WriteLog(sString & ("Error writing data."))
  End Select

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' generate e-mail when temp is above threshold
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub MailWarning(sMessage As String)

  Mail.SendMail(Application.Name & ": HDDTemp", sMessage, Main.sEmailToAddress)

End
