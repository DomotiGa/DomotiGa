' Gambas module file

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Public var
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public DisplaySortStep As Integer = 10



'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Retrieve all favorites list
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetFavoritesList() As Result

  Dim rLocation As Result

  Try rLocation = Main.hDB.Exec("SELECT * FROM favorites order by displayorder")
  If Error Then
    Main.WriteDebugLog("[Devices] ERROR: '" & Error.Text & "' at " & Error.Where)
  Endif

  Return rLocation

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' create favorite with iId with the DisplayOrder
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub createFavorite(iObjectId As Integer, iObjectType As Integer) As Boolean

  Dim rResult As Result
  Dim rResult2 As Result
  Dim imaxdisplayorder As Integer

  rResult2 = main.hDB.exec("SELECT max(displayorder) as maxdisplayorder from favorites")
  
  If IsNull(rResult2!maxdisplayorder) Then
      Main.WriteDebugLog("[Favorites] DEBUG: maxdisplayorder = null")
      imaxdisplayorder = 0
  Else
      Main.WriteDebugLog("[Favorites] DEBUG: maxdisplayorder = " & rResult2!maxdisplayorder)
      imaxdisplayorder = rResult2!maxdisplayorder
  Endif

  'Note that the displayorder is always begin with at least DisplaySortStep (10)
  rResult = Main.hDB.Exec("INSERT INTO favorites (ObjectId, ObjectType, displayorder) VALUES(&1,&2,&3)", iObjectId, iObjectType, (imaxdisplayorder + DisplaySortStep))

  If rResult.Count Then
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Set the DisplayOrder
' iId: The Favorite Id
' iSteps: NUmber of steps the display must move up or down
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub updateFavoriteDisplayOrderByStep(iId As Integer, iSteps As Integer) As Boolean

  Dim rResultm As Result
  Dim rResultd As Result
  Dim rResultt As Result
  Dim imaxdisplayorder As Integer
  Dim icdisplayorder As Integer
  Dim iDisplaySteps As Integer
  Dim iTargetFavoriteId As Integer
  Dim iTargetFavoriteDisplayOrder As Integer
  Dim bSuccess As Boolean = False
  Dim iFrom As Integer
  Dim iTo As Integer
  Dim iiterator As Integer
   
  Main.WriteDebugLog("[Favorites] DEBUG: ----updateFavoriteDisplayOrderByStep----")

  rResultm = main.hDB.exec("SELECT max(displayorder) as maxdisplayorder from favorites")
  
  If IsNull(rResultm!maxdisplayorder) Then
      Main.WriteDebugLog("[Favorites] DEBUG: maxdisplayorder = null")
      imaxdisplayorder = 0
  Else
      Main.WriteDebugLog("[Favorites] DEBUG: maxdisplayorder = " & rResultm!maxdisplayorder)
      imaxdisplayorder = rResultm!maxdisplayorder
  Endif

  rResultd = main.hDB.exec("SELECT displayorder as displayorder from favorites where id = &1", iId)
  
  If IsNull(rResultd!displayorder) Then
      Main.WriteDebugLog("[Favorites] DEBUG: no favorites")
      icdisplayorder = 0
  Else
      Main.WriteDebugLog("[Favorites] DEBUG: displayorder = " & rResultd!displayorder)
      icdisplayorder = rResultd!displayorder
  Endif

  Select Case icdisplayorder 
    Case 0
      'Should not occur there is no favorite in the db, we can skip
      Return True
    Case DisplaySortStep
      'We are at the top of the list
      If iSteps < 0 Then
        'Should not be possible to move up if we are already at the top
        Return True
      Endif

    Case imaxdisplayorder
        'We are at the bottom of the list
        If iSteps > 0 Then
        'Should not be possible to move down if we are already at the bottom
          Return True
        Endif
    Case Else
  End Select    
  
  'We can safely update now
      
  iDisplaySteps = iSteps * DisplaySortStep
  Main.WriteDebugLog("[Favorites] DEBUG: DisplaySteps = " & iDisplaySteps)
  iTargetFavoriteDisplayOrder = icdisplayorder + iDisplaySteps
  Main.WriteDebugLog("[Favorites] DEBUG: TargetId DisplayOrder= " & iTargetFavoriteDisplayOrder)

  rResultt = main.hDB.exec("SELECT Id as Id, DisplayOrder as DisplayOrder from favorites where DisplayOrder = &1", iTargetFavoriteDisplayOrder)
 
  iTargetFavoriteId = rResultt!Id
  
  Main.WriteDebugLog("[Favorites] DEBUG: TargetId = " & iTargetFavoriteId)
  
  If Abs(iSteps) = 1 Then
    'Only support a step 1 (move down) or -1  (move up), a swap of the DisplayOrder can be executed
        
    If updateFavorite(iId, iTargetFavoriteDisplayOrder) = True Then
      Main.WriteDebugLog("[Favorites] DEBUG: iId = " & iId & " updated with dispayorder = " & iTargetFavoriteDisplayOrder)
      If updateFavorite(iTargetFavoriteId, icdisplayorder) = True Then
        Main.WriteDebugLog("[Favorites] DEBUG: iTargetFavoriteId = " & iTargetFavoriteId & " updated with dispayorder = " & icdisplayorder)
        bSuccess = True
      Endif
    Endif
  Else
    'Multiple steps are not supprted yet, e.g. in case of drag & drop
    'The alogrythm should query all rows between the targetFavoriteId and selected iId and update the displayorder for each rows
    bSuccess = False 
  Endif

  Return bSuccess

End


'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update favorite with iId with the DisplayOrder
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub updateFavorite(iId As Integer, iOrder As Integer) As Boolean

  Dim rResult As Result

  rResult = Main.hDB.Exec("UPDATE favorites SET displayorder = &1 WHERE id = &2", iOrder, iId)

'  If rResult.Count Then
    Return True
'  Else
'    Return False
'  Endif

End


'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' delete favorite with iId 
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub deleteFavorite(iId As Integer) As Boolean

  Dim rResult As Result
  Dim rResultd As Result
  Dim rResultu As Result
  Dim icdisplayorder As Integer
  Dim iiterator As Integer
  
  rResultd = main.hDB.exec("SELECT displayorder as displayorder from favorites where id = &1", iId)
  
  If IsNull(rResultd!displayorder) Then
    Main.WriteDebugLog("[Favorites] DEBUG: no favorites")
    icdisplayorder = 0
  Else
    Main.WriteDebugLog("[Favorites] DEBUG: displayorder = " & rResultd!displayorder)
    icdisplayorder = rResultd!displayorder
  Endif

  rResult = Main.hDB.Exec("DELETE FROM favorites WHERE id = &1", iId)
  
  rResultu = Main.hDB.Exec("select Id as Id FROM favorites WHERE displayorder > &1", icdisplayorder)

  iiterator = 0
  For Each rResultu
    updateFavorite(rResultu!Id, icdisplayorder + (iiterator * DisplaySortStep))
    iiterator = iiterator + 1
  Next

  Return True

End

