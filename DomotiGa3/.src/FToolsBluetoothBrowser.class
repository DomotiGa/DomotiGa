' Gambas class file

' Description:
' FToolsBluetoothMon.class
' Bluetooth network browser.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PRIVATE sOutput AS String

PUBLIC SUB Form_Open()

  ME.Move(FMain.X + 50, FMain.Y + 70)

  ' tweak tableview
  WITH tbvBTDevices
    .Columns.Count = 3
    .Columns[0].Title = ("MAC Address")
    .Columns[0].Width = 130
    .Columns[1].Title = ("Device Name")
    .Columns[1].Width = 131
    .Columns[2].Title = ("RSSI")
    .Columns[2].Width = 50
  END WITH
  ScanDevices()

END

PUBLIC SUB btnClose_Click()

  ME.Close

END

PUBLIC SUB ScanDevices()

  ' clear history
  sOutput = NULL

  ' run tool to find devices
  EXEC ["hcitool", "-i", Main.sBluetoothDevice, "scan"] FOR READ AS "Bluetooth"
  txtStatus.Text = ("Scanning, please wait ...")
  btnRescan.Enabled = FALSE

END

PUBLIC SUB Bluetooth_Read()

  DIM sLine AS String

  sLine = READ #LAST, -256
  sOutput &= sLine

END

PUBLIC SUB Bluetooth_Error(sString AS String)

  txtStatus.Text = sString
  IF Main.bBluetoothDebug THEN Main.WriteDebugLog("[Bluetooth] " & sString)
  btnRescan.Enabled = TRUE

END

PUBLIC SUB Bluetooth_Kill()

  DIM aText AS String[]
  DIM aStr AS String
  DIM iCount AS Integer

  aText = Split(sOutput, "\n")
  IF Main.bBluetoothDebug THEN Main.WriteDebugLog(sOutput)
  IF aText.Count = 2 THEN
    txtStatus.Text = ("No devices found ...")
  ELSE
    IF aText.Count = 3 THEN
      txtStatus.Text = ("Found a device ...")
    ELSE IF aText.Count > 3 THEN
      txtStatus.Text = ("Found devices ...")
    END IF
    FOR EACH aStr IN aText
      IF InStr(LTrim$(aStr), ":") = 3 THEN
        tbvBTDevices.Rows.Count = aText.Count - 2
        tbvBTDevices[iCount, 0].Text = LTrim$(Left$(aStr, 18))
        tbvBTDevices[iCount, 1].Text = LTrim$(Right$(aStr, -18))
        tbvBTDevices[iCount, 2].Text = Bluetooth.GetRSSI(LTrim$(Left$(aStr, 18)))
        INC icount
      END IF
    NEXT
  END IF
  btnRescan.Enabled = TRUE

END

PUBLIC SUB btnRescan_Click()

  ScanDevices()

END

PUBLIC SUB tbvBTDevices_DblClick()

  Clipboard.Copy(tbvBTDevices[tbvBTDevices.Row, 0].Text, "text/plain")

END
