' Gambas class file

' Description:
' FSettingsProwl.class
' Settings form for Prowl API support.

' Development Status:
' Development just started.

' Links:
' http://www.prowlapp.com/api.php

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  chkEnabled.Value = Main.bProwlEnabled
  txtApiKey.Text = Main.sProwlApiKey
  txtApplication.Text = Main.sProwlApplication
  txtEvent.Text = Main.sProwlEvent
  chkDebug.Value = Main.bProwlDebug

  chkDebug.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  txtApplication.Enabled = chkEnabled.Value
  txtEvent.Enabled = chkEnabled.Value
  btnTestApiKey.Enabled = chkEnabled.Value
  btnTestProwl.Enabled = chkEnabled.Value
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  Dim rResult As Result

  rResult = Main.GetSettingTable("prowl", True) ' get defaults
  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    chkDebug.Value = rResult!debug
    txtApiKey.Text = rResult!apikey
    txtApplication.Text = rResult!application
    txtEvent.Text = rResult!event
  End If
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  chkDebug.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  txtApplication.Enabled = chkEnabled.Value
  txtEvent.Enabled = chkEnabled.Value
  btnTestApiKey.Enabled = chkEnabled.Value
  btnTestProwl.Enabled = chkEnabled.Value
  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not txtApiKey.Text Then
    Balloon(("Please enter your Prowl API key"), txtApiKey)
    Return False
  End If

  If Not txtApplication.Text Then
    Balloon(("Please enter an application text"), txtApplication)
    Return False
  End If

  If Not txtEvent.Text Then
    Balloon(("Please enter an event text"), txtEvent)
    Return False
  End If
  
  Return True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  If Not ValidInput() Then Return

  '--- save new Prowl settings ---
  rResult = Main.hDB.Exec("UPDATE settings_prowl SET enabled = &1, debug = &2, apikey = &3, application = &4, event = &5  WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtApiKey.Text, txtApplication.Text, txtEvent.Text)
  rResult = Main.GetSettingTable("prowl") ' reload settings
  If rResult.Count Then
    Main.bProwlEnabled = rResult!enabled
    Main.bProwlDebug = rResult!debug
    Main.sProwlApiKey = rResult!apikey
    Main.sProwlApplication = rResult!application
    Main.sProwlEvent = rResult!event
  End If

  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.prowlapp.com/")
   
End

Public Sub btnTestApiKey_Click()
  
  Dim sResponse As String
  
  If txtApiKey.Text Then
    sResponse = Prowl.ValidateProwlApiKey(txtApiKey.Text)
    If Not sResponse Then
      Message.Info("Prowl API key is VALID")
    Else
      Message.Error("Prowl API key is INVALID\n" & sResponse)
    Endif
  Else
    Balloon(("Please enter your Prowl API key"), txtApiKey)
  End If
  
End

Public Sub btnTestProwl_Click()

  Dim sResponse As String

  If txtApiKey.Text Then
    ' If the sResponse is blank, all went well
    sResponse = Prowl.PostProwl(txtApiKey.Text, "DomotiGa", "Info", "Test message from DomotiGa :-)")
    If Not sResponse Then
      Message.Info("Prowl message successfully send")
    Else
      Message.Error("Failed to send Prowl message\n" & sResponse)
    Endif
  Else
    Balloon(("Please enter your Prowl API key"), txtApiKey)
  End If

End
