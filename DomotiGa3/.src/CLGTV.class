' Gambas class file

' Description:
' CLGTV.class
' Support for LG TV control.

' Development Status:
' Beta. Needs Testing.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PROPERTY TCPHost AS String
PROPERTY TCPPort AS Integer
PROPERTY LGTVDebug AS Boolean
PROPERTY Interface AS String
PROPERTY SerPort AS String
PROPERTY Baud AS String

PRIVATE sTCPHost AS String
PRIVATE iTCPPort AS Integer
PRIVATE bLGTVDebug AS Boolean
PRIVATE sInterface AS String
PRIVATE sSerPort AS String
PRIVATE sBaud AS String

PUBLIC hLGTV AS NEW Socket
PUBLIC hLGTVSer AS NEW SerialPort

PUBLIC sBuffer AS String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the tcp host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION ConnectTCP() AS Boolean

  ' try to close the connection
  TRY hLGTV.Close

  ' get a new one
  hLGTV = NEW Socket AS "LGTV"
  hLGTV.Connect(sTCPHost, iTCPPort)

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("LG TV receiver Error: ") & ERROR.Text)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION ConnectSerial() AS Boolean

  ' try to close the connection
  TRY hLGTVSer.Close

  ' get a new one
  hLGTVSer = NEW Serialport AS "LGTVSer"
  WITH hLGTVSer
    .PortName = sSerPort
    .Speed = sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("LG TV receiver Error: ") & ERROR.Text)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is connected
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB LGTV_Ready()

  Main.WriteLog(("LG TV receiver TCP socket connected."))

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' socket is closed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB LGTV_Closed()

  Main.WriteLog(("LG TV receiver TCP socket closed."))

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the tcp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  TRY hLGTV.Close
  TRY hLGTVSer.Close

  IF InStr(sInterface, "tcp") THEN
    Main.WriteLog(("LG TV receiver TCP socket close."))
  ELSE
    Main.WriteLog(("LG TV receiver serial port close."))
  ENDIF
  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog(("LG TV receiver Error: ") & ERROR.Text)
  RETURN FALSE

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' error while connected/connecting to host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB LGTV_Error()

  ' handle error
  SELECT CASE hLGTV.Status
    CASE Net.CannotCreateSocket
      Main.WriteLog(("LGTV: The system does not allow to create a socket."))
    CASE Net.HostNotFound
      Main.WriteLog(("LGTV: Host '") & sTCPHost & ("' not found."))
    CASE Net.ConnectionRefused
      Main.WriteLog(("LGTV: Unable to connect to receiver. Connection refused."))
    CASE Net.CannotRead
      Main.WriteLog(("LGTV: Error reading data."))
    CASE Net.CannotWrite
      Main.WriteLog(("LGTV: Error writing data."))
  END SELECT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send command via serial port or tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB SendCmd(sCmd AS String)

  DIM sStr AS String = sCmd & "\r"

  IF sInterface = "tcp" THEN
    TRY PRINT #hLGTV, sStr
    IF ERROR THEN Main.WriteDebugLog(("[LGTV] Error writing data to the TCP port! -> ") & Error.Text)
  ELSE
    TRY PRINT #hLGTVSer, sStr
    IF ERROR THEN Main.WriteDebugLog(("[LGTV] Error writing data to the serial port! -> ") & Error.Text)
  END IF
  IF bLGTVDebug THEN Main.WriteDebugLog("[LGTV] > " & sCmd)

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' to switch unit with device control
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB SendCommand(sAddress AS String, sValue AS String)

  ' ignore sAddress, only fixed ID 1
  SELECT CASE LCase(sValue)
    CASE "on"
      SendCmd("ka 01 01")
    CASE "off"
      SendCmd("ka 01 00")
    CASE ELSE
      IF Main.bLGTVDebug THEN Main.WriteDebugLog(("[LGTV] Unsupported command received: ") & sValue)
  END SELECT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' to control from xmlrpc or music page buttons
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB SetCommand(sCmd AS String, sValue AS String, sAddress AS String)

  ' ignore sAddress, only fixed ID 1
  SELECT CASE LCase(sCmd)
    CASE "power"
      ' 00 - Off, 01 - On
      SendCmd(IIf(sValue == "on", "ka 01 01", "ka 01 00"))
    CASE "volume"
      ' 0-100 (0-64 Hex)
      SendCmd("kf 01 " & Hex(Val(sValue)))
    CASE "mute"
      ' 00 - Off, 01 - On
      SendCmd(IIf(sValue == "on", "ke 01 01", "ke 01 00"))
    CASE "screenmute"
      ' 00 - Off, 01 - On
      SendCmd(IIf(sValue == "on", "kd 01 01", "kd 01 00"))
    CASE "input"
      SELECT CASE sValue
        CASE "dtv"
          SendCmd("kb 01 00")
        CASE "analog"
          SendCmd("kb 01 01")
        CASE "av1"
          SendCmd("kb 01 02")
        CASE "av2"
          SendCmd("kb 01 03")
        CASE "comp1"
          SendCmd("kb 01 04")
        CASE "comp2"
          SendCmd("kb 01 05")
        CASE "rgb", "pc"
          SendCmd("kb 01 07")
        CASE "hdmi1", "dvi"
          SendCmd("kb 01 08")
        CASE "hdmi2"
          SendCmd("kb 01 09")
      END SELECT
    CASE "aspect"
      SELECT CASE sValue
        CASE "4:3"
          SendCmd("kc 01 01")
        CASE "16:9"
          SendCmd("kc 01 02")
      END SELECT
    CASE "channel"
      SELECT CASE sValue
        CASE "back"
          SendCmd("mc 01 B2")
      END SELECT
    CASE "cli"
      SendCmd(sValue)
    CASE ELSE
      IF Main.bLGTVDebug THEN Main.WriteDebugLog(("[LGTV] Unsupported command received: ") & sCmd & " " & sValue)
  END SELECT

END

PUBLIC SUB GetCommand(sCmd AS String, sAddress AS String) AS String

  ' ' ignore sAddress, only fixed unit 1
  ' SELECT CASE LCase(sCmd)
  '   CASE "power"
  '     SendCmd("ka 01 FF")
  '   CASE ELSE
  '     IF Main.bLGTVDebug THEN Main.WriteDebugLog(("[LGTV] Unsupported get value received: ") & sCmd)
  ' END SELECT

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' data received on tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB LGTV_Read()

  DIM sData AS String

  TRY sData = READ #hLGTV, 1
  IF ERROR THEN
    Main.WriteDebugLog(("[LGTV] Error reading data from the TCP port! -> ") & Error.Text)
  ELSE
    IF sData = Chr$(10) THEN ' buffer until linefeed then parse
      IF Len(sBuffer) > 1 THEN ParseLine(sBuffer)
      sBuffer = NULL
    ELSE
      sBuffer &= sData
    ENDIF
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' data received on serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB LGTVSer_Read()

  DIM sData AS String

  TRY sData = READ #hLGTVSer, 1
  IF ERROR THEN
    Main.WriteDebugLog(("[LGTV] Error reading data from the serial port! -> ") & Error.Text)
  ELSE
    IF sData = Chr$(10) THEN ' buffer until linefeed then parse
      IF Len(sBuffer) > 1 THEN ParseLine(sBuffer)
      sBuffer = NULL
    ELSE
      sBuffer &= sData
    ENDIF
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse received data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB ParseLine(sBuffer AS String)

  DIM sStr, sCat, sItem, sValue AS String

  IF Main.bLGTVDebug THEN Main.WriteDebugLog(("[LGTV] < ") & sBuffer)

  ' a 00 OK01x
  ' a 00 NG01x
  IF InStr(sBuffer, "OK") THEN
    sCat = Left(sBuffer, 1)
    sStr = Mid(sBuffer, 8, 2)
    SELECT CASE sCat
      CASE "a" ' main power
        sItem = "Power"
        IF sStr = "01" THEN sValue = "On"
        IF sStr = "00" THEN sValue = "Off"
        LGTVSetValue(sItem, sValue)
    END SELECT
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' save received values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE SUB LGTVSetValue(sItem AS String, vValue AS Variant)

  Main.SetGlobalVar("LGTV_" & sItem, vValue)
  IF Main.bLGTVDebug THEN Main.WriteDebugLog("[LGTV] Received Item: " & sItem & " Value: " & vValue)

END

' implement properties
PRIVATE FUNCTION TCPHost_Read() AS String

  RETURN sTCPHost

END

PRIVATE SUB TCPHost_Write(Value AS String)

  sTCPHost = Value

END

PRIVATE FUNCTION TCPPort_Read() AS Integer

  RETURN iTCPPort

END

PRIVATE SUB TCPPort_Write(Value AS Integer)

  iTCPPort = Value

END

PRIVATE FUNCTION LGTVDebug_Read() AS Boolean

  RETURN bLGTVDebug

END

PRIVATE SUB LGTVDebug_Write(Value AS Boolean)

  bLGTVDebug = Value

END

PRIVATE FUNCTION Baud_Read() AS String

  RETURN sBaud

END

PRIVATE SUB Baud_Write(Value AS String)

  sBaud = Value

END

PRIVATE FUNCTION SerPort_Read() AS String

  RETURN sSerPort

END

PRIVATE SUB SerPort_Write(Value AS String)

  sSerPort = Value

END

PRIVATE FUNCTION Interface_Read() AS String

  RETURN sInterface

END

PRIVATE SUB Interface_Write(Value AS String)

  sInterface = Value

END
