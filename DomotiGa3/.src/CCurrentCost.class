' Gambas class file

' Description:
' CCurrentCost.class
' Support for Current Cost energy meters.

' Development Status:
' Beta. Needs Testing.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module was written by Renaud in 2010.

' Read file called COPYING for license details.

PROPERTY Port AS String
PROPERTY Baud AS String
PROPERTY CurrentCostDebug AS Boolean

PRIVATE sPort AS String
PRIVATE sBaud AS String
PRIVATE bCurrentCostDebug AS Boolean

PUBLIC hCurrentCost AS NEW SerialPort

PUBLIC sBuffer AS String
PUBLIC sAddress AS String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Connect() AS Boolean

  ' try to close the port
  TRY hCurrentCost.Close

  ' get a new one
  hCurrentCost = NEW Serialport AS "CurrentCost"

  WITH hCurrentCost
    .PortName = sPort
    .Speed = sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .FlowControl = 0
    .Open()
  END WITH

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog("CurrentCost Error: " & ERROR.Text)
  RETURN FALSE

END

PUBLIC SUB CurrentCost_Read()

  DIM sData AS String

  TRY READ #hCurrentCost, sData, 1
  IF ERROR THEN Main.WriteDebugLog(("[CurrentCost] Error reading data from serial port! -> ") & Error.Text)
  IF sData = Chr(10) OR IF sData = Chr(13) THEN ' buffer until newline or cr then parse
    IF Right(sBuffer, 6) = "</msg>" THEN ParseMessage(sBuffer)
    sBuffer = NULL
  ELSE
    sBuffer &= sData
  END IF
END

PUBLIC SUB ParseMessage(sStr AS String)

  DIM iDeviceId AS Integer
  DIM aScan AS String[]
  DIM sValue, sMaxValue AS String

  IF bCurrentCostDebug THEN Main.WriteDebugLog("[CurrentCost] " & sStr)

  ' scan message
  aScan = Scan(sStr, "*<id>*</id>*<watts>*</watts>*")
  ' scan again with different pattern
  IF aScan.Count <> 5 THEN aScan = Scan(sStr, "*<sensor>*</sensor>*<watts>*</watts>*")
  ' here we have instantaneous data logging
  IF aScan.Count = 5 THEN
    sValue = aScan[3]
    sAddress = aScan[1]
    ' try to find device with address, and correct interface type.
    iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Current Cost Receiver"), "CCOST")
    ' if found then update it's value
    IF iDeviceId THEN
      Devices.ValueUpdate(iDeviceId, "", sValue, "", "")
      sMaxValue = Devices.GetCurrentValueForDevice(iDeviceId, 3)
      IF sValue > sMaxValue THEN
        Main.WriteLog(("[Current Cost] Max electrical consumption reached ") & sValue & (" Watts"))
        Devices.ValueUpdate(iDeviceId, "", "", sValue, "")
      END IF
    END IF
  END IF

  aScan = Scan(sStr, "*<h004>*</h004>*")
  ' here we have real time data
  IF aScan.Count = 3 THEN
    sValue = aScan[1]
    ' try to find device with address, and correct interface type.
    iDeviceId = Devices.Find(sAddress, Devices.FindInterface("Current Cost Receiver"), "CCOST")
    ' if found then update it's value
    IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, sValue, "", "", "")
  END IF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' try to close the connection
  TRY hCurrentCost.Close
  Main.WriteLog("CurrentCost serial port close.")

  ' all ok
  RETURN TRUE

CATCH ' some errors
  Main.WriteLog("CurrentCost Error: " & ERROR.Text)
  RETURN FALSE

END

' implement properties
FUNCTION Port_Read() AS String

  RETURN sPort

END

SUB Port_Write(Value AS String)

  sPort = Value

END

PRIVATE FUNCTION Baud_Read() AS String

  RETURN sBaud

END

PRIVATE SUB Baud_Write(Value AS String)

  sBaud = Value

END

PRIVATE FUNCTION CurrentCostDebug_Read() AS Boolean

  RETURN bCurrentCostDebug

END

PRIVATE SUB CurrentCostDebug_Write(Value AS Boolean)

  bCurrentCostDebug = Value

END
