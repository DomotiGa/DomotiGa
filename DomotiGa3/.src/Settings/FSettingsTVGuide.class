' Gambas class file

' Description:
' FSettingsTVGuide.class
' Settings form for XML TV Guide support.

' Development Status:
' Just started.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  ' save new TVGuide settings
  rResult = Main.hDB.Exec("UPDATE settings_tvguide SET debug = &1, xmlgrabcmd = &2, xmlfile = &3, enabled = &4 WHERE id = 1", chkDebug.Value, txtXMLGrabCommand.Text, txtXMLFile.Text, chkEnabled.Value)

  If Main.bServer Then
    Plugin.Restart("TVGuide", 1)
  Else
    Client.ModuleRestart("TVGuide")
  Endif

  Me.Close

End

Public Sub btnImportGuide_Click()

  If Message.Question(("It can take a few minutes before the download and processing is completed, continue ?"), ("Yes"), ("No")) Then
    Try Plugin.GetPluginObject("TVGuide").Interface.ImportGuide()
    If Error Then
      Main.WriteLog("[TVGuide] Unable to Import Guide: " & Error.Text)
    Endif
  Endif

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub txtXMLGrabCommand_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtXMLFile_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_tvguide", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtXMLGrabCommand.Text = rResult!xmlgrabcmd
    txtXMLFile.Text = rResult!xmlfile
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtXMLGrabCommand.Enabled = chkEnabled.Value
  txtXMLFile.Enabled = chkEnabled.Value
  btnImportGuide.Enabled = chkEnabled.Value

End

Public Sub btnSelectXML_Click()

  Dim sFile As String = FSelectFile.Run(txtXMLFile.Text)

  If Len(sFile) Then txtXMLFile.Text = sFile
  btnSave.Enabled = True

End
