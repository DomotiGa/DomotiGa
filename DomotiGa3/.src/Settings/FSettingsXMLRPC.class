' Gambas class file

' Description:
' FSettingsXMLRPC.class
' Settings form for XMLRPC server support.

' Development Status:
' Just started, needs Gambas patch (RpcServer.class)

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnSave_Click()

  Dim rResult As Result

  ' save new XMLRPC settings
  rResult = Main.hDB.Exec("UPDATE settings_xmlrpc SET debug = &1, maxconn = &2, httpport = &3, broadcastudp = &4, enabled = &5 WHERE id = 1", chkDebug.Value, txtMaxConn.Text, txtHTTPPort.Text, chkBroadcastUDP.Value, chkEnabled.Value)

  rResult = Main.GetSettingTable("xmlrpc") ' reload settings
  If rResult.Count Then
    Main.bXMLRPCEnabled = rResult!enabled
    Main.iXMLRPCMaxConn = rResult!maxconn
    Main.iXMLRPCHTTPPort = rResult!httpport
    Main.bXMLRPCDebug = rResult!debug
    Main.bBroadcastUDPEnabled = rResult!broadcastudp
  Endif

  If Main.bServer Then
    Main.Restart_XMLRPC()
  Else
    If Main.bXMLRPCEnabled Then
      Message.Info(("You have to restart the DomotiGa Server manually."))
    Else
      Message.Info(("Warning, you have disabled XMLRPC on server, so you cannot connect with client anymore!"))
    Endif
  Endif

  Me.Close

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkBroadcastUDP_Click()

  txtBroadcastUDPPort.Enabled = chkBroadcastUDP.Value
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub txtMaxConn_KeyPress()

  btnSave.Enabled = True

End

Public Sub HTTPPort_Change()

  Try txtBroadcastUDPPort.Text = txtHTTPPort.Text + 10000
  btnSave.Enabled = True

End

Public Sub txtHTTPPort_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_xmlrpc", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtMaxConn.text = rResult!maxconn
    txtHTTPPort.text = rResult!httpport
    chkBroadcastUDP.Value = rResult!broadcastudp
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtMaxConn.Enabled = chkEnabled.Value
  txtHTTPPort.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  chkBroadcastUDP.Enabled = chkEnabled.Value

End

