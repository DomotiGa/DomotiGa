' Gambas class file

' Description:
' FSettingsGUI.class
' Settings form for GUI related items.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private bRestartClicked As Boolean

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  txtLogBuffer.Text = Main.iLogBuffer
  chkAuth.Value = Main.bAuthEnabled
  cmbStartPage.Text = Main.sStartpage

  ' JSON-RPC/XML-RPC
  txtJSONRPCHost.Text = Main.sJSONRPCHost
  vbJSONRPCPort.Text = Main.iJSONRPCPort
  txtXMLRPCHost.Text = Main.sXMLRPCHost
  vbXMLRPCPort.Text = Main.iXMLRPCPort

  Select Main.sRPCType
    Case "JSONRPC"
      cmbRPCType.Text = "JSON-RPC"
    Case "XMLRPC"
      cmbRPCType.Text = "XML-RPC"
    Default
      cmbRPCType.Text = "XML-RPC"
  End Select

  txtGraphsUrl.Text = Main.sGraphsUrl

  If Main.bServerCurrent Then
    cmbProgramMode.Text = ("Server")
  Else
    cmbProgramMode.Text = ("Client")
  Endif

  EnableRPCFields()

  Select Case Main.sLanguage
    Case "en_US.UTF-8"
      cmbLanguage.Text = ("English")
      pbLanguage.Picture = Picture["icons/en.png"]
    Case "nl_NL.UTF-8"
      cmbLanguage.Text = ("Dutch")
      pbLanguage.Picture = Picture["icons/nl.png"]
    Case "fr_FR.UTF-8"
      cmbLanguage.Text = ("French")
      pbLanguage.Picture = Picture["icons/fr.png"]
    Case "de_DE.UTF-8"
      cmbLanguage.Text = ("German")
      pbLanguage.Picture = Picture["icons/de.png"]
    Case "ru_RU.UTF-8"
      cmbLanguage.Text = ("Russian")
      pbLanguage.Picture = Picture["icons/ru.png"]
    Case "it_IT.UTF-8"
      cmbLanguage.Text = ("Italiano")
      pbLanguage.Picture = Picture["icons/it.png"]
    Case "nb_NO.UTF-8"
      cmbLanguage.Text = ("Norwegian")
      pbLanguage.Picture = Picture["icons/no.png"]
  End Select

  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnDefaults_Click()

  Dim rResult As Result

  rResult = Main.GetSettingTable("main", True) ' get defaults
  If rResult.Count Then
    txtLogBuffer.text = rResult!logbuffer
    chkAuth.Value = rResult!authentication
    cmbStartPage.Text = rResult!startpage
  Endif

  btnSave.Enabled = True

End

Public Sub cmbStartPage_Click()

  btnSave.Enabled = True

End

Public Sub EnableRPCFields()

  ' Index=0 - JSON-RPC, Index=1 - XML-RPC
  TextLblJSONRPCHost.Visible = cmbRPCType.Index = 0
  TextLblJSONRPCPort.Visible = cmbRPCType.Index = 0
  TextLblXMLRPCHost.Visible = cmbRPCType.Index = 1
  TextLblXMLRPCPort.Visible = cmbRPCType.Index = 1
  txtJSONRPCHost.Visible = cmbRPCType.Index = 0
  vbJSONRPCPort.Visible = cmbRPCType.Index = 0
  txtXMLRPCHost.Visible = cmbRPCType.Index = 1
  vbXMLRPCPort.Visible = cmbRPCType.Index = 1

  ' Index=0 - Server Mode
  cmbRPCType.Enabled = cmbProgramMode.Index <> 0
  txtXMLRPCHost.Enabled = cmbProgramMode.Index <> 0
  vbXMLRPCPort.Enabled = cmbProgramMode.Index <> 0
  txtJSONRPCHost.Enabled = cmbProgramMode.Index <> 0
  vbJSONRPCPort.Enabled = cmbProgramMode.Index <> 0

End

Public Sub cmbProgramMode_Click()

  EnableRPCFields()
  bRestartClicked = True
  btnSave.Enabled = True

End

Public Sub chkAuth_Click()

  btnSave.Enabled = True

End

Public Sub txtLogBuffer_KeyPress()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  ' save new settings
  rResult = Main.hDB.Exec("UPDATE settings_main SET logbuffer = &1, authentication = &2, startpage = &3 WHERE id = 1", txtLogBuffer.Text, chkAuth.Value, cmbStartPage.Text)
  Select Case cmbLanguage.Index
    Case 0
      Main.sLanguage = "en_US.UTF-8"
      pbLanguage.Picture = Picture["icons/en.png"]
    Case 1
      Main.sLanguage = "nl_NL.UTF-8"
      pbLanguage.Picture = Picture["icons/nl.png"]
    Case 2
      Main.sLanguage = "fr_FR.UTF-8"
      pbLanguage.Picture = Picture["icons/fr.png"]
    Case 3
      Main.sLanguage = "de_DE.UTF-8"
      pbLanguage.Picture = Picture["icons/de.png"]
    Case 4
      Main.sLanguage = "ru_RU.UTF-8"
      pbLanguage.Picture = Picture["icons/ru.png"]
    Case 5
      Main.sLanguage = "it_IT.UTF-8"
      pbLanguage.Picture = Picture["icons/it.png"]
    Case 6
      Main.sLanguage = "nb_NO.UTF-8"
      pbLanguage.Picture = Picture["icons/no.png"]
  End Select

  Select cmbProgramMode.index
    Case 0
      Main.bServerCurrent = True
    Case 1
      Main.bServerCurrent = False
  End Select

  Select cmbRPCType.Index
    Case 0
      Main.sRPCType = "JSONRPC"
    Case 1
      Main.sRPCType = "XMLRPC"
  End Select

  Main.sJSONRPCHost = txtJSONRPCHost.Text
  Main.iJSONRPCPort = vbJSONRPCPort.Text
  Main.sXMLRPCHost = txtXMLRPCHost.Text
  Main.iXMLRPCPort = vbXMLRPCPort.Text
  Main.sGraphsUrl = txtGraphsUrl.Text

  Main.SetConfig()

  rResult = Main.GetSettingTable("main") ' reload settings
  Main.iLogBuffer = rResult!logbuffer
  Main.bAuthEnabled = rResult!authentication
  Main.sStartPage = rResult!startpage
  Main.GetConfig()

  If bRestartClicked Then Message.Info(("You need to restart DomotiGa to activate changes."))

  Me.Close

End

Public Sub cmbLanguage_Click()

  btnSave.Enabled = True
  Select Case cmbLanguage.Index
    Case 0
      pbLanguage.Picture = Picture["icons/en.png"]
    Case 1
      pbLanguage.Picture = Picture["icons/nl.png"]
    Case 2
      pbLanguage.Picture = Picture["icons/fr.png"]
    Case 3
      pbLanguage.Picture = Picture["icons/de.png"]
    Case 4
      pbLanguage.Picture = Picture["icons/ru.png"]
    Case 5
      pbLanguage.Picture = Picture["icons/it.png"]
    Case 6
      pbLanguage.Picture = Picture["icons/no.png"]
  End Select
  bRestartClicked = True

End

Public Sub txtJSONRPCHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub vbJSONRPCPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtXMLRPCHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub vbXMLRPCPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtGraphsUrl_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbRPCType_Click()

  EnableRPCFields()
  btnSave.Enabled = True

End
