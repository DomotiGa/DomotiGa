' Gambas class file

' Description:
' FSettingsMQTT.class
' Settings form for MQTT support.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private $iSSLCertificate As Integer
Private $cSSLCertificate As New Collection

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

  FillSSLCertificates()

End

Public Sub FillSSLCertificates()

  Dim rResult As Result
  Dim iIndex As Integer

  $cSSLCertificate.Clear()
  cmbSSLCertificate.Clear()

  ' Add an empty/dummy entry - we can disable the SSL client certificate
  cmbSSLCertificate.Add("")

  rResult = Main.hDB.Exec("SELECT * FROM sslcertificates ORDER BY name")
  If rResult And If rResult.Available And If rResult.Count >= 1 Then

    iIndex = 0

    For Each rResult
      $cSSLCertificate.Add(rResult!id, rResult!name)
      cmbSSLCertificate.Add(rResult!name)

      If $iSSLCertificate = rResult!id Then
        ' Don't raise the Click event, so lock and unlock
        Object.Lock(cmbSSLCertificate)
        cmbSSLCertificate.Index = iIndex + 1
        Object.Unlock(cmbSSLCertificate)
      Endif

      iIndex += 1
    Next
  Endif

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub txtPubTopic_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtSubTopic_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtHeartbeat_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtUsername_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtPassword_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtTCPHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtTCPPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkRetain_Click()

  btnSave.Enabled = True

End

Public Sub spQoS_Change()

  btnSave.Enabled = True

End

Public Sub chkSSLEnabled_Click()

  btnSave.Enabled = True
  cmbSSLCertificate.Enabled = chkSSLEnabled.Value

  If chkSSLEnabled.Value Then
    If txtTCPPort.Text = "1883" Then
      txtTCPPort.Text = "8883"
    Endif
  Else
    If txtTCPPort.Text = "8883" Then
      txtTCPPort.Text = "1883"
    Endif
  Endif

End

Public Sub cmbSSLCertificate_Click()

  btnSave.Enabled = True

End

Public Sub chkBirthLastWill_Click()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  If $cSSLCertificate.Exist(cmbSSLCertificate.Current.Text) Then
    $iSSLCertificate = $cSSLCertificate[cmbSSLCertificate.Current.Text]
  Else
    $iSSLCertificate = 0
  Endif

  ' save new MQTT settings
  rResult = Main.hDB.Exec("UPDATE settings_mqtt SET debug = &1, tcphost = &2, tcpport = &3, username = &4, password = &5, heartbeat = &6, pubtopic = &7, subtopic = &8, enabled = &9, retain = &{10}, qos = &{11} , clientname = &{12}, enablepublish = &{13}, enablesubscribe = &{14}, sslenabled = &{15}, sslcertificate_id = &{16}, birthlastwill = &{17} WHERE id = 1", chkDebug.Value, txtTCPHost.Text, txtTCPPort.Text, txtUsername.Text, txtPassword.Text, txtHeartbeat.Text, txtPubTopic.Text, txtSubTopic.Text, chkEnabled.Value, chkRetain.Value, spQoS.Value, txtClientname.Text, chkEnablePublish.Value, chkEnableSubscribe.Value, chkSSLEnabled.Value, $iSSLCertificate, chkBirthLastWill.Value)

  If Main.bServer Then
    Plugin.Restart("MQTT", 1)
  Else
    Client.Plugin_Restart("MQTT")
  Endif

  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_mqtt", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtTCPHost.Text = rResult!tcphost
    txtTCPPort.Text = rResult!tcpport
    txtClientname.Text = rResult!clientname
    txtUsername.Text = rResult!username
    txtPassword.Text = rResult!password
    txtHeartbeat.Text = rResult!heartbeat
    txtPubTopic.Text = rResult!pubtopic
    txtSubTopic.Text = rResult!subtopic
    chkEnablePublish.Value = rResult!enablepublish
    chkEnableSubscribe.Value = rResult!enablesubscribe
    chkRetain.Value = rResult!retain
    spQoS.Value = rResult!qos
    chkSSLEnabled.Value = rResult!sslenabled
    $iSSLCertificate = rResult!sslcertificate_id
    chkBirthLastWill.Value = rResult!birthlastwill
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtTCPHost.Enabled = chkEnabled.Value
  txtTCPPort.Enabled = chkEnabled.Value
  txtClientname.Enabled = chkEnabled.Value
  txtUsername.Enabled = chkEnabled.Value
  txtPassword.Enabled = chkEnabled.Value
  txtHeartbeat.Enabled = chkEnabled.Value
  txtPubTopic.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  chkRetain.Enabled = chkEnabled.Value
  spQoS.Enabled = chkEnabled.Value
  chkEnablePublish.Enabled = chkEnabled.Value
  chkEnableSubscribe.Enabled = chkEnabled.Value
  txtPubTopic.Enabled = chkEnabled.Value And chkEnablePublish.Value
  txtSubTopic.Enabled = chkEnabled.Value And chkEnableSubscribe.Value
  chkSSLEnabled.Enabled = chkEnabled.Value
  chkBirthLastWill.Enabled = chkEnabled.Value

  If chkEnabled.Value Then
    cmbSSLCertificate.Enabled = chkSSLEnabled.Value
  Else
    cmbSSLCertificate.Enabled = False
  Endif

End

Public Sub chkEnablePublish_Click()

  txtPubTopic.Enabled = chkEnablePublish.Value
  btnSave.Enabled = True

End

Public Sub chkEnableSubscribe_Click()

  txtSubTopic.Enabled = chkEnableSubscribe.Value
  btnSave.Enabled = True

End
