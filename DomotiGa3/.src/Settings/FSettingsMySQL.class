' Gambas class file

' Description:
' FSettingsMySQL.class
' Settings form for database connection.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private hDBTest As New Connection

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  With Main
    txtDBHost.Text = .sSQLHost
    txtUser.Text = .sSQLUser
    txtPassword.Text = .sSQLPass
    txtDatabase.Text = .sSQLDatabase
    chkDebug.Value = .bSQLDebug
  End With
  btnSave.Enabled = False

End

Public Sub btnSave_Click()

  CheckDb.Text = "Checking Database connection ..."
  Me.Refresh
  Wait 0.01
  
  ' We should check first if the "new" parameters are correct, else DomotiGa will hang
  Try hDBTest.Close

  With hDBTest 
    .Type = "mysql"
    .Host = txtDBHost.Text
    .Login = txtUser.Text
     .Password = txtPassword.Text
    .Name = txtDatabase.Text
  End With
  Try hDBTest.Open

  CheckDb.Text = ""

  If Error Then
    Message.Error("DomotiGa cannot connect to MySQL Database\n\nPlease check if MySQL Server is running and/or\nuserid/password are correct")
    Return
  Endif

  Try hDBTest.Close

  With Main
    .sSQLHost = txtDBHost.Text
    .sSQLUser = txtUser.Text
    .sSQLPass = txtPassword.Text
    .sSQLDatabase = txtDatabase.Text
    .bSQLDebug = chkDebug.Value
  End With

  ' reconnect with new settings
  Main.SetConfig()
  Main.GetConfig()
  Main.ConnectDatabase()
  Me.Close

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub txtDBHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub textDBHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDatabase_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtUser_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtPassword_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End
