' Gambas class file

' Description:
' FSettingsPachube.class
' Settings form for Pachube EEML upload.

' Development Status:
' Just started.

' Links:
' www.pachube.com

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public iCurRow As Integer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  chkEnabled.Value = Main.bPachubeEnabled
  txtFeed.Text = Main.iPachubeFeed
  txtPushTime.Text = Main.iPachubePushTime
  txtAPIKey.Text = Main.sPachubeAPIKey
  chkDebug.Value = Main.bPachubeDebug

  If Main.bPachubeEnabled = False Then
    txtFeed.Enabled = False
    txtAPIKey.Enabled = False
    txtPushTime.Enabled = False
    tbvPachubeDevices.Enabled = False
    btnUpload.Enabled = False
    chkDebug.Enabled = False
  Endif
  FillPachubeDevices()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' fill tableview with current devices
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FillPachubeDevices()

  Dim rResult As Result
  Dim iRows, iCount As Integer

  rResult = Main.hDB.Exec("SELECT * FROM devices_pachube")
  If Not rResult Then
    Message.Info(("Error: table 'devices_pachube' not found!"))
    Return
  Endif

  iRows = rResult.Count
  With tbvPachubeDevices
    .Columns.Count = 5
    .Rows.Count = iRows
    .Columns[0].Title = ("Id")
    .Columns[0].Width = 25
    .Columns[1].Title = ("ID")
    .Columns[1].Width = 25
    .Columns[2].Title = ("Tags")
    .Columns[2].Width = 200
    .Columns[3].Title = ("Value")
    .Columns[3].Width = 60
    .Columns[4].Title = ("Units")
    .Columns[4].Width = 60
  End With

  For iCount = 0 To rResult.Max
    tbvPachubeDevices[iCount, 0].Text = rResult!id
    tbvPachubeDevices[iCount, 1].Text = rResult!datastreamid
    tbvPachubeDevices[iCount, 2].Text = rResult!tags
    If rResult!deviceid And rResult!value Then
      tbvPachubeDevices[iCount, 3].Text = Devices.GetCurrentValueForDevice(rResult!deviceid, rResult!value)
    Endif
    If Len(rResult!units) Then tbvPachubeDevices[iCount, 4].Text = rResult!units & "(" & rResult!devicelabel & ")"
    rResult.MoveNext
  Next

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  Dim rResult As Result

  rResult = Main.GetSettingTable("pachube", True) ' get defaults
  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtFeed.Text = rResult!feed
    txtAPIKey.Text = rResult!apikey
    txtPushTime.Text = rResult!pushtime
    chkDebug.Value = rResult!debug
  Endif
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  txtFeed.Enabled = chkEnabled.Value
  txtAPIKey.Enabled = chkEnabled.Value
  txtPushTime.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  tbvPachubeDevices.Enabled = chkEnabled.Value
  btnUpload.Enabled = chkEnabled.Value
  btnSave.Enabled = True

End

Public Sub txtFeed_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtAPIKey_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtPushTime_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' save new settings
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSave_Click()

  Dim rResult As Result

  ' save new Pachube settings
  rResult = Main.hDB.Exec("UPDATE settings_pachube SET debug = &1, feed = &2, apikey = &3, pushtime = &4, enabled = &5 WHERE id = 1", chkDebug.Value, txtFeed.Text, txtAPIKey.Text, txtPushTime.Text, chkEnabled.Value)
  rResult = Main.GetSettingTable("pachube") ' reload settings
  If rResult.Count Then
    Main.bPachubeEnabled = rResult!enabled
    Main.iPachubeFeed = rResult!feed
    Main.sPachubeAPIKey = rResult!apikey
    Main.iPachubePushTime = rResult!pushtime
    Main.bPachubeDebug = rResult!debug
  Endif
  If Main.bServer Then
    Main.Restart_Pachube()
  Else
    XMLClient.ModuleRestart("Pachube")
  Endif
  Me.Close

End

Public Sub btnUpload_Click()

  Pachube.UploadPachubeData()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open entry for editing
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvPachubeDevices_DblClick()

  iCurRow = tbvPachubeDevices.Row
  ' open pachube device editor window
  FEditPachubeDevices.Show()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' view pachube's feed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnWeb_Click()

  Desktop.Open("http://www.pachube.com/feeds/" & txtFeed.Text)

End
