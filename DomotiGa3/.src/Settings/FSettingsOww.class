' Gambas class file

' Description:
' FSettingsOww.class
' Settings form for One-Wire Weather.

' Development Status:
' Just started to build this one.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module was written by Timo Sariwating in 2009.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  GetInstances()

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    Message.Error("Couldn't load settings for instance #1!")
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

  SetTcpUdpText()

End

Public Sub GetInstances()

  Dim iMax As Integer = GetMaxInstanceforPlugin("oww")
  Dim iInstance As Integer

  cmbInstance.Clear
  If iMax Then
    For iInstance = 1 To iMax
      cmbInstance.Add("Instance #" & iInstance)
    Next
  Else
    Message.Error("Couldn't find MaxInstances value for this plugin!")
  Endif

End

Private Sub GetMaxInstanceforPlugin(sPluginName As String) As Integer

  If Main.bServer Then
    Return Plugin.GetPluginMaxInstances(sPluginName)
  Else
    ' FIXME: We are running in client/server mode - call json
    Return 9
  Endif

End

Public Sub cmbInstance_Click()

  ' load selected instance, if not available load defaults
  If LoadSettings(cmbInstance.Index + 1) = False Then
    LoadSettings(0)
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub txtTCPHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtTCPPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rTest As Result
  Dim iInstance As Integer = cmbInstance.Index + 1

  ' test if instance is already present
  rTest = Main.hDB.Exec("SELECT * from settings_oww WHERE id = &1", iInstance)
  If rTest.Count = 1 Then
    ' update settings
    Main.hDB.Exec("UPDATE settings_oww SET debug = &1, tcphost = &2, tcpport = &3, servertype = &4, enabled = &5 WHERE id = &6", chkDebug.Value, txtTCPHost.Text, txtTCPPort.Text, cmbServer.Text, chkEnabled.Value, iInstance)
  Else
    ' insert settings
    Main.hDB.Exec("INSERT INTO settings_oww (debug,tcphost,tcpport,servertype,enabled,id) values (&1, &2, &3, &4, &5, &6)", chkDebug.Value, txtTCPHost.Text, txtTCPPort.Text, cmbServer.Text, chkEnabled.Value, iInstance)
  Endif

  If Main.bServer Then
    Plugin.Restart("OWW", iInstance)
  Else
    Client.Plugin_Restart("Oww", iInstance)
  Endif
  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub cmbServer_Click()

  setTcpUdpText()
  setTcpUdpPort()
  btnSave.Enabled = True

End

Public Sub SetTcpUdpText()

  If Not chkEnabled.Value Then Return

  If cmbServer.Text = "Henriksen WServer TCP" Then
    txtTCPHost.Enabled = True
    TextLblTCPPort.Text = "TCP Port"
  Else If cmbServer.Text = "Henriksen WServer UDP" Then
    txtTCPHost.Enabled = False
    TextLblTCPPort.Text = "UDP Port"
  Else If cmbServer.Text = "Oww text-format" Then
    txtTCPHost.Enabled = True
    TextLblTCPPort.Text = "TCP Port"
  Endif

End

Public Sub setTcpUdpPort()

  If cmbServer.Text = "Henriksen WServer TCP" Then
    txtTCPPort.Text = "8888"
  Else If cmbServer.Text = "Henriksen WServer UDP" Then
    txtTCPPort.Text = "8890"
  Else If cmbServer.Text = "Oww text-format" Then
    txtTCPPort.Text = "8891"
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_oww", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtTCPHost.Text = rResult!tcphost
    txtTCPPort.Text = rResult!tcpport
    cmbServer.Text = rResult!servertype
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtTCPHost.Enabled = chkEnabled.Value
  txtTCPPort.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  cmbServer.Enabled = chkEnabled.Value

  setTcpUdpText()
  setTcpUdpPort()

End

