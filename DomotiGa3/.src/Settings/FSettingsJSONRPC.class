' Gambas class file

' Description:
' FSettingsJSONRPC.class
' Settings form for JSONRPC server support.

' Development Status:
' Finished

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private $bEnabled As Boolean
Private $iSSLCertificate As Integer
Private $cSSLCertificate As New Collection

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  $bEnabled = chkEnabled.Value

  EnableDisableSettings()
  btnSave.Enabled = False

  FillSSLCertificates()

End

Public Sub FillSSLCertificates()

  Dim rResult As Result
  Dim iIndex As Integer

  $cSSLCertificate.Clear()
  cmbSSLCertificate.Clear()

  rResult = Main.hDB.Exec("SELECT * FROM sslcertificates ORDER BY name")
  If rResult And If rResult.Available And If rResult.Count >= 1 Then

    iIndex = 0

    For Each rResult
      $cSSLCertificate.Add(rResult!id, rResult!name)
      cmbSSLCertificate.Add(rResult!name)

      If $iSSLCertificate = rResult!id Then
        ' Don't raise the Click event, so lock and unlock
        Object.Lock(cmbSSLCertificate)
        cmbSSLCertificate.Index = iIndex
        Object.Unlock(cmbSSLCertificate)
      Endif

      iIndex += 1
    Next
  Else
    cmbSSLCertificate.Add("")
  Endif

End


Public Sub btnSave_Click()

  If $cSSLCertificate.Exist(cmbSSLCertificate.Current.Text) Then
    $iSSLCertificate = $cSSLCertificate[cmbSSLCertificate.Current.Text]
  Else
    $iSSLCertificate = 0
  Endif

  If Not ValidInput() Then Return

  ' save new JSONRPC settings
  Main.hDB.Exec("UPDATE settings_jsonrpc SET enabled = &1, debug = &2, maxconn = &3, httpport = &4, sslenabled = &5, sslcertificate_id = &6 WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtMaxConn.Text, txtHTTPPort.Text, chkSSLEnabled.Value, $iSSLCertificate)

  If Main.bServer Then
    Plugin.Restart("JSONRPC")
  Else
    If $bEnabled Then
      Message.Info(("You have to restart the DomotiGa Server manually."))
    Else
      Message.Info(("Warning, you have disabled JSONRPC on server, so you cannot connect with client anymore!"))
    Endif
  Endif

  Me.Close

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub txtMaxConn_KeyPress()

  btnSave.Enabled = True

End

Public Sub HTTPPort_Change()

  btnSave.Enabled = True

End


Public Sub HTTPPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkSSLEnabled_Click()

  btnSave.Enabled = True
  cmbSSLCertificate.Enabled = chkSSLEnabled.Value

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_jsonrpc", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtMaxConn.text = rResult!maxconn
    txtHTTPPort.text = rResult!httpport
    chkDebug.Value = rResult!debug
    chkSSLEnabled.Value = rResult!sslenabled
    $iSSLCertificate = rResult!sslcertificate_id
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtMaxConn.Enabled = chkEnabled.Value
  txtHTTPPort.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  chkSSLEnabled.Enabled = chkEnabled.Value

  If chkEnabled.Value Then
    cmbSSLCertificate.Enabled = chkSSLEnabled.Value
  Else
    cmbSSLCertificate.Enabled = False
  Endif

End

Public Sub cmbSSLCertificate_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Validate the input files, before saving them
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ValidInput() As Boolean

  If chkSSLEnabled.Value And If cmbSSLCertificate.Index < 0 Then
    Balloon(("Please select a SSL certificate"), cmbSSLCertificate)
    Return False
  Endif

  Return True

End
