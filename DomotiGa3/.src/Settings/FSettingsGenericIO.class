' Gambas class file

' Description:
' FSettingsGenericIO.class
' Settings form for generic serial and IP devices.

' Development Status:
' Beta:) But it works so far!

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This code is written by 'thechad' in 2013.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub txtSerialPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  ' save new GenericIO settings
  rResult = Main.hDB.Exec("UPDATE settings_genericio SET debug = &1, type = &2, tcphost = &3, tcpport = &4, serialport = &5, baudrate = &6, enabled = &7, regex = &8, delimiter = &9 WHERE id = 1", chkDebug.Value, cmbType.Text, txtTCPHost.Text, txtTCPPort.Text, txtSerialPort.Text, cmbBaudrate.Text, chkEnabled.Value, txtRegex.Text, txtDelimiter.Text)

  If Main.bServer Then
    Plugin.Restart("GenericIO", 1)
  Else
    XMLClient.ModuleRestart("GenericIO")
  Endif

  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub cmbType_Click()

  If chkEnabled.Value Then SetButtons()
  btnSave.Enabled = True

End

Public Sub txtTCPHost_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtTCPPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbBaudrate_Click()

  btnSave.Enabled = True

End

Public Sub txtRegex_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtRegex_Change()

  btnTest_Click()

End

Public Sub txtDelimiter_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDelimiter_Change()

  btnTest_Click()

End

Public Sub txtInputTest_Change()

  btnTest_Click()

End

Public Sub txtDeviceAddress_Change()

  btnTest_Click()

End

Public Sub btnTest_Click()

  Dim aScan, aValueToUpdate As String[]
  Dim sLine, sSingleAddress, sAddress, sRegexExpression, sAddressToChange, sValueFields As String
  Dim iDeviceId, iValueToUpdate As Integer
  Dim rRegex As Regexp
  Dim iAddressPosition As Integer

  txtValue1.Text = ""
  txtValue2.Text = ""
  txtValue3.Text = ""
  txtValue4.Text = ""
  txtBattery.Text = ""

  ' sRegexExpression = Replace$(sGenericIORegex, "\\", "\\")  ' Gambas needs the " \ " character in a regex to be doubled to be recognised"
  '
  ' parse each line
  ' parse sensor data using the regex for the interface to get the Address field to change
  rRegex = New Regexp(txtInputTest.Text, txtRegex.Text)
  sAddressToChange = rRegex.text
  txtAddressMatch.Text = rRegex.text

  ' try to find device with the address from the parsed input data as part of it's address field, and correct interface type.
  sAddress = txtDeviceAddress.Text
  aScan = Split(sAddress, "|")
  iValueToUpdate = aScan.Find(sAddressToChange)
  iAddressPosition = InStr(txtInputTest.Text, sAddressToChange)
  sValueFields = Mid(txtInputTest.Text, iAddressPosition + Len(sAddressToChange))
  aValueToUpdate = Split(sValueFields, txtDelimiter.Text)

  Select aValueToUpdate.Count
    Case 1
      Select iValueToUpdate
         Case 1
            txtValue1.Text = Trim(aValueToUpdate[0])
         Case 2
            txtValue2.Text = Trim(aValueToUpdate[0])
         Case 3
            txtValue3.Text = Trim(aValueToUpdate[0])
         Case 4
            txtValue4.Text = Trim(aValueToUpdate[0])
         Default
            If chkDebug.Value Then Main.WriteDebugLog(("[GenericIO] Unknown value to update :") & iValueToUpdate)
      End Select
    Case 2
      txtValue1.Text = Trim(aValueToUpdate[0])
      txtValue3.Text = Trim(aValueToUpdate[1])
    Case 3
      txtValue1.Text = Trim(aValueToUpdate[0])
      txtValue2.Text = Trim(aValueToUpdate[1])
      txtValue3.Text = Trim(aValueToUpdate[2])
    Case 4
      txtValue1.Text = Trim(aValueToUpdate[0])
      txtValue2.Text = Trim(aValueToUpdate[1])
      txtValue3.Text = Trim(aValueToUpdate[2])
      txtValue4.Text = Trim(aValueToUpdate[3])
    Case 5
      txtValue1.Text = Trim(aValueToUpdate[0])
      txtValue2.Text = Trim(aValueToUpdate[1])
      txtValue3.Text = Trim(aValueToUpdate[2])
      txtValue4.Text = Trim(aValueToUpdate[3])
      txtBattery.Text = Trim(aValueToUpdate[4])
    Default
       Return
  End Select

Catch
  If chkDebug.Value Then Main.WriteDebugLog(("[GenericIO] ERROR: Parsing GenericIO regex ") & txtRegex.Text & " " & "Input Test to test " & txtInputTest.Text & " " & ERROR.Text & " at " & ERROR.Where)
  txtValue1.Text = ""
  txtValue2.Text = ""
  txtValue3.Text = ""
  txtValue4.Text = ""
  txtBattery.Text = ""

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_genericio", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    cmbType.Text = rResult!type
    txtTCPPort.Text = rResult!tcpport
    txtTCPHost.Text = rResult!tcphost
    txtSerialPort.text = rResult!serialport
    cmbBaudrate.Text = rResult!baudrate
    chkDebug.Value = rResult!debug
    txtRegex.Text = rResult!regex
    txtDelimiter.Text = rResult!delimiter
    txtInputTest.Text = "R2=1"
    txtDeviceAddress.Text = "|R1=|R2=|R3=|R4=|"
    txtAddressMatch.Text = ""
    txtValue1.Text = ""
    txtValue2.Text = ""
    txtValue3.Text = ""
    txtValue4.Text = ""
    txtBattery.Text = ""
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  cmbType.Enabled = chkEnabled.Value
  txtTCPHost.Enabled = chkEnabled.Value
  txtTCPPort.Enabled = chkEnabled.Value
  txtSerialPort.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  cmbBaudrate.Enabled = chkEnabled.Value
  txtRegex.Enabled = chkEnabled.Value
  txtDelimiter.Enabled = chkEnabled.Value
  If chkEnabled.Value Then SetButtons()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SetButtons()

  If cmbType.Text = "tcp" Then
    txtSerialPort.Enabled = False
    cmbBaudrate.Enabled = False
    txtTCPHost.Enabled = True
    txtTCPPort.Enabled = True
  Endif
  If cmbType.Text = "serial" Then
    txtSerialPort.Enabled = True
    cmbBaudrate.Enabled = True
    txtTCPHost.Enabled = False
    txtTCPPort.Enabled = False
  Endif

End

