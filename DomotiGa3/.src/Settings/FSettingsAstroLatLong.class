' Gambas class file

' Description:
' FSettingsAstro.class
' Settings form for Astro and Location.

' Development Status:
' Working, Daylight Saving checkbox not used yet.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private cLatitude As New Collection
Private cLongitude As New Collection

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

End

Public Sub btnSave_Click()

  FSettingsAstro.vbLatitude.Value = cLatitude[cmbResults.Index]
  FSettingsAstro.vbLongitude.Value = cLongitude[cmbResults.Index]

  Me.Close

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnSearch_Click()

  Dim cData As Collection
  Dim cItem As Collection

  ' Result values to empty
  cmbResults.Clear
  cLatitude.Clear
  cLongitude.Clear
  vbLatitude.Value = 0
  vbLongitude.Value = 0

  If Not txtLocation.Text Then
    Balloon("No location supplied", txtLocation)
    Return
  Endif

  cData = Util.GoogleGeoCodeSearch(txtLocation.Text)

  If cData.Exist("info") Then
    Select cData["info"]
      Case "NOLOCATION"
        Balloon("No location supplied", txtLocation)
      Case "NOTFOUND"
        Balloon("Google Geocode didn't find any matching location(s)", txtLocation)
      Default
        Message.Info(cData["info"])
    End Select
  Else
    If cData.Exist("error") Then
      Message.Error(cData["error"])
    Else

      For Each cItem In cData["city"]
        cmbResults.Add(cItem["city"])
        cLatitude.Add(cItem["latitude"], cmbResults.Count - 1)
        cLongitude.Add(cItem["longitude"], cmbResults.Count - 1)
      Next

      If cLatitude.Exist(0) Then vbLatitude.Value = cLatitude[cmbResults.Index]
      If cLongitude.Exist(0) Then vbLongitude.Value = cLongitude[cmbResults.Index]

    Endif
  Endif

End

Public Sub cmbResults_Click()

  vbLatitude.Value = cLatitude[cmbResults.Index]
  vbLongitude.Value = cLongitude[cmbResults.Index]

End
