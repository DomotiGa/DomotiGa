' Gambas class file

' Description:
' FSettingsTwitter.class
' Settings form for Twitter support.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public hForm As Form
Public hSendButton As Button
Public hTextBox As TextBox

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub txtUser_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtPassword_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub chkTimeStamp_Click()

  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  ' save new twitter settings
  Main.hDB.Exec("UPDATE settings_twitter SET debug = &1, enabled = &2, username = &3, password = &4, sendtimestamp = &5 WHERE id = 1", chkDebug.Value, chkEnabled.Value, txtUser.Text, txtPassword.Text, chkTimeStamp.Value)

  If Main.bServer Then
    Plugin.Restart("Twitter", 1)
  Else
    Client.ModuleRestart("Twitter")
  Endif

  Me.Close

End

Public Sub btnTest_Click()

  hForm = New Form
  With hForm
    .X = FMain.X + 50
    .Y = FMain.Y + 70
    .Background = Color.TextBackground
    .Height = 36
    .Width = 500
    .Text = "Enter your tweet"
  End With
  hTextBox = New TextBox(hForm) As "TweetBox"
  With hTextBox
    .X = 7
    .Y = 7
    .Height = 21
    .Width = 430
  End With
  hSendButton = New Button(hForm) As "btnSend"
  With hSendButton
    .X = 445
    .Y = 7
    .Width = 49
    .Height = 21
    .Text = "Send"
  End With
  hForm.Show

End

Public Sub btnWeb_Click()

  Desktop.Open("http://www.twitter.com/domotiga")

End

Public Sub btnSend_Click()

  Dim hTwitter As New CTwitter

  If Len(hTextBox.Text) < 5 Then
    Balloon(("Please type some text first!"), hTextBox)
  Else
    If Main.bServer Then
      hTwitter.PostTweet(hTextBox.Text)
    Else
      Client.SendTweet(hTextBox.Text)
    Endif
    hForm.Close
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_twitter", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    chkTimeStamp.Value = rResult!sendtimestamp
    txtUser.Text = rResult!username
    txtPassword.Text = rResult!password
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtUser.Enabled = chkEnabled.Value
  txtPassword.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  chkTimeStamp.Enabled = chkEnabled.Value
  btnTest.Enabled = chkEnabled.Value

End