' Gambas class file

' Description:
' FSettingsK8055.class
' Settings form for Velleman K8055 support.

' Development Status:
' In progress

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  GetInstances()

  ' always load first instance
  If LoadSettings(1) = False Then
    Message.Error("Couldn't load settings for instance #1!")
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub GetInstances()

  Dim iMax As Integer = GetMaxInstanceforPlugin("K8055")
  Dim iInstance As Integer

  cmbInstance.Clear
  If iMax Then
    For iInstance = 1 To iMax
      cmbInstance.Add("Instance #" & iInstance)
    Next
  Else
    Message.Error("Couldn't find MaxInstances value for this plugin!")
  Endif

End

Private Sub GetMaxInstanceforPlugin(sPluginName As String) As Integer

  If Main.bServer Then
    Return Plugin.GetPluginMaxInstances(sPluginName)
  Else
    ' FIXME: We are running in client/server mode - call json
    Return 9
  Endif

End

Public Sub cmbInstance_Click()

  ' load selected instance, if not available load defaults
  If LoadSettings(cmbInstance.Index + 1) = False Then
    LoadSettings(0)
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rSettings As Result
  Dim iInstance As Integer = cmbInstance.Index + 1

  ' test if instance is already present
  rSettings = Main.hDB.Exec("SELECT * from settings_k8055 WHERE id = &1", iInstance)
  If rSettings.Count = 1 Then
    ' update settings
    Main.hDB.Exec("UPDATE settings_k8055 SET debug = &1, polltime = &2, boardaddress = &3, debouncetime1 = &4, debouncetime2 = &5, enabled = &6 WHERE id = &7", chkDebug.Value, txtPolltime.Text, cmbBoardAddress.Text, txtdebouncetime1.Text, txtdebouncetime2.Text, chkEnabled.Value, iInstance)
  Else
    ' insert settings
    Main.hDB.Exec("INSERT INTO settings_k8055 (debug, polltime, boardaddress, debouncetime1, debouncetime2, enabled, id) values (&1, &2, &3, &4, &5, &6, &7)", chkDebug.Value, txtPolltime.Text, cmbBoardAddress.Text, txtdebouncetime1.Text, txtdebouncetime2.Text, chkEnabled.Value, iInstance)
  Endif

  If Main.bServer Then
    Plugin.Restart("K8055", iInstance)
  Else
    Client.Plugin_Restart("K8055", iInstance)
  Endif

  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub txtPolltime_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDebounceTime1_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDebounceTime2_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbAddressBoard_Click()

  btnSave.Enabled = True

End

Public Sub cmbBoardAddress_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_k8055", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtPolltime.Text = rResult!polltime
    cmbBoardAddress.Text = rResult!boardaddress
    txtdebouncetime1.Text = rResult!debouncetime1
    txtdebouncetime2.Text = rResult!debouncetime2
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtPolltime.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  cmbBoardAddress.Enabled = chkEnabled.Value
  txtDebounceTime1.Enabled = chkEnabled.Value
  txtDebounceTime2.Enabled = chkEnabled.Value

End

