' Gambas class file

' Description:
' FSettingsDSC.class
' Settings form for DSC Security support.

' Development Status:
' Alpha.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module was written by Timo Sariwating in 2009.

' Credits:
' Tradiuz for IT100 code.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub txtSerialPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbBaudrate_Click()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  ' save new DSC Security settings
  rResult = Main.hDB.Exec("UPDATE settings_dsc SET debug = &1, serialport = &2, baudrate = &3, enabled = &4, mastercode=&5, type=&6 WHERE id = 1", chkDebug.Value, txtSerialPort.Text, cmbBaudrate.Text, chkEnabled.Value, txtMasterCode.Text, cmbType.Index)

  rResult = Main.GetSettingTable("dsc") ' reload settings
  If rResult.Count Then
    Main.bDSCEnabled = rResult!enabled
    Main.sDSCSerialPort = rResult!serialport
    Main.sDSCBaudrate = rResult!baudrate
    Main.bDSCDebug = rResult!debug
    Main.sDSCMasterCode = rResult!mastercode
    Main.iDSCType = rResult!type
  Endif

  If Main.bServer Then
    Main.Restart_DSC()
  Else
    XMLClient.ModuleRestart("DSC")
  Endif

  FMain.UpdateStatusPanel()
  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub btnArm_Stay_Click()

  If Main.bServer And If Main.hDSC Then
    Main.hDSC.TX("0311")
  Else If Main.bDSCEnabled Then
    XMLClient.SetAlarmMode("0311")
  Else
    Main.WriteDebugLog(("[DSC] Connection with alarm system not enabled yet."))
  Endif

End

Public Sub btnSend_Time_Click()

  Dim sTime_Now As String

  sTime_Now = "010" & Format$(Now, "hhnnmmddyy")
  If Main.bServer And If Main.hDSC Then
    Main.hDSC.TX(sTime_Now)
    Main.WriteDebugLog("[DSC] System time set to " & Now & " on alarm system")
  Else
    Main.WriteDebugLog("[DSC] Connection with alarm system not enabled yet.")
  Endif

End

Public Sub btnArm_Away_Click()

  If Main.bServer And If Main.hDSC Then
    Main.hDSC.TX("0301")
  Else If Main.bDSCEnabled Then
    XMLClient.SetAlarmMode("0301")
  Else
    Main.WriteDebugLog("[DSC] Connection with alarm system not enabled yet.")
  Endif

End

Public Sub txtMasterCode_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbType_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_dsc", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtSerialPort.text = rResult!serialport
    cmbBaudrate.text = rResult!baudrate
    chkDebug.Value = rResult!debug
    txtMasterCode.Text = rResult!mastercode
    cmbType.Index = rResult!type
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtSerialPort.Enabled = chkEnabled.Value
  cmbBaudrate.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnSend_Time.Enabled = chkEnabled.Value
  btnArm_Stay.Enabled = chkEnabled.Value
  btnArm_Away.Enabled = chkEnabled.Value
  cmbType.Enabled = chkEnabled.Value
  txtMasterCode.Enabled = chkEnabled.Value

End

