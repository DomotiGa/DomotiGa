' Gambas class file

' Description:
' FSettingsForecastIo.class
' Settings form for forecast.io API support.

' Development Status:
' Development just started.

' Links:
' http://www.forecast.io

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private $cCity As New Collection

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable button
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Search city for cityid, but we only can do it when we search for the weather
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnCitySearch_Click()

  Dim cData, cItem As Collection

  $cCity.Clear
  cmbCities.Clear
  vbLatitude.Value = ""
  vbLongitude.Value = ""

  cData = Util.GoogleGeoCodeSearch(txtCitySearch.Text)

  If cData.Exist("info") Then
    Select cData["info"]
      Case "NOLOCATION"
        Balloon("No location supplied", txtCitySearch)
      Case "NOTFOUND"
        Balloon("Google Geocode didn't find any matching location(s)", txtCitySearch)
      Default
        Message.Info(cData["info"])
    End Select
  Else
    If cData.Exist("error") Then
      Message.Error(cData["error"])
    Else

      For Each cItem In cData["city"]
        cmbCities.Add(cItem["city"])
        $cCity[cItem["city"]] = ["latitude": cItem["latitude"], "longitude": cItem["longitude"]]
      Next

      vbLatitude.Value = $cCity[cmbCities.Text]["latitude"]
      vbLongitude.Value = $cCity[cmbCities.Text]["longitude"]
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub txtCitySearch_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub txtApiKey_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbCities_Click()

  If $cCity Then
    If $cCity.Count >= 1 Then
      vbLatitude.Value = $cCity[cmbCities.Text]["latitude"]
      vbLongitude.Value = $cCity[cmbCities.Text]["longitude"]
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Validate input fields
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ValidInput() As Boolean

  If Not cmbCities.Text Then
    Balloon(("Please select a city!"), cmbCities)
    Return False
  Endif

  If Not txtApiKey.Text Then
    Balloon(("Please enter your forecast.io API key!"), txtApiKey)
    Return False
  Endif

  Return True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSave_Click()

  If Not ValidInput() Then Return

  '--- save new forecast.io settings ---
  Main.hDB.Exec("UPDATE settings_forecastio SET debug = &1, city = &2, latitude = &3, longitude = &4, enabled = &5, apikey = &6, polltime = &7 WHERE id = 1", chkDebug.Value, cmbCities.Text, vbLatitude.Value, vbLongitude.Value, chkEnabled.Value, txtApiKey.Text, txtRefresh.Text)

  If Main.bServer Then
    Plugin.Restart("ForecastIo", 1)
  Else
    Client.ModuleRestart("ForecastIo")
  Endif

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_forecastio", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    cmbCities.Add(rResult!city)
    cmbCities.Text = rResult!city
    vbLatitude.Value = rResult!latitude
    vbLongitude.Value = rResult!longitude
    chkDebug.Value = rResult!debug
    txtApiKey.Text = rResult!apikey
    txtRefresh.Text = rResult!polltime
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  cmbCities.Enabled = chkEnabled.Value
  vbLatitude.Enabled = chkEnabled.Value
  vbLongitude.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnCitySearch.Enabled = chkEnabled.Value
  cmbCities.Enabled = chkEnabled.Value
  txtCitySearch.Enabled = chkEnabled.Value
  txtRefresh.Enabled = chkEnabled.Value

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Open web page for directly registering the api key
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub TextLabelLink_MouseUp()

  Desktop.Open("https://developer.forecast.io/register")

End
