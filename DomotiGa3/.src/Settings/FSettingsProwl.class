' Gambas class file

' Description:
' FSettingsProwl.class
' Settings form for Prowl API support.

' Development Status:
' Development just started.

' Links:
' http://www.prowlapp.com/api.php

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  txtApiKey.Text = LTrim$(txtApiKey.Text)
  txtApiKey.Text = RTrim$(txtApiKey.Text)

  If Not txtApiKey.Text Then
    Balloon(("Please enter your Prowl API key"), txtApiKey)
    Return False
  Endif

  If Not txtApplication.Text Then
    Balloon(("Please enter an application text"), txtApplication)
    Return False
  Endif

  If Not txtEvent.Text Then
    Balloon(("Please enter an event text"), txtEvent)
    Return False
  Endif

  ' Check if API key looks to be valid
  If Len(txtApiKey.Text) <> 40 Then
  Select Message.Question("Your API key length is " & Len(txtApiKey.Text) & ", normally a valid API key is 40 characters. Do you want to continue?", "Yes", "No")
    Case 2 ' Answer=No
      Return False
  End Select

  Endif

  Return True

End

Public Sub btnSave_Click()

  If chkEnabled.Value Then
    If Not ValidInput() Then Return
  Endif

  '--- save new Prowl settings ---
  Main.hDB.Exec("UPDATE settings_prowl SET enabled = &1, debug = &2, apikey = &3, application = &4, event = &5  WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtApiKey.Text, txtApplication.Text, txtEvent.Text)

  If Main.bServer Then
    Plugin.Restart("Prowl", 1)
  Else
    Client.ModuleRestart("Prowl")
  Endif

  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.prowlapp.com/")

End

Public Sub btnTestApiKey_Click()

  Dim sResponse As String
  Dim hProwl As New cProwl

  If txtApiKey.Text Then
    sResponse = hProwl.ValidateProwlApiKey(txtApiKey.Text, chkDebug.Value)
    If Not sResponse Then
      Message.Info("Prowl API key is VALID")
    Else
      Message.Error("Prowl API key is INVALID\n" & sResponse)
    Endif
  Else
    Balloon(("Please enter your Prowl API key"), txtApiKey)
  Endif

End

Public Sub btnTestProwl_Click()

  Dim sResponse As String
  Dim hProwl As New CProwl

  If txtApiKey.Text Then
    ' If the sResponse is blank, all went well
    sResponse = hProwl.PostProwlTest(txtApiKey.Text, "DomotiGa", "Info", "Test message from DomotiGa :-)", chkDebug.Value)
    If Not sResponse Then
      Message.Info("Prowl message successfully send")
    Else
      Message.Error("Failed to send Prowl message\n" & sResponse)
    Endif
  Else
    Balloon(("Please enter your Prowl API key"), txtApiKey)
  Endif

End

Public Sub txtApiKey_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtApplication_KeyRelease()

  btnSave.Enabled = True

End

Public Sub txtEvent_KeyRelease()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_prowl", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    chkDebug.Value = rResult!debug
    txtApiKey.Text = rResult!apikey
    txtApplication.Text = rResult!application
    txtEvent.Text = rResult!event
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  chkDebug.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  txtApplication.Enabled = chkEnabled.Value
  txtEvent.Enabled = chkEnabled.Value
  btnTestApiKey.Enabled = chkEnabled.Value
  btnTestProwl.Enabled = chkEnabled.Value

End

