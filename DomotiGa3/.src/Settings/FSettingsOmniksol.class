' Gambas class file

' Description:
' FSettingsOmniksol.class
' Settings form for Omniksol plugin.

' Development Status:
' Works.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  GetInstances()

  ' always load first instance
  If LoadSettings(1) = False Then
    Message.Error("Couldn't load settings for instance #1!")
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub GetInstances()

  Dim iMax As Integer = GetMaxInstanceforPlugin("omniksol")
  Dim iInstance As Integer

  cmbInstance.Clear
  If iMax Then
    For iInstance = 1 To iMax
      cmbInstance.Add("Instance #" & iInstance)
    Next
  Else
    Message.Error("Couldn't find MaxInstances value for this plugin!")
  Endif

End

Private Sub GetMaxInstanceforPlugin(sPluginName As String) As Integer

  If Main.bServer Then
    Return Plugin.GetPluginMaxInstances(sPluginName)
  Else
    ' FIXME: We are running in client/server mode - call json
    Return 9
  Endif

End

Public Sub cmbInstance_Click()

  ' load selected instance, if not available load defaults
  If LoadSettings(cmbInstance.Index + 1) = False Then
    LoadSettings(0)
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub chkDiscover_Click()

  SetButtons()
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SetButtons()

  If chkDiscover.Value Then
    txtSerial.Enabled = False
    txtTCPHost.Enabled = False
    vbTCPPort.Enabled = False
  Else
    txtSerial.Enabled = True
    txtTCPHost.Enabled = True
    vbTCPPort.Enabled = True
  Endif

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Public Sub btnSave_Click()

  Dim rTest As Result
  Dim iInstance As Integer = cmbInstance.Index + 1

  ' test if instance is already present
  rTest = Main.hDB.Exec("SELECT * from settings_omniksol WHERE id = &1", iInstance)
  If rTest.Count = 1 Then
    ' update settings
    Main.hDB.Exec("UPDATE settings_omniksol SET debug = &1, tcphost = &2, tcpport = &3, serial = &4, cron = &5, enabled = &6, discover = &7 WHERE id = &8", chkDebug.Value, txtTCPHost.Text, vbTCPPort.Value, txtSerial.Text, txtCronTab.Text, chkEnabled.Value, chkDiscover.Value, iInstance)
  Else
    ' insert settings
    Main.hDB.Exec("INSERT INTO settings_omniksol (debug, tcphost, tcpport, serial, cron, enabled, discover, id) values (&1, &2, &3, &4, &5, &6, &7, &8)", chkDebug.Value, txtTCPHost.Text, vbTCPPort.Value, txtSerial.Text, txtCronTab.Text, chkEnabled.Value, chkDiscover.Value, iInstance)
  Endif

  If Main.bServer Then
    Plugin.Restart("Omniksol", iInstance)
  Else
    Client.ModuleRestart("Omniksol")
  Endif

  Me.Close

End

Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_omniksol", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtTCPHost.Text = rResult!tcphost
    vbTCPPort.Value = rResult!tcpport
    txtSerial.Text = rResult!serial
    txtCronTab.Text = rResult!cron
    ChkDiscover.Value = rResult!discover
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  chkDiscover.Enabled = chkEnabled.Value
  SetButtons()
  txtCronTab.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value

End

Public Sub txtCronTab_Change()

  btnSave.Enabled = True

End

Public Sub txtTCPHost_Change()

  btnSave.Enabled = True

End

Public Sub vbTCPPort_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtSerial_Change()

  btnSave.Enabled = True

End
