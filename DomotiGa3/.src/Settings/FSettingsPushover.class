' Gambas class file

' Description:
' FSettingsPushover.class
' Settings form for Notify My Android API support.

' Development Status:
' Development just started.

' Links:
' http://pushover.net/api

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not txtToken.Text Then
    Balloon(("Please enter your Pushover API token"), txtToken)
    Return False
  Endif

  If Not txtUser.Text Then
    Balloon(("Please enter an user key"), txtUser)
    Return False
  Endif

  Return True

End

Public Sub btnSave_Click()

  If chkEnabled.Value Then
    If Not ValidInput() Then Return
  Endif

  '--- save new Pushover settings ---
  Main.hDB.Exec("UPDATE settings_pushover SET enabled = &1, debug = &2, token = &3, user = &4, device = &5  WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtToken.Text, txtUser.Text, txtDevice.Text)

  If Main.bServer Then
    Plugin.Restart("Pushover", 1)
  Else
    XMLClient.ModuleRestart("Pushover")
  Endif

  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.pushover.net/")

End

Public Sub btnTestTokenUser_Click()

  Dim sResponse As String
  Dim hPushover As New CPushover

  If txtToken.Text And txtUser.Text Then
    sResponse = hPushover.ValidatePushoverTokenUser(txtToken.Text, txtUser.Text, chkDebug.Value)
    If Not sResponse Then
      Message.Info("Pushover API token and user key are VALID")
    Else
      Message.Error("Pushover API token or user key are INVALID\n" & sResponse)
    Endif
  Else
    If Not TxtToken.Text Then
      Balloon(("Please enter your Pushover API token"), txtToken)
    Else
      If Not TxtUser.Text Then
        Balloon(("Please enter your Pushover user key"), txtToken)
      Endif
    Endif
  Endif
  
End

Public Sub btnTestPushover_Click()

  Dim sResponse As String
  Dim hPushover As New CPushover

  If txtToken.Text And txtUser.Text Then
    ' If the sResponse is blank, all went well
    sResponse = hPushover.PostPushoverTest(txtToken.Text, txtUser.Text, "", "Test message from DomotiGa :-)", chkDebug.Value)
    If Not sResponse Then
      Message.Info("Pushover message successfully send")
    Else
      Message.Error("Failed to send Pushover message\n" & sResponse)
    Endif
  Else
    If Not TxtToken.Text Then
      Balloon(("Please enter your Pushover API token"), txtToken)
    Else
      If Not TxtUser.Text Then
        Balloon(("Please enter your Pushover user key"), txtToken)
      Endif
    Endif
  Endif

End

Public Sub txtToken_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtUser_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDevice_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_pushover", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    chkDebug.Value = rResult!debug
    txtToken.Text = rResult!token
    txtUser.Text = rResult!user
    txtDevice.Text = rResult!device
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  chkDebug.Enabled = chkEnabled.Value
  txtToken.Enabled = chkEnabled.Value
  txtUser.Enabled = chkEnabled.Value
  txtDevice.Enabled = chkEnabled.Value
  btnTestTokenUser.Enabled = chkEnabled.Value
  btnTestPushover.Enabled = chkEnabled.Value

End

