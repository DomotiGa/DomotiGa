' Gambas class file

' Description:
' FSettingsPushover.class
' Settings form for Notify My Android API support.

' Development Status:
' Development just started.

' Links:
' http://pushover.net/api

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  chkEnabled.Value = Main.bPushoverEnabled
  txtToken.Text = Main.sPushoverToken
  txtUser.Text = Main.sPushoverUser
  txtDevice.Text = Main.sPushoverDevice
  chkDebug.Value = Main.bPushoverDebug

  chkDebug.Enabled = chkEnabled.Value
  txtToken.Enabled = chkEnabled.Value
  txtUser.Enabled = chkEnabled.Value
  txtDevice.Enabled = chkEnabled.Value
  btnTestTokenUser.Enabled = chkEnabled.Value
  btnTestPushover.Enabled = chkEnabled.Value
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  Dim rResult As Result

  rResult = Main.GetSettingTable("pushover", True) ' get defaults
  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    chkDebug.Value = rResult!debug
    txtToken.Text = rResult!token
    txtUser.Text = rResult!user
    txtDevice.Text = rResult!device
  Endif
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  chkDebug.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  txtToken.Enabled = chkEnabled.Value
  txtUser.Enabled = chkEnabled.Value
  txtDevice.Enabled = chkEnabled.Value
  btnTestTokenUser.Enabled = chkEnabled.Value
  btnTestPushover.Enabled = chkEnabled.Value
  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not txtToken.Text Then
    Balloon(("Please enter your Pushover API token"), txtToken)
    Return False
  Endif

  If Not txtUser.Text Then
    Balloon(("Please enter an user key"), txtUser)
    Return False
  Endif

  Return True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  If Not ValidInput() Then Return

  '--- save new Pushover settings ---
  rResult = Main.hDB.Exec("UPDATE settings_pushover SET enabled = &1, debug = &2, token = &3, user = &4, device = &5  WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtToken.Text, txtUser.Text, txtDevice.Text)
  rResult = Main.GetSettingTable("pushover") ' reload settings
  If rResult.Count Then
    Main.bPushoverEnabled = rResult!enabled
    Main.bPushoverDebug = rResult!debug
    Main.sPushoverToken = rResult!token
    Main.sPushoverUser = rResult!user
    Main.sPushoverDevice = rResult!device
  Endif

  If Main.bServer Then
    Main.Restart_Pushover()
  Else
    XMLClient.ModuleRestart("Pushover")
  Endif

  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.pushover.net/")

End

Public Sub btnTestTokenUser_Click()

  Dim sResponse As String

  If txtToken.Text And txtUser.Text Then
    sResponse = Pushover.ValidatePushoverTokenUser(txtToken.Text, txtUser.Text)
    If Not sResponse Then
      Message.Info("Pushover API token and user key are VALID")
    Else
      Message.Error("Pushover API token or user key are INVALID\n" & sResponse)
    Endif
  Else
    If Not TxtToken.Text Then
      Balloon(("Please enter your Pushover API token"), txtToken)
    Else
      If Not TxtUser.Text Then
        Balloon(("Please enter your Pushover user key"), txtToken)
      Endif
    Endif
  Endif
  
End

Public Sub btnTestPushover_Click()

  Dim sResponse As String

  If txtToken.Text And txtUser.Text Then
    ' If the sResponse is blank, all went well
    sResponse = Pushover.PostPushoverTest(txtToken.Text, txtUser.Text, "", "Test message from DomotiGa :-)")
    If Not sResponse Then
      Message.Info("Pushover message successfully send")
    Else
      Message.Error("Failed to send Pushover message\n" & sResponse)
    Endif
  Else
    If Not TxtToken.Text Then
      Balloon(("Please enter your Pushover API token"), txtToken)
    Else
      If Not TxtUser.Text Then
        Balloon(("Please enter your Pushover user key"), txtToken)
      Endif
    Endif
  Endif

End

Public Sub txtToken_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtUser_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDevice_KeyPress()

  btnSave.Enabled = True

End
