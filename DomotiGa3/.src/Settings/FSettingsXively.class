' Gambas class file

' Description:
' FSettingsXively.class
' Settings form for Xively upload.

' Development Status:
' Just started.

' Links:
' www.xively.com

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public iCurRow As Integer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

  FillXivelyDevices()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' fill tableview with current devices
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FillXivelyDevices()

  Dim rResult As Result
  Dim iRows, iCount As Integer

  rResult = Main.hDB.Exec("SELECT * FROM devices_xively")
  If Not rResult Then
    Message.Info(("Error: table 'devices_xively' not found!"))
    Return
  Endif

  iRows = rResult.Count
  With tbvXivelyDevices
    .Columns.Count = 5
    .Rows.Count = iRows
    .Columns[0].Title = ("Id")
    .Columns[0].Width = 25
    .Columns[1].Title = ("Channel")
    .Columns[1].Width = 100
    .Columns[2].Title = ("Tags")
    .Columns[2].Width = 150
    .Columns[3].Title = ("Value")
    .Columns[3].Width = 60
    .Columns[4].Title = ("Units")
    .Columns[4].Width = 60
  End With

  For iCount = 0 To rResult.Max
    tbvXivelyDevices[iCount, 0].Text = rResult!id
    tbvXivelyDevices[iCount, 1].Text = rResult!datastreamid
    tbvXivelyDevices[iCount, 2].Text = rResult!tags
    If rResult!deviceid And rResult!value Then
      tbvXivelyDevices[iCount, 3].Text = Devices.GetCurrentValueForDevice(rResult!deviceid, rResult!value)
    Endif
    If Len(rResult!units) Then tbvXivelyDevices[iCount, 4].Text = rResult!units & " (" & rResult!devicelabel & ")"
    rResult.MoveNext
  Next

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub txtFeed_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtAPIKey_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtPushTime_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' save new settings
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSave_Click()

  ' save new Xively settings
  Main.hDB.Exec("UPDATE settings_xively SET debug = &1, feed = &2, apikey = &3, pushtime = &4, enabled = &5 WHERE id = 1", chkDebug.Value, txtFeed.Text, txtAPIKey.Text, txtPushTime.Text, chkEnabled.Value)

  If Main.bServer Then
    Plugin.Restart("Xively", 1)
  Else
    Client.Plugin_Restart("Xively")
  Endif

  Me.Close

End

Public Sub btnUpload_Click()

  Dim cPl As New CPluginEntry
  Dim hXively As New CXively

  cPl.Settings["enabled"] = chkEnabled.Value
  cPl.Settings["feed"] = txtFeed.Text
  cPl.Settings["apikey"] = txtAPIKey.Text
  cPl.Settings["pushtime"] = txtPushTime.Text
  cPl.Settings["debug"] = chkDebug.Value

  hXively.StartPlugin(cPl, False)

  hXively.UploadXivelyData()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open entry for editing
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tbvXivelyDevices_DblClick()

  If Not chkEnabled.Value Then Return

  btnSave.Enabled = True
  iCurRow = tbvXivelyDevices.Row
  ' open xively device editor window
  FEditXivelyDevices.Show()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' view xively's feed
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnWeb_Click()

  Desktop.Open("http://www.xively.com/feeds/" & txtFeed.Text)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_xively", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    txtFeed.Text = rResult!feed
    txtAPIKey.Text = rResult!apikey
    txtPushTime.Text = rResult!pushtime
    chkDebug.Value = rResult!debug
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  txtFeed.Enabled = chkEnabled.Value
  txtAPIKey.Enabled = chkEnabled.Value
  txtPushTime.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  tbvXivelyDevices.Enabled = chkEnabled.Value
  btnUpload.Enabled = chkEnabled.Value
  btnAddDevice.Enabled = chkEnabled.Value
  tbvXivelyDevices.Enabled = chkEnabled.Value

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Add device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnAddDevice_Click()

  iCurRow = tbvXivelyDevices.Row
  ' open xively device editor window
  FEditXivelyDevices.Show()

End
