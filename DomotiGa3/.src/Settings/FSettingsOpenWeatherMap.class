' Gambas class file

' Description:
' FSettingsOpenWeatherMap.class
' Settings form for OpenWeatherMap API support.

' Development Status:
' Development just started.

' Links:
' http://www.openweathermap.org

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private $cCity As New Collection

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable button
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Search city for cityid, but we only can do it when we search for the weather
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnCitySearch_Click()

  Dim cData As Collection
  Dim cCity As Collection
  Dim hWeather As New COpenWeatherMap

  $cCity.Clear
  cmbCities.Clear
  txtCityId.Text = ""

  cData = hWeather.WeatherGetCity(txtApiKey.Text, txtCitySearch.Text)

  ' Expected JSON format is:
  ' { "city": [ {"cityname": "name", "citycode": 12345}, {"cityname": "name", "citycode": 12346}] }

  If cData.Exist("city") Then
    For Each cCity In cData["city"]
      $cCity[cCity["cityname"]] = cCity["cityid"]
      cmbCities.Add(cCity["cityname"])
    Next

    cmbCities_Click()
  Else
    Message.Error("Unable to retrieve Country/City List\n" & cData["error"])
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub txtCitySearch_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub txtApiKey_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub cmbCities_Click()

  If $cCity Then
    If $cCity.Count >= 1 Then
      txtCityId.Text = $cCity[cmbCities.Text]
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Validate input fields
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ValidInput() As Boolean

  Dim iCityId As Integer

  If Not cmbCities.Text Then
    Balloon(("Please select a city!"), cmbCities)
    Return False
  Endif

  If Not txtCityId.Text Then
    Balloon(("Please select a city!"), cmbCities)
    Return False
  Endif

  If Not txtApiKey.Text Then
    Balloon(("Please enter your OpenWeatherMap API key!"), txtApiKey)
    Return False
  Endif

  Try iCityId = Val(txtCityId.Text)
  If Error Or If iCityId <= 0 Then
    Balloon(("Please select a valid city code!"), cmbCities)
    Return False
  Endif

  Return True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnSave_Click()

  If Not ValidInput() Then Return

  '--- save new OpenWeatherMap settings ---
  Main.hDB.Exec("UPDATE settings_openweathermap SET debug = &1, city = &2, cityid = &3, enabled = &4, apikey = &5, polltime = &6 WHERE id = 1", chkDebug.Value, cmbCities.Text, txtCityId.Text, chkEnabled.Value, txtApiKey.Text, txtRefresh.Text)

  If Main.bServer Then
    Plugin.Restart("OpenWeatherMap", 1)
  Else
    Client.Plugin_Restart("OpenWeatherMap")
  Endif

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_openweathermap", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    $cCity[rResult!city] = rResult!cityid
    cmbCities.Add(rResult!city)
    cmbCities.Text = rResult!city
    txtCityId.Text = rResult!cityid
    chkDebug.Value = rResult!debug
    txtApiKey.Text = rResult!apikey
    txtRefresh.Text = rResult!polltime
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  cmbCities.Enabled = chkEnabled.Value
  txtCityId.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnCitySearch.Enabled = chkEnabled.Value
  cmbCities.Enabled = chkEnabled.Value
  txtCitySearch.Enabled = chkEnabled.Value
  txtRefresh.Enabled = chkEnabled.Value

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Open web page for directly registering the api key
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://openweathermap.org/appid")

End