' Gambas class file

' Description:
' FSettingsWeatherUnderground.class
' Settings form for wunderground API support.

' Development Status:
' Development just started.

' Links:
' http://www.wundeground.com

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub btnCitySearch_Click()

  Dim cData As Collection
  Dim sStr As String
  Dim hWeather As New CWeatherUnderground

  btnSave.Enabled = True

  If Not txtApiKey.Text Then
    Balloon(("Please enter your WeatherUnderground API key!"), txtApiKey)
    Return
  Endif

  cData = hWeather.WeatherGetCity(txtApiKey.Text, txtCitySearch.Text)

  cmbCity.Clear

  If cData.Exist("city") Then
    For Each sStr In cData["city"]
      cmbCity.Add(sStr)
    Next
  Else
    Message.Error("Unable to retrieve Country/City List\n" & cData["error"])
  Endif

End

Public Sub txtCitySearch_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtApiKey_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not cmbCity.Text Then
    Balloon(("Please select a city!"), cmbCity)
    Return False
  Endif

  If Not txtApiKey.Text Then
    Balloon(("Please enter your WeatherUnderground API key!"), txtApiKey)
    Return False
  Endif

  Return True

End

Public Sub btnSave_Click()

  If Not ValidInput() Then Return

  '--- save new WeatherUnderground settings ---
  Main.hDB.Exec("UPDATE settings_weatherug SET debug = &1, city = &2, enabled = &3, apikey = &4, polltime = &5 WHERE id = 1", chkDebug.Value, cmbCity.Text, chkEnabled.Value, txtApiKey.Text, txtRefresh.Text)

  If Main.bServer Then
    Plugin.Restart("WeatherUnderground", 1)
  Else
    Client.ModuleRestart("WeatherUnderground")
  Endif

  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.wunderground.com/weather/api")

End

Public Sub txtRefresh_KeyPress()

  btnSave.Enabled = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_weatherug", iRow)

  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    cmbCity.Add(rResult!city)
    cmbCity.Text = rResult!city
    chkDebug.Value = rResult!debug
    txtRefresh.Text = rResult!polltime
    txtApiKey.Text = rResult!apikey
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  cmbCity.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnCitySearch.Enabled = chkEnabled.Value
  txtCitySearch.Enabled = chkEnabled.Value
  txtRefresh.Enabled = chkEnabled.Value

End
