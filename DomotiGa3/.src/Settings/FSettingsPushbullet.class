' Gambas class file

' Description:
' FSettingsPushbullet.class
' Settings form for Pushbullet API support.

' Development Status:
' Development just started.

' Links:
' http://pushbullet.com/api

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public vResponse As Variant
Public vDevices As Variant

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' TODO: add more instances
  If LoadSettings(1) = False Then
    ' TODO: Handle problems
    Message.Error("Couldn't load settings table, perhaps database is too old?")
    Try Me.Close
    Return
  Endif

  EnableDisableSettings()
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  LoadSettings(0)
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  EnableDisableSettings()
  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not txtToken.Text Then
    Balloon(("Please enter your Pushbullet Access token"), txtToken)
    Return False
  Endif
  Return True

End

Public Sub btnSave_Click()

  If chkEnabled.Value Then
    If Not ValidInput() Then Return
  Endif

  '--- save new Pushbullet settings ---
  Main.hDB.Exec("UPDATE settings_pushbullet SET enabled = &1, debug = &2, token = &3, device = &4  WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtToken.Text, txtDeviceId.Text)

  If Main.bServer Then
    Plugin.Restart("Pushbullet", 1)
  Else
    Client.Plugin_Restart("Pushbullet")
  Endif
  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.pushbullet.com/")

End

Public Sub btnTestToken_Click()

  Dim hPushbullet As New CPushbullet

  If txtToken.Text Then
    vResponse = hPushbullet.GetDevices(txtToken.Text, chkDebug.Value)
    If TypeOf(vResponse) = gb.String Then
      Message.Error("Error:\n" & vResponse)
    Else
      Message.Error("Your Pushbullet Access token is valid!")
    Endif
  Else
    Balloon(("Please enter your Pushbullet API token"), txtToken)
  Endif

End

Public Sub btnSendMessage_Click()

  Dim sResponse As String
  Dim hPushbullet As New CPushbullet

  If txtToken.Text Then
    sResponse = hPushbullet.PostPushbulletTest(txtToken.Text, txtDeviceId.Text, "DomotiGa", "Test pushbullet message from DomotiGa :-)", chkDebug.Value)
    If Not sResponse Then
      Message.Info("Pushbullet message successfully sent!")
    Else
      Message.Error("Failed to send Pushbullet message!\n" & sResponse)
    Endif
  Else If Not txtToken
    Balloon(("Please enter your Pushbullet Access token!"), txtToken)
  Endif

End

Public Sub txtToken_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtDeviceId_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbDevice_Click()

  btnSave.Enabled = True

  If cmbDevice.Index = 0 Then
    txtDeviceId.Text = ""
  Else If cmbDevice.Index >= 1 Then ' skip empty first line
    Try txtDeviceId.Text = vResponse[cmbDevice.Index - 1]["iden"]
  Else
    txtDeviceId.Text = ""
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Load table row
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub LoadSettings(iRow As Integer) As Boolean

  Dim rResult As Result

  rResult = Plugin.GetSettingsTable("settings_pushbullet", iRow)
  If rResult Then
    If rResult.Count Then
      chkEnabled.Value = rResult!enabled
      chkDebug.Value = rResult!debug
      txtToken.Text = rResult!token
      txtDeviceId.Text = rResult!device
      Return True
    Else
      Return False
    Endif
  Else
    Return True
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Enable/Disable all input fields depending on enable value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub EnableDisableSettings()

  chkDebug.Enabled = chkEnabled.Value
  txtToken.Enabled = chkEnabled.Value
  cmbDevice.Enabled = chkEnabled.Value
  txtDeviceId.Enabled = chkEnabled.Value
  btnTestToken.Enabled = chkEnabled.Value
  btnSendMessage.Enabled = chkEnabled.Value

End

Public Sub btnFetchDevices_Click()

  Dim hPushbullet As New CPushbullet

  If txtToken.Text Then
    vResponse = hPushbullet.GetDevices(txtToken.Text, chkDebug.Value)
    If TypeOf(vResponse) = gb.String Then
      Message.Error("Error:\n" & vResponse)
    Else
      cmbDevice.Clear
      cmbDevice.Add("All")
 
      If vResponse <> Null Then
        If vResponse.Count Then
          For Each vDevices In vResponse
            If vDevices["extras"]["nickname"] Then
              cmbDevice.Add(vDevices["extras"]["nickname"])
            Else
              cmbDevice.Add(vDevices["extras"]["manufacturer"] & " " & vDevices["extras"]["model"])
            Endif
          Next
        Endif
      Endif
    Endif
  Else
    Balloon(("Please enter your Pushbullet Access token!"), txtToken)
  Endif

End
