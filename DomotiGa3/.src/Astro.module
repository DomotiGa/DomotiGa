' Gambas module file

' Description:
' Astro.module
' Calculate Sun and Moon related things.

' Development Status:
' Ported by Dali from link below.

' Links:
' http://williams.best.vwh.net/sunrise_sunset_algorithm.htm
' http://www.timeanddate.com/worldclock/astronomy.html?n=1382

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Function CalcSunTimes(lon As Float, lat As Float, timezone As Float, isRise As Boolean, twilight As Integer) As String

  Dim n, lnghour, m, l, ra, lquadrant, raquadrant, sindec, cosdec, coshour, zenith, h, t, ut, ihour, imin, gTime As Float
  Dim localt As Integer

  ' twilight setting
  If (twilight = 0) Then zenith = 90 + (50 / 60) ' effective sunrise/sunset
  If (twilight = 1) Then zenith = 96 ' civil twilight (brightest)
  If (twilight = 2) Then zenith = 102 ' nautical twilight
  If (twilight = 3) Then zenith = 108 ' astronomical twilight (darkest)

  ' first calculate the day of the year
  n = DateDiff("01/01/" & Year(Now()), Now(), gb.day)

  ' convert the longitude to hour value and calculate an approximate time
  lnghour = lon / 15
  If (isRise) Then
    t = n + ((6 - lnghour) / 24) ' looking for sunrise
  Else
    t = n + ((18 - lnghour) / 24) ' looking for sunset
  Endif

  ' calculate the Sun's mean anomaly
  m = (0.9856 * t) - 3.289

  ' calculate the Sun's true longitude
  l = m + (1.916 * Sin(Rad(m))) + (0.020 * Sin(Rad(2 * m))) + 282.634
  ' L potentially needs to be adjusted into the range [0,360] by adding/subtracting 360
  If (l < 0) Then
    l += 360
  Else If (l > 360) Then
    l -= 360
  Endif

  ' calculate the Sun's right ascension
  ra = Deg(ATan(0.91764 * Tan(Rad(l))))
  ' RA potentially needs to be adjusted into the range [0,360] by adding/subtracting 360
  If (ra < 0) Then
    ra += 360
  Else If (ra > 360) Then
    ra -= 360
  Endif

  ' right ascension value needs to be in the same quadrant as L
  lquadrant = Fix(l / 90) * 90
  raquadrant = Fix(ra / 90) * 90
  ra = ra + (lquadrant - raquadrant)

  ' right ascension value needs to be converted into hours
  ra = ra / 15

  ' calculate the Sun's declination
  sindec = 0.39782 * Sin(Rad(l))
  cosdec = Cos(Rad(Deg(ASin(sindec))))

  ' calculate the Sun's local hour angle
  coshour = (Cos(Rad(zenith)) - (sindec * Sin(Rad(lat)))) / (cosdec * Cos(Rad(lat)))

  ' this is not working properly, trying to figure out why...
  If isRise And If (coshour > 1) Then Print "Midnight sun"
  If Not isRise And If (coshour < -1) Then Print "Polar night"

  ' finish calculating H and convert into hours
  If (isRise) Then
    h = 360 - Deg(ACos(coshour)) ' looking for sunrise
  Else
    h = Deg(ACos(coshour)) ' looking for sunset
  Endif
  h = h / 15

  ' calculate local mean time of rising/setting
  t = h + ra - (0.06571 * t) - 6.622

  ' adjust back to UTC
  ut = t - lnghour
  ' UT potentially needs to be adjusted into the range [0,24] by adding/subtracting 24
  If (ut < 0) Then
    ut += 24
  Else If (ut > 24) Then
    ut -= 24
  Endif

  ' convert UT value to local Time zone of latitude / longitude
  ' convert hours to seconds
  ' add seconds from Jan 1st, 1970 until todays date to convert value to unix time (gambas doesnt calculate time in the unix way)
  localt = Int((CFloat(Date(Year(Now()), Month(Now()), Day(Now))) - CFloat(Date(1970, 1, 1))) * 86400) + (ut + timezone) * 60 * 60

  ' add unix time to Jan 1st, 1970 to get gambas time
  gTime = DateAdd(Date(1970, 1, 1), gb.Second, localt)

  ' get hour and minute for sunset/sunrise
  ihour = Hour(gTime)
  imin = Minute(gTime)

  Return Format(ihour, "0#") & ":" & Format(imin, "0#")

End

Public Sub CalcMoonPhase()

  Dim yy, mm, k1, k2, k3, jd, y, d, m As Integer
  Dim ip, dp, np, rp As Float
  Dim ag As Float ' moon's age
  Dim di As Float ' moon's distance in earth radii
  Dim la As Float ' moon's ecliptic latitude
  Dim lo As Float ' moon's ecliptic longitude
  Dim phase, zodiac As String

  y = Year(Now)
  m = Month(Now)
  d = Day(Now)

  ' calculate the Julian Date at 12h UT
  YY = Y - Int((12 - M) / 10)
  MM = M + 9
  If (MM >= 12) Then MM = MM - 12

  K1 = Int(365.25 * (YY + 4712))
  K2 = Int(30.6 * MM + 0.5)
  K3 = Int(Int((YY / 100) + 49) * 0.75) - 38

  JD = K1 + K2 + D + 59 ' for dates in Julian calendar
  If (JD > 2299160) Then JD = JD - K3 ' for Gregorian calendar

  ' calculate moon's age in days
  IP = normalize((JD - 2451550.1) / 29.530588853)
  AG = IP * 29.53

  If (AG < 1.84566) Then
    Phase = ("New")
  Else If (AG < 5.53699) Then
    Phase = ("Evening crescent")
  Else If (AG < 9.22831) Then
    Phase = ("First quarter")
  Else If (AG < 12.91963) Then
    Phase = ("Waxing gibbous")
  Else If (AG < 16.61096) Then
    Phase = ("Full")
  Else If (AG < 20.30228) Then
    Phase = ("Waning gibbous")
  Else If (AG < 23.99361) Then
    Phase = ("Last quarter")
  Else If (AG < 27.68493) Then
    Phase = ("Morning crescent")
  Else
    Phase = ("New")
  Endif

  IP = IP * 2 * Pi ' convert phase to radians

  ' calculate moon's distance
  DP = 2 * Pi * normalize((JD - 2451562.2) / 27.55454988)
  DI = 60.4 - 3.3 * Cos(DP) - 0.6 * Cos(2 * IP - DP) - 0.5 * Cos(2 * IP)

  ' calculate moon's ecliptic latitude
  NP = 2 * Pi * normalize((JD - 2451565.2) / 27.212220817)
  LA = 5.1 * Sin(NP)

  ' calculate moon's ecliptic longitude
  RP = normalize((JD - 2451555.8) / 27.321582241)
  LO = 360 * RP + 6.3 * Sin(DP) + 1.3 * Sin(2 * IP - DP) + 0.7 * Sin(2 * IP)

  If (LO < 33.18) Then
    Zodiac = ("Pisces")
  Else If (LO < 51.16) Then
    Zodiac = ("Aries")
  Else If (LO < 93.44) Then
    Zodiac = ("Taurus")
  Else If (LO < 119.48) Then
    Zodiac = ("Gemini")
  Else If (LO < 135.30) Then
    Zodiac = ("Cancer")
  Else If (LO < 173.34) Then
    Zodiac = ("Leo")
  Else If (LO < 224.17) Then
    Zodiac = ("Virgo")
  Else If (LO < 242.57) Then
    Zodiac = ("Libra")
  Else If (LO < 271.26) Then
    Zodiac = ("Scorpio")
  Else If (LO < 302.49) Then
    Zodiac = ("Sagittarius")
  Else If (LO < 311.72) Then
    Zodiac = ("Capricorn")
  Else If (LO < 348.58) Then
    Zodiac = ("Aquarius")
  Else
    Zodiac = ("Pisces")
  Endif

  ' so longitude is not greater than 360!
  If (LO > 360) Then LO = LO - 360

  Main.MoonPhase = Phase
  Main.MoonZodiac = Zodiac
  Main.MoonAge = Round(ag, -2)
  Main.MoonDistance = Round(di, -2)
  Main.MoonLatitude = Round(la, -2)
  Main.MoonLongitude = Round(lo, -2)
  Main.MoonIsLeapYear = isleapyear(y)

End

Private Function normalize(v As Float) As Float

  v = v - Int(v)
  If (v < 0) Then v = v + 1
  Return v

End

Private Function isleapyear(y As Integer) As Boolean

  Dim x, w, z As Integer

  x = Int(y - 4 * Int(y / 4))
  w = Int(y - 100 * Int(y / 100))
  z = Int(y - 400 * Int(y / 400))

  If (x = 0) Then ' possible leap Year
    If ((w = 0) & (Not z = 0))
      Return False ' not a leap Year
    Else
      Return True ' is leap Year
    Endif
  Endif
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Cosine function with degree as input
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CalcCos(fF As Float) As Float

  Return Cos(fF * Pi / 180)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Calculate 24 Periodic Terms
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CalcPeriodic24(T As Float) As Float

  Dim A As Integer[] = [485, 203, 199, 182, 156, 136, 77, 74, 70, 58, 52, 50, 45, 44, 29, 18, 17, 16, 14, 12, 12, 12, 9, 8]
  Dim B As Float[] = [324.96, 337.23, 342.08, 27.85, 73.14, 171.52, 222.54, 296.72, 243.58, 119.81, 297.17, 21.02, 247.54, 325.15, 60.93, 155.12, 288.79, 198.04, 199.76, 95.39, 287.11, 320.81, 227.73, 15.45]
  Dim C As Float[] = [1934.136, 32964.467, 20.186, 445267.112, 45036.886, 22518.443, 65928.934, 3034.906, 9037.513, 33718.147, 150.678, 2281.226, 29929.562, 31555.956, 4443.417, 67555.328, 4562.452, 62894.029, 31436.921, 14577.848, 31931.756, 34777.259, 1222.114, 16859.074]
  Dim fP24 As Float
  Dim iCount As Integer

  fP24 = 0
  For iCount = 0 To 23 Step 1
    fP24 += A[iCount] * CalcCos(B[iCount] + (C[iCount] * T))
  Next

  Return fP24

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Calculate the Equiniox or Solstice, this will give us the date/time of the season change
' Northern Hemisphere: 1=Spring, 2=Summer, 3=Fall/Autumn, 4=Winter
' Southern Hemisphere: 1=Fall/Autumn, 2=Winter, 3=Spring, 4=Summer 
' Latitude >= 0 == Northern, Latitude < 0 == Southern
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub CalcEquilSol(iYear As Integer, iSeason As Integer) As Float

  Dim fJDE As Float
  Dim fY As Float
  Dim fT As Float
  Dim fW As Float

  fY = (iYear - 2000) / 1000

  ' Calculate initial estimate of date event
  Select iSeason
    Case 1
      fJDE = 2451623.80984 + 365242.37404 * fY + 0.05169 * (fY ^ 2) - 0.00411 * (fY ^ 3) - 0.00057 * (fY ^ 4)
    Case 2
      fJDE = 2451716.56767 + 365241.62603 * fY + 0.00325 * (fY ^ 2) + 0.00888 * (fY ^ 3) - 0.00030 * (fY ^ 4)
    Case 3
      fJDE = 2451810.21715 + 365242.01767 * fY - 0.11575 * (fY ^ 2) + 0.00337 * (fY ^ 3) + 0.00078 * (fY ^ 4)
    Case 4
      fJDE = 2451900.05952 + 365242.74049 * fY - 0.06223 * (fY ^ 2) - 0.00823 * (fY ^ 3) + 0.00032 * (fY ^ 4)
    Default ' Bail if the season is invalid
      Return 0
  End Select

  fT = (fJDE - 2451545.0) / 36525
  fW = (35999.373 * fT) - 2.47
  Return fJDE + ((0.00001 * CalcPeriodic24(fT)) / (1 + 0.0334 * CalcCos(fW) + 0.0007 * CalcCos(2 * fW)))

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Convert Julian Date to localtime
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub CalcJDtoLocalTime(fJD As Float) As Date

  Dim iYear, iMonth, iDay, iHour, iMin, iSec As Integer
  Dim B, C, D, E, Z As Integer
  Dim F, H, M, DT, fDelta As Float
  Dim dDate As Date

  Z = Floor(fJD + 0.5)
  F = (fJD + 0.5) - Z
  If (Z < 2299161) Then
    B = Z + 1524
  Else
    B = Floor((Z - 1867216.25) / 36524.25)
    B = Z + 1 + B - Floor(B / 4) + 1524
  Endif

  C = Floor((B - 122.1) / 365.25)
  D = Floor(365.25 * C)
  E = Floor((B - D) / 30.6001)
  DT = B - D - Floor(30.6001 * E) + F
  iMonth = E - IIf(E < 13.5, 1, 13)
  iYear = C - IIf(iMonth > 2.5, 4716, 4715)
  iDay = Floor(DT)
  H = 24 * (DT - iDay)
  iHour = Floor(H)
  M = 60 * (H - iHour)
  iMin = Floor(M)
  iSec = Floor(60 * (M - iMin))

  ' Calculate simple the TDT (Terrestrial Dynamical Time) offset to UTC in seconds
  If iYear >= 2000 Then
    fDelta = (iYear - 2000) / 100
    fDelta = 102 + 102 * fDelta + 25.3 * fDelta ^ 2
    fDelta += 0.37 * (iYear - 2100)
  Endif

  ' Calculate the TDT date/time from the Julian Date
  dDate = Date(iYear, iMonth, iDay, iHour, iMin, iSec)

  ' Add/substract TDT offset to get the UTC date/time
  dDate = DateAdd(dDate, gb.Second, CInt(Ceil(fDelta) * -1))

  ' Add/substract timezone information
  dDate = DateAdd(dDate, gb.Minute, CInt(Main.fAstroAutoTimezone * 60))

  Return dDate

End

