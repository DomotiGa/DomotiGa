' Gambas class file

' Description:
' CTemp08.class
' Support for Midon's TEMP08 1-wire interface.

' Development Status:
' Working, but only temp sensors implemented tested yet.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "Temp08"
Public PluginType As Integer = Plugin.Type_Class
Public PluginFriendlyName As String = "Midon Temp08"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Integer = 1

Public KeyName As String
Public LogLabel As String = "[Temp08] "
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $sSerPort As String
Private $sBaud As String
Private $bTemp08Debug As Boolean

Public hTemp08 As New SerialPort
Public tTemp08 As New Timer

Public sBuffer As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)

  KeyName = cPl.KeyName
  Instance = cPl.Instance
  If Instance <> 1 Then LogLabel = Replace(LogLabel, "] ", "#" & Instance & "] ")

  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $sSerPort = cPl.Settings["serialport"]
  $sBaud = cPl.Settings["baudrate"]
  $bTemp08Debug = cPl.Settings["debug"]
 
  ' Connect/Initialize connection
  Connect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for EACH Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()

  Try Disconnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open serial port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the port
  Try hTemp08.Close

  ' get a new one
  hTemp08 = New Serialport As "Temp08"

  With hTemp08
    .PortName = $sSerPort
    .Speed = $sBaud
    .Parity = 0
    .DataBits = 8
    .StopBits = 1
    .Open()
  End With

  ' start poll timer for Temp08 status LED
  tTemp08 = New Timer As "tTemp08LED"
  tTemp08.Delay = 250
  tTemp08.Stop

  ' Write to main logfile we connected successfully
  Main.WriteLog(LogLabel & PluginFriendlyName & " interface connected to Port " & $sSerPort)
  IsRunning = True

  ' All went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " interface FAILED to connect to Port " & $sSerPort)
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

Public Sub Temp08_Read()

  Dim sData As String

  Try sData = Read #hTemp08, 1
  If Error Then Main.WriteDebugLog(LogLabel & "Error reading data from serial port! -> " & Error.Text)
  If sData = Chr(10) Then ' buffer until newline then parse
    If Len(sBuffer) > 1 Then ParseLine(sBuffer)
    sBuffer = Null
  Else
    sBuffer &= sData
  Endif

End

Public Sub ParseLine(sStr As String)

  Dim iDeviceId As Integer
  Dim aScan As String[]

  Main.ControlLed("OneWire", "On")
  If Main.bServer Then tTemp08.Start

  ' example output
  ' SUN 00:36:20
  ' Reading Sensors...
  ' Voltage #01[4 D0000004FC78A26] = 00.32 V 04.98 V
  ' Temp #01[4D0000004FC78A26]=23.75C
  ' Temp #02[150008013A024910]=22.43C
  ' Temp #03[7A0008014A6BC310]=22.68C

  If $bTemp08Debug Then Main.WriteDebugLog(LogLabel & sStr)

  ' Temp08 temp sensor support
  If Mid$(sStr, 1, 4) = "Temp" Then
    If $bTemp08Debug Then Main.WriteDebugLog(LogLabel & "We got DS1820 temperature data.")
    aScan = Scan(sStr, "*[[]*]=*")
    If aScan.Count = 3 Then
      iDeviceId = Devices.Find(Instance, aScan[1], Devices.FindInterface("Midon TEMP08"), "DS18B20")
      If iDeviceId Then Devices.ValueUpdateExt(iDeviceId, 1, LTrim(Left(aScan[2], -2)), Devices.ValueType_Temperature)
    Endif
  Endif
  ' put support for other sensortypes here

End

Public Sub tTemp08LED_Timer()

  Main.ControlLed("OneWire", "Off")
  tTemp08.Stop

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try hTemp08.Close

  Main.WriteLog(LogLabel & PluginFriendlyName & " Port closed.")

  ' all ok
  Return True

Finally
  IsRunning = False
  ErrorText = ""
  ErrorWhere = ""

Catch
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  Return False

End

