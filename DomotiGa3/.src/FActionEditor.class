' Gambas class file

' Description:
' FActionEditor.class
' Support for creating and edit actions.

' Development Status:
' Working ok.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private rResult As Result
Private rResultDevices As Result
Public bAddAction As Boolean
Private iSavedActionType As Integer
Public bActionEdit As Boolean

Public Sub Form_Open()

  Dim vValue As Variant

  If bAddAction Then Me.Text = ("Create Action")
  Me.Move(FMain.X + 50, FMain.Y + 70)

  ' fill combo with available global vars
  cmbVariables.Add("")
  For Each vValue In Main.GlobalVar
    cmbVariables.Add(Main.GlobalVar.Key)
  Next

  ' fill combo with available devices
  cmbDevices.Add("")
  cmbDevicesJson.Add("")
'  rResultDevices = Main.hDB.Exec("SELECT * FROM devices WHERE enabled is TRUE ORDER BY name")
  rResultDevices = Devices.FindAllEnabledDevices("name")
  If rResultDevices Then
    If rResultDevices.Count Then
      For Each rResultDevices
        cmbDevices.Add(rResultDevices!name)
        cmbDevicesJson.Add(rResultDevices!name)
      Next
    Endif
  Else
    Message.Info(("Error: couldn't find devices table!"))
    Return
  Endif
  
   ' fill combo with json post or get
  cmbJsonMethode.Add("")
  cmbJsonMethode.Add("Post")
  cmbJsonMethode.Add("Get")

  If Not bAddAction Then
    ' fetch action details
    If bActionEdit Then
      Try rResult = Main.hDB.Exec("SELECT * FROM actions WHERE id = &1", FEvents.txtCurEvent)
    Else
      Try rResult = Main.hDB.Exec("SELECT * FROM actions WHERE id = &1", FEventEditor.iAction)
    Endif
    If Not rResult Then
      Message.Info(("Error: couldn't find trigger details!"))
      Return
    Endif
    If rResult.Count = 1 Then
      txtActionName.Text = rResult!name
      txtDescription.Text = rResult!description
      iSavedActionType = rResult!type
      ' preload controls to values from database
      Select rResult!type
        Case 1 ' set device value
          If rResult!param1 Then cmbDevices.Text = Devices.FindNameForDevice(rResult!param1)
          cmbValue.Index = rResult!param2
          txtDeviceValue.Text = rResult!param3
        Case 2 ' set global var
          cmbVariables.Text = rResult!param1
          txtVariableValue.Text = rResult!param2
        Case 3 ' sent e-mail
          txtToAddress.Text = rResult!param1
          txtSubject.Text = rResult!param2
          txtMessage.Text = rResult!param3
        Case 4 ' speak text
          cmbSpeakVoice.Text = rResult!param1
          txtSpeakText.Text = rResult!param2
        Case 5 ' execute command
          txtExecuteCmd.Text = rResult!param1
        Case 6 ' send tweet
          txtSendTweet.Text = rResult!param1
        Case 7 ' send SMS
          txtToSMS.Text = rResult!param1
          txtSMSMessage.Text = rResult!param2
        Case 8 ' send IRTRans
          txtIRCommand.Text = rResult!param1
        Case 9 ' play sound
          txtSound.Text = rResult!param1
          spVolume.Value = rResult!param2
        Case 10 ' write log
          txtLog.Text = rResult!param1
        Case 11 ' display text on led matrix
          txtLEDMessage.Text = rResult!param1
          vbID.Value = rResult!param2
          cmbColor.Text = rResult!param3
          cmbSpeed.Text = rResult!param4
        Case 12 ' av control command
          cmbAVModel.Text = rResult!param1
          cmbAVCommand.Text = rResult!param2
          cmbAVValue.Text = rResult!param3
          txtAVAddress.Text = rResult!param4
        Case 13 ' delay timer
          vbDelay.Value = rResult!param1
          If rResult!param3 = "random" Then
            chkRandom.Value = True
            vbDelayMax.Value = rResult!param2
          Else
            chkRandom.Value = False
          Endif
          chkRandom_Click()
        Case 14 ' notify-send
          txtNotifyTitle.Text = rResult!param1
          txtNotifyText.Text = rResult!param2
        Case 15 ' script
          txtScript.Text = rResult!param1
        Case 16 ' json
           If rResult!param1 Then cmbDevicesJson.Text = Devices.FindNameForDevice(rResult!param1)
           If rResult!param2 Then cmbJsonMethode.Text = rResult!param2
           If rResult!param3 Then TextBoxJsonUrl.Text = rResult!param3
      End Select
      Try tabActionTypes.Index = rResult!type - 1
      tabActionTypes.SetFocus()
      btnRun.Enabled = True
    Else
      bAddAction = True
    Endif
  Endif

End

Public Sub txtActionName_Change()

  EnableButtons()

End

Public Sub tabActionTypes_Click()

  EnableButtons()

End

Private Sub EnableButtons()

  btnSave.Enabled = True
  btnDeleteAction.Enabled = True
  btnNewAction.Enabled = True
  btnRun.Enabled = False

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim iType As Integer
  Dim sSql, sParam1, sParam2, sParam3, sParam4, sParam5 As String

  If Not txtActionName.Text Then
    Balloon(("Please enter a name for this action!"), txtActionName)
    Return
  Endif

  If Not bAddAction Then
    ' name has changed
    If rResult!name <> txtActionName.Text Then
      If ActionNameExist(txtActionName.Text) Then
        Balloon(("Please enter a unique name for this action!"), txtActionName)
        Return
      Endif
    Endif
  Endif
  iType = tabActionTypes.Index + 1
  ' active tab is different then saved action type
  If iType <> iSavedActionType Then
    Message("You changed action type, sure to save current active tab?", "Ok")
  Endif

  Select iType
    Case 1 ' set device
      If Not cmbDevices.Text Then
        Balloon(("Please select a device!"), cmbDevices)
        Return
      End If
      sParam1 = Devices.FindIdForDevice(cmbDevices.Text)
      If Not cmbValue.Text Then
        Balloon(("Please select a field to set!"), cmbValue)
        Return
      End If
      sParam2 = cmbValue.Index
      If Not txtDeviceValue.Text Then
        Balloon(("Please enter a value to set!"), txtDeviceValue)
        Return
      End If
      sParam3 = txtDeviceValue.Text
    Case 2 ' set globalvar
      If Not cmbVariables.Text Then
        Balloon(("Please select or enter a new variable!"), cmbVariables)
        Return
      End If
      sParam1 = cmbVariables.Text
      If Not txtVariableValue.Text Then
        Balloon(("Please enter a value to set!"), txtVariableValue)
        Return
      End If
      sParam2 = txtVariableValue.Text
    Case 3 ' send e-mail message
      If Not txtToAddress.Text Then
        Balloon(("Please enter a e-mail address!"), txtToAddress)
        Return
      End If
       If Not txtSubject.Text Then
        Balloon(("Please enter a subject!"), txtSubject)
        Return
      End If
       If Not txtMessage.Text Then
        Balloon(("Please enter a msg to send!"), txtMessage)
        Return
      End If
      sParam1 = txtToAddress.Text
      sParam2 = txtSubject.Text
      sParam3 = txtMessage.Text
    Case 4 ' speak text
      If Not cmbSpeakVoice.Text Then
        Balloon(("Please enter a voicename or select type!"), cmbSpeakVoice)
        Return
      End If
      If Not txtSpeakText.Text Then
        Balloon(("Please enter a text to speak!"), txtSpeakText)
        Return
      End If
      sParam1 = cmbSpeakVoice.Text
      sParam2 = txtSpeakText.Text
    Case 5 ' run shell command
      If Not txtExecuteCmd.Text Then
        Balloon(("Please enter a command to execute!"), txtExecuteCmd)
        Return
      End If
      sParam1 = txtExecuteCmd.Text
    Case 6 ' send tweet message
      If Not txtSendTweet.Text Then
        Balloon(("Please enter a message to tweet!"), txtSendTweet)
        Return
      End If
      sParam1 = txtSendTweet.Text
    Case 7 ' send SMS message
      If Not txtToSMS.Text Then
        Balloon(("Please enter a number to SMS to!"), txtToSMS)
        Return
      End If
      If Not txtSMSMessage.Text Then
        Balloon(("Please enter a message to send!"), txtSMSMessage)
        Return
      End If
      sParam1 = txtToSMS.Text
      sParam2 = txtSMSMessage.Text
    Case 8 ' send IRTrans command
      If Not txtIRCommand.Text Then
        Balloon(("Please enter a command string!"), txtIRCommand)
        Return
      End If
      sParam1 = txtIRCommand.Text
    Case 9 ' play sound file
      If Not txtSound.Text Then
        Balloon(("Please enter a sound to play!"), txtSound)
        Return
      End If
      sParam1 = txtSound.Text
      sParam2 = spVolume.Value
    Case 10 ' write log
      If Not txtLog.Text Then
        Balloon(("Please enter a text to log!"), txtLog)
        Return
      End If
      sParam1 = txtLog.Text
    Case 11 ' display text on led matrix
      If Not txtLEDMessage.Text Then
        Balloon(("Please enter a text to display!"), txtLEDMessage)
        Return
      End If
      If Not vbID.Text Then
        Balloon(("Please enter an display id!"), vbID)
        Return
      End If
      sParam1 = txtLEDMessage.Text
      sParam2 = vbID.Value
    Case 12 ' av control command
      If Not cmbAVModel.Text Then
        Balloon(("Please select the equipment model!"), cmbAVModel)
        Return
      End If
      If Not cmbAVCommand.Text Then
        Balloon(("Please enter a command to send!"), cmbAVCommand)
        Return
      End If
      sParam1 = cmbAVModel.Text
      sParam2 = cmbAVCommand.Text
      sParam3 = cmbAVValue.Text
      sParam4 = txtAVAddress.Text
    Case 13 ' delay timer
      If vbDelay.Text = 0 Then
        Balloon(("Please enter a delay value!"), vbDelay)
        Return
      End If
      If chkRandom.Value Then
        If vbDelayMax.Text = 0 Then
          Balloon(("Please enter a max delay value!"), vbDelayMax)
          Return
        End If
        If vbDelayMax.Value < vbDelay.Value Then
          Balloon(("Please enter a value higher than min delay value!"), vbDelayMax)
          Return
        End If
        sParam2 = vbDelayMax.Value
        sParam3 = "random"
      Else
        sParam2 = ""
        sParam3 = "fixed"
      Endif
      sParam1 = vbDelay.Value
    Case 14 ' notify-send
      If Not txtNotifyTitle.Text Then
        Balloon(("Please enter a title to display!"), txtNotifyTitle)
        Return
      End If
      If Not txtNotifyText.Text Then
        Balloon(("Please enter a text to display!"), txtNotifyText)
        Return
      End If
      sParam1 = txtNotifyTitle.Text
      sParam2 = txtNotifyText.Text
    Case 15 ' script
      If Not txtScript.Text Then
        Balloon(("Please enter a script to run!"), txtScript)
        Return
      End If
      sParam1 = txtScript.Text
    Case 16 ' JSON
      If Not cmbDevicesJson.Text Then
        Balloon(("Please select a device!"), cmbDevicesJson)
        Return
      End If
      sParam1 = Devices.FindIdForDevice(cmbDevicesJson.Text)
      
      If Not cmbJsonMethode.Text Then
        Balloon(("Please select a methode!"), cmbJsonMethode)
        Return
      End If
      sParam2 = cmbJsonMethode.Text
     
     If Not TextBoxJsonUrl.Text Then
        Balloon(("Please enter a url!"), TextBoxJsonUrl)
        Return
      End If
      sParam3 = TextBoxJsonUrl.Text
      
      
  End Select

  If bAddAction Then
    If ActionNameExist(txtActionName.Text) Then
      Balloon(("Please enter a unique name for this action!"), txtActionName)
      Return
    End If
    Main.hDB.Begin()
    rResult = Main.hDB.Create("actions")
    rResult!name = txtActionName.Text
    rResult!description = txtDescription.Text
    rResult!type = iType
    rResult!param1 = sParam1
    rResult!param2 = sParam2
    rResult!param3 = sParam3
    rResult!param4 = sParam4
    rResult!param5 = sParam5
    rResult.Update()
    Main.hDB.Commit()
  Else
    ' save new trigger settings
    sSql = "UPDATE actions SET name = &1, type = &2, param1 = &3, param2 = &4, param3 = &5, param4 = &6, param5 = &7, description = &8 WHERE id = &9"
    Try rResultUpdate = Main.hDB.Exec(sSql, txtActionName.Text, iType, sParam1, sParam2, sParam3, sParam4, sParam5, txtDescription.Text, rResult!id)
  Endif

  If FEventEditor.Id Then
    If FEventEditor.iEditingAction = 1 Then
      FEventEditor.GetActions()
      FEventEditor.cmbAction.Text = txtActionName.Text
    Endif
  Endif

  bAddAction = False
  Me.Close
  FEvents.GetEventList()

End

Public Sub btnNewAction_Click()

  bAddAction = True
  btnSave_Click()

End

Public Sub btnCancel_Click()

  Me.Close

End

Private Sub ActionNameExist(sName As String) As Boolean

  Dim rResultAction As Result

  rResultAction = Main.hDB.Exec("SELECT id FROM actions WHERE name = &1", sName)
  If rResultAction Then
    If rResultAction.Count Then
      Return True
    End If
  End If
  Return False

End

Public Sub btnDeleteAction_Click()

  Dim iId As Integer

  Select Message.Question(("Are you sure that you want to delete this action?"), ("Yes"), ("No"))
    Case 1
      iId = GetActionId(txtActionName.Text)
      If iId Then
        Main.hDB.Exec("DELETE FROM actions WHERE id = &1", iId)
        Main.hDB.Exec("DELETE FROM events_actions WHERE action = &1", iId)
      End If
      If FEventEditor.Id Then
        If FEventEditor.iEditingAction = 1 Then
          FEventEditor.GetActions
        Endif
      Endif
      Me.Close
      FEvents.GetEventList()
  End Select


End

Private Sub GetActionId(sName As String) As Integer

  Dim rResultAction As Result

  rResultAction = Main.hDB.Exec("SELECT id FROM actions WHERE name = &1", sName)
  Try Return rResultAction!id

End

Public Sub txtDescription_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbAVModel_Click()

  FillAVFields(cmbAVModel.Text, cmbAVCommand.Text, cmbAVValue.Text)
  EnableButtons

End

Public Sub cmbAVCommand_Click()

  FillAVValue(cmbAVModel.Text, cmbAVCommand.Index)
  EnableButtons

End

Private Sub FillAVFields(sModel As String, sCmd As String, sValue As String)

  Dim sTemp As String
  Dim iCnt As Integer

  cmbAVCommand.Clear
  cmbAVCommand.Add("")
  cmbAVValue.Clear
  cmbAVValue.Add("")
  Select Case LCase(sModel)
    Case "denon"
      If AVControl.sDenonCmds Then
        For Each sTemp In AVControl.sDenonCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
    Case "onkyo"
      If AVControl.sOnkyoCmds.Count Then
        For Each sTemp In AVControl.sOnkyoCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
    Case "iport"
      If AVControl.sIPortCmds Then
        For Each sTemp In AVControl.sIPortCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
    Case "squeezeserver"
      If AVControl.sSqueezeServerCmds Then
        For Each sTemp In AVControl.sSqueezeServerCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
    Case "sharptv"
      If AVControl.sSharpTVCmds Then
        For Each sTemp In AVControl.sSharpTVCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
    Case "lgtv"
      If AVControl.sLGTVCmds Then
        For Each sTemp In AVControl.sLGTVCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
    Case "pioneer"
      If AVControl.sPioneerCmds Then
        For Each sTemp In AVControl.sPioneerCmds
          cmbAVCommand.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
  End Select
  cmbAVCommand.Text = sCmd
  FillAVValue(sModel, cmbAVCommand.Index)

End

Private Sub FillAVValue(sModel As String, iCmd As Integer)

  Dim sTemp As String
  Dim iCnt As Integer
  Dim aScan As String[]

  cmbAVValue.Clear
  cmbAVValue.Add("")
  Select Case LCase(sModel)
    Case "denon"
      Try aScan = Split(AVControl.sDenonValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sDenonInfo[cmbAVCommand.index - 1]
    Case "onkyo"
      Try aScan = Split(AVControl.sOnkyoValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sOnkyoInfo[cmbAVCommand.index - 1]
    Case "iport"
      Try aScan = Split(AVControl.sIPortValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sIPortInfo[cmbAVCommand.index - 1]
    Case "squeezeserver"
      Try aScan = Split(AVControl.sSqueezeServerValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sSqueezeServerInfo[cmbAVCommand.index - 1]
    Case "sharptv"
      Try aScan = Split(AVControl.sSharpTVValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sSharpTVInfo[cmbAVCommand.index - 1]
    Case "lgtv"
      Try aScan = Split(AVControl.sLGTVValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sLGTVInfo[cmbAVCommand.index - 1]
    Case "pioneer"
      Try aScan = Split(AVControl.sPioneerValues[iCmd - 1], ",")
      If Not Error Then
        For Each sTemp In aScan
          cmbAVValue.Add(sTemp, iCnt + 1)
          Inc iCnt
        Next
      Endif
      If cmbAVCommand.Text Then Try txtAVHelp.Text = AVControl.sPioneerInfo[cmbAVCommand.index - 1]
    End Select

End

Public Sub btnAVTest_Click()

  AVControl.Set(cmbAVModel.Text, cmbAVCommand.Text, cmbAVValue.Text, txtAVAddress.Text)

End

Public Sub chkRandom_Click()

  If chkRandom.Value Then
    Label23.Text = "Min. Seconds"
    Label24.Enabled = True
    vbDelayMax.Enabled = True
  Else
    Label23.Text = "Seconds"
    Label24.Enabled = False
    vbDelayMax.Enabled = False
  Endif
  EnableButtons

End

Public Sub btnRun_Click()

  If Main.bServer Then
    Events.RunAction(rResult!id, 0, 0)
  Else
    XMLClient.RunAction(rResult!id)
  Endif
  Balloon("Action executed", btnRun)

End

Public Sub txtNotifyTitle_KeyPress()

  EnableButtons()

End

Public Sub txtNotifyText_KeyPress()

  EnableButtons()

End

Public Sub vbDelay_KeyPress()

  EnableButtons()

End

Public Sub vbDelayMax_KeyPress()

  EnableButtons()

End

Public Sub txtAVAddress_KeyPress()

  EnableButtons()

End

Public Sub cmbAVValue_Click()

  EnableButtons()

End

Public Sub txtLEDMessage_KeyPress()

  EnableButtons()

End

Public Sub cmbColor_Click()

  EnableButtons()

End

Public Sub cmbSpeed_Click()

  EnableButtons()

End

Public Sub vbID_KeyPress()

  EnableButtons()

End

Public Sub txtLog_KeyPress()

  EnableButtons()

End

Public Sub txtSound_KeyPress()

  EnableButtons()

End

Public Sub spVolume_Change()

  EnableButtons()

End

Public Sub txtIRCommand_KeyPress()

  EnableButtons()

End

Public Sub txtToSMS_KeyPress()

  EnableButtons()

End

Public Sub txtSMSMessage_KeyPress()

  EnableButtons()

End

Public Sub txtSendTweet_KeyPress()

  EnableButtons()

End

Public Sub txtExecuteCmd_KeyPress()

  EnableButtons()

End

Public Sub cmbSpeakVoice_Click()

  EnableButtons()

End

Public Sub txtSpeakText_KeyPress()

  EnableButtons()

End

Public Sub txtToAddress_KeyPress()

  EnableButtons()

End

Public Sub txtSubject_KeyPress()

  EnableButtons()

End

Public Sub txtMessage_KeyPress()

  EnableButtons()

End

Public Sub cmbVariables_Click()

  EnableButtons()

End

Public Sub txtVariableValue_KeyPress()

  EnableButtons()

End

Public Sub cmbDevices_Click()

  EnableButtons()

End

Public Sub cmbValue_Click()

  EnableButtons()

End

Public Sub txtDeviceValue_KeyPress()

  EnableButtons()

End

Public Sub btnScriptEditor_Click()

  txtScript.Text = FActionScriptEditor.Run(txtScript.Text)

End

Public Sub cmbDevicesJson_Click()

  EnableButtons()

End

Public Sub TextBoxJsonUrl_KeyPress()

    EnableButtons()

End

Public Sub cmbJsonMethode_Click()
  
  EnableButtons()
  
End


