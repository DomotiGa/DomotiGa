' Gambas module file

' Description:
' Mail.module
' Support for sending e-mails via SMTP and fetching Gmail RSS.

' Development Status:
' Working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "Mail"
Public PluginType As Integer = Plugin.Type_Module
Public PluginFriendlyName As String = "Mail"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Byte = 1

Public KeyName As String
Public LogLabel As String = "[Mail] "
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public tGmail As Timer
Private sOutput As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' start timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function GmailRun()

  ' start poll timer for Gmail
  tGmail = New Timer As "tGmail"
  tGmail.Delay = Main.iGmailPolltime * 1000 ' multiply for seconds
  tGmail.Start

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tGmail_Timer()

  CheckGmail()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' fetch xml from gmail
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub CheckGmail()

  ' fetch rss xml in background with help of wget
  Shell "wget -q -O - --user=" & Main.sGmailUser & " --password=" & Main.sGmailPassword & " https://mail.google.com/mail/feed/atom --no-check-certificate" For Read As "Gmail"
  If Main.bGmailDebug Then Main.WriteDebugLog("[Gmail] fetching RSS feed.")

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse gmail xml data
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Gmail_Read()

  Dim sXml As String
  Dim iCurrentMails As Integer

  sXml = Read #Last, -256
  sOutput &= sXml

  ' check if we got a complete xml document
  If InStr(sOutput, "</feed>") Then
    If Main.bGmailDebug Then Main.WriteDebugLog("[Gmail] parsing RSS xml.")
    iCurrentMails = Main.iNewMails
    ParseFeed(sOutput)
    ' IF IsString(Main.GetGlobalVar("Minute")) THEN Main.GetGlobalVar("Minute") = Val(Main.GetGlobalVar("Minute"))
    If Main.bGmailDebug Then Main.WriteDebugLog("[Gmail] you have " & Main.iNewMails & " new e-mails.")
    If (Main.iNewMails <> iCurrentMails) Or If Main.GetGlobalVar("Minute") Mod 30 = 0 Then
      If Main.iNewMails Then Main.WriteLog("[Gmail] You have " & Main.iNewMails & " new " & If(Main.iNewMails <> 1, "e-mails", "e-mail") & " on Gmail.")
      Main.SetGlobalVar("EmailCount", Main.iNewMails)
    Endif
    sOutput = Null
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse xml document
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub ParseFeed(data As String)

  Dim Xml As New XmlReader

  Try Xml.FromString(data)
  If Error Then Return

  Do While True
    Try Xml.Read()
    If Error Or Xml.Eof Then Return
    Select Xml.Node.Type
      Case XmlReaderNodeType.Element
        ' The root element
        PharseNode(Xml)
    End Select
  Loop

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' parse xml node
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub PharseNode(Xml As XmlReader)

  Dim sName As String

  ' if it has no children, stop
  If Xml.Node.IsEmptyElement Then Return

  ' check all nodes of the element
  sName = Xml.Node.Name
  Do While True
    Try Xml.Read()
    If Error Or Xml.Eof Then Break
    Select Xml.Node.Type
      Case XmlReaderNodeType.EndElement
        If Xml.Node.Name = sName Then Return
      Case XmlReaderNodeType.Element
        PharseNode(Xml)
      Case XmlReaderNodeType.Text
        ' look for element called fullcount
        If sName = "fullcount" Then
          Main.iNewMails = Val(Xml.Node.Value)
          Main.UpdateCounters()
        Endif
    End Select
  Loop

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send e-mail via configured smtp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendMail(sSubject As String, sBody As String, Optional sTo As String) As Boolean

  Dim hMsg As New SmtpClient

  ' if mail is disabled, return
  If Not Main.bEmailEnabled Then
    Main.WriteDebugLog(LogLabel & "Email is disabled")
    Return False
  Endif

  ' sent msg to alternate address instead of default one
  If sTo Then
    hMsg.To.Add(sTo)
  Else
    hMsg.To.Add(Main.sEmailToAddress)
  Endif

  If Main.bEMailDebug Then
    Main.WriteDebugLog("[Mail] From address: " & Main.sEmailFromAddress)
    Main.WriteDebugLog("[Mail] SMTP server: " & Main.sEmailSMTPServer)
    Main.WriteDebugLog("[Mail] SMTP Port: " & Main.iEmailSMTPPort)
    If sTo Then Main.WriteDebugLog("[Mail] To Address: " & sTo)
    Main.WriteDebugLog("[Mail] Subject: " & sSubject)
    Main.WriteDebugLog("[Mail] Body: " & sBody)
  Endif

  hMsg.Subject = sSubject
  hMsg.Add(sBody & "\n\n" & ("-- \nPowered by ") & Application.Name & " V" & Main.sProgramVersion & "\n")
  hMsg.From = Main.sEmailFromAddress
  hMsg.Host = Main.sEmailSMTPServer
  hMsg.Port = Main.iEmailSMTPPort

  ' Both username and password need to be available
  If Main.sEmailUserName & Main.sEmailPassword Then
    hMsg.User = Main.sEmailUserName
    hMsg.Password = Main.sEmailPassword
  Endif

  If Main.bEmailSSL Then
    hMsg.Encrypt = Net.SSL
  Endif

  Try hMsg.Send()
  If Not Error Then
    Main.WriteLog("[Mail] Just sent an e-mail message with subject '" & sSubject & "' to '" & sTo & "'.")
  Else
    Main.WriteLog("[Mail] An error occured sending an e-mail message with subject '" & sSubject & "' to '" & sTo & "'.")
    Return False
  Endif

  Return True

Catch
  Main.WriteLog("[Mail] Error sending an e-mail message with subject '" & sSubject & "' to '" & sTo & "'.")
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' send e-mail with captured image via configured smtp host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SendCaptureByMail(sSubject As String, sBody As String, iCam As Integer, Optional sTo As String)

  Dim hMsg As New SmtpClient
  Dim rResult As Result
  Dim sTable As String = "capture_camera"
  Dim sTempFile As String = Temp() & ".jpg"

  ' if mail is disabled, return
  If Not Main.bEmailEnabled Then Return

  sTable = sTable & iCam
  ' get last image
  rResult = Main.hDB.Exec(Subst("SELECT * FROM &1 ORDER BY stamp DESC LIMIT 1", sTable))
  If rResult.Available Then
    If rResult.Count = 1 Then
      ' save blob to temp file
      Try File.Save(sTempFile, rResult!image.Data)
    Endif
  Endif
  ' if temp file exists, add to e-mail and delete it
  If Exist(sTempFile) Then
    hMsg.Add(File.Load(sTempFile), "image/jpeg", "FrontDoor" & Format(Now(), "yyyy-mm-dd-hh-nn-ss") & ".jpg ")
    Kill sTempFile
  Endif
  ' sent msg to alternate address instead of default one
  If (Len(sTo)) Then
    hMsg.To.Add(sTo)
  Else
    hMsg.To.Add(Main.sEmailToAddress)
  Endif

  If Main.bEMailDebug Then
    Main.WriteDebugLog("[Mail] " & Main.sEmailFromAddress)
    Main.WriteDebugLog("[Mail] " & Main.sEmailSMTPServer)
    Main.WriteDebugLog("[Mail] " & Main.iEmailSMTPPort)
  Endif

  hMsg.Subject = sSubject
  hMsg.Add(sBody & "\n\n" & ("-- \nPowered by ") & Application.Name & " V" & Main.sProgramVersion & "\n")
  hMsg.From = Main.sEmailFromAddress
  hMsg.Host = Main.sEmailSMTPServer
  hMsg.Port = Main.iEmailSMTPPort
  Try hMsg.Send()
  If Not Error Then Main.WriteLog("[Mail] Just sent an e-mail message.")

Catch
  Main.WriteLog("[Mail] Error: Sending e-mail message failed!")

End
