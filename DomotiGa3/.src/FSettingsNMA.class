' Gambas class file

' Description:
' FSettingsNMA.class
' Settings form for Notify My Android API support.

' Development Status:
' Development just started.

' Links:
' http://www.notifymyandroid.com/api.jsp

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  chkEnabled.Value = Main.bNMAEnabled
  txtApiKey.Text = Main.sNMAApiKey
  txtApplication.Text = Main.sNMAApplication
  txtEvent.Text = Main.sNMAEvent
  chkDebug.Value = Main.bNMADebug

  chkDebug.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  txtApplication.Enabled = chkEnabled.Value
  txtEvent.Enabled = chkEnabled.Value
  btnTestApiKey.Enabled = chkEnabled.Value
  btnTestNMA.Enabled = chkEnabled.Value
  btnSave.Enabled = False

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  Dim rResult As Result

  rResult = Main.GetSettingTable("nma", True) ' get defaults
  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    chkDebug.Value = rResult!debug
    txtApiKey.Text = rResult!apikey
    txtApplication.Text = rResult!application
    txtEvent.Text = rResult!event
  End If
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  chkDebug.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  txtApplication.Enabled = chkEnabled.Value
  txtEvent.Enabled = chkEnabled.Value
  btnTestApiKey.Enabled = chkEnabled.Value
  btnTestNMA.Enabled = chkEnabled.Value
  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  txtApiKey.Text = LTrim$(txtApiKey.Text)
  txtApiKey.Text = RTrim$(txtApiKey.Text)

  If Not txtApiKey.Text Then
    Balloon(("Please enter your NMA API key"), txtApiKey)
    Return False
  End If

  If Not txtApplication.Text Then
    Balloon(("Please enter an application text"), txtApplication)
    Return False
  End If

  If Not txtEvent.Text Then
    Balloon(("Please enter an event text"), txtEvent)
    Return False
  End If

  ' Check if API key looks to be valid
  If Len(txtApiKey.Text) <> 48 Then
    Select Message.Question("Your API key length is " & Len(txtApiKey.Text) & ", normally a valid API key is 48 characters. Do you want to continue?", "Yes", "No")
      Case 2 ' Answer=No
        Return False
    End Select
  Endif
  
  Return True

End

Public Sub btnSave_Click()

  Dim rResult As Result

  If Not ValidInput() Then Return

  '--- save new NMA settings ---
  rResult = Main.hDB.Exec("UPDATE settings_nma SET enabled = &1, debug = &2, apikey = &3, application = &4, event = &5  WHERE id = 1", chkEnabled.Value, chkDebug.Value, txtApiKey.Text, txtApplication.Text, txtEvent.Text)
  rResult = Main.GetSettingTable("nma") ' reload settings
  If rResult.Count Then
    Main.bNMAEnabled = rResult!enabled
    Main.bNMADebug = rResult!debug
    Main.sNMAApiKey = rResult!apikey
    Main.sNMAApplication = rResult!application
    Main.sNMAEvent = rResult!event
  End If

  Me.Close

End

Public Sub TextLabelLink_MouseUp()

  Desktop.Open("http://www.notifymyandroid.com/")
   
End

Public Sub btnTestApiKey_Click()
  
  Dim sResponse As String
  
  If txtApiKey.Text Then
    sResponse = NMA.ValidateNMAApiKey(txtApiKey.Text)
    If Not sResponse Then
      Message.Info("NMA API key is VALID")
    Else
      Message.Error("NMA API key is INVALID\n" & sResponse)
    Endif
  Else
    Balloon(("Please enter your NMA API key"), txtApiKey)
  End If
  
End

Public Sub btnTestNMA_Click()

  Dim sResponse As String

  If txtApiKey.Text Then
    ' If the sResponse is blank, all went well
    sResponse = NMA.PostNMA(txtApiKey.Text, "DomotiGa", "Info", "Test message from DomotiGa :-)", False)
    If Not sResponse Then
      Message.Info("NMA message successfully send")
    Else
      Message.Error("Failed to send NMA message\n" & sResponse)
    Endif
  Else
    Balloon(("Please enter your NMA API key"), txtApiKey)
  End If

End

Public Sub txtApiKey_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtApplication_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtEvent_KeyPress()

  btnSave.Enabled = True

End
