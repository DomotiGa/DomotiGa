' Gambas class file

' Description:
' FScenarioEditor.class
' Support for creating and editing of all scenario.

' Development Status:
' Works for 99%.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private rResult As Result
Public bAddScenario As Boolean

Public Sub Form_Open()

  If bAddScenario Then
    Me.Text = ("Create Scenario")
    btnDelete.Visible = False
  Endif

  Me.Move(FMain.X + 50, FMain.Y + 70)
  LoadScenario()

End

Public Sub LoadScenario()

  If Not bAddScenario Then
    ' get selected event values
    Try rResult = Main.hDB.Exec("SELECT * FROM thermostat_scenarii WHERE scenario_id = &1", FThermostat.txtCurTherm)
    If Not Error Then
      If rResult.Count >= 1 Then
        tbName.Text = rResult!name
        If rResult!name = Main.GetGlobalVar("Thermostat_Mode") Then chkEnabled.Value = True
        txtDescription.Text = rResult!description
      Else
        Message(("Couldn't load scenario record!"))
        Me.Close
      Endif
    Endif
  Endif

End

Public Sub btnCancel_Click()

  bAddScenario = False
  Me.Close

End

Public Sub btnDelete_Click()

  Select Message.Question(("Are you sure that you want to delete this scenario?"), ("Yes"), ("No"))
    Case 1
      Main.hDB.Exec("DELETE FROM thermostat_scenarii WHERE scenario_id = &1", rResult!id)
      Me.Close
      FThermostat.GetThermList()
  End Select

End

Public Sub btnNew_Click()

  bAddScenario = True
  btnSave_Click()

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim sSql As String
  Dim iRet As Integer

  If Not tbName.Text Then
    Balloon(("Please enter a name for this scenario!"), tbName)
    Return
  Endif
  If bAddScenario = False Then
    ' name has changed
    If rResult!name <> tbName.Text Then
      If ScenarioNameExist(tbName.Text) Then
        Balloon(("Please enter a unique name for this scenario!"), tbName)
        Return
      Endif
    Endif
    If Not (chkEnabled.Value) And rResult!name = Main.GetGlobalVar("Thermostat_Mode") Then
      iRet = Message.Question(("There will be no more enabled scenario, are you sure you want to continue ?"), ("Yes"), ("No"))
      If iret = 1 Then
        Main.SetGlobalVar("Thermostat_Mode", "")
      Endif
      If iRet = 2 Then
        chkEnabled.Value = True
        Return
      Endif
    Endif
  Endif
  If bAddScenario = True Then
    ' create new scenario row
    sSql = "INSERT INTO thermostat_scenarii SET  name = &1, description = &2"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, txtDescription.Text)
    If chkEnabled.Value Then
      If Main.GetGlobalVar("Thermostat_Mode") <> tbName.Text Then
        Main.SetGlobalVar("Thermostat_Mode", tbName.Text)
        Thermostat.DeleteAllDerogateHeating()
      Endif
    Endif
  Else
    ' update new event details
    sSql = "UPDATE thermostat_scenarii SET name = &1, description= &2 WHERE scenario_id = &3"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, txtDescription.Text, rResult!scenario_id)
    If chkEnabled.Value Then
      If Main.GetGlobalVar("Thermostat_Mode") <> tbName.Text Then
        Main.SetGlobalVar("Thermostat_Mode", tbName.Text)
        Thermostat.DeleteAllDerogateHeating()
      Endif
    Endif
  Endif

  FThermostat.GetThermList()
  bAddScenario = False
  Me.Close

End

Private Sub ScenarioNameExist(sName As String) As Boolean

  Dim rResultName As Result

  rResultName = Main.hDB.Exec("SELECT scenario_id FROM thermostat_scenarii WHERE name = &1", sName)
  If rResultName Then
    If rResultName.Count Then
      Return True
    Endif
  Endif
  Return False

End

Public Sub tbName_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  btnSave.Enabled = True

End

Public Sub txtDescription_KeyPress()

  btnSave.Enabled = True

End
