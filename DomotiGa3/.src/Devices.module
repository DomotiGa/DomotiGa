' Gambas module file

' Description:
' Devices.module
' Contains code which is device related.

' Development Status:
' Working, maybe needs more error checking.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' ValueType Constants
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Const ValueType_Undefined As Integer = 0
Public Const ValueType_Switch As Integer = 1
Public Const ValueType_Temperature As Integer = 2
Public Const ValueType_Humidity As Integer = 3
Public Const ValueType_Luminance As Integer = 4
Public Const ValueType_WindSpeed As Integer = 5
Public Const ValueType_WindDirection As Integer = 6
Public Const ValueType_WindDescription As Integer = 7
Public Const ValueType_WindAvgSpeed As Integer = 8
Public Const ValueType_Weight As Integer = 9
Public Const ValueType_UV As Integer = 10
Public Const ValueType_RainRate As Integer = 11
Public Const ValueType_RainTotal As Integer = 12
Public Const ValueType_Remote As Integer = 13
Public Const ValueType_Security As Integer = 14
Public Const ValueType_Tamper As Integer = 15
Public Const ValueType_Blinds As Integer = 16
Public Const ValueType_Energy_kWh As Integer = 17
Public Const ValueType_Power_Watt As Integer = 18

'Public Const ValueType_ As Integer = X
' barometric, datetime, current, gas, water, volt, camera

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update device value with name
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub UpdateDevice(vDeviceName As Variant, sValue As String, iValueNum As Integer) As Boolean

  Dim iDeviceId As Integer

  iDeviceId = Devices.FindDeviceId(vDeviceName, "device update")
  If Not iDeviceId Then Return
  
  ' TODO: Migrate to devicevalues
  If iValueNum >= 1 And iValueNum <= 4 Then
    ValueUpdateV2(iDeviceId, iValueNum, sValue)
  Else
    If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] ERROR: Device with id '" & iDeviceId & "' invalid ValueNum=" & iValueNum & " given!")
  Endif
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' control device via linked interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub SetDevice(vDeviceName As Variant, sValue As String) As Boolean

  Dim iInterface, iDeviceId As Integer
  Dim iInstance As Integer
  Dim sText, sAddress, sCurValue As String
  Dim sDeviceName As String

  ' TODO: fix for server code
  ' If Not Main.bServer Then
  '   XMLClient.DeviceSetDevice(sDeviceName, sValue)
  '   Return True
  ' Endif


  iDeviceId = FindDeviceId(vDeviceName, "set device") 
  If Not iDeviceId Then Return False

' TODO set devicename
  Try sDeviceName = FindNameForDevice(iDeviceId)

  iInterface = FindInterfaceForDevice(iDeviceId)
  If Not iInterface Then Return False

  sAddress = FindAddressForDevice(iDeviceId)
  If Not sAddress Then Return False

  iInstance = FindInstanceForDevice(iDeviceId)
  
  If LCase(sValue) = "toggle" And If iDeviceId Then
    sCurValue = GetCurrentValueForDevice(iDeviceId, 1)
    sValue = Toggle(LCase(sCurValue))
  Endif

  ' Convert "Dim 100" to "On" and "Dim 0" to "Off - some devices don't support "Dim 100"
  If LCase(sValue) = "dim 100" Then sValue = "On"
  If LCase(sValue) = "dim 0" Then sValue = "Off"

  Select Case iInterface
    Case 1 ' RFXCom Receiver
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] ERROR: RFXCom Receiver can't control anything!")
    Case 2 ' Xanura CTX35
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Xanura CTX35: " & sAddress & " " & sValue)
      If Plugin.SendCommand("CTX35", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 3, 4, 5, 6, 8
      Main.WriteDebugLog("[Devices] ERROR: Read-only interface used for switching!")
    Case 7 ' X10Cmd
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] X10Cmd: " & sAddress & " " & sValue)
      If Plugin.SendCommand("X10Cmd", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 9 ' Weeder WTSSR
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Weeder WTSSR: " & sAddress & " " & sValue)
      If InStr(FindTypeNameForDevice(iDeviceId), "ITHO ECO Fan") Then
        If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] ITHO ECO Fan")
        If Plugin.SendCommand("Weeder", iInstance, sAddress, sValue) Then
          Devices.ValueUpdateV2(iDeviceId, 1, sValue)
          sText = ("Switched ") & sDeviceName & " " & sValue
        Endif
      Endif
    Case 10 ' Plugwise
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Plugwise Stick: " & sAddress & " " & sValue)
      If Plugin.SendCommand("Plugwise", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 11 ' DSC
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] DSC Alarm: " & sAddress & " " & sValue)
      If Main.hDSC Then
        Main.hDSC.SendCommand(sAddress, sValue, iDeviceId)
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Partition ") & sDeviceName & " set to " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device '") & sDeviceName & ("' because DSC is disabled!"))
      Endif
    Case 12 ' RFXCom Transmitter
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] RFXCom Transmitter: " & sAddress & " " & sValue)
      If sValue <> "STOP" Then
        If Plugin.SendCommand("RFXComTX", iInstance, sAddress, sValue) Then
          Devices.ValueUpdateV2(iDeviceId, 1, sValue)
          sText = ("Switched ") & sDeviceName & " " & sValue
        Endif
      Endif
    Case 13 ' KNX/EIB Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] KNX/EIB Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("EIB", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 15 ' OpenZWave Controller
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] OpenZWave Interface: " & sAddress & " " & sValue)
      If Main.hZWave Then
        Main.hZWave.SendCommand(sAddress, sValue)
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device '") & sDeviceName & ("' because OpenZWave interface is disabled!"))
      Endif
    Case 16 ' PLCBUS Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] PLCBUS Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("PLCBUS", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 17 ' Virtual Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Virtual Interface: " & sAddress & " " & sValue)
      Devices.ValueUpdateV2(iDeviceId, 1, sValue)
      sText = ("Set ") & sDeviceName & " to " & sValue
    Case 20 ' EZcontrol Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] EZcontrol Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("EZcontrol", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 22 ' PwrCtrl Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] PwrCtrl Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("PwrCtrl", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 23 ' Denon Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Denon Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("Denon", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 24 ' Onkyo Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Onkyo Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("Onkyo", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 25 ' SharpTV Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] SharpTV Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("SharpTV", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 26 ' LGTV Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] LGTV Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("LGTV", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device '") & sDeviceName & ("' because LGTV interface is disabled!"))
      Endif
    Case 29 ' JeeLabs Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Jeelabs Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("JeeLabs", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 30 ' Velleman Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Velleman K8055 : " & sAddress & " " & sValue)
      If Plugin.SendCommand("K8055", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 31 ' OWFS Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] OWFS Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("OWFS", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 32 ' CUL Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] CUL Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("CUL", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 33 ' RFXCom xPL
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] RFXCom xPL Interface: " & sAddress & " " & sValue)
      If Main.hRFXComxPL Then
        Main.hRFXComxPL.SendCommand(sAddress, sValue)
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device '") & sDeviceName & ("' because RFXCom xPL interface is disabled!"))
      Endif
    Case 34 ' Shell
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Shell Interface: " & sAddress & " " & sValue)
      If Main.bShellEnabled Then
        Shell.RunShellCommand(iDeviceId, sAddress, sValue)
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device '") & sDeviceName & ("' because Shell interface is disabled!"))
      Endif
    Case 35 ' RFXCom TRX
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] RFXCom Transceiver Interface: " & sAddress & " " & sValue)
      ' TODO: more instances
      If Plugin.SendCommand("RFXComTRX", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 36 ' HomeMatic LAN
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] HomeMatic Interface: " & iDeviceId & ", Temperature " & sValue)
      If Plugin.SendCommand("HomeMatic", iInstance, iDeviceId, "Temperature " & sValue) Then
        ' Main.hHM.SendCommand(sAddress, sValue, iDeviceId)
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 37 ' OpenTherm gateway
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] OpenTherm gateway interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("OpenTherm", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Changed ") & sDeviceName & " temperature setpoint to " & sValue
      Endif
    Case 39 ' Pioneer interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Pioneer Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("Pioneer", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 40 ' XBMC xPL interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] XBMC xPL Interface: " & sAddress & " " & sValue)
      If Main.hXBMCxPL Then
        Main.hXBMCxPL.SendCommand(sAddress, sValue)
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Else
        Main.WriteDebugLog(("[Devices] Can't control device '") & sDeviceName & ("' because XBMC xPL interface is disabled!"))
      Endif
    Case 42 ' ELV MAX! Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] ELV MAX! Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("ELVMAX", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 44 ' Mochad
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Mochad: " & sAddress & " " & sValue)
      If Plugin.SendCommand("Mochad", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 47 ' KMTronicUDP Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] KMTronicUDP Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("KMTronicUDP", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 48 ' DMXPlayer
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] DMXPlayer Interface: " & sAddress & " " & sValue)
      If Plugin.SendCommand("DMXPlayer", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 49 ' GenericIO Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] GenericIO Interface: Calling SendCommand with Address='" & sAddress & "' and command='" & sValue & "'")
      If Plugin.SendCommand("GenericIO", iInstance, sAddress, sValue) Then
        ' Note: Unlike many of the above interface commands, this does not update the value.
        '       Value needs to be updated by the device we have sent the command to.
        '       ie. we send a command to turn on a relay. Device then responds that the relay is on (which updates Value).
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case 51 ' RaZberry Interface
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] RaZberry Interface: " & sAddress & " " & sValue)
      ' TODO: more instances
      If Plugin.SendCommand("RaZberry", iInstance, sAddress, sValue) Then
        Devices.ValueUpdateV2(iDeviceId, 1, sValue)
        sText = ("Switched ") & sDeviceName & " " & sValue
      Endif
    Case -1
      Main.WriteDebugLog(("[Devices] Interface for device '") & sDeviceName & ("' does not support Write!"))
      Return False
  Case Else
    Main.WriteDebugLog(("[Devices] Unknown Interface ") & Str(iInterface) & (" for device '") & sDeviceName & "'!")
    Return False
  End Select

  If sText And If iInterface Then
    If iDeviceId And If FindLogSpeakForDevice(iDeviceId) Then VoiceText.Speak(sText)
  Endif

  If sText Then
    Return True
  Else
    Return False
  Endif

End

Public Sub SetDeviceIfDifferent(iId As Integer, sValue As String) As Boolean

  If sValue == GetCurrentValueForDevice(iId, 1) Then
    Return False
  Else
    SetDevice(iId, sValue)
  Endif

End

Private Sub AddDeviceToContext(cContext As Collection, rResult As Result)

  Dim rResultDev As Result

  cContext["Value"] = rResult!value ' TODO: Migrate to devicevalues, for backwards compatibility
  cContext["Value1"] = rResult!value
  cContext["Value2"] = rResult!value2
  cContext["Value3"] = rResult!value3
  cContext["Value4"] = rResult!value4


  ' TODO: Migrate to devicevalues
  rResultDev = Main.hDB.Exec("SELECT id,value,value2,value3,value4 FROM devices WHERE enabled IS TRUE ORDER BY name")

  rResultDev.MoveFirst()
  While rResultDev.Available
    cContext["Dev_" & rResultDev!id & "_Value"] = rResultDev!value ' TODO: Migrate to devicevalues, for backwards compatibility
    cContext["Dev_" & rResultDev!id & "_Value1"] = rResultDev!value
    cContext["Dev_" & rResultDev!id & "_Value2"] = rResultDev!value2
    cContext["Dev_" & rResultDev!id & "_Value3"] = rResultDev!value3
    cContext["Dev_" & rResultDev!id & "_Value4"] = rResultDev!value4
    rResultDev.MoveNext()
  Wend


  'rResultDev = Main.hDB.Exec("SELECT deviceid,value,valuenum FROM device_values LEFT JOIN devices ON devices.id = device_values.deviceid WHERE devices.enabled IS TRUE ORDER BY name")

  'rResultDev.MoveFirst()
  'While rResultDev.Available
  '  cContext["Dev_" & rResultDev!deviceid & "_Value" & rResultDev!valuenum] = rResultDev!value
  '  rResultDev.MoveNext()
  'Wend

End

Public Function CorrectValue(sValue As String, sFormula As String, rResult As Result) As String

  Dim sResult As String
  Dim cContext As New Collection

  ' calibration needed
  If Len(sFormula) > 0
    ' insert devices values
    AddDeviceToContext(cContext, rResult)
    ' evaluate the formula
    Try sResult = Eval(Replace(sFormula, "\n", " "), cContext)
    If Error Then
      sResult = "ERROR: " & Error.Text
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] CorrectValue failed. sValue=" & sValue & ", sFormula=" & sFormula & ". Message=" & sResult)
    Endif
    Return sResult
  Else
    ' nothing to calibrate
    Return sValue
  Endif

Catch
  Return Error.Text

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' toggle value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private Sub Toggle(sValue As String) As String

  If Lower(sValue) = "on" Or If InStr(Lower(sValue), "dim") Then
    Return "Off"
  Else
    Return "On"
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find interface with name sInterfaceName, return it's id.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindInterface(sInterfaceName As String) As Integer

  Dim rInterface As Result

  Try rInterface = Main.hDB.Exec("SELECT * FROM interfaces WHERE name = &1", sInterfaceName)
  If Not Error Then
  If rInterface.Available Then
    If rInterface.Count = 1 Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Interface with name '") & sInterfaceName & ("' has id '") & rInterface!id & "'")
      Return rInterface!id
    Else
      Main.WriteDebugLog(("[Devices] More then one interface with name '") & sInterfaceName & ("' found!"))
      Return 0
    Endif
  Else
    Main.WriteDebugLog(("[Devices] Interface with name '") & sInterfaceName & ("' not found!"))
    Return 0
  Endif
  Else
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find devicetype with type, return it's id.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDeviceType(sDeviceType As String) As Integer

  Dim rDeviceType As Result

  Try rDeviceType = Main.hDB.Exec("SELECT * FROM devicetypes WHERE type LIKE &1", sDeviceType)
  If Not Error Then
    If rDeviceType.Available Then
      If rDeviceType.Count = 1 Then
        If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] DeviceType with type '") & sDeviceType & ("' has id '") & rDeviceType!id & "'")
        Return rDeviceType!id
      Else
        If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] DeviceType with type '") & sDeviceType & ("' not found!"))
        Return 0
      Endif
    Else
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Cannot get DeviceType records!"))
      Return 0
    Endif
  Else
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Cannot get DeviceType records!"))
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return module/devicetype for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindModuleForDevice(iDeviceId As Integer) As Integer

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT module FROM devices WHERE id = &1", iDeviceId)
  If Not Error Then
    If rDevice.Available Then
      Return rDevice!module
    Else
      Return 0
    Endif
  Else
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return type for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindTypeForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result
  Dim iDeviceType As Integer

  iDeviceType = FindModuleForDevice(iDeviceId)
  Try rDevice = Main.hDB.Exec("SELECT type FROM devicetypes WHERE id = &1", iDeviceType)
  Return rDevice!type

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return typename for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindTypeNameForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result
  Dim iDeviceType As Integer

  iDeviceType = FindModuleForDevice(iDeviceId)
  Try rDevice = Main.hDB.Exec("SELECT name FROM devicetypes WHERE id = &1", iDeviceType)
  Return rDevice!name

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return description for devicetype with iId
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDescrForDeviceType(iId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT description FROM devicetypes WHERE id = &1", iId)
  If rDevice.Available Then
    Return rDevice!description
  Else
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return name for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindNameForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT name FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then
    Return rDevice!name
  Else
    Main.WriteDebugLog(("[Devices] Cannot find name for device with id ") & iDeviceId)
    Return ""
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return if device with id exists
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub CheckIfDeviceExists(iDeviceId As Integer) As Boolean

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT id FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then
    Return True
  Else
    Main.WriteDebugLog(("[Devices] Cannot find device with id ") & iDeviceId)
    Return False
  Endif

End


'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return device id for device with name(deprecated) or address, and also check if device exsits
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDeviceId(vDeviceId As Variant, Optional sComment As String) As Integer
  Dim iDeviceId As Integer
  Dim rDevice As Result
  
   ' If we get a byte/integer or a string that can be parsed to integer
  Try iDeviceId = CInt(vDeviceId)
  If Error Then
     'TODO deprecated 
    If sComment Then 
      Main.WriteDebugLog("[Warning] You are using a device name '" & vDeviceId & "' instead of device id, this option is be deprecated and will be removed! We have also a comment where to look: '" & sComment & "'.")
    Else
      Main.WriteDebugLog("[Warning] You are using a device name '" & vDeviceId & "' instead of device id, this option is be deprecated and will be removed!")
    Endif
    Try iDeviceId = FindIdForDevice(vDeviceId)
    If Error Or If Not iDeviceId Then
      Main.WriteDebugLog("[Devices] ERROR: Cannot convert device with name '" & iDeviceId & "' to id!")
      Return
    Endif
  Endif
  
  If Not iDeviceId Then
    Main.WriteDebugLog("[Devices] Cannot find device with id " & iDeviceId)
    Return
  Endif 
  
  Try rDevice = Main.hDB.Exec("SELECT id FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then
    Return rDevice!id
  Else
    Main.WriteDebugLog("[Devices] Cannot find device with id " & iDeviceId)
    Return
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return device id for virtual device with address
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindVirtualDeviceID(sAddress As String) As Integer

  Dim iId As Integer

  iId = Find(sAddress, FindInterface("Virtual Interface"), "Virtual Device")
  Return iId

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return name for interface with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindNameForInterface(iInterfaceId As Integer) As String

  Dim rInterface As Result

  Try rInterface = Main.hDB.Exec("SELECT name FROM interfaces WHERE id = &1", iInterfaceId)
  If Not Error And If rInterface.Available Then Return rInterface!name

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return name for devicetype with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindNameForDeviceType(iDeviceTypeId As Integer) As String

  Dim rDeviceType As Result

  Try rDeviceType = Main.hDB.Exec("SELECT name FROM devicetypes WHERE id = &1", iDeviceTypeId)
  If Not Error And If rDeviceType.Available Then Return rDeviceType!name

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return log boolean for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindLogForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT log FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then Return rDevice!log

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return address string for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindAddressForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT address FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then Return rDevice!address

End


'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return print boolean for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindLogDisplayForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT logdisplay FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then Return rDevice!logdisplay

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return speak boolean for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindLogSpeakForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT logspeak FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then Return rDevice!logspeak

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return hide boolean for device with id
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindHideForDevice(iDeviceId As Integer) As String

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT hide FROM devices WHERE id = &1", iDeviceId)
  If Not Error And If rDevice.Available Then Return rDevice!hide

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find all enabled devices, return all fields from database
' sorted by sOrder if specified.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindAllEnabledDevices(Optional sSortOrder As String) As Result

  Dim rDevice As Result
  
 If sSortOrder Then
    Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE enabled IS TRUE ORDER BY &1", sSortOrder)
  Else
    Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE enabled IS TRUE")
  Endif
  Return rDevice

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with interface iInterface, return complete device details.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDevicesForInterface(iInterface As Integer, Optional sSortOrder As String) As Result

  Dim rDevice As Result

  If iInterface Then
   If sSortOrder Then
      Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE interface = &1 AND enabled is TRUE ORDER BY &2", iInterface, sSortOrder)
    Else
      Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE interface = &1 AND enabled is TRUE", iInterface)
    Endif
    Return rDevice
  Else
    Main.WriteDebugLog(("[Devices] FindDeviceForInterface: No interface Id given!"))
    Return Null
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with group sGroup, return complete device details.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindDevicesForGroup(sGroup As String, Optional sSortOrder As String) As Result

  Dim rDevice As Result

  If sGroup Then
   If sSortOrder Then
      Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE groups LIKE &1 AND enabled is TRUE ORDER BY &2", sGroup, sSortOrder)
    Else
      Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE groups LIKE &1 AND enabled is TRUE", sGroup)
    Endif
    Return rDevice
  Else
    Main.WriteDebugLog(("[Devices] FindDeviceForGroup: No group specified!"))
    Return Null
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find interface for device with iDeviceId.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindInterfaceForDevice(iDeviceId As Integer) As Integer

  Dim rDevice, rResult As Result

  Try rDevice = Main.hDB.Exec("SELECT address, name, interface FROM devices WHERE id = &1", iDeviceId)
  If rDevice.Available Then
    Try rResult = Main.hDB.Exec("SELECT mode FROM interfaces WHERE id = &1", rDevice!interface)
    If InStr(rResult!mode, "Write") Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Interface for device with id '") & iDeviceId & "' is '" & rDevice!interface & "'")
      Return rDevice!interface
    Else
      ' read-only
      Return -1
    Endif
  ' no interface found, something is wrong
  Else
    Main.WriteDebugLog(("[Devices] Device with id '") & iDeviceId & ("' not found!"))
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find address for device with sDeviceName.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindInstanceForDevice(iDeviceId As Integer) As Integer

  Return 1

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iDeviceId, return it's current value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetValueForDevice(iDeviceId As Integer, Optional sValue As String) As String
  Dim iValueNum As Integer = 1

  If Not CheckIfDeviceExists(iDeviceId) Then Return 0
  
  sValue = LCase(Trim(sValue))
  
  If Len(sValue) > 0 Then
    Try iValueNum = CInt(Replace(sValue, "value", ""))
    If Error Then
      Select Case sValue
        Case "value"  ' TODO: Migrate to devicevalues, for backwards compatibility
          iValueNum = 1
        Case "lastchanged"
          Return GetLastChangedForDevice(iDeviceId)
        Case "lastseen"
          Return GetLastSeenForDevice(iDeviceId)
        Case "batterystatus"
          Return GetBatteryStatusForDevice(iDeviceId)
        Case Else
          iValueNum = 1
      End Select
    Endif
  Endif
   
  Return GetCurrentValueForDevice(iDeviceId, iValueNum)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's lastchanged value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetLastChangedForDevice(iId As Integer) As String
  ' TODO: Migrate to devicevalues
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT lastchanged FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!lastchanged
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's lastseen value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetLastSeenForDevice(iId As Integer) As String
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT lastseen FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!lastseen
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's batterystatus value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetBatteryStatusForDevice(iId As Integer) As String
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT batterystatus FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Return rDevice!batterystatus
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current calibrated iValue value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentValueForDevice(iId As Integer, iValue As Integer) As String
  ' TODO: Migrate to devicevalues
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value, value2, value3, value4, lastseen, correction, correction2, correction3, correction4 FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Select Case iValue
      Case 1
        Return Devices.CorrectValue(rDevice!value, rDevice!correction, rDevice)
      Case 2
        Return Devices.CorrectValue(rDevice!value2, rDevice!correction2, rDevice)
      Case 3
        Return Devices.CorrectValue(rDevice!value3, rDevice!correction3, rDevice)
      Case 4
        Return Devices.CorrectValue(rDevice!value4, rDevice!correction4, rDevice)
    End Select
  Endif
  
  ' Dim rDevice As Result
  ' 
  ' Try rDevice = Main.hDB.Exec("SELECT * FROM device_values WHERE deviceid = &1 AND valuenum = &2", iId, iValue)
  ' If rDevice.Count Then
  '   Return Devices.CorrectValue(rDevice!value, rDevice!correction, rDevice)
  ' Endif
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current uncorrected iValue value
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentUnCorrectedValueForDevice(iId As Integer, iValue As Integer) As String
  ' TODO: Migrate to devicevalues
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT value, value2, value3, value4, lastseen, correction, correction2, correction3, correction4 FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    Select Case iValue
      Case 1
        Return rDevice!value
      Case 2
        Return rDevice!value2
      Case 3
        Return rDevice!value3
      Case 4
        Return rDevice!value4
    End Select
  Endif
  
  ' Dim rDevice As Result
  ' 
  ' Try rDevice = Main.hDB.Exec("SELECT * FROM device_values WHERE deviceid = &1 AND valuenum = &2", iId, iValue)
  ' If rDevice.Count Then
  '   Return rDevice!value
  ' Endif
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with iId, return it's current units field
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetCurrentUnitsForDevice(iId As Integer, iValue As Integer) As String
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT units FROM device_values WHERE deviceid = &1 AND valuenum = &2", iId, iValue)
  If rDevice.Count Then
        Return rDevice!units
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with sAddress and iInterface, return id if found.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Find(iInstance As Integer, sAddress As String, iInterface As Integer, Optional sDevice As String, sPrefName As String) As Integer

  Dim rDeviceFind As Result

  If iInterface Then
    Try rDeviceFind = Main.hDB.Exec("SELECT * FROM devices WHERE address LIKE &1 AND interface = &2 AND enabled is TRUE", sAddress, iInterface)
    If Not Error Then Return FindDevice(rDeviceFind, sAddress, iInterface, sDevice, sPrefName)
  Else
    Main.WriteDebugLog(("[Devices] Interface for device '") & sDevice & ("' with address '") & sAddress & ("' not found!"))
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with sAddress in it's address and iInterface, return id if found.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindRegExp(sAddress As String, iInterface As Integer, Optional sDevice As String, sRegex1 As String = "[[:<:]]", sRegex2 As String = "[[:>:]]") As Integer

  Dim rDeviceFind As Result

  If iInterface Then
    Try rDeviceFind = Main.hDB.Exec("SELECT * FROM devices WHERE address REGEXP '" & sRegex1 & sAddress & sRegex2 & "' AND interface = &1 AND enabled is TRUE", iInterface)
    If Not Error Then Return FindDevice(rDeviceFind, sAddress, iInterface, sDevice)
  Else
    Main.WriteDebugLog(("[Devices] Interface for device '") & sDevice & ("' with address '") & sAddress & ("' not found!"))
    Return 0
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find device with sAddress for all Interfaces, return id if found.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindAll(sAddress As String, iInterface As Integer, Optional sDevice As String) As Integer

  Dim rDeviceFind As Result

  Try rDeviceFind = Main.hDB.Exec("SELECT devices.* FROM devices, interfaces WHERE devices.interface = interfaces.id AND interfaces.type REGEXP '[[:<:]]" & sDevice & "[[:>:]]' AND devices.address LIKE &1 AND devices.enabled is TRUE LIMIT 1", sAddress)
  If Not Error Then Return FindDevice(rDeviceFind, sAddress, iInterface, sDevice)

End

Public Sub FindDevice(rDevice As Result, sAddress As String, iInterface As Integer, Optional sDevice As String, sPrefName As String) As Integer

  Dim rResult As Result

  If Not sDevice Then sDevice = "Unknown"

  If rDevice.Count > 1 Then
    Main.WriteDebugLog(("[Devices] The following devices have the same address:"))
    For Each rDevice
      Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' is '") & rDevice!name & "'")
    Next
    Return 0
  Else If rDevice.Count = 1 Then
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' is '") & rDevice!name & "'")
    If rDevice!firstseen = "00:00:00" Or If rDevice!firstseen = "" Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and '") & rDevice!name & ("' is a new device"))
      rResult = Main.hDB.Exec("UPDATE devices SET firstseen = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), rDevice!id)
    Endif
    Return rDevice!id
  Else
    If Not IsBlacklisted(sAddress, iInterface) Then
      If sDevice Then
        Main.WriteDebugLog(("[Devices] Device of type '" & sDevice & "' with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' not found!"))
      Else
        Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' not found!"))
      Endif
      If Main.bAutoDeviceCreate Then
        Return AutoCreateDevice(sAddress, iInterface, sDevice, sPrefName)
      Else
        Return 0
      Endif
    Else
      If Main.bDevicesDebug Then
        If sDevice Then
          Main.WriteDebugLog(("[Devices] Device " & IIf(Len(sDevice), "of type '" & sDevice & "' ", "") & "with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' is blacklisted!"))
        Else
          Main.WriteDebugLog(("[Devices] Device with address '") & sAddress & ("' and interface '") & FindNameForInterface(iInterface) & ("' is blacklisted!"))
        Endif
      Endif
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find out if an device has firstseen filled
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub CheckFirstSeen(iId As Integer)

  Dim rResult, rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT * FROM devices WHERE id = &1", iId)
  If rDevice.Count Then
    If rDevice!firstseen = "00:00:00" Or If rDevice!firstseen = "" Then
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device with address '") & rDevice!address & ("' and '") & rDevice!name & ("' is a new device!"))
      rResult = Main.hDB.Exec("UPDATE devices SET firstseen = &1 WHERE id = &2", Format(Now(), "yyyy-mm-dd hh:nn:ss"), rDevice!id)
    Endif
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' find out if address is blacklisted and return true
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub IsBlacklisted(sAddress As String, iInterface As Integer) As Boolean

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT * FROM deviceblacklist WHERE address = &1", sAddress)
  If rDevice.Count Then
    If rDevice!id Then
      If iInterface = rDevice!id Then
        If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Address '") & sAddress & ("' with Interface '") & FindNameForInterface(iInterface) & ("' is blacklisted, so I ignore it."))
        Return True
      Else
        Return False
      Endif
    Endif
    If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Address '") & sAddress & ("' is blacklisted, so I ignore it."))
    Return True
  Endif
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' TEMPORARY wrapper for the new ValueUpdate functionality
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ValueUpdateV2(iId As Integer, iValueNum As Integer, vValue As Variant, Optional iValueType As Integer = ValueType_Undefined) As Boolean

  Dim rResult, rDevice As Result
  Dim bUpdated As Boolean
  Dim rDeviceLog As Result
  Dim vVal As Variant
  Dim sLabel, sValue, sName, sType As String
  Dim iInstance As Integer
  Dim sTimeNow As String = Format(Now(), "yyyy-mm-dd hh:nn:ss")
  Dim vValueName As String

  ' TODO: Migrate to devicevalues, for backwards compatibility
  Select iValuenum
    Case 1
      vValueName = "value"
    Case 2
      vValueName = "value2"
    Case 3
      vValueName = "value3"
    Case 4
      vValueName = "value4"
    Case Else
      If Main.bDevicesDebug Then Main.WriteDebugLog("[Devices] Invalid valuenum given!")
      Return False
  End Select
    
  
  If vValue = "Dim 100" Then vValue = "On"
  If vValue = "Dim 99" Then vValue = "On"
  If vValue = "Dim 0" Then vValue = "Off"

  rDevice = Main.hDB.Exec("SELECT units, value, log, dimable, switchable FROM device_values WHERE deviceid = &1 AND valuenum = &2", iId, iValuenum)
  
  If rDevice.Count = 0 Then
    ' New devicevalue, we need an insert instead of an update!
    rResult = Main.hDB.Exec("INSERT INTO device_values (deviceid, value, valuenum, lastchanged) values (&1, &2, &3, &4)", iId, vValue, iValuenum, sTimeNow)
    rDevice = Main.hDB.Exec("SELECT units, value, log, dimable, switchable FROM device_values WHERE deviceid = &1 AND valuenum = &2", iId, iValuenum)  
  Endif
  If rDevice.Count = 1 Then
    sName = FindNameForDevice(iId)
    sLabel = rDevice!units
    sValue = rDevice!value
    If rDevice!switchable Then
      sType = "switch"
    Else If rDevice!dimable Then
      sType = "dimmer"
    Endif
  Endif

  ' Now hardcoded, should come from the devices table
  iInstance = 1

  If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] ValueUpdate() called for device with id '") & iId & "'")
  If vValue <> Null Then
    If Comp(vValue, sValue, gb.IgnoreCase) <> 0 Then
      ' TODO: Migrate to devicevalues
      rResult = Main.hDB.Exec("UPDATE devices SET " & vValueName & " = &1 WHERE id = &2", vValue, iId)
      
      rResult = Main.hDB.Exec("UPDATE devices SET lastseen = &1, lastchanged = &1 WHERE id = &2", sTimeNow, iId)
      rResult = Main.hDB.Exec("UPDATE device_values SET lastseen = &1, lastchanged = &1, value = &2 WHERE deviceid = &3 AND valuenum = &4", sTimeNow, vValue, iId, iValuenum)
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Device change triggered for device with id '") & iId & ("' and ") & vValueName & " '" & vValue & "'")
      If FindLogDisplayForDevice(iId) Then Main.WriteLog(sName & " status changed to " & vValue & IIf(sLabel, " " & sLabel, "") & ".")
      If rDevice!dimable = True Or If rDevice!switchable = True Then Main.RefreshControlPage()


      Events.CheckDeviceChangeEvents(iId, iValueNum, vValue)
      bUpdated = True

      If Plugin.IsPluginEnabled("IViewer", 0) Then
        Try Plugin.GetPluginObject("IViewer", iInstance).Interface.SendUpdateForDevice(iId, vValue, vValueName, IIf(sType, sType, vValueName))
      Endif

      If Plugin.IsPluginEnabled("MQTT", 0) Then
        Try Plugin.GetPluginObject("MQTT", iInstance).Interface.PublishMsg(sName & "/" & vValueName, vValue)
      Endif

      If Plugin.IsPluginEnabled("SmartVisuServer", 0) Then
        Try Plugin.GetPluginObject("SmartVisuServer", iInstance).Interface.SendUpdateDevice(sName, vValue, IIf(sType, sType, vValueName))
      Endif
    Endif
  Endif

  If Not bUpdated Then
    rResult = Main.hDB.Exec("UPDATE devices SET lastseen = &1 WHERE id = &2", sTimeNow, iId)
    rResult = Main.hDB.Exec("UPDATE device_values SET lastseen = &1 WHERE deviceid = &2 AND valuenum = &3", sTimeNow, iId, iValuenum)
  Else
    If rDevice!log Then
       ' TODO: Migrate to devicevalues
      Main.hDB.Begin()
      rDeviceLog = Main.hDB.Create("devices_log")
      rDeviceLog!deviceid = iId
      rDeviceLog!lastchanged = Now()
      rDeviceLog[vValueName] = vValue
      If Main.bDevicesDebug Then Main.WriteDebugLog(("[Devices] Creating log record for device with id '") & iId & "' and '" & vValueName & "' = '" & vValue & "'")
      rDeviceLog.Update()
      Main.hDB.Commit()
    Endif
  Endif

  Events.CheckDeviceLastseenChangeEvents(iId, sTimeNow)
  Main.RefreshDeviceList()

  If rResult Then
    Return True
  Else
    Return False
  Endif


End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update device with iId with vValue, vValue2 etc
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ValueUpdate(iId As Integer, vValue As Variant, vValue2 As Variant, vValue3 As Variant, vValue4 As Variant) As Boolean

 ValueUpdateV2(iId, 1, vValue)
 ValueUpdateV2(iId, 2, vValue2)
 ValueUpdateV2(iId, 3, vValue3)
 ValueUpdateV2(iId, 4, vValue4)
 
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' update battery status for device with iId
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Battery(iId As Integer, vValue As Variant) As Boolean

  Dim rResult As Result

  rResult = Main.hDB.Exec("UPDATE devices SET batterystatus = &1, lastseen = &2 WHERE id = &3", vValue, Format(Now(), "yyyy-mm-dd hh:nn:ss"), iId)

  If rResult.Count Then
    Return True
  Else
    Return False
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' mail battery status for device with battery status set (sType can be '', all or empty)
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function MailBatteryStatus(Optional sType As String) As Boolean

  Dim rDevice As Result
  Dim sMessageBody As String
  Dim bFound As Boolean = False

  Try rDevice = Main.hDB.Exec("SELECT name, address, batterystatus, lastseen FROM devices WHERE enabled is TRUE")
  If rDevice.Available Then
    If rDevice.Count Then
      For Each rDevice
        If LCase(sType) = "empty" Then
          If LCase(rDevice!batterystatus) = "empty" Then
            sMessageBody &= "\r" & rDevice!address & " - " & rDevice!name & "  " & rDevice!batterystatus & "  " & Replace$(Str$(Format(rDevice!lastseen, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
            bFound = True
          Endif
        Else
          If rDevice!batterystatus Then
            sMessageBody &= "\r" & rDevice!address & " - " & rDevice!name & "  " & rDevice!batterystatus & "  " & Replace$(Str$(Format(rDevice!lastseen, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
            bFound = True
          Endif
        Endif
      Next
      ' sent e-mail with status
      If bFound Then
        Mail.SendMail(Application.Name & (": Battery Status"), ("These Devices have their Battery status match selection '") & sType & "':\n" & sMessageBody, Main.sEmailToAddress)
      Endif
    Endif
  Endif

  Return True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' erase all logged data for device with iId
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub EraseLog(iId As Integer)

  Try Main.hDB.Exec("DELETE FROM devices_log WHERE deviceid = &1", iId)

End

Private Sub FindUniqueName(sName As String) As String

  Dim rResult As Result
  Dim iNo As Integer

  For iNo = 1 To 50
    Try rResult = Main.hDB.Exec("SELECT * FROM devices WHERE name = &1", sName & iNo)
    If rResult.Count Then
      Continue
    Else
      Return sName & iNo
    Endif
  Next

End

Public Sub AutoCreateDevice(sAddress As String, iInterface As Integer, sType As String, Optional sPrefName As String) As Integer

  Dim rResult As Result
  Dim sName As String
  Dim iType As Integer

  If sPrefName Then
    sName = FindUniqueName(Trim(sPrefName) & " #")
  Else
    sName = FindUniqueName(sType & " device #")
  Endif

  ' try to select correct device type
  Select UCase(sType)
    Case "DS18S20"
      iType = 15
    Case "DS2423"
      iType = 58
    Case "DS2438"
      iType = 65
    Case "DS18B20"
      iType = 213
    Case "X10"
      iType = 36
    Case "X10SECURITY", "VISONICSENSOR", "VISONICMOTION", "VISONICCODE", "VISONICPWRCODE"
      iType = 1
    Case "AC"
      iType = 60
    Case "FS20"
      iType = 210
    Case "FHTTFK"
      iType = 156
    Case "FHT80"
      iType = 157
    Case "EM1000", "EM1000-S", "EM1000-EM"
      iType = 166
    Case "EM1000-GZ"
      iType = 167
    Case "HDDTEMP"
      iType = 11
    Case "DSC DWS"
      iType = 41
    Case "DSC PIR"
      iType = 40
    Case "DSC SMOKE"
      iType = 42
    Case "PLCBUS"
      iType = 117
    Case "JEELABS NODE"
      iType = 142
    Case "PWRCTRL"
      iType = 135
    Case "RFXSENSOR TEMP"
      iType = 100
    Case "RFXSENSOR BARO"
      iType = 139
    Case "RFXSENSOR SUPPLY"
      iType = 102
    Case "ARC"
      iType = 59
    Case "ATI REMOTE WONDER", "ATI"
      iType = 113
    Case "ATI WONDER PLUS", "ATI2"
      iType = 114
    Case "HE", "HEUK"
      iType = 43
    Case "HEEU"
      iType = 154
    Case "RFXMETER"
      iType = 9
    Case "TEMP1"
      iType = 214
    Case "TEMP2"
      iType = 81
    Case "TEMP3"
      iType = 84
    Case "TEMP4"
      iType = 85
    Case "TEMP5"
      iType = 223
    Case "HUM1"
      iType = 223
    Case "TH1"
      iType = 7
    Case "TH2"
      iType = 26
    Case "TH3"
      iType = 39
    Case "TH4"
      iType = 215
    Case "TH5"
      iType = 74
    Case "TH6"
      iType = 93
    Case "TH7"
      iType = 226
    Case "TH8"
      iType = 235
    Case "THB1"
      iType = 32
    Case "THB2"
      iType = 33
    Case "RAIN1"
      iType = 95
    Case "RAIN2"
      iType = 38
    Case "RAIN3"
      iType = 231
    Case "WIND1"
      iType = 74
    Case "WIND2"
      iType = 97
    Case "WIND3"
      iType = 37
    Case "WIND4"
      iType = 233
    Case "UV1"
      iType = 8
    Case "UV2"
      iType = 99
    Case "UV3"
      iType = 232
    Case "DT1"
      iType = 39
    Case "WEIGHT1"
      iType = 22
    Case "WEIGHT2"
      iType = 23
    Case "ELEC1", "ELEC1_1", "ELEC1_2", "ELEC1_3"
      iType = 66
    Case "ELEC2"
      iType = 78
    Case "DWS"
      iType = 1
    Case "MOTION"
      iType = 69
    Case "REMOTE"
      iType = 71
    Case "ANSLUT"
      iType = 228
    Case "DM10"
      iType = 179
    Case "DIGIMAX"
      iType = 180
    Case "NINJA"
      iType = 194
    Case "SMOKE"
      iType = 174
    Case "IO0", "IO1", "IO2", "IO3", "IO4", "IO5", "IO6", "IO7"
      iType = 192
    Case "UPS"
      iType = 14
    Case "ZWAVE"
      iType = 61
    Case "BLUETOOTH"
      iType = 18
    Case "S300TH"
      iType = 173
    Case "SQUEEZEPLAYER"
      iType = 112
    Case "HM_CC_TC"
      iType = 234
    Case "P1-POWERUSAGE"
      iType = 246
    Case "P1-POWERDELIVER"
      iType = 255
    Case "P1-GASUSAGE"
      iType = 256
    Case "CCOST"
      iType = 131
    Case "TEMP6"
      iType = 248
    Case "TEMP7"
      iType = 251
    Case "TEMP8"
      iType = 247
    Case "TEMP9"
      iType = 252
    Case "HUM2"
      iType = 247
    Case "TH9"
      iType = 250
    Case "RAIN4"
      iType = 253
    Case "RAIN5"
      iType = 247
    Case "WIND5"
      iType = 254
    Case "WIND6"
      iType = 247
    Case "PIONEER"
      iType = 258
    Case "MEDIA.BASIC"
      iType = 259
    Case "MEDIA.MPTRNSPT"
      iType = 260
    Case "MEDIA.MPMEDIA"
      iType = 261
    Case "OSD.BASIC"
      iType = 262
    Case "T"
      iType = 263
    Case "TH"
      iType = 264
    Case "THB"
      iType = 265
    Case "WIND"
      iType = 266
    Case "RAIN"
      iType = 267
    Case "SOL"
      iType = 268
    Case "UV"
      iType = 269
    Case "ELVCUBE"
      iType = 270
    Case "ELVDOOR"
      iType = 271
    Case "ELVWALLTHERMO"
      iType = 272
    Case "ELVRADTHERMO"
      iType = 273
    Case "ELEC3"
      iType = 274
    Case "YOULESS"
      iType = 281
    Case "OPENTHERM THERMOSTAT"
      iType = 237
    Case "OPENTHERM BURNER"
      iType = 238
    Case "OPENTHERM BOILER"
      iType = 239
    Case "OPENTHERM CENTRAL HEATING"
      iType = 240
    Case "OPENTHERM OUTSIDE SENSOR"
      iType = 241
    Case "LIGHTWAVERF"
      iType = 230
    Case "MEIANTECH"
      iType = 285
    Case "PLUGWISE SWITCH"
      iType = 297
    Case "KMTRONIC INPUT"
      iType = 298
    Case "KMTRONIC OUTPUT"
      iType = 299
    Case "GENERICIO"
      iType = 301
    Case "BINARY POWER SWITCH"
      iType = 61
    Case "ROUTING MULTILEVEL SWITCH", "MULTILEVEL SWITCH"
      iType = 62
    Case "ROUTING BINARY SENSOR", "BINARY SENSOR"
      iType = 63
    Case "DOOR LOCK"
      iType = 333
    Case Else
      Main.WriteDebugLog(("[Devices] Auto create device '" & sName & "' failed, because of unknown type!"))
      Main.WriteDebugLog(("[Devices] Please report device type '" & sType & "' with address '" & sAddress & "' and interface '" & FindNameForInterface(iInterface) & "' (" & iInterface & ") to support@domotiga.nl"))
      Return
  End Select

  Main.WriteDebugLog(("[Devices] Auto created device '" & sName & "' with address '" & sAddress & "' of type '" & FindNameForDeviceType(iType) & "' with interface '" & FindNameForInterface(iInterface) & "'"))

  ' create device
  Main.hDB.Begin()
  rResult = Main.hDB.Create("devices")
  rResult!name = sName
  rResult!module = iType
  rResult!interface = iInterface
  rResult!address = sAddress
  rResult!enabled = True
  rResult!onicon = "new.png"
  rResult!dimicon = "new.png"
  rResult!officon = "new.png"
  rResult!hide = False
  rResult!log = False
  rResult!logdisplay = False
  rResult!logspeak = False
  rResult!dimable = False
  rResult!switchable = False
  rResult!firstseen = Now()
  rResult!lastseen = Now()
  rResult!location = 1
  rResult.Update()
  Main.hDB.Commit()

  ' try to get id of created device
  Try rResult = Main.hDB.Exec("SELECT MAX(id) AS lastid FROM devices")
  If Not Error Then
    Return rResult!lastid
  Else
    Return 0
  Endif

End

Public Sub CheckRepeatDevice()

  Dim rResult As Result
  Dim dLastSeen As Date
  Dim period As Integer

  rResult = Main.hDB.Exec("SELECT * FROM devices WHERE repeatstate IS TRUE AND enabled is TRUE")
  For Each rResult
    period = rResult!repeatperiod
    dLastSeen = rResult!lastseen
    If (Now() > DateAdd(dLastSeen, gb.Minute, period)) Then
      Devices.SetDevice(rResult!id, rResult!value)
    Endif
  Next

End

Public Sub CheckResetDevice()

  Dim rResult As Result
  Dim dLastSeen As Date
  Dim period As Integer

  rResult = Main.hDB.Exec("SELECT * FROM devices WHERE reset IS TRUE AND enabled is TRUE")
  For Each rResult
    period = rResult!resetperiod
    dLastSeen = rResult!lastseen
    If (rResult!value <> rResult!resetvalue) And (Now() > DateAdd(dLastSeen, gb.Minute, period)) Then
      If rResult!switchable Then
        Devices.SetDevice(rResult!id, rResult!resetvalue)
      Else
        Devices.ValueUpdateV2(rResult!id, 1, rResult!resetvalue)
      Endif
    Endif
  Next

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return type of control for device
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub CheckControlDevice(sAddress As String, iInterface As Integer) As String

  Dim rDeviceFind As Result

  Try rDeviceFind = Main.hDB.Exec("SELECT switchable, dimable FROM devices WHERE address LIKE &1 AND interface = &2 AND enabled is TRUE", sAddress, iInterface)
  If rDeviceFind.Available Then
    If rDeviceFind!switchable Then
      Return "Switch"
    Else If rDeviceFind!dimable Then
      Return "Dimmer"
    Else
      Return "Other"
    Endif
  Else
    Return "Switch"
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' get the ID of the device with sDeviceName.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindIdForDevice(sDeviceName As String) As Integer
  ' TODO deprecated
  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT id FROM devices WHERE name = &1", sDeviceName)
  ' found only one unique entry?
  If rDevice.Count > 1 Then
    Main.WriteDebugLog(("[Devices] The following devices have same name:"))
    For Each rDevice
      Main.WriteDebugLog(("[Devices] Device with address '") & rDevice!address & "' is '" & rDevice!name & "'")
    Next
    Return 0
  ' ok this one seems useful
  Else If rDevice.Count = 1 Then
      Return rDevice!id
  ' no interface found, something is wrong
  Else
    Main.WriteDebugLog(("[Devices] Device with name '") & sDeviceName & ("' not found!"))
    Return 0
  Endif

Catch
  Main.WriteDebugLog(("[Devices] Error while trying to find id: ") & Error.Text & (" at ") & Error.Where)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' delete device with iId.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DeleteDevice(iId As Integer) As Boolean
  ' TODO: handle in database
  
  Main.hDB.Exec("DELETE FROM devices WHERE id = &1", iId)
  Main.hDB.Exec("DELETE FROM device_values WHERE deviceid = &1", iId)
  Main.hDB.Exec("DELETE FROM devices_log WHERE deviceid = &1", iId)
  Main.hDB.Exec("DELETE FROM devices_pachube WHERE deviceid = &1", iId)
  Main.hDB.Exec("DELETE FROM devices_bwired WHERE deviceid = &1", iId)
  Return True

Catch
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' make device with iId hidden.
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub HideDevice(iId As Integer) As Boolean

  Dim rResult As Result

  rResult = Main.hDB.Exec("UPDATE devices SET hide = TRUE WHERE id = &1", iId)
  Return True

Catch
  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Toggle device state
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ToggleDeviceValue(iId As Integer) As String
 
  If CheckIfDeviceExists(iId)
    Return Toggle(GetCurrentValueForDevice(iId, 1))
  Else 
    Return ""
  Endif 

Catch
  Return ""

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Find devices and return fields from the database, depending which devices are required.
' Optionally the output van be sorted, by specifying the sOrder.
' sWhich can have (or a combination, delimited by a +):
' all, enabled, disabled, visible, hidden, switch, dim, sensor, ext (default is: enabled+visible)
' ext - all device information, gathered from multiple tables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetDeviceList(Optional sWhich As String, Optional sSortOrder As String) As Result

  Dim rDevice As Result
  Dim sWhere As String
  Dim sAppend As String
  Dim sTemp As String
  Dim sSQL As String

  sWhich = LCase(sWhich)

  ' If nobody specifies anything, we just it all
  If sWhich = "" Then
    sWhich = "all+ext"
  Endif

  If Not InStr(sWhich, "all") Then

    If Not sWhich Then
      sWhich = "enabled+visible"
    Endif

    sAppend = ""
    sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "enabled"), "enabled IS TRUE", ""), sAppend)
    sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "disabled"), "enabled IS FALSE", ""), sAppend)

    If sAppend Then
      If sWhere Then
        sWhere &= " AND (" & sAppend & ")"
      Else
        sWhere &= "WHERE (" & sAppend & ")"
      Endif
    Endif

    sAppend = ""
    sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "visible"), "hide IS FALSE", ""), sAppend)
    sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "hidden"), "hide IS TRUE", ""), sAppend)

    If sAppend Then
      If sWhere Then
        sWhere &= " AND (" & sAppend & ")"
      Else
        sWhere &= "WHERE (" & sAppend & ")"
      Endif
    Endif

    If InStr(sWhich, "switch") Or InStr(sWhich, "dim") Or InStr(sWhich, "sensor") Then

      sAppend = ""
      sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "switch"), "switchable IS TRUE", ""), sAppend)
      sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "dim"), "dimable IS TRUE", ""), sAppend)
      sAppend = GetDeviceList_BuildSQL(IIf(InStr(sWhich, "sensor"), "(switchable IS FALSE AND dimable IS FALSE)", ""), sAppend)

      If sAppend Then
        If sWhere Then
          sWhere &= " AND (" & sAppend & ")"
        Else
          sWhere &= "WHERE (" & sAppend & ")"
        Endif
      Endif

    Endif

  Endif
 
  If InStr(sWhich, "ext") Then
    sSQL = "SELECT devices.*, locations.name as 'locationname', floors.name as 'floorplanname', floors.image as `floorplanimage`, devicetypes.name as 'devicetypename', interfaces.name as `interfacename` FROM devices LEFT OUTER JOIN locations ON devices.location = locations.id LEFT OUTER JOIN floors ON devices.floorplan = floors.floor LEFT JOIN devicetypes ON devices.module = devicetypes.id LEFT JOIN interfaces ON devices.interface = interfaces.id " & sWhere
  Else
    sSQL = "SELECT devices.*, locations.name as 'locationname' FROM devices LEFT OUTER JOIN locations on devices.location = locations.id " & sWhere
  Endif

  If sSortOrder Then
    sSQL &= " ORDER BY " & sSortOrder
  Endif

  Try rDevice = Main.hDB.Exec(sSQL)
  If Error Then
    Main.WriteDebugLog("[Devices] ERROR: '" & Error.Text & "' at " & Error.Where)
  Endif

  Return rDevice

End

Private Sub GetDeviceList_BuildSQL(sTemp As String, sAppend As String) As String

  If sTemp Then
    If sAppend Then
      Return sAppend & " OR " & sTemp
    Else
      Return sAppend & sTemp
    Endif
  Else
    Return sAppend 
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Retrieve all locations possible for the devices
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Public Sub GetLocationList() As Result

  Dim rLocation As Result

  Try rLocation = Main.hDB.Exec("SELECT * FROM locations")
  If Error Then
    Main.WriteDebugLog("[Devices] ERROR: '" & Error.Text & "' at " & Error.Where)
  Endif

  Return rLocation

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' return disable openzwavepoll boolean for device with address
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FindOpenZWavePollForDevice(iInstance As Integer, sAddress As String) As Boolean

  Dim rDevice As Result

  Try rDevice = Main.hDB.Exec("SELECT log FROM devices WHERE address = &1 AND interface = 15", sAddress)
  If Not Error Then
    If rDevice.Available Then
      If rDevice.Count = 1 Then
        Return rDevice!poll
      Else
        Return False
      Endif
    Else
      Return False
    Endif
  Else
    Return False
  Endif

End
