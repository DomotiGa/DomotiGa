' Gambas class file

' Description:
' FDeviceEditor.class
' Support for creating and edit all device parameters.

' Development Status:
' Finished and working, maybe needs better error checking.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public rResult As Result
Public rResultType As Result
Public rResultInterface As Result
Public rResultInterfaces As Result
Public rResultLocations As Result
Public rResultLocation As Result
Public rResultGroups As Result
Public rResultDeviceTypes As Result
Public rResultFloorplan As Result
Public bAddDevice As Boolean
Private sCurrentDeviceName As String
Private sRRDValueType As String
Private sRRDValue2Type As String
Private sRRDValue3Type As String
Private sRRDValue4Type As String
Private sValueDS As String
Private sValue2DS As String
Private sValue3DS As String
Private sValue4DS As String
Private bDeletedRRD As Boolean

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  cmbHouseCode.Hide
  spDeviceCode.Hide
  LoadDevice()
  FillLog()

  ' give focus to first field
  tbName.SetFocus

End

Private Sub FillLog()

  If Not bAddDevice Then
    DataSourceLog.Filter = "deviceid = " & FDevices.tbvDevices[FDevices.iCurRow, 0].Text
  Else
    DataSourceLog.Filter = "deviceid = -1"
  Endif

  ' [GB2:DBGV] WITH DataBrowserLog.GridView
  With DataBrowserLog.View
    .Columns[0].Text = ("Changed")
    .Columns[0].Width = 170
    .Columns[1].Text = ("Value1")
    .Columns[1].Width = 60
    .Columns[2].Text = ("Value2")
    .Columns[2].Width = 60
    .Columns[3].Text = ("Value3")
    .Columns[3].Width = 60
    .Columns[4].Text = ("Value4")
    .Columns[4].Width = 60
  End With

End

Public Sub LoadDevice()

  Dim cDeviceType As New Collection
  Dim sDeviceType As String

  ' fill combo with available modules/devicetypes
  cmbModule.Add(" ")
  cmbDeviceType.Add(" ")
  Try rResultDeviceTypes = Main.hDB.Exec("SELECT * FROM devicetypes ORDER BY name")
  If rResultDeviceTypes Then
    If rResultDeviceTypes.Count Then
      For Each rResultDeviceTypes
        cmbModule.Add(rResultDeviceTypes!name)
        ' weed out double device types
        If Len(rResultDeviceTypes!type)
          If cDeviceType.Exist(rResultDeviceTypes!type) Then Continue
          cDeviceType.Add(rResultDeviceTypes!type, rResultDeviceTypes!type)
        Endif
      Next
      For Each sDeviceType In cDeviceType
        cmbDeviceType.Add(sDeviceType)
      Next
    Else
      Message(("No devicetypes found!"))
    End If
  Else
    Message(("No devicetypes database table found!"))
  End If

  ' fill combo with available interfaces
  cmbInterface.Add(" ")
  Try rResultInterfaces = Main.hDB.Exec("SELECT * FROM interfaces ORDER BY name")
  If rResultInterfaces Then
    If rResultInterfaces.Count Then
      For Each rResultInterfaces
        cmbInterface.Add(rResultInterfaces!name)
      Next
    Else
      Message(("No interfaces found!"))
    End If
  Else
    Message(("No interfaces database table found!"))
  End If

  ' fill available groups list
  rResultGroups = Main.hDB.Exec("SELECT * FROM groups ORDER BY name")
  If rResultGroups Then
    If rResultGroups.Count Then
      For Each rResultGroups
        Try lvGroupAvail.Add(rResultGroups!name, rResultGroups!name)
      Next
    End If
  Else
    Message(("No groups database table found!"))
  End If

  ' fill combo with available locations
  cmbLocation.Add(" ")
  rResultLocations = Main.hDB.Exec("SELECT * FROM locations ORDER BY name")
  If rResultLocations Then
    If rResultLocations.Count Then
      For Each rResultLocations
        cmbLocation.Add(rResultLocations!name)
      Next
    End If
  Else
    Message(("No locations database table found!"))
  End If

  ' fill combo with available floorplans
  cmbFloorplan.Add(" ")
  rResultFloorplan = Main.hDB.Exec("SELECT * FROM floors ORDER BY name")
  If rResultFloorplan Then
    If rResultFloorplan.Count Then
      For Each rResultFloorplan
        cmbFloorplan.Add(rResultFloorplan!name)
      Next
    End If
  Else
    Message(("No floorplans database table found!"))
  End If

  If Not bAddDevice Then
    ' get selected device values
    rResult = Main.hDB.Exec("SELECT * FROM devices WHERE id = &1", FDevices.tbvDevices[FDevices.iCurRow, 0].Text)
    If rResult.Count >= 1 Then
      tbName.Text = rResult!name
      FDeviceEditor.Title = "Edit Device: " & rResult!name
      sCurrentDeviceName = rResult!name
      Try rResultType = Main.hDB.Exec("SELECT * FROM devicetypes WHERE id = &1", rResult!module)
      If rResultType.Count Then
        cmbModule.Text = rResultType!name
        cmbDeviceType.Text = rResultType!type
        tbDeviceDescr.Text = rResultType!description
        FillAddressFields(rResultType!type)
        SelectAddressFields(rResultType!type, rResultType!addressformat)
      End If
      ' load device picture if available
      LoadDeviceImage()
      ' select interface
      Try rResultInterface = Main.hDB.Exec("SELECT * FROM interfaces WHERE id = &1", rResult!interface)
      If rResultInterface.Count Then cmbInterface.Text = rResultInterface!name

      ' select location
      rResultLocation = Main.hDB.Exec("SELECT * FROM locations WHERE id = &1", rResult!location)
      If rResultLocation.Count Then
        Try cmbLocation.Text = rResultLocation!name
      Else
        cmbLocation.Text = ("Not Defined")
      End If

      ' put groups in selected or available groups list
      rResultGroups = Main.hDB.Exec("SELECT * FROM groups")
      If (rResultGroups.Count >= 1) Then
        For Each rResultGroups
          If InStr("|" & rResult!groups & "|", "|" & rResultGroups!name & "|") Then
            Try lvGroupActive.Add(rResultGroups!name, rResultGroups!name)
            Try lvGroupAvail.Remove(rResultGroups!name)
          End If
        Next
      End If

      ' rest of fields
      cmbLabel.Text = rResult!label
      cmbLabel2.Text = rResult!label2
      cmbLabel3.Text = rResult!label3
      cmbLabel4.Text = rResult!label4
      tbValue.Text = rResult!value
      tbValue2.Text = rResult!value2
      tbValue3.Text = rResult!value3
      tbValue4.Text = rResult!value4
      tbOnIcon.Text = rResult!onicon
      tbOffIcon.Text = rResult!officon
      tbDimIcon.Text = rResult!dimicon
      Try tbLastSeen.Text = Replace$(Str$(Format(rResult!lastseen, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
      Try tbFirstSeen.Text = Replace$(Str$(Format(rResult!firstseen, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
      Try tbLastChanged.Text = Replace$(Str$(Format(rResult!lastchanged, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")

      tbCorrection1.Text = rResult!correction
      tbCorrection2.Text = rResult!correction2
      tbCorrection3.Text = rResult!correction3
      tbCorrection4.Text = rResult!correction4

      FillCorrectionFields()

      DisplayIcons()

      Try chkEnabled.Value = rResult!enabled
      Try chkHide.Value = rResult!hide
      Try chkLog.Value = rResult!log
      Try chkLogDisplay.Value = rResult!logdisplay
      Try chkLogSpeak.Value = rResult!logspeak
      Try chkSwitchable.Value = rResult!switchable
      Try chkDimable.Value = rResult!dimable
      Try chkExtended.Value = rResult!extcode
      Try chkRepeat.Value = rResult!repeatstate
      Try chkPoll.Value = rResult!poll
      vbRepeatPeriod.Value = rResult!repeatperiod
      vbRepeatPeriod.Enabled = chkRepeat.Value
      Try chkReset.Value = rResult!reset
      vbResetPeriod.Value = rResult!resetperiod
      vbResetPeriod.Enabled = chkReset.Value
      tbResetValue.Text = rResult!resetvalue
      tbResetValue.Enabled = chkReset.Value

      tbComments.Text = rResult!comments
      tbBatt.Text = rResult!batterystatus
      ' floorplan values
      tbX.Text = rResult!x
      tbY.Text = rResult!y

      ' select floorplan
      rResultFloorplan = Main.hDB.Exec("SELECT * FROM floors WHERE floor = &1", rResult!floorplan)
      If rResultFloorplan.Count Then
        cmbFloorplan.Text = rResultFloorplan!name
      Else
        cmbFloorplan.Text = ("Not Defined")
      End If

      ' rrdtool graph stuff
      Try chkRRD.Value = rResult!rrd
      chkRRD_Click()
      Try chkGraph.Value = rResult!graph
      tbValueDS.Text = rResult!valuerrddsname
      tbValue2DS.Text = rResult!value2rrddsname
      tbValue3DS.Text = rResult!value3rrddsname
      tbValue4DS.Text = rResult!value4rrddsname
      cmbValueType.Text = rResult!valuerrdtype
      cmbValue2Type.Text = rResult!value2rrdtype
      cmbValue3Type.Text = rResult!value3rrdtype
      cmbValue4Type.Text = rResult!value4rrdtype
      sRRDValueType = rResult!valuerrdtype
      sRRDValue2Type = rResult!value2rrdtype
      sRRDValue3Type = rResult!value3rrdtype
      sRRDValue4Type = rResult!value4rrdtype
      sValueDS = rResult!valuerrddsname
      sValue2DS = rResult!value2rrddsname
      sValue3DS = rResult!value3rrddsname
      sValue4DS = rResult!value4rrddsname
    Else
      Message(("Couldn't load device record!"))
      Me.Close
    End If
  End If

End

Public Sub FillCorrectionFields()

  tbResult1.Text = Devices.CorrectValue(rResult!value, tbCorrection1.Text, rResult)
  tbResult2.Text = Devices.CorrectValue(rResult!value2, tbCorrection2.Text, rResult)
  tbResult3.Text = Devices.CorrectValue(rResult!value3, tbCorrection3.Text, rResult)
  tbResult4.Text = Devices.CorrectValue(rResult!value4, tbCorrection4.Text, rResult)

End

Public Sub RefreshDeviceType()

  rResultType = Main.hDB.Exec("SELECT * FROM devicetypes WHERE name = &1", cmbModule.Text)
  If rResultType.Count Then
    ' enable correct address fields
    SelectAddressFields(rResultType!type, rResultType!addressformat)
    cmbDeviceType.Text = rResultType!type
    tbDeviceDescr.Text = rResultType!description
  Endif

End

Public Sub DoesInterfaceDoDeviceType() As Boolean

  Dim rResultInt, rResultDevType As Result
  Dim sDevProtocol, sIntProtocol As String

  rResultDevType = Main.hDB.Exec("SELECT type FROM devicetypes WHERE name = &1", cmbModule.Text)
  sDevProtocol = rResultDevType!type

  rResultInt = Main.hDB.Exec("SELECT type FROM interfaces WHERE name = &1", cmbInterface.Text)
  sIntProtocol = rResultInt!type

  If InStr(sIntProtocol, sDevProtocol) Then
    Return True
  Else
    Return False
  End If

End

Public Sub GetDeviceType() As Integer

  rResultType = Main.hDB.Exec("SELECT id FROM devicetypes WHERE name = &1", cmbModule.Text)
  Return rResultType!id

End

Public Sub GetInterface() As Integer

  rResultInterface = Main.hDB.Exec("SELECT id FROM interfaces WHERE name = &1", cmbInterface.Text)
  Return rResultInterface!id

End

Public Sub GetLocation() As Integer

  rResultLocation = Main.hDB.Exec("SELECT id FROM locations WHERE name = &1", cmbLocation.Text)
  Try Return rResultLocation!id

End

Public Sub SelectAddressFields(sType As String, sAddressFormat As String)

  cmbHouseCode.Enabled = False
  cmbHouseCode.Hide
  spDeviceCode.Hide
  tbOtherAddress.Show
  chkPoll.Enabled = False
  lblOther.Text = "Address"
  
  Select Case sType
    Case "Shell"
      lblOther.Text = ("Command")
    Case "Ping"
      lblOther.Text = ("IP Addr.")
    Case "1-Wire"
      lblOther.Text = ("1-Wire ID")
    Case "Bluetooth"
      lblOther.Text = ("MAC Addr.")
    Case "HDDTemp"
      lblOther.Text = ("Device")
    Case "Plugwise"
      lblOther.Text = ("MAC Addr.")
    Case "HEUK", "AC", "HEEU"
      lblOther.Text = ("Dev Unit")
    Case "UPS"
      lblOther.Text = ("UPS Name")
    Case "DSC"
      lblOther.Text = ("Zone Name")
    Case "Weeder"
      lblOther.Text = ("Output")
    Case "X10", "KAKU", "ARC", "PLCBUS", "Harrison", "Koppla", "Flamingo", "Waveman", "Impuls", "HE105", "RTS10", "RisingSun", "PhilipsSBC", "EMW200", "AB400"
      lblOther.Text = "X10 Addr."
      cmbHouseCode.Show
      spDeviceCode.Show
      tbOtherAddress.Hide
      cmbHouseCode.Enabled = True
    Case "KNX/EIB"
    Case "Z-Wave"
      lblOther.Text = ("Node ID")
      chkPoll.Enabled = True
    Case "T10-Preset"
      lblOther.Text = ("SwitchNo.")
    Case "FS10", "FS20", "RS200", "AB601", "IT", "REV", "BS-QU", "MARMI", "OA-FM", "KO-FC", "RS862"
      lblOther.Text = ("Addr.Str.")
    Case "GLatitude"
      lblOther.Text = ("Badge ID")
    Case "K8055"
      lblOther.Text = ("Port")
    Case Else
      lblOther.Text = ("Address")
  End Select
  lblInfo.Text = sAddressFormat

End

Public Sub FillAddressFields(sType As String)

  Select Case sType
    Case "X10", "KAKU", "ARC", "PLCBUS", "Harrison", "Koppla", "Flamingo", "Waveman", "Impuls", "HE105", "RTS10", "RisingSun", "PhilipsSBC", "EMW200", "AB400"
      ' fill address fields
      If rResult!address Then
        cmbHouseCode.Text = Left$(rResult!address, 1)
        Try spDeviceCode.Value = Right$(rResult!address, 2)
        If Error Then
          Try spDeviceCode.Value = Right$(rResult!address, 1)
        Endif
      End If
    Case Else
      If rResult!address Then
        tbOtherAddress.Text = rResult!address
      End If
  End Select

End

Public Sub GetAddress() As String

  If cmbHouseCode.Enabled = True Then
    Return cmbHouseCode.Text & Format$(spDeviceCode.Value, "0#")
  Else
    Return tbOtherAddress.Text
  End If

End

Public Sub tbName_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbInterface_Click()

  btnSave.Enabled = True

End

Public Sub cmbHouseCode_Click()

  btnSave.Enabled = True

End

Public Sub spDeviceCode_Click()

  btnSave.Enabled = True

End

Public Sub tbOtherAddress_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbLocation_Click()

  btnSave.Enabled = True

End

Public Sub tbLabel_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbLabel2_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbLabel3_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbLabel4_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbValue_KeyPress()

  btnSave.Enabled = True

End

Public Sub btnCancel_Click()

  bAddDevice = False
  Me.Close

End

Public Sub cmbModule_Click()

  RefreshDeviceType()
  btnSave.Enabled = True
  LoadDeviceImage()

End

Private Sub LoadDeviceImage()

  Dim sImage, sImage2, sName, sName2 As String

  sName = LCase(Replace(cmbModule.Text, " ", "-"))
  sName = Replace(sName, "/", "-")

  sName2 = LCase(Replace(tbName.Text, " ", "-"))
  sName2 = Replace(sName2, "/", "-")

  sImage = Main.sBaseDir &/ "icons/devices" &/ sName & ".jpg"
  sImage2 = Main.sBaseDir &/ "icons/devices" &/ sName2 & ".jpg"
  txtDeviceImage.Hide
  Try pbDevice.Picture = Picture.Load(sImage2)
  If Error Then
    Try pbDevice.Picture = Picture.Load(sImage)
    If Error Then
      pbDevice.Hide
      txtDeviceImage.Text = sName2 & ".jpg\n" & sName & ".jpg"
      txtDeviceImage.Show
    Else
      pbDevice.Show
    End If
  End If

End

Public Sub GetGroups() As String

  Dim iCount As Integer
  Dim sGroups As String = "|"

  lvGroupActive.MoveFirst

  For iCount = 1 To lvGroupActive.Count
    sGroups &= lvGroupActive.Item.Text & "|"
    lvGroupActive.MoveNext
  Next

  Return sGroups

End

Public Sub lvGroupAvail_Click()

  btnSave.Enabled = True
  lvGroupActive.Add(lvGroupAvail.Key, lvGroupAvail.Key)
  lvGroupAvail.Remove(lvGroupAvail.Key)

End

Public Sub lvGroupActive_Click()

  btnSave.Enabled = True
  lvGroupAvail.Add(lvGroupActive.Key, lvGroupActive.Key)
  lvGroupActive.Remove(lvGroupActive.Key)

End

Public Sub btnDeleteDevice_Click()

  Select Message.Question(("Are you sure that you want to delete this device?"), ("Yes"), ("No"))
    Case 1
      Devices.DeleteDevice(rResult!id)
      Me.Close
      FDevices.GetDeviceList()
  End Select

End

Private Sub DeviceNameExist(sName As String, Optional iDeviceId As Integer) As Boolean

  Dim iId As Integer

  iId = Devices.FindIdForDevice(sName)
  If iId And If iId <> iDeviceId Then Return True

  Return False

End

Private Sub DeviceAddressExist(sAddress As String, iInterface As Integer, Optional iDeviceId As Integer) As Boolean

  Dim rResultDevice As Result

  If Len(sAddress) = 0 Then Return True

  rResultDevice = Main.hDB.Exec("SELECT id,address FROM devices WHERE interface = &1 AND enabled is TRUE", iInterface)
  If rResultDevice Then
    If rResultDevice.Count Then
      For Each rResultDevice
        If iDeviceId Then
          If sAddress = rResultDevice!address And If rResultDevice!id <> iDeviceId Then Return True
        Else
          If sAddress = rResultDevice!address Then Return True
        End If
      Next
    End If
  End If
  Return False

End

Public Sub btnNewDevice_Click()

  Dim iId As Integer

  If Not tbName.Text Then
    Balloon(("Please enter a name for this device!"), tbName)
    Return
  End If
  If DeviceNameExist(tbName.Text) Then
    Balloon(("Please enter a unique name for this device!"), tbName)
    Return
  End If
  If Len(cmbModule.Text) < 2 Then
    Balloon(("Please select a device type for this device!"), cmbModule)
    Return
  End If
  If Len(cmbInterface.Text) < 2 Then
    Balloon(("Please select an interface for this device!"), cmbInterface)
    Return
  End If
  If Not DoesInterfaceDoDeviceType() Then
    Balloon(("Selected interface does not support this devicetype!"), cmbDeviceType)
    Return
  End If
  If chkEnabled.Value = False And bAddDevice Then
    Select Message.Question(("You haven't enabled this device, shall I enabled it?"), ("Yes"), ("No"))
      Case 1
        chkEnabled.Value = True
      Case 0
        ' you want it
    End Select
  Endif
  If chkEnabled.Value = True Then
    If DeviceAddressExist(GetAddress(), GetInterface()) Then
      Balloon(("Please enter a unique address for this device!"), Frame2)
      Return
    Endif
  Endif

  Main.hDB.Begin()
  rResult = Main.hDB.Create("devices")
  rResult!name = tbName.Text
  rResult!module = GetDeviceType()
  rResult!interface = GetInterface()
  rResult!address = GetAddress()
  rResult!location = GetLocation()
  rResult!groups = GetGroups()
  rResult!label = cmbLabel.Text
  rResult!label2 = cmbLabel2.Text
  rResult!label3 = cmbLabel3.Text
  rResult!label4 = cmbLabel4.Text
  rResult!onicon = tbOnIcon.Text
  rResult!correction = tbCorrection1.Text
  rResult!correction2 = tbCorrection2.Text
  rResult!correction3 = tbCorrection3.Text
  rResult!correction4 = tbCorrection4.Text
  rResult!officon = tbOffIcon.Text
  rResult!dimicon = tbDimIcon.Text
  rResult!comments = tbComments.Text
  rResult!enabled = chkEnabled.Value
  rResult!hide = chkHide.Value
  rResult!log = chkLog.Value
  rResult!logdisplay = chkLogDisplay.Value
  rResult!logspeak = chkLogSpeak.Value
  rResult!switchable = chkSwitchable.Value
  rResult!dimable = chkDimable.Value
  rResult!poll = chkPoll.Value
  rResult!rrd = chkRRD.Value
  rResult!graph = chkGraph.Value
  rResult!valuerrddsname = LCase(tbValueDS.Text)
  rResult!value2rrddsname = LCase(tbValue2DS.Text)
  rResult!value3rrddsname = LCase(tbValue3DS.Text)
  rResult!value4rrddsname = LCase(tbValue4DS.Text)
  rResult!valuerrdtype = cmbValueType.Text
  rResult!value2rrdtype = cmbValue2Type.Text
  rResult!value3rrdtype = cmbValue3Type.Text
  rResult!value4rrdtype = cmbValue4Type.Text
  If tbX.Text Then rResult!x = tbX.Text
  If tbY.Text Then rResult!y = tbY.Text
  rResult!floorplan = GetFloorplan()
  rResult!repeatstate = chkRepeat.Value
  rResult!repeatperiod = vbRepeatPeriod.Value
  rResult!reset = chkReset.Value
  rResult!resetperiod = vbRepeatPeriod.Value
  rResult!resetvalue = tbResetValue.Text
  rResult.Update()
  Main.hDB.Commit()
  FDevices.GetDeviceList()
  If chkRRD.Value Then
    rResult = Main.hDB.Exec("SELECT MAX(id) AS lastid FROM devices")
    iId = rResult!lastid
    If Main.bServer Then
      RRDTool.CreateRRDs(iId)
    Else
      XMLClient.CreateRRDs(iId)
    Endif
  Endif
  If FControl.Visible Then FControl.ReloadPage()
  bAddDevice = False

  Me.Close

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim sSql, sAddress, sGroups As String
  Dim iLocation, iInterface, iDeviceType, iFloorplan As Integer
  Dim sDivider1, sDivider2, sDivider3, sDivider4, sCorrection1, sCorrection2, sCorrection3, sCorrection4 As String

  If Main.bRRDToolEnabled = False And If chkRRD.Value = True Then
    Message(("You also need to enable the RRDTool module!"))
  Endif

  If bAddDevice Then
    btnNewDevice_Click()
    Return
  End If
  If Not tbName.Text Then
    Balloon(("Please enter a name for this device!"), tbName)
    Return
  End If
  If DeviceNameExist(tbName.Text, rResult!id) Then
    Balloon(("Please enter a unique name for this device!"), tbName)
    Return
  End If
  If Len(cmbModule.Text) < 2 Then
    Balloon(("Please select an device type for this device!"), cmbModule)
    Return
  End If
  If Len(cmbInterface.Text) < 2 Then
    Balloon(("Please select an interface for this device!"), cmbInterface)
    Return
  End If
  If Not DoesInterfaceDoDeviceType() Then
    Balloon(("Selected interface does not support this devicetype!"), cmbDeviceType)
    Return
  End If
  If sCurrentDeviceName <> tbName.Text And If chkGraph.Value Then
    RenameRRD(sCurrentDeviceName, tbName.Text)
  End If

  ' check if one or more datasource names are specified if RRD is enabled
  If chkRRD.Value Then
    If Not tbValueDS.Text And If Not tbValue2DS.Text And If Not tbValue3DS.Text And If Not tbValue4DS.Text Then
      tabDevice.Index = 2
      Balloon(("Please define at least one Data Source name!"), Frame8)
      Return
    Endif
  Endif

  If Not bDeletedRRD Then
    ' check for changed RRD datasource types
    If (Len(sRRDValueType) And sRRDValueType <> cmbValueType.Text) Or (Len(tbValueDS.Text) And sValueDS <> tbValueDS.Text) Then
      tabDevice.Index = 2
      Select Message.Question(("Because you change the Value1 RRD DS type or name,\nthe RRD database has to be deleted."), ("Delete"), ("Cancel"), ("Ignore"))
        Case 1
          btnDeleteRRD_Click()
        Case 2
          Return
        Case 3
      End Select
    Endif
    If (Len(sRRDValue2Type) And sRRDValue2Type <> cmbValue2Type.Text) Or (Len(tbValue2DS.Text) And sValue2DS <> tbValue2DS.Text) Then
      tabDevice.Index = 2
      Select Message.Question(("Because you change the Value2 RRD DS type or name,\nthe RRD database has to be deleted."), ("Delete"), ("Cancel"), ("Ignore"))
        Case 1
          btnDeleteRRD_Click()
        Case 2
          Return
        Case 3
      End Select
    Endif
    If (Len(sRRDValue3Type) And sRRDValue3Type <> cmbValue3Type.Text) Or (Len(tbValue3DS.Text) And sValue3DS <> tbValue3DS.Text) Then
      tabDevice.Index = 2
      Select Message.Question(("Because you change the Value3 RRD DS type or name,\nthe RRD database has to be deleted."), ("Delete"), ("Cancel"), ("Ignore"))
        Case 1
          btnDeleteRRD_Click()
        Case 2
          Return
        Case 3
      End Select
    Endif
    If (Len(sRRDValue4Type) And sRRDValue4Type <> cmbValue4Type.Text) Or (Len(tbValue4DS.Text) And sValue4DS <> tbValue4DS.Text) Then
      tabDevice.Index = 2
      Select Message.Question(("Because you change the Value4 RRD DS type or name,\nthe RRD database has to be deleted."), ("Delete"), ("Cancel"), ("Ignore"))
        Case 1
          btnDeleteRRD_Click()
        Case 2
          Return
        Case 3
      End Select
    Endif
  Endif
  ' check for valid RRD datasource names
  If Len(tbValueDS.Text) And If Len(CheckValidDSName(tbValueDS.Text)) Then
  'If InStr(tbValueDS.Text, " ") Or If InStr(tbValueDS.Text, "/") Then
    tabDevice.Index = 2
    Balloon.Warning("A ds-name must be 1 to 19 characters long using only the characters [a-zA-Z0-9_].", tbValueDS)
    Return
  Endif
  If Len(tbValue2DS.Text) And If Len(CheckValidDSName(tbValue2DS.Text)) Then 
    tabDevice.Index = 2
    Balloon.Warning("A ds-name must be 1 to 19 characters long using only the characters [a-zA-Z0-9_].", tbValue2DS)
    Return
  Endif
  If Len(tbValue3DS.Text) And If Len(CheckValidDSName(tbValue3DS.Text)) Then 
    tabDevice.Index = 2
    Balloon.Warning("A ds-name must be 1 to 19 characters long using only the characters [a-zA-Z0-9_].", tbValue3DS)
    Return
  Endif
  If Len(tbValue4DS.Text) And If Len(CheckValidDSName(tbValue4DS.Text)) Then 
    tabDevice.Index = 2
    Balloon.Warning("A ds-name must be 1 to 19 characters long using only the characters [a-zA-Z0-9_].", tbValue4DS)
    Return
  Endif
  If chkEnabled.Value = False And chkHide.Value = False And bAddDevice = True Then
    Select Message.Question(("You haven't enabled this device, shall I enabled it?"), ("Yes"), ("No"))
      Case 1
        chkEnabled.Value = True
      Case 0
        ' you want it
    End Select
  End If
  If chkEnabled.Value = True Then
    If DeviceAddressExist(GetAddress(), GetInterface(), rResult!id) Then
      Balloon(("Please enter a unique address for this device!"), Frame2)
      Return
    Endif
  Endif
  iDeviceType = GetDeviceType()
  iInterface = GetInterface()
  sAddress = GetAddress()
  iLocation = GetLocation()
  sGroups = GetGroups()
  iFloorplan = GetFloorplan()
  sCorrection1 = tbCorrection1.Text
  sCorrection2 = tbCorrection2.Text
  sCorrection3 = tbCorrection3.Text
  sCorrection4 = tbCorrection4.Text

  ' save new device settings
  sSql = "UPDATE devices SET name = &1, module = &2, interface = &3, address = &4, location = &5,"
  sSql &= "label = &6, label2 = &7, label3 = &8, value = &9, officon = &{10}, onicon = &{11},"
  sSql &= "batterystatus = &{12}, comments = &{13}, enabled = &{14}, groups = &{15}, value2 = &{16}, value3 = &{17},"
  sSql &= "hide = &{18}, switchable = &{19}, dimable = &{20}, log = &{21}, graph = &{22}, valuerrddsname = &{23},"
  sSql &= "value2rrddsname = &{24}, value3rrddsname = &{25}, valuerrdtype = &{26}, x = &{27}, y = &{28}, floorplan = &{29},"
  sSql &= "value2rrdtype = &{30}, value3rrdtype = &{31}, value4 = &{32}, label4 = &{33}, value4rrddsname = &{34},"
  sSql &= "value4rrdtype = &{35}, dimicon = &{36}, extcode = &{37}, correction = &{38}, correction2 = &{39}, correction3 = &{40}, correction4 = &{41}, logdisplay = &{42},"
  sSql &= "logspeak = &{43}, rrd = &{44}, repeatstate = &{45}, repeatperiod = &{46}, reset = &{47}, resetperiod = &{48}, resetvalue = &{49}, poll = &{50} WHERE id = &{51}"

  rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, rResultType!id, iInterface, sAddress, iLocation, cmbLabel.Text, cmbLabel2.Text, cmbLabel3.Text, tbValue.Text, tbOffIcon.Text, tbOnIcon.Text, tbBatt.Text, tbComments.Text, chkEnabled.Value, sGroups, tbValue2.Text, tbValue3.Text, chkHide.Value, chkSwitchable.Value, chkDimable.Value, chkLog.Value, chkGraph.Value, LCase(tbValueDS.Text), LCase(tbValue2DS.Text), LCase(tbValue3DS.Text), cmbValueType.Text, tbX.Text, tbY.Text, iFloorplan, cmbValue2Type.Text, cmbValue3Type.Text, tbValue4.Text, cmbLabel4.Text, LCase(tbValue4DS.Text), cmbValue4Type.Text, tbDimIcon.Text, chkExtended.Value, sCorrection1, sCorrection2, sCorrection3, sCorrection4, chkLogDisplay.Value, chkLogSpeak.Value, chkRRD.Value, chkRepeat.Value, vbRepeatPeriod.Value, chkReset.Value, vbResetPeriod.Value, tbResetValue.Text, chkPoll.Value, rResult!id)

  FDevices.GetDeviceList()
  If chkRRD.Value Then
    If Main.bServer Then
      RRDTool.CreateRRDs(rResult!id)
    Else
      XMLClient.CreateRRDs(rResult!id)
    Endif
  Endif
  If FControl.Visible Then FControl.ReloadPage()
  bAddDevice = False
  Me.Close

End

Private Sub CheckValidDSName(sText As String) As String

  Dim iCnt, iCheck As Integer
  Dim sCheck As String

  For iCnt = 1 To Len(sText)
    sCheck = Mid(sText, iCnt, 1)
    iCheck = Asc(sCheck)
    If iCheck > 47 And If iCheck < 58 Then
      Continue
    Else If iCheck > 64 And If iCheck < 91 Then
      Continue
    Else If iCheck > 96 And If iCheck < 123 Then
      Continue
    Else If iCheck = 95 Then
      Continue
    Else
      Return sCheck
    Endif
  Next

End

Public Sub tbValue2_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbValue3_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbValue4_KeyPress()

  btnSave.Enabled = True

End

Public Sub tbBatt_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  btnSave.Enabled = True

End

Public Sub chkHide_Click()

  btnSave.Enabled = True

End

Public Sub chkLog_Click()

  btnSave.Enabled = True

End

Public Sub tbComments_KeyPress()

  btnSave.Enabled = True

End

Public Sub spDeviceCode_Change()

  btnSave.Enabled = True

End

Public Sub chkGraph_Click()

  btnSave.Enabled = True

End

Public Sub tbValueDS_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbValueType_Click()

  btnSave.Enabled = True

End

Public Sub tbValue2DS_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbValue2Type_Click()

  btnSave.Enabled = True

End

Public Sub chkLogSpeak_Click()

  btnSave.Enabled = True

End

Public Sub chkLogDisplay_Click()

  btnSave.Enabled = True

End

Public Sub tbValue4DS_KeyPress()

  btnSave.Enabled = True

End

Public Sub cmbValue4Type_Click()

  btnSave.Enabled = True

End

Public Sub chkDimable_Click()

  chkSwitchable.Value = False
  btnSave.Enabled = True

End

Public Sub chkSwitchable_Click()

  chkDimable.Value = False
  btnSave.Enabled = True

End

Public Sub btnBattReplaced_Click()

  tbComments.Text &= ("Battery is replaced on ") & Format(Now(), "yyyy-mm-dd hh:nn:ss")
  tbBatt.Text = Null
  btnSave.Enabled = True

End

Public Sub btnIconOn_Click()

  tbOnIcon.Text = FSelectImage.Run(Main.sBaseDir &/ "icons" &/ tbOnIcon.Text, "icons")
  btnSave.Enabled = True
  DisplayIcons()

End

Public Sub btnIconOff_Click()

  tbOffIcon.Text = FSelectImage.Run(Main.sBaseDir &/ "icons" &/ tbOffIcon.Text, "icons")
  btnSave.Enabled = True
  DisplayIcons()

End

Public Sub btnIconDim_Click()

  tbDimIcon.Text = FSelectImage.Run(Main.sBaseDir &/ "icons" &/ tbDimIcon.Text, "icons")
  btnSave.Enabled = True
  DisplayIcons()

End

Public Sub btnEditor_Click()

  ' only start editor if location/floorplan is chosen
  If cmbFloorplan.Text And cmbFloorplan.Text <> " " Then
    FFloorplanEditor.pbIcon.Picture = Picture[Main.sBaseDir &/ "icons" &/ tbOnIcon.Text]
    FFloorplanEditor.txtX.Text = tbX.Text
    FFloorplanEditor.txtY.Text = tbY.Text
    FFloorplanEditor.Show()
  Else
    Message(("Select a floorplan first!"))
  End If

End

Public Sub GetFloorplan() As Integer

  rResultFloorplan = Main.hDB.Exec("SELECT floor FROM floors WHERE name = &1", cmbFloorplan.Text)
  Try Return rResultFloorplan!floor

End

Public Sub DisplayIcons()

  If Exist(Main.sBaseDir &/ "icons") Then
    If tbOnIcon.Text Then
      Try mbIconOn.Path = Main.sBaseDir &/ "icons" &/ tbOnIcon.Text
      If Not Error Then mbIconOn.Playing = True
    Else
      mbIconOn.Path = ""
      mbIconOn.Playing = False
      mbIconOn.Refresh
    End If

    If tbDimIcon.Text Then
      Try mbIconDim.Path = Main.sBaseDir &/ "icons" &/ tbDimIcon.Text
      If Not Error Then mbIconDim.Playing = True
    Else
      mbIconDim.Path = ""
      mbIconDim.Playing = False
      mbIconDim.Refresh
    End If

    If tbOffIcon.Text Then
      Try mbIconOff.Path = Main.sBaseDir &/ "icons" &/ tbOffIcon.Text
      If Not Error Then mbIconOff.Playing = True
    Else
      mbIconOff.Path = ""
      mbIconOff.Playing = False
      mbIconOff.Refresh
    End If
  Else
    Main.WriteDebugLog(("Error: Cannot find " & Main.sBaseDir &/ "icons" & " directory!"))
  End If

Catch ' some errors
  Main.WriteDebugLog(("Error: ") & ERROR.Text & (" at ") & ERROR.Where)

End

Private Sub RenameRRD(sOld As String, sNew As String)

  Dim sRRDOld, sRRDNew, sRRDOldName, sRRDNewName As String

  sRRDOldName = LCase(Replace$(sOld, " ", ""))
  sRRDOldName = Replace$(sRRDOldName, "/", "")
  sRRDOld = Main.sBaseDir &/ "rrd" &/ sRRDOldName & ".rrd"

  sRRDNewName = LCase(Replace$(sNew, " ", ""))
  sRRDNewName = Replace$(sRRDNewName, "/", "")
  sRRDNew = Main.sBaseDir &/ "rrd" &/ sRRDNewName & ".rrd"

  Try Move sRRDOld To sRRDNew
  If Error Then
    Try Copy sRRDOld To sRRDNew
    If Not Error Then Kill sRRDOld
  Endif

End

Public Sub cmbFloorplan_Click()

  btnSave.Enabled = True

End

Public Sub cmbLabel_Click()

  btnSave.Enabled = True

End

Public Sub cmbLabel2_Click()

  btnSave.Enabled = True

End

Public Sub cmbLabel3_Click()

  btnSave.Enabled = True

End

Public Sub btnDeleteRRD_Click()

  RRDTool.DeleteRRD(rResult!id)
  bDeletedRRD = True

End

Public Sub chkExtended_Click()

  btnSave.Enabled = True

End

Public Sub cmbLabel4_Click()

  btnSave.Enabled = True

End

Public Sub Corr_Change()

  FillCorrectionFields()

End

Public Sub chkRRD_Click()

  chkGraph.Enabled = chkRRD.Value

End

Public Sub btnDeleteLog_Click()

  Select Message.Question(("Are you sure that you want to delete all logged values for this device?"), ("Yes"), ("No"))
    Case 1
      Devices.EraseLog(rResult!id)
      DataSourceLog.Refresh
      DataBrowserLog.Refresh
  End Select

End

Public Sub RefreshModuleList()

  Dim rResultDeviceTypes2 As Result
  Dim sPattern As String = cmbDeviceType.Text

  If (Len(cmbDeviceType.Text) < 2) Then sPattern = "%"
  ' fill module combo with modules/devicetypes of sType
  Try rResultDeviceTypes2 = Main.hDB.Exec("SELECT name FROM devicetypes WHERE type LIKE &1 ORDER BY name", sPattern)
  If Not Error Then
    If rResultDeviceTypes2 Then
      If rResultDeviceTypes2.Count Then
        cmbModule.Clear
        cmbModule.Refresh
        For Each rResultDeviceTypes2
          cmbModule.Add(rResultDeviceTypes2!name)
        Next
      Endif
    Else
      Message(("No devicetypes database table found!"))
    Endif
  Endif
  Try cmbModule.Text = rResultType!name
  LoadDeviceImage()
  If bAddDevice Then RefreshInterfaceList()
  RefreshDeviceType()

End

Public Sub RefreshInterfaceList()

  Dim rResultInterface2 As Result
  Dim sPattern As String = "%" & cmbDeviceType.Text & "%"

  If (Len(cmbDeviceType.Text) < 2) Then sPattern = "%"
  ' fill interfaces combo with interfaces which support sType
  rResultInterface2 = Main.hDB.Exec("SELECT name FROM interfaces WHERE type LIKE &1 ORDER BY name", sPattern)
  If Not Error Then
    If rResultInterface2 Then
      If rResultInterface2.Count Then
        cmbInterface.Clear
        cmbInterface.Refresh
        For Each rResultInterface2
          cmbInterface.Add(rResultInterface2!name)
        Next
      Endif
    Else
      Message(("No interfaces database table found!"))
    Endif
  Endif

End

Public Sub cmbDeviceType_Click()

  RefreshModuleList()
  btnSave.Enabled = True

End

Public Sub chkReset_Click()

  tbResetValue.Enabled = chkReset.Value
  vbResetPeriod.Enabled = chkReset.Value
  btnSave.Enabled = True

End

Public Sub chkRepeat_Click()

  vbRepeatPeriod.Enabled = chkRepeat.Value
  btnSave.Enabled = True

End
