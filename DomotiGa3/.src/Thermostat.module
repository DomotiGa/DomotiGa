' Gambas module file

' Description:
' Thermostat.module
' Support for thermostat functionality.

' Development Status:
' Started.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "Thermostat"
Public PluginType As Integer = Plugin.Type_Module
Public PluginFriendlyName As String = "Thermostat"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Byte = 1

Public KeyName As String
Public LogLabel As String = "[Thermostat] "
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $bThermostatDebug As Boolean
Private $iPollTime As Integer

Public tThermostat As Timer
Public NO_TEMP As Integer = -999
Private aDerogatedHeating As New Collection
Private aPreviousRequestedTemperature As New Collection

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)
  
  KeyName = cPl.KeyName
  Instance = 1 ' Module can only have 1 instance 
  
  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $bThermostatDebug = cPl.Settings["debug"]
  $iPollTime = cPl.Settings["polltime"]
  
  ' Initialize 
  Run()
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for each Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()
  
  Try tThermostat.Stop
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Run timer
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Run()
  
  ' start poll timer for thermostat
  tThermostat = New Timer As "tThermostat"
  tThermostat.Delay = Main.iThermostatPollTime * 1000
  tThermostat.Start
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' gets called at each timer event
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tThermostat_Timer()
  
  ' do the job
  DoTheJob()
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub DoTheJob()
  
  Dim dtCurrent As Date
  Dim iScen, iHeat As Integer
  Dim rSched, rHeat As Result
  Dim fRequestedTemp, fMeasuredTemp As Float
  Dim sStatusDev, sStatusReg, sDeviceName, sRegulatorName As String
  Dim sOnDev As String = "On"
  Dim sOffDev As String = "Off"
  Dim sOnReg As String = "On"
  Dim sOffReg As String = "Off"
  Dim bOutOfDeviceOffset As Boolean
  
  ' get current date/time
  dtCurrent = Now()
  
  ' get active scenario - if iScen=0 -> no active scenario
  iScen = GetActiveScenario()
  If iScen = 0 Then Return
  
  ' get scenario's schedules
  rSched = Main.hDB.Exec("SELECT * FROM thermostat_schedule WHERE scenario=&1", iScen)
  If rSched Then
    If rSched.Count Then
      ' for each schedule
      For Each rSched
        ' get schedule's heating - can be only 1 record
        iHeat = rSched!heating
        rHeat = Main.hDB.Exec("SELECT * FROM thermostat_heating WHERE id=&1", iHeat)
        If rHeat Then
          If rHeat.Count Then
            If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Working on heating : " & rHeat!name)
            
            ' check for constant change
            fRequestedTemp = GetRequestedTempForHeating(iScen, iHeat)
            If aPreviousRequestedTemperature.Exist(iHeat) Then
              If aPreviousRequestedTemperature[iHeat] <> fRequestedTemp Then
                DeleteDerogateHeating(iHeat)
                If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Ending derogation for : " & rHeat!name)
              Endif
              aPreviousRequestedTemperature[iHeat] = fRequestedTemp
            Else
              aPreviousRequestedTemperature.Add(fRequestedTemp, iHeat)
            Endif
            
            ' get requested temp
            fRequestedTemp = GetDerogateHeating(iHeat)
            If fRequestedTemp = NO_TEMP Then
              fRequestedTemp = GetRequestedTempForHeating(iScen, iHeat)
            Endif
            
            ' get real temp
            Try fMeasuredTemp = CFloat(Replace(Devices.GetCurrentValueForDevice(rHeat!sensor, 1), ",", ".", gb.String))
            If Error Then
              Main.WriteLog(LogLabel & "ERROR: Measured temp is not numeric (Devicename: " & Devices.FindNameForDevice(rHeat!sensor) & ")")
              Return
            Endif
            
            If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Requested temp is : " & IIf(fRequestedTemp = NO_TEMP, "NONE (turn off)", fRequestedTemp) & " / Measured temp is : " & fMeasuredTemp)
            
            sDeviceName = Devices.FindNameForDevice(rHeat!device)
            sStatusDev = Devices.GetCurrentValueForDevice(rHeat!device, 1)
            
            If rHeat!regulator <> 0 Then
              sRegulatorName = Devices.FindNameForDevice(rHeat!regulator)
              If sRegulatorName Then
                sStatusReg = Devices.GetCurrentValueForDevice(rHeat!regulator, 1)
              Endif
            Else
              sRegulatorName = ""
            Endif
            If rHeat!deviceInverted = True Then
              sOnDev = "Off"
              sOffDev = "On"
            Else
              sOnDev = "On"
              sOffDev = "Off"
            Endif
            If rHeat!regulatorInverted = True Then
              sOnReg = "Off"
              sOffReg = "On"
            Else
              sOnReg = "On"
              sOffReg = "Off"
            Endif
            
            bOutOfDeviceOffset = False
            If Not (isInsideLatency(rHeat!device, rHeat!deviceLatency)) Then
              ' compare requested temp to sensor's one
              If fMeasuredTemp + rHeat!deviceOffsetBottom <= fRequestedTemp Then
                ' too cold
                bOutOfDeviceOffset = True
                If String.Comp(sStatusDev, sOffDev, gb.IgnoreCase) = 0 Then
                  If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Too cold here. Switching On device : " & sDeviceName)
                  Devices.SetDevice(rHeat!device, sOnDev)
                  sStatusDev = sOnReg
                Endif
                
                If sRegulatorName Then
                  If String.Comp(sStatusReg, sOffReg, gb.IgnoreCase) = 0 Then
                    If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Too cold here. Switching On regulator : " & sRegulatorName)
                    Devices.SetDevice(rHeat!regulator, sOnReg)
                    sStatusReg = sOnReg
                  Endif
                Endif
              Endif
              
              If fMeasuredTemp - rHeat!deviceOffsetTop >= fRequestedTemp Then
                ' too hot
                bOutOfDeviceOffset = True
                If String.Comp(sStatusDev, sOnDev, gb.IgnoreCase) = 0 Then
                  If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Too hot here. Switching Off device : " & sDeviceName)
                  Devices.SetDevice(rHeat!device, sOffDev)
                  sStatusDev = sOffReg
                Endif
                
                If sRegulatorName Then
                  If String.Comp(sStatusReg, sOnReg, gb.IgnoreCase) = 0 Then
                    If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Too hot here. Switching Off regulator : " & sRegulatorName)
                    Devices.SetDevice(rHeat!regulator, sOffReg)
                    sStatusReg = sOffReg
                  Endif
                Endif
              Endif
              
              If Not (bOutOfDeviceOffset) And (String.Comp(sStatusDev, sOnDev, gb.IgnoreCase) = 0) Then
                If sRegulatorName Then
                  sStatusReg = Devices.GetCurrentValueForDevice(rHeat!regulator, 1)
                  If fMeasuredTemp + rHeat!regulatorOffsetBottom <= fRequestedTemp Then
                    ' little cold
                    If String.Comp(sStatusReg, sOffReg, gb.IgnoreCase) = 0 Then
                      If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Little cold here. Switching On regulator : " & sRegulatorName)
                      Devices.SetDevice(rHeat!regulator, sOnReg)
                      sStatusReg = sOnReg
                    Endif
                  Endif
                  
                  If fMeasuredTemp - rHeat!regulatorOffsetTop >= fRequestedTemp Then
                    ' little hot
                    If String.Comp(sStatusReg, sOnReg, gb.IgnoreCase) = 0 Then
                      If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Little hot here. Switching Off regulator : " & sRegulatorName)
                      Devices.SetDevice(rHeat!regulator, sOffReg)
                      sStatusReg = sOffReg
                    Endif
                  Endif
                  
                Endif
              Endif
            Endif
          Endif
        Endif
      Next
    Endif
  Endif
  
End

Private Function GetActiveScenario() As Integer
  
  Dim rResultScen As Result
  
  rResultScen = Main.hDB.Exec("SELECT id, name FROM thermostat_scenarii WHERE name=&1", Main.GetGlobalVar("Thermostat_Mode"))
  
  If rResultScen Then
    If rResultScen.Count >= 1 Then
      If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Active scenario is : " & rResultScen!name)
      Return rResultScen!id
    Endif
  Endif
  
  If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "No active scenario for Thermostat Mode : " & Main.GetGlobalVar("Thermostat_Mode"))
  Return 0
  
End

Public Function GetRequestedTempForHeating(iScenario As Integer, iHeating As Integer) As Float
  
  Dim sSql As String
  Dim rTemp As Result
  Dim fReturn As Float
  
  fReturn = NO_TEMP
  
  sSql = "SELECT curtime() as curtime, dayofweek(curdate()) as curday, day, time, value FROM `thermostat_schedule_entry` se , `thermostat_constant` "
  sSql &= " c , `thermostat_heating` h WHERE c.id=se.constant AND se.secondary=h.secondary AND dayofweek(curdate())=Day AND se.scenario=&1 AND se.heating=&2 AND h.id=&2 ORDER BY Day, Time"
  rTemp = Main.hDB.Exec(sSql, iScenario, iHeating)
  If rTemp Then
    If rTemp.Count Then
      For Each rTemp
        If rTemp!curtime >= rTemp!time And rTemp!curday >= rTemp!day Then
          fReturn = rTemp!value
        Else
          Break
        Endif
      Next
      
      Return fReturn
    Else 
      Return NO_TEMP ' used when no secondary schedule is provided
    Endif
  Endif
  
  Return NO_TEMP
  
End

Public Function GetNextRequestedTempForHeating(iScenario As Integer, iHeating As Integer) As Float
  
  Dim sSql As String
  Dim rTemp As Result
  Dim fCurrentReq, fReturn As Float
  
  sSql = "SELECT curtime() as curtime,  time, value FROM `thermostat_schedule_entry` se , `thermostat_constant` "
  sSql &= " c , `thermostat_heating` h WHERE c.id=se.constant AND se.secondary=h.secondary AND dayofweek(curdate())=Day AND se.scenario=&1 AND se.heating=&2 AND h.id=&2 ORDER BY Time"
  rTemp = Main.hDB.Exec(sSql, iScenario, iHeating)
  If rTemp Then
    If rTemp.Count Then
      For Each rTemp
        If rTemp!curtime >= rTemp!time Then
          ' before now
          fCurrentReq = rTemp!value
        Else
          fReturn = rTemp!value
          If fCurrentReq <> fReturn Then Return fReturn
        Endif
      Next
    Endif
  Endif
  Return NO_TEMP
  
End

Private Function isInsideLatency(iDevice As Integer, iLatency As Integer) As Boolean
  
  If DateDiff(Devices.GetLastChangedForDevice(iDevice), Now(), gb.Minute) <= iLatency Then
    If Main.bThermostatDebug Then Main.WriteDebugLog(LogLabel & "Inside latency, nothing to do here.")
    Return True
  Endif
  
  Return False
  
Catch ' used when heating device is new (never changed) which causes DateDiff to raises an overflow
  Return False
  
End

Public Sub SetDerogateHeating(iScen As Integer, iHeat As Integer, fTemp As Float)
  
  If aDerogatedHeating.Exist(iHeat) Then
    If GetRequestedTempForHeating(iscen, iheat) = fTemp Then
      aDerogatedHeating.Remove(iHeat)
    Else
      aDerogatedHeating[iHeat] = fTemp
    Endif
  Else
    ' not found
    aDerogatedHeating.Add(fTemp, iHeat)
  Endif
  
End

Public Function GetDerogateHeating(iHeat As Integer) As Float
  
  If aDerogatedHeating.Exist(iHeat) Then
    Return aDerogatedHeating[iHeat]
  Else
    Return NO_TEMP
  Endif
  
End

Public Sub DeleteAllDerogateHeating()
  
  If aDerogatedHeating.Count Then
    Main.WriteLog(LogLabel & "Ending all derogated temp.")
    aDerogatedHeating.Clear
  Endif
  
End

Public Sub DeleteDerogateHeating(iHeat As Integer)
  
  If aDerogatedHeating.Exist(iHeat) Then aDerogatedHeating.Remove(iHeat)
  
End

Public Sub SetIsSecondary(iHeat As Integer, bSecondary As Boolean) As Boolean
  
  Dim rTemp As Result
  
  rTemp = Main.hDB.Exec("UPDATE thermostat_heating SET secondary = &1 WHERE id = &2", bSecondary, iHeat)
  
  If rTemp Then 
    Return True
  Else
    Return False
  Endif
  
End

Public Sub GetIsSecondary(iHeat As Integer) As Boolean
  
  Dim rResult As Result
  
  rResult = Main.hDB.Exec("Select secondary from thermostat_heating WHERE id = &1", iHeat)
  Return rResult!secondary
  
End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub GetHeatSensor(iHeat As Integer) As Integer
  
  Dim rResult As Result
  
  rResult = Main.hDB.Exec("Select sensor from thermostat_heating WHERE id=&1", iHeat)
  Try Return rResult!sensor
  
End
