' Gambas class file

' Description:
' FConditionEditor.class
' Support for creating and edit event conditions.

' Development Status:
' Just started, not working 100% yet.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private rResult As Result
Public bAddCondition As Boolean
Public bConditionEdit As Boolean
Public tMessage As New Timer

Public Sub Form_Open()

  ' give focus to first field
  txtConditionName.SetFocus

  LoadTreeView()

  If bAddCondition Then Me.Text = ("Create Condition")
  Me.Move(FMain.X + 50, FMain.Y + 70)

  If Not bAddCondition Then
    ' fetch condition details
    If bConditionEdit Then
      Try rResult = Main.hDB.Exec("SELECT * FROM conditions WHERE id = &1", FEvents.txtCurEvent)
    Else
      Try rResult = Main.hDB.Exec("SELECT * FROM conditions WHERE id = &1", FEventEditor.iCondition)
    Endif
    If Not rResult Then
      Message.Info(("Error: couldn't find condition details!"))
      Return
    Endif
    If rResult.Count = 1 Then
      txtConditionName.Text = rResult!name
      txtDescription.Text = rResult!description
      taFormula.Text = rResult!formula
    Else
      bAddCondition = True
    Endif
  Endif
  If txtConditionName.Text Then EnableButtons()

  tMessage = New Timer As "tMessage"
  tMessage.Delay = 3000 ' 3 seconds
  tMessage.Stop

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim sSql As String

  If Not txtConditionName.Text Then
    Balloon(("Please enter a name for this condition!"), txtConditionName)
    Return
  Endif

  If Not bAddCondition Then
    ' name has changed
    If rResult!name <> txtConditionName.Text Then
      If ConditionNameExist(txtConditionName.Text) Then
        Balloon(("Please enter a unique name for this condition!"), txtConditionName)
        Return
      Endif
    Endif
  Endif

  ' here we check the formula value
  If Not (Main.IsBool(Events.EvalFormula(taFormula.Text))) Then
    Balloon(("Please enter a boolean expression"), taFormula)
    Return
  Endif

  If bAddCondition Then
    If ConditionNameExist(txtConditionName.Text) Then
      Balloon(("Please enter a unique name for this condition!"), txtConditionName)
      Return
    Endif
    Main.hDB.Begin()
    rResult = Main.hDB.Create("conditions")
    rResult!name = txtConditionName.Text
    rResult!description = txtDescription.Text
    rResult!formula = taFormula.Text
    rResult.Update()
    Main.hDB.Commit()
  Else
    ' save new trigger settings
    sSql = "UPDATE conditions SET name = &1, formula = &2, description = &3 WHERE id = &4"
    Try rResultUpdate = Main.hDB.Exec(sSql, txtConditionName.Text, taFormula.Text, txtDescription.Text, rResult!id)
  Endif

  If FEventEditor.Id Then
    If FEventEditor.iEditingCondition = 1 Then
      FEventEditor.GetConditions(FEventEditor.cmbCondition1)
      FEventEditor.cmbCondition1.Text = txtConditionName.Text
    Endif
    If FEventEditor.iEditingCondition = 2 Then
      FEventEditor.GetConditions(FEventEditor.cmbCondition2)
      FEventEditor.cmbCondition2.Text = txtConditionName.Text
    Endif
  Endif

  bAddCondition = False
  Me.Close
  FEvents.GetEventList()

End

Public Sub btnNewCondition_Click()

  bAddCondition = True
  btnSave_Click()

End

Public Sub btnCancel_Click()

  Me.Close

End

Private Sub ConditionNameExist(sName As String) As Boolean

  Dim rResultCondition As Result

  rResultCondition = Main.hDB.Exec("SELECT id FROM conditions WHERE name = &1", sName)
  If rResultCondition Then
    If rResultCondition.Count Then
      Return True
    Endif
  Endif
  Return False

End

Private Sub EnableButtons()

  btnSave.Enabled = True
  btnDeleteCondition.Enabled = True
  btnNewCondition.Enabled = True

End

Public Sub txtConditionName_Change()

  EnableButtons()

End

Public Sub btnDeleteCondition_Click()

  Dim iId As Integer

  Select Message.Question(("Are you sure that you want to delete this condition?"), ("Yes"), ("No"))
    Case 1
      iId = GetConditionId(txtConditionName.Text)
      If iId Then
        Main.hDB.Exec("DELETE FROM conditions WHERE id = &1", iId)
      Endif
      If FEventEditor.Id Then
        If FEventEditor.iEditingCondition = 1 Then
          FEventEditor.GetConditions(FEventEditor.cmbCondition1)
        Endif
        If FEventEditor.iEditingCondition = 2 Then
          FEventEditor.GetConditions(FEventEditor.cmbCondition2)
        Endif
      Endif
      Me.Close
      FEvents.GetEventList()
  End Select

End

Private Sub GetConditionId(sName As String) As Integer

  Dim rResultCond As Result

  rResultCond = Main.hDB.Exec("SELECT id FROM conditions WHERE name = &1", sName)
  Try Return rResultCond!id

End

Public Sub txtDescription_KeyPress()

  btnSave.Enabled = True

End

Public Sub taFormula_Change()

  btnSave.Enabled = True

End

Public Sub LoadVariables(sParent As String)

  Dim vValue As Variant

  tvVariables.Add(Left(sParent, 3), sParent)
  For Each vValue In Main.GlobalVar
    LoadItem(Main.GlobalVar.Key, Main.GlobalVar.Key, Left(sParent, 3))
  Next

End

Public Sub LoadDevices(sParent As String)

  Dim iCount As Integer
  Dim rDevices As Result

  tvVariables.Add(Left(sParent, 3), sParent)

  ' TODO: Migrate to devicevalues
  rDevices = Devices.FindAllEnabledDevices("name")
  If Not rDevices Then
    LoadItem("ERROR", "Error: table 'devices' not found!", Left(sParent, 3))
    Return
  Endif

  For iCount = 0 To rDevices.Max
    tvVariables.Add(Left(sParent, 3) & "_" & rDevices!id, rDevices!name & " (" & rDevices!id & ")",, Left(sParent, 3))
    LoadItem("Value1", "Value1", Left(sParent, 3) & "_" & rDevices!id)
    LoadItem("Value2", "Value2", Left(sParent, 3) & "_" & rDevices!id)
    LoadItem("Value3", "Value3", Left(sParent, 3) & "_" & rDevices!id)
    LoadItem("Value4", "Value4", Left(sParent, 3) & "_" & rDevices!id)
    LoadItem("LastChanged", "LastChanged", Left(sParent, 3) & "_" & rDevices!id)
    LoadItem("LastSeen", "LastSeen", Left(sParent, 3) & "_" & rDevices!id)
    LoadItem("BatteryStatus", "BatteryStatus", Left(sParent, 3) & "_" & rDevices!id)
    rDevices.MoveNext
  Next

End

Public Sub LoadFunctions(sParent As String)

  Dim sSubText, sSubKey As String

  tvVariables.Add(Left(sParent, 3), sParent)

  LoadItem("if", "If(<Cond>, <TrueExpr>, <FalseExpr>)", Left(sParent, 3))

  sSubText = "Math"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("max", "Max(<Expr>, <Expr>)", sSubKey)
  LoadItem("min", "Min(<Expr>, <Expr>)", sSubKey)
  LoadItem("abs", "Abs(<Expr>)", sSubKey)
  LoadItem("frac", "Frac(<Expr>)", sSubKey)
  LoadItem("int", "Int(<Expr>)", sSubKey)
  LoadItem("sgn", "Sgn(<Expr>)", sSubKey)
  LoadItem("round", "Round(<Expr>)", sSubKey)
  LoadItem("rnd", "Rnd([<Min>], [<Max>])", sSubKey)

  sSubText = "Date"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("time", "Time(<Hours>, <Minutes>, <Seconds>)", sSubKey)
  LoadItem("date", "Date(<Year>, <Month>, <Day>)", sSubKey)
  LoadItem("now", "Now()", sSubKey)
  LoadItem("second", "Second(<Date>)", sSubKey)
  LoadItem("minute", "Minute(<Date>)", sSubKey)
  LoadItem("hour", "Hour(<Date>)", sSubKey)
  LoadItem("day", "Day(<Date>)", sSubKey)
  LoadItem("week", "Week(<Date>)", sSubKey)
  LoadItem("weekday", "WeekDay(<Date>)", sSubKey)
  LoadItem("month", "Month(<Date>)", sSubKey)
  LoadItem("quarter", "Quarter(<Date>)", sSubKey)
  LoadItem("year", "Year(<Date>)", sSubKey)
  LoadItem("datediff", "DateDiff(<Date1>, <Date2>, <Period>)", sSubKey)
  LoadItem("dateadd", "DateAdd(<Date>, <Period>, <Count>)", sSubKey)

  sSubText = "Period"
  tvVariables.Add(sSubKey & "_" & sSubText, sSubText,, sSubKey)
  sSubKey = sSubKey & "_" & sSubText
  LoadItem("gbsecond", "gb.Second", sSubKey)
  LoadItem("gbminute", "gb.Minute", sSubKey)
  LoadItem("gbhour", "gb.Hour", sSubKey)
  LoadItem("gbday", "gb.Day", sSubKey)
  LoadItem("gbweek", "gb.Week", sSubKey)
  LoadItem("gbweekday", "gb.WeekDay", sSubKey)
  LoadItem("gbmonth", "gb.Month", sSubKey)
  LoadItem("gbquarter", "gb.Quarter", sSubKey)
  LoadItem("gbyear", "gb.Year", sSubKey)

  sSubText = "String"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("left", "Left(<Expr>, <Length>)", sSubKey)
  LoadItem("right", "Right(<Expr>, <Length>)", sSubKey)
  LoadItem("mid", "Mid(<Expr>, <Start>, <Length>)", sSubKey)
  LoadItem("trim", "Trim(<Expr>)", sSubKey)
  LoadItem("replace", "Replace(<Expr>, <Pattern>, <ReplaceString>)", sSubKey)
  LoadItem("len", "Len(<Expr>)", sSubKey)
  LoadItem("format", "Format(<Expr>, [<Format>])", sSubKey)

  sSubText = "Conversion"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("cfloat2", "CFloat(Replace(<Expr>, \", \", \".\", gb.String))", sSubKey)
  LoadItem("cbool", "CBool(<Expr>)", sSubKey)
  LoadItem("cdate", "CDate(<Expr>)", sSubKey)
  LoadItem("cfloat", "CFloat(<Expr>)", sSubKey)
  LoadItem("cint", "CInt(<Expr>)", sSubKey)
  LoadItem("cstr", "CStr(<Expr>)", sSubKey)

End

Public Sub LoadOperators(sParent As String)

  tvVariables.Add(Left(sParent, 3), sParent)
  LoadItem("=", "=", Left(sParent, 3))
  LoadItem("<=", "<=", Left(sParent, 3))
  LoadItem(">=", ">=", Left(sParent, 3))
  LoadItem("<>", "<>", Left(sParent, 3))
  LoadItem("==", "==", Left(sParent, 3))
  LoadItem("(", "(", Left(sParent, 3))
  LoadItem(")", ")", Left(sParent, 3))
  LoadItem("And", "And", Left(sParent, 3))
  LoadItem("Or", "Or", Left(sParent, 3))
  LoadItem("Not", "Not", Left(sParent, 3))

End

Public Sub LoadItem(sKey As String, sText As String, sParent As String)

  tvVariables.Add(sParent & "_" & sKey, sText,, sParent)

End

Public Sub LoadActions(sParent As String)

  Dim sSubText, sSubKey As String

  tvVariables.Add(Left(sParent, 3), sParent)

  sSubText = "Devices"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("devices.valueupdate", "Devices.ValueUpdate(<Id>, <Value1>, <Value2>, <Value3>, <Value4>)", sSubKey) ' TODO: Migrate to devicevalues
  LoadItem("devices.setdevice", "Devices.SetDevice(<DeviceId>, <Value>)", sSubKey)

  sSubText = "Messaging"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("twitter.posttweet", "Twitter.PostTweet(\"<Msg>\")", sSubKey)
  LoadItem("mail", "Mail.SendMail(\"<Subject>\", \"<Body>\", OPTIONAL \"<To>\")", sSubKey)

  sSubText = "Status"
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("devices.mailbatterystatus", "Devices.MailBatteryStatus([\"all\"|\"empty\"])", sSubKey)

  sSubText = "Misc."
  sSubKey = Left(sParent, 3) & "_" & sSubText
  tvVariables.Add(sSubKey, sSubText,, Left(sParent, 3))
  LoadItem("main.setglobalvar", "Main.SetGlobalVar(<Name>,<Value>,OPTIONAL <Broadcast>)", sSubKey)

End

Public Sub LoadMacros(sParent As String)

  Dim iCount As Integer
  Dim rMacros As Result
  Dim sSql As String

  tvVariables.Add(Left(sParent, 3), sParent)

  sSql = "SELECT * FROM macros"
  rMacros = Main.hDB.Exec(sSql)
  If Not rMacros Then
    LoadItem("ERROR", "Error: table 'macro' not found!", Left(sParent, 3))
    Return
  Endif

  For iCount = 0 To rMacros.Max
    tvVariables.Add("Macro_" & rMacros!name, "Macro_" & rMacros!name,, Left(sParent, 3))
    rMacros.MoveNext
  Next

End

Public Sub LoadTreeView()

  tvVariables.Clear
  LoadVariables("Variables")
  LoadDevices("Devices")
  LoadFunctions("Functions")
  LoadOperators("Operators")
  LoadActions("Actions")
  LoadMacros("Macros")

End

Public Sub btnAdd_Click()

  tvVariables_DblClick

End

Public Sub tvVariables_Click()

  Dim vVal As Variant
  Dim sVal As String
  Dim aScan As String[]

  If tvVariables.Current.Children = 0 Then
    If Left(tvVariables.Current.Key, 3) = "Var" Then
      ' here the variable value is fetched
      Try vVal = Main.GetGlobalVar(Right(tvVariables.Current.Key, Len(tvVariables.Current.Key) - 4))
      If gb.Boolean = TypeOf(vVal) Then
        sVal = Main.DisplayBool(vVal)
      Else
        sVal = vVal
      Endif
      txtCurrentValue.Text = sVal
    Else If Left(tvVariables.Current.Key, 3) = "Dev" Then
      ' here the device value is fetched
      aScan = Split(tvVariables.Current.Key, "_")
      If aScan.Count = 3 Then
        Try txtCurrentValue.Text = Devices.GetValueForDevice(aScan[1], aScan[2])
      Endif
    Endif
  Endif

End

Public Sub tvVariables_DblClick()

  Dim sVal As String

  If tvVariables.Current.Children = 0 Then
    If Left(tvVariables.Current.Key, 3) = "Var" Then
      ' here a Variable is added to the formula
      sVal = tvVariables.Current.Key
    Else If Left(tvVariables.Current.Key, 3) = "Dev" Then
      ' here a Device value is added to the formula
      sVal = tvVariables.Current.Key
    Else
      sval = tvVariables.Current.Text
    Endif
    taFormula.Insert(sVal)
  Endif

End

Public Sub btnEval_Click()

  Dim sToEval As String

  ' if a part of the formula is selected, we only elvaluate this part
  If taFormula.Selection.Length = 0 Then
    sToEval = taFormula.Text
  Else
    sToEval = taFormula.Selection.Text
  Endif

  txtEvalResult.Text = Events.EvalFormula(sToEval)
  tMessage.Start

End

Public Sub btnClear_Click()

  taFormula.Clear

End

Public Sub tMessage_Timer()

  txtEvalResult.Text = ""
  tMessage.Stop

End
