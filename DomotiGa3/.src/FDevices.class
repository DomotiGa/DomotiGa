' Gambas class file

' Description:
' FDevices.class
' Display all devices.

' Development Status:
' Finished.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public iCurRow As Integer
Public bAutoRefresh As Boolean
Private bExpand As Boolean
Private mDevice As Menu

Public Sub Form_Open()

  CreateDeviceMenu()
  GetDeviceList()

End

Public Sub Form_GotFocus()

  If bAutoRefresh Then GetDeviceList()

End

Public Sub GetDeviceList()

  Dim iCount As Integer
  Dim rResult As Result
  Dim sValue, sValue2, sValue3, sValue4, sSql As String

  Application.Busy = True

  If bExpand Then
    sSql = "SELECT devices.*,locations.name AS 'locationname',interfaces.name AS 'interfacename',devicetypes.name AS 'devicetypename',floors.name AS 'floorplanname' FROM devices LEFT JOIN locations ON locations.id = devices.location LEFT JOIN interfaces ON interfaces.id = devices.interface LEFT JOIN devicetypes ON devicetypes.id = devices.module LEFT JOIN floors ON floors.floor = devices.floorplan"
  Else
    sSql = "SELECT devices.*,locations.name AS 'locationname' FROM devices LEFT JOIN locations ON locations.id = devices.location"
  Endif
  ' toggle enabled/disabled devices
  sSql &= IIf(chkDisabled.Value, " WHERE enabled IS FALSE", " WHERE enabled IS TRUE")
  ' hide hidden devices
  sSql &= IIf(chkHidden.Value, " AND hide IS TRUE ORDER BY name", " AND hide IS FALSE ORDER BY name")

  rResult = Main.hDB.Exec(sSql)
  If Not rResult Then
    Message.Info(("Error: table 'devices' not found!"))
    Return
  Endif

  With tbvDevices
    .Font.Size = "9"
    .Columns.Count = IIf(bExpand, 25, 9)
    .Rows.Count = rResult.Count
    .Columns[0].Width = 30
    .Columns[0].Title = ("Id")
    .Columns[1].Width = 20
    .Columns[2].Title = ("Device")
    .Columns[2].Width = 165
    .Columns[3].Title = ("Value1")
    .Columns[3].Width = 90
    .Columns[4].Title = ("Value2")
    .Columns[4].Width = 90
    .Columns[5].Title = ("Value3")
    .Columns[5].Width = 90
    .Columns[6].Title = ("Value4")
    .Columns[6].Width = 90
    .Columns[7].Title = ("Location")
    .Columns[7].Width = 100
    .Columns[8].Title = ("Last Seen")
    If bExpand Then
      .Columns[9].Title = ("DeviceType")
      .Columns[9].Width = 138
      .Columns[10].Title = ("Address")
      .Columns[10].Width = 98
      .Columns[11].Title = ("Interface")
      .Columns[11].Width = 125
      .Columns[12].Title = ("First Seen")
      .Columns[12].Width = 140
      .Columns[13].Title = ("Last Changed")
      .Columns[13].Width = 140
      .Columns[14].Title = ("Enabled")
      .Columns[14].Width = 58
      .Columns[15].Title = ("Hide")
      .Columns[15].Width = 55
      .Columns[16].Title = ("Log")
      .Columns[16].Width = 40
      .Columns[17].Title = ("Graph")
      .Columns[17].Width = 50
      .Columns[18].Title = ("On Icon")
      .Columns[18].Width = 75
      .Columns[19].Title = ("Off Icon")
      .Columns[19].Width = 75
      .Columns[20].Title = ("Battery")
      .Columns[20].Width = 55
      .Columns[21].Title = ("Groups")
      .Columns[21].Width = 204
      .Columns[22].Title = ("X")
      .Columns[22].Width = 25
      .Columns[23].Title = ("Y")
      .Columns[23].Width = 25
      .Columns[24].Title = ("Floorplan")
      .Columns[24].Width = 94
    Endif
  End With

  For iCount = 0 To rResult.Max
    tbvDevices[iCount, 0].Text = rResult!id
    tbvDevices[iCount, 1].Picture = Picture[FMain.GetStatusIcon(rResult!value, rResult!onicon, rResult!officon, rResult!dimicon)]

    tbvDevices[iCount, 2].Text = rResult!name
    sValue = Devices.CorrectValue(rResult!value, rResult!correction, rResult)
    sValue2 = Devices.CorrectValue(rResult!value2, rResult!correction2, rResult)
    sValue3 = Devices.CorrectValue(rResult!value3, rResult!correction3, rResult)
    sValue4 = Devices.CorrectValue(rResult!value4, rResult!correction4, rResult)

    ' fetch devicetype values
    If sValue Then
      tbvDevices[iCount, 3].Text = sValue & " " & rResult!label
    Else
      tbvDevices[iCount, 3].Text = ""
    Endif
    If sValue2 Then
      tbvDevices[iCount, 4].Text = sValue2 & " " & rResult!label2
    Else
      tbvDevices[iCount, 4].Text = ""
    Endif
    If sValue3 Then
      tbvDevices[iCount, 5].Text = sValue3 & " " & rResult!label3
    Else
      tbvDevices[iCount, 5].Text = ""
    Endif
    If sValue4 Then
      tbvDevices[iCount, 6].Text = sValue4 & " " & rResult!label4
    Else
      tbvDevices[iCount, 6].Text = ""
    Endif
    Try tbvDevices[iCount, 7].Text = rResult!locationname
    If rResult!lastseen Then
      tbvDevices[iCount, 8].Text = Replace$(Str$(Format(rResult!lastseen, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
    Else
      tbvDevices[iCount, 8].Text = ("Never")
    Endif
    If bExpand Then
      Try tbvDevices[iCount, 9].Text = rResult!devicetypename
      tbvDevices[iCount, 10].Text = rResult!address
      Try tbvDevices[iCount, 11].Text = rResult!interfacename
      If rResult!firstseen Then tbvDevices[iCount, 12].Text = Replace$(Str$(Format(rResult!firstseen, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
      If rResult!lastchanged Then tbvDevices[iCount, 13].Text = Replace$(Str$(Format(rResult!lastchanged, "yyyy-mm-dd hh:nn:ss")), Format(Date(), "yyyy-mm-dd") & " ", "")
      tbvDevices[iCount, 14].Text = Main.DisplayBool(rResult!enabled)
      tbvDevices[iCount, 15].Text = Main.DisplayBool(rResult!hide)
      tbvDevices[iCount, 16].Text = Main.DisplayBool(rResult!log)
      tbvDevices[iCount, 17].Text = Main.DisplayBool(rResult!graph)
      tbvDevices[iCount, 18].Text = rResult!onicon
      tbvDevices[iCount, 19].Text = rResult!officon
      tbvDevices[iCount, 20].Text = rResult!batterystatus
      Try tbvDevices[iCount, 21].Text = LTrim$(Replace$(rResult!groups, "|", " "))
      tbvDevices[iCount, 22].Text = rResult!x
      tbvDevices[iCount, 23].Text = rResult!y
      Try tbvDevices[iCount, 24].Text = rResult!floorplanname
    Endif
    rResult.MoveNext
  Next

Finally
  Application.Busy = False

End

Public Sub tbvDevices_ColumnClick(Column As Integer)

  FMain.SortTableView(FDevices.tbvDevices, Column, True)

End

Public Sub Form_Resize()

  tbvDevices.Move(4, 30, Me.ClientWidth - 8, Me.ClientHeight - 31)
  HBox1.Width = Me.ClientWidth - 15

End

Public Sub tbvDevices_DblClick()

  ' only continue if Row <> -1, which normally happens when DblClick on column and gambas3
  ' with gambas2 the first DblClick is unpredictable 
  If tbvDevices.Row = -1 Then Return

  ' open device editor window
  iCurRow = tbvDevices.Row
  If Main.bUserisAdmin Or Not Main.bAuthEnabled Then
    FDeviceEditor.Show()
  Else
    Message(("Sorry, editing devices is not enabled for users.\nLogin as an admin."))
  End If

End

Public Sub tbtnRefresh_Click()

  If tbtnRefresh.Value = True Then
    bAutoRefresh = True
    btnRefresh.Enabled = False
  Else
    bAutoRefresh = False
    btnRefresh.Enabled = True
  Endif

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' refresh tableview contents
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnRefresh_Click()

  GetDeviceList()

End

Public Sub tbtnExpand_Click()

  If tbtnExpand.Value = True Then
    bExpand = True
    tbtnExpand.Text = ("Shrink")
  Else
    bExpand = False
    tbtnExpand.Text = ("Expand")
  Endif
  GetDeviceList()

End

Public Sub chkDisabled_Click()

  GetDeviceList()

End

Public Sub chkHidden_Click()

  GetDeviceList()

End

Public Sub CreateDeviceMenu()

  Dim mDelete, mHide, mEdit As Menu

  mDevice = New Menu(FDevices)

  mEdit = New Menu(mDevice)
  mEdit.Text = "Edit"
  mEdit.Action = "edit"

  mDelete = New Menu(mDevice)
  mDelete.Text = "Delete"
  mDelete.Action = "delete"

  mHide = New Menu(mDevice)
  mHide.Text = "Hide"
  mHide.Action = "hide"

  mDevice.Hide

End

Public Sub tbvDevices_Menu()

  If Main.bUserisAdmin Or Not Main.bAuthEnabled Then mDevice.Popup()

End

Public Sub Action_Activate(key As String) As Boolean

  Dim iId As Integer

  If tbvDevices.Row = -1 Then Return
  iCurRow = tbvDevices.Row

  iId = Devices.FindIdForDevice(tbvDevices[iCurRow, 2].Text)
  If iId Then
    Select Case key
      Case "hide"
        Devices.HideDevice(iId)
        GetDeviceList()
      Case "edit"
        FDeviceEditor.Show()
      Case "delete"
        Select Message.Question(("Are you sure that you want to delete device '" & tbvDevices[iCurRow, 2].Text & "'?"), ("Yes"), ("No"))
          Case "1"
            Devices.DeleteDevice(iId)
            GetDeviceList()
        End Select
    End Select
  Endif
End
