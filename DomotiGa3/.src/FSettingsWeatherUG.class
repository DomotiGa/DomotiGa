' Gambas class file

' Description:
' FSettingsWeatherUG.class
' Settings form for wunderground API support.

' Development Status:
' Development just started.

' Links:
' http://www.wundeground.com

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private sCity As New String[]

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' initialize buttons and fill in current values
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)

  chkEnabled.Value = Main.bWeatherUGEnabled
  txtApiKey.Text = Main.sWeatherUGApiKey
  cmbCity.Text = Main.sWeatherUGCity
  txtRefresh.Text = Main.iWeatherUGRefresh
  chkDebug.Value = Main.bWeatherUGDebug

  If Main.bWeatherUGEnabled = False Then
    cmbCity.Enabled = False
    chkDebug.Enabled = False
    txtApiKey.Enabled = False
    txtCitySearch.Enabled = False
    btnCitySearch.Enabled = False
  End If
  If cmbCity.Text <> "" Then
    btnSave.Enabled = True
  Else
    btnSave.Enabled = False
  Endif

End

Public Sub btnCancel_Click()

  Me.Close

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' load defaults from database
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub btnDefaults_Click()

  Dim rResult As Result

  rResult = Main.GetSettingTable("weatherug", True) ' get defaults
  If rResult.Count Then
    chkEnabled.Value = rResult!enabled
    cmbCity.Text = rResult!city
    chkDebug.Value = rResult!debug
    txtRefresh.Text = rResult!polltime
    txtApiKey.Text = rResult!apikey
  End If
  btnSave.Enabled = True

End

Public Sub chkEnabled_Click()

  cmbCity.Enabled = chkEnabled.Value
  txtApiKey.Enabled = chkEnabled.Value
  chkDebug.Enabled = chkEnabled.Value
  btnCitySearch.Enabled = chkEnabled.Value
  txtCitySearch.Enabled = chkEnabled.Value
  btnSave.Enabled = True

End

Public Sub btnCitySearch_Click()

  Dim cJSON As Collection = WeatherUG.JSONWeather(txtCitySearch.text)

  Try cmbCity.Text = cJSON["response"]["results"][0]["country"] & "/" & cJSON["response"]["results"][0]["city"]
  If Error Then
    Try Message("ERROR: " & cJSON["response"]["error"]["type"] & "\n" & cJSON["response"]["error"]["description"])
  Endif

End

Public Sub txtCitySearch_KeyPress()

  btnSave.Enabled = True

End

Public Sub txtApiKey_KeyPress()

  btnSave.Enabled = True

End

Public Sub chkDebug_Click()

  btnSave.Enabled = True

End

Private Sub ValidInput() As Boolean

  If Not cmbCity.Text Then
    Balloon(("Please select a city!"), cmbCity)
    Return False
  End If
  If Not txtApiKey.Text Then
    Balloon(("Please enter your WeatherUG API key!"), txtApiKey)
    Return False
  End If
  Return True

End

Public Sub btnSave_Click()

  Dim rResult As Result
  Dim sCountryName As String

  If Not ValidInput() Then Return
  '--- save new WeatherUG settings ---
  rResult = Main.hDB.Exec("UPDATE settings_weatherug SET debug = &1, city = &2, enabled = &3, apikey = &4, polltime = &5 WHERE id = 1", chkDebug.Value, cmbCity.Text, chkEnabled.Value, txtApiKey.Text, txtRefresh.Text)
  rResult = Main.GetSettingTable("weatherug") ' reload settings
  If rResult.Count Then
    Main.bWeatherUGEnabled = rResult!enabled
    Main.sWeatherUGCity = rResult!city
    Main.bWeatherUGDebug = rResult!debug
    Main.sWeatherUGApiKey = rResult!apikey
    Main.iWeatherUGRefresh = rResult!polltime
  End If
  If Main.bServer Then
    Main.Restart_WeatherUG()
  Else
    XMLClient.ModuleRestart("WeatherUG")
  End If
  Me.Close

End
