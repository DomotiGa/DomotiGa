' Gambas class file

' Description:
' FTVGuideChannelDetail.class
' Form for showing TV channel details.

' Development Status:
' Imported from Kris's own project, needs testing.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module is written by Kris Wauters on February, 16th - 2010
' For more info or help, mailto: kris@digitalplayground.be

' Read file called COPYING for license details.

Public Sub Form_Open()

  PreInit()
  ShowData()

End

'--- initialises everything onscreen ---
Private Sub ShowData()

  '--- declare variables ---
  Dim StrSQLDate, StrText As String
  Dim rRes, rResDate As Result
  Dim IntI, IntCount As Integer

  ColVw.Clear
  ColVw.Columns[0].Width = LblTitle1.Width
  ColVw.Columns[1].Width = LblTitle2.Width
  ColVw.Columns[2].Width = LblTitle3.Width
  ColVw.Columns[3].Width = LblTitle4.Width
  StrSQLDate = Main.ParseTag(Me.tag, 2, "|")
  '--- fillup the programlist ---
  rRes = Main.hDB.Exec("SELECT * FROM tv_programs WHERE ChannelID = '" & Main.ParseTag(Me.tag, 1, "|") & "' AND (left(StartPoint,8) = '" & StrSQLDate & "' OR left(EndPoint,8) = '" & StrSQLDate & "') ORDER BY StartPoint")
  If rRes Then
    If rRes.Count > 0 Then
      For Each rRes
        ColVw.Add("P" & rRes!StartPoint, rRes!ProgramName)
        ColVw.MoveTo("P" & rRes!StartPoint)
        ColVw.Item[1] = rRes!CategoryName
        ColVw.Item[2] = Main.DateTimeFromSqlToApp(rRes!StartPoint)
        ColVw.Item[3] = Main.DateTimeFromSqlToApp(rRes!EndPoint)
        StrText = Main.Wrapit(rRes!ProgramDescription, LblTitle1.Width - 25, Me)
        IntCount = 0
        For IntI = 1 To Len(StrText)
          If Mid$(StrText, IntI, 1) = gb.newline Then IntCount = IntCount + 1
        Next
        For IntI = 1 To IntCount + 1
          ColVw.Add("C" & rRes!StartPoint & "_" & CStr(IntI), Main.ParseTag(StrText, IntI, gb.newline), "", "P" & rRes!StartPoint)
        Next
      Next
    End If
  End If
  '--- next, set date-labels, and check if next/previous-buttons needs to be enabled/disabled ---
  LblDate.Text = Main.DateFromSQLToApp(StrSqlDate)
  LblDateDay.Text = Format(Date(CInt(Left(StrSQLDate, 4)), CInt(Mid(StrSQLDate, 5, 2)), CInt(Mid(StrSQLDate, 7, 2))), "dddd")
  rResDate = Main.hDB.Exec("SELECT MIN(StartPoint) as MinDate,MAX(EndPoint) as MaxDate FROM tv_programs WHERE ChannelID = '" & Main.ParseTag(Me.tag, 1, "|") & "'")
  If rResDate Then
    If rResDate.Count > 0 Then
      rResDate.MoveFirst
      If Left(rResDate!MinDate, 8) < StrSQLDate Then
        BtnPrevious.Enabled = True
      Else
        BtnPrevious.Enabled = False
      End If
      If Left(rResDate!MaxDate, 8) > StrSQLDate Then
        BtnNext.Enabled = True
      Else
        BtnNext.Enabled = False
      End If
    End If
  End If

End

'--- show icon, channel-name etc ... ---
Private Sub PreInit()

  '--- declare variables ---
  Dim rRes As Result
  Dim StrFile As String

  rRes = Main.hDB.Exec("SELECT * FROM tv_channels WHERE ChannelID = '" & Main.ParseTag(Me.tag, 1, "|") & "'")
  If rRes Then
    If rRes.count > 0 Then
      rRes.MoveFirst
      Me.LblChannel.text = rRes!ChannelName
      If Len(rRes!BLOBlogo.data) > 10 Then
        StrFile = Main.BlobFromDB("SELECT * FROM tv_channels WHERE RecID=" & rRes!RecID, "BLOBlogo", "EXTlogo")
        Me.PicLogo.Picture = Picture.Load(StrFile)
      End If
    End If
  End If
  rRes = Null
  '--- do columnheaders ---
  ColVw.Columns.count = 4
  ColVw.Columns[0].Text = LblTitle1.Text
  ColVw.Columns[0].Width = LblTitle1.Width
  ColVw.Columns[1].Text = LblTitle2.Text
  ColVw.Columns[1].Width = LblTitle2.Width
  ColVw.Columns[2].Text = LblTitle3.Text
  ColVw.Columns[2].Width = LblTitle3.Width
  ColVw.Columns[3].Text = LblTitle4.Text
  ColVw.Columns[3].Width = LblTitle4.Width

End

'--- goto previous day ---
Public Sub BtnPrevious_Click()

  '--- declare variables ---
  Dim StrSQLDate As String

  StrSQLDate = Main.ParseTag(Me.tag, 2, "|")
  Me.tag = Main.ParseTag(Me.tag, 1, "|") & "|" & Format(DateAdd(Date(CInt(Left(StrSQLDate, 4)), CInt(Mid(StrSQLDate, 5, 2)), CInt(Mid(StrSQLDate, 7, 2))), gb.day, -1), "yyyymmdd")
  ShowData()

End

'--- goto next day ---
Public Sub BtnNext_Click()

  '--- declare variables ---
  Dim StrSQLDate As String

  StrSQLDate = Main.ParseTag(Me.tag, 2, "|")
  Me.tag = Main.ParseTag(Me.tag, 1, "|") & "|" & Format(DateAdd(Date(CInt(Left(StrSQLDate, 4)), CInt(Mid(StrSQLDate, 5, 2)), CInt(Mid(StrSQLDate, 7, 2))), gb.day, 1), "yyyymmdd")
  ShowData()

End
