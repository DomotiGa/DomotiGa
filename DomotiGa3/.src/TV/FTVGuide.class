' Gambas class file

' Description:
' FTVGuide.class
' Form for showing TV guide.

' Development Status:
' Imported from Kris's own project, needs testing.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' This module is written by Kris Wauters on February, 14th - 2010
' For more info or help, mailto: kris@digitalplayground.be

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean

Public Sub Form_Open()

  Dim rResult As Result

  ' TODO: add more instances
  rResult = Plugin.GetSettingsTable("settings_tvguide", 1)
  If rResult.Count Then
    $bEnabled = rResult!enabled
  Else
    $bEnabled = rResult!enabled
  Endif

  If $bEnabled Then
    TxtDate.text = Format(Now, "dd/mm/yyyy")
    LoadPreview(Main.DateFromAppToSQL(TxtDate.text))
    ChkProgramName.Value = True
  Else
    TxtDate.Enabled = False
    BtnSearch.Enabled = False
    BtnPreview.Enabled = False
    ChkProgramDescription.Enabled = False
    ChkProgramName.Enabled = False
    TxtSearch.Enabled = False
  Endif

End

Public Sub Form_Resize()

  Toolbar1.Move(0, 0, Me.ClientWidth - 8, 40)
  hBox1.Move(0, 0, Me.ClientWidth - 8, 40)
  frmPreview.Move(0, 23, Me.ClientWidth - 8, Me.ClientHeight - 5)
  LblDate.Move(8, 8, 119, 40)
  ScrollScale.Move(130, 8, Me.ClientWidth - 140, 35)
  ScrollPrograms.Move(130, 40, Me.ClientWidth - 142, Me.ClientHeight - 100)

  ScrollChannels.Move(7, 48, 119, Me.ClientHeight - 100)
  LblInfo.Move(7, Me.ClientHeight - 58, Me.ClientWidth - 25, 32)

End

' make a preview of the tvguide data
Private Sub LoadPreview(StrDate As String)

  '--- declare variables ---
  Dim rResChannel, rResProgram As Result
  Dim TmpBtnChannel, TmpBtnProgram As Button
  Dim TmpLbl As Label
  Dim SepLine As Separator
  Dim IntChannelCount, IntWidth, IntLeft, IntI, IntStartHour, IntStartMinute, IntStopHour, IntStopMinute As Integer
  Dim BoolOK As Boolean
  Dim StrFileLogo As String

  '--- first remove previous childs ---
  For IntI = ScrollChannels.Children.Count - 1 To 0 Step -1
    ScrollChannels.Children[IntI].Delete
  Next
  For IntI = ScrollPrograms.Children.Count - 1 To 0 Step -1
    ScrollPrograms.Children[IntI].Delete
  Next

  '--- set datelabel ---
  LblDate.text = Format(Date(CInt(Left(StrDate, 4)), CInt(Mid(StrDate, 5, 2)), CInt(Mid(StrDate, 7, 2))), "dddd") & gb.newline & Format(Date(CInt(Left(StrDate, 4)), CInt(Mid(StrDate, 5, 2)), CInt(Mid(StrDate, 7, 2))), "dd mmmm yyyy")
  'FrmPreview.Enabled = FALSE
  Application.Busy = True
  '--- setup 2 dummy labels to "fullfill" the correct width ---
  TmpLbl = New Label(ScrollPrograms)
  TmpLbl.X = 0
  TmpLbl.Width = 24 * 480
  TmpLbl.Text = ""
  TmpLbl.Transparent = True
  TmpLbl = New Label(ScrollScale)
  TmpLbl.X = 0
  TmpLbl.Width = 24 * 480
  TmpLbl.Text = ""
  TmpLbl.Transparent = True
  '--- put Hour-scale on top of scrollbox ---
  For IntI = 0 To 24
    '--- Hours ---
    If IntI > 0 Then
      TmpLbl = New Label(ScrollScale)
      TmpLbl.Width = 50
      TmpLbl.Foreground = Color.DarkRed
      TmpLbl.Alignment = Align.Center
      If IntI = 24 Then
        TmpLbl.Text = "[" & Format(IntI, "#0") & ":00]"
      Else
        TmpLbl.Text = "[" & Format(IntI, "#0") & ":00]"
      Endif
      TmpLbl.Y = 1
      TmpLbl.X = (IntI * 480) - (TmpLbl.Width / 2)
      SepLine = New Separator(ScrollScale)
      SepLine.Y = 25
      SepLine.Height = 15
      SepLine.Background = Color.DarkRed
      SepLine.Width = 2
      SepLine.X = (IntI * 480)
      ScrollPrograms.ScrollX = SepLine.X - (ScrollScale.Width / 2)
    Endif
    '--- Half Hours ---
    If IntI < 24 Then
      TmpLbl = New Label(ScrollScale)
      TmpLbl.Width = 50
      TmpLbl.Foreground = Color.DarkRed
      TmpLbl.Alignment = Align.Center
      TmpLbl.Text = "[" & Format(IntI, "00") & ":30]"
      TmpLbl.Y = 1
      TmpLbl.X = (IntI * 480) - (TmpLbl.Width / 2) + 240
      SepLine = New Separator(ScrollScale)
      SepLine.Y = 25
      SepLine.Height = 15
      SepLine.Background = Color.DarkRed
      SepLine.Width = 2
      SepLine.X = (IntI * 480) + 240
      ScrollPrograms.ScrollX = SepLine.X - (ScrollScale.Width / 2)
    Endif
  Next
  '--- next, loop trough all marked channels, and display the programinfo ---
  rResChannel = Main.hDB.Exec("SELECT * FROM tv_channels WHERE isUsed = 1 ORDER BY ChannelOrder")
  'rResChannel = Main.hDB.Exec("SELECT * FROM tv_channels ORDER BY ChannelOrder")
  IntChannelCount = 0
  If rResChannel Then
    If rResChannel.Count > 0 Then
      For Each rResChannel
        If IntChannelCount / 5 = Int(IntChannelCount / 5) Then
          LblInfo.text = "Rendering channel " & CStr(IntChannelCount) & " from " & CStr(rResChannel.Count)
          LblInfo.Refresh
          Wait
        Endif
        IntChannelCount = IntChannelCount + 1
        TmpBtnChannel = New Button(ScrollChannels)
        TmpBtnChannel.Background = Color.White
        TmpBtnChannel.Border = Border.None
        TmpBtnChannel.Mouse = Mouse.Pointing
        TmpBtnChannel.X = 0
        TmpBtnChannel.Y = (IntChannelCount - 1) * 42
        TmpBtnChannel.Width = 119
        TmpBtnChannel.Height = 43
        TmpBtnChannel.Action = "Channel|" & rResChannel!ChannelID
        If Len(rResChannel!BLOBlogo.data) > 10 Then
          StrFileLogo = Main.BlobFromDB("SELECT * FROM tv_channels WHERE RecID=" & rResChannel!RecID, "BLOBlogo", "EXTlogo")
          TmpBtnChannel.Picture = Picture.Load(StrFileLogo)
        Else
          TmpBtnChannel.Text = rResChannel!ChannelName
        Endif
        '--- next, read program info for the current channel ---
        BoolOK = False
        rResProgram = Main.hDB.Exec("SELECT tv_programs.*,tv_categories.BackColor,tv_categories.ForeColor FROM tv_programs,tv_categories WHERE tv_programs.CategoryName = tv_categories.CategoryName AND (left(EndPoint,8) = '" & Left(StrDate, 8) & "' OR left(StartPoint,8) = '" & Left(StrDate, 8) & "') AND ChannelID = '" & rResChannel!ChannelID & "' ORDER BY StartPoint")
        If rResProgram Then
          If rResProgram.Count > 0 Then
            BoolOK = True
            For Each rResProgram
              TmpBtnProgram = New Button(ScrollPrograms)
              If Len(Trim(rResProgram!BackColor)) = 0 Then
                TmpBtnProgram.Background = Color.White
              Else
                TmpBtnProgram.Background = Val(rResProgram!BackColor)
              Endif
              If Len(Trim(rResProgram!ForeColor)) = 0 Then
                TmpBtnProgram.Foreground = Color.Black
              Else
                TmpBtnProgram.Foreground = Val(rResProgram!ForeColor)
              Endif
              TmpBtnProgram.Border = Border.None
              TmpBtnProgram.Mouse = Mouse.Pointing
              TmpBtnProgram.Action = "Program|" & rResChannel!ChannelID & "|" & rResProgram!StartPoint
              TmpBtnProgram.Text = rResProgram!ProgramName & gb.newline & Mid(rResProgram!StartPoint, 9, 2) & ":" & Mid(rResProgram!StartPoint, 11, 2) & " - " & Mid(rResProgram!EndPoint, 9, 2) & ":" & Mid(rResProgram!EndPoint, 11, 2)
              TmpBtnProgram.ToolTip = TmpBtnProgram.Text
              TmpBtnProgram.Y = TmpBtnChannel.Y
              TmpBtnProgram.Height = TmpBtnChannel.Height
              If Left(rResProgram!StartPoint, 8) <> StrDate Then
                IntStartHour = 0
                IntStartMinute = 0
              Else
                IntStartHour = CInt(Mid(rResProgram!StartPoint, 9, 2))
                IntStartMinute = CInt(Mid(rResProgram!StartPoint, 11, 2))
              Endif
              If Left(rResProgram!EndPoint, 8) <> StrDate Then
                IntStopHour = 24
                IntStopMinute = 00
              Else
                IntStopHour = CInt(Mid(rResProgram!EndPoint, 9, 2))
                IntStopMinute = CInt(Mid(rResProgram!EndPoint, 11, 2))
              Endif
              IntLeft = (IntStartHour * 480) + (IntStartMinute * 8)
              IntWidth = ((IntStopHour * 480) + (IntStopMinute * 8)) - ((IntStartHour * 480) + (IntStartMinute * 8))
              TmpBtnProgram.X = IntLeft
              TmpBtnProgram.Width = IntWidth
            Next
          Endif
        Endif
        If BoolOK = False Then
          '--- add dummy label, to get the exact same height in both scrollviews
          TmpLbl = New Label(ScrollPrograms)
          TmpLbl.Y = TmpBtnChannel.Y
          TmpLbl.X = 0
          TmpLbl.Width = 24 * 480
          TmpLbl.Height = TmpBtnChannel.Height
          TmpLbl.Text = ""
          TmpLbl.Transparent = True
        Endif
      Next
    Endif
  Endif
  '--- re-enable frame, because otherwise positioning of scrollview is not possible ---
  Application.Busy = False
  'FrmPreview.Enabled = TRUE
  '--- draw the "current time line" (do this only if the current date is today) ---
  If Format(Now, "yyyymmdd") = StrDate Then
    SepLine = New Separator(ScrollPrograms)
    SepLine.Y = 1
    SepLine.Height = ScrollPrograms.ScrollHeight - 2
    SepLine.Background = Color.Red
    SepLine.Width = 2
    SepLine.X = ((CInt(Format(Now, "hh")) * 480) + (CInt(Format(Now, "nn")) * 8))
    ScrollPrograms.ScrollX = SepLine.X - (ScrollPrograms.Width / 2)
    'LblInfo.text = ("Guide contains ") & CStr(rResChannel.Count) & (" channels.")
    LblInfo.Text = ""
  Endif

End

Public Sub BtnPreview_Click()

  LoadPreview(Main.DateFromAppToSQL(TxtDate.text))

End

Public Sub ScrollPrograms_Scroll()

  ScrollChannels.ScrollY = ScrollPrograms.ScrollY
  ScrollScale.ScrollX = ScrollPrograms.ScrollX

End

'--- handles the clicking action of the dynamically generated channel- & program-labels ---
Public Sub Action_Activate(sKey As String) As Boolean

  '--- declare variables ---
  Dim rRes As Result
  Dim FrmTmpProgram As New FTVGuideProgramDetail
  Dim FrmTmpChannel As New FTVGuideChannelDetail
  Dim StrFile, StrTmpDate As String

  Select Case UCase(Main.ParseTag(sKey, 1, "|"))
    Case "CHANNEL"
      FrmTmpChannel.tag = Main.ParseTag(sKey, 2, "|") & "|" & Main.DateFromAppToSQL(TxtDate.Text)
      FrmTmpChannel.ShowDialog
    Case "PROGRAM"
      rRes = Main.hDB.Exec("SELECT tv_programs.*,tv_channels.ChannelName,tv_channels.RecID as TVChannelRecID,tv_channels.BLOBlogo FROM tv_programs,tv_channels WHERE tv_programs.ChannelID = tv_channels.ChannelID AND tv_programs.ChannelID = '" & Main.ParseTag(sKey, 2, "|") & "' AND tv_programs.StartPoint = '" & Main.ParseTag(sKey, 3, "|") & "'")
      If rRes Then
        If rRes.count > 0 Then
          rRes.MoveFirst
          FrmTmpProgram.LblChannel.text = rRes!ChannelName
          FrmTmpProgram.LblDate.text = Main.DateFromSQLToApp(Left(rRes!StartPoint, 8))
          FrmTmpProgram.LblTime.Text = "[" & Main.TimeFromSQLToApp(Right(rRes!StartPoint, 4)) & " - " & Main.TimeFromSQLToApp(Right(rRes!EndPoint, 4)) & "]"
          FrmTmpProgram.LblCategory.text = rRes!CategoryName
          FrmTmpProgram.LblSubCategory.text = rRes!SubCategoryName
          FrmTmpProgram.LblProgramName.text = rRes!ProgramName
          FrmTmpProgram.TxtProgramDescription.text = rRes!ProgramDescription
          StrTmpDate = Left(rRes!StartPoint, 8)
          FrmTmpProgram.LblDateDay.text = Format(Date(CInt(Left(StrTmpDate, 4)), CInt(Mid(StrTmpDate, 5, 2)), CInt(Mid(StrTmpDate, 7, 2))), "dddd")
          If Len(rRes!BLOBlogo.data) > 10 Then
            StrFile = Main.BlobFromDB("SELECT * FROM tv_channels WHERE RecID=" & rRes!TVChannelRecID, "BLOBlogo", "EXTlogo")
            FrmTmpProgram.PicLogo.Picture = Picture.Load(StrFile)
          Endif
          FrmTmpProgram.ShowDialog
        Endif
      Endif
      rRes = Null
  End Select

End

Public Sub DtChooser_Change()

  '--- declare variables ---
  Dim rRes As Result
  Dim StrDate As String

  '--- take care that the selected date is within the min/max-range of the items in the tv_programs table ---
  StrDate = Format(DtChooser.Year, "0000") & Format(DtChooser.Month, "00") & Format(DtChooser.Day, "00")
  rRes = Main.hDB.Exec("SELECT MIN(StartPoint) AS MinDate, MAX(EndPoint) AS MaxDate FROM tv_programs")
  If rRes Then
    If rRes.count > 0 Then
      rRes.MoveFirst
      If StrDate < Left(rRes!MinDate, 8) Then
        DtChooser.value = Date(CInt(Left(rRes!MinDate, 4)), CInt(Mid(rRes!MinDate, 5, 2)), CInt(Mid(rRes!MinDate, 7, 2)))
      Endif
      If StrDate > Left(rRes!MaxDate, 8) Then
        DtChooser.value = Date(CInt(Left(rRes!MaxDate, 4)), CInt(Mid(rRes!MaxDate, 5, 2)), CInt(Mid(rRes!MaxDate, 7, 2)))
      Endif
    Endif
  Endif
  rRes = Null
  TxtDate.text = Format(DtChooser.Day, "00") & "/" & Format(DtChooser.Month, "00") & "/" & Format(DtChooser.Year, "0000")

End

Public Sub DtChooser_Leave()

  DtChooser.Visible = False

End

Public Sub TxtDate_Enter()

  If $bEnabled Then
    If TxtDate.Text = "00/00/0000" Then
      DtChooser.Value = Now
    Else
      DtChooser.Value = Date(CInt(Mid(TxtDate.Text, 7, 4)), CInt(Mid(TxtDate.text, 4, 2)), CInt(Left(TxtDate.text, 2)))
    Endif
    DtChooser.Visible = True
  Endif

End

Public Sub BtnSearch_Click()

  '--- declare variables ---
  Dim StrQuery As String
  Dim FrmTmp As New FTVGuideSearchDetail

  '--- at least one checkbox needs to be marked ! ---
  If ChkProgramDescription.value = False And ChkProgramName.value = False Then
    Message.Info(("At least one checkbox need to be marked for a search !"))
    Return
  Endif
  '--- also, search with empty search-string is not allowed ---
  If Len(Trim(TxtSearch.text)) < 1 Then
    Message.Info(("No search-criteria found !"))
    Return
  Endif
  '--- if we are here, we can perform a search, so build a query ---
  StrQuery = "SELECT tv_channels.RecID,tv_channels.ChannelOrder, tv_channels.ChannelName,tv_channels.ChannelID,tv_channels.BLOBlogo,tv_channels.EXTlogo,tv_programs.* FROM tv_channels,tv_programs where tv_channels.ChannelID = tv_programs.ChannelID AND tv_channels.IsUsed=1"
  If ChkProgramName.value = True And ChkProgramDescription.value = False Then
    StrQuery = StrQuery & " AND tv_programs.ProgramName like '%" & TxtSearch.Text & "%'"
  Endif
  If ChkProgramName.value = False And ChkProgramDescription.value = True Then
    StrQuery = StrQuery & " AND tv_programs.ProgramDescription like '%" & TxtSearch.Text & "%'"
  Endif
  If ChkProgramName.value = True And ChkProgramDescription.value = True Then
    StrQuery = StrQuery & " AND (tv_programs.ProgramName like '%" & TxtSearch.Text & "%' OR tv_programs.ProgramDescription like '%" & TxtSearch.text & "%')"
  Endif
  StrQuery = StrQuery & " AND left(StartPoint,8) >= '" & Format(Now, "yyyymmdd") & "' ORDER BY StartPoint,tv_channels.ChannelOrder"
  FrmTmp.Tag = StrQuery & "|" & Format(Now, "yyyymmdd") & "|" & TxtSearch.Text
  If ChkProgramName.value = True Then
    FrmTmp.Tag = FrmTmp.tag & "|1"
  Else
    FrmTmp.Tag = FrmTmp.tag & "|0"
  Endif
  If ChkProgramDescription.value = True Then
    FrmTmp.Tag = FrmTmp.tag & "|1"
  Else
    FrmTmp.Tag = FrmTmp.tag & "|0"
  Endif
  FrmTmp.ShowDialog

End
