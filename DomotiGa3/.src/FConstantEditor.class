' Gambas class file

' Description:
' FScenarioEditor.class
' Support for creating and editing of all thermostat constants.

' Development Status:
' Works for 99%.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Private rResult As Result
Public bAddConstant As Boolean
Private bInitial As Boolean

Public Sub Form_Open()

 ' give focus to first field
 tbName.SetFocus
 
  If bAddConstant Then
    Me.Text = ("Create Constant")
    btnDelete.Visible = False
  Endif

  Me.Move(FMain.X + 50, FMain.Y + 70)
  LoadConstant()

End

Public Sub LoadConstant()

  Dim fValue As Float
  If Not bAddConstant Then
    ' get selected event values
    Try rResult = Main.hDB.Exec("SELECT * FROM thermostat_constant WHERE constant_id=&1", FThermostat.txtCurTherm)
    If Not Error Then
      If rResult.Count >= 1 Then
        tbName.Text = rResult!name
        bInitial = True
        fValue = rResult!value
        vbValue.Value = fValue
        txtDescription.Text = rResult!description
        cbtnColor.Color = rResult!color
      Else
        Message(("Couldn't load constant record!"))
        Me.Close
      Endif
    Endif
  Endif

End

Public Sub btnCancel_Click()

  bAddConstant = False
  Me.Close

End

Public Sub btnDelete_Click()

  Select Message.Question(("Are you sure that you want to delete this constant ?"), ("Yes"), ("No"))
    Case 1
      Main.hDB.Exec("DELETE FROM thermostat_constant WHERE constant_id=&1", rResult!constant_id)
      Me.Close
      FThermostat.GetThermList()
  End Select

End

Public Sub btnNew_Click()

  bAddConstant = True
  btnSave_Click()

End

Public Sub btnSave_Click()

  Dim rResultUpdate As Result
  Dim sSql As String

  If Not tbName.Text Then
    Balloon(("Please enter a name for this constant !"), tbName)
    Return
  Endif

  If bAddConstant Then
    ' create new scenario row
    sSql = "INSERT INTO thermostat_constant SET name=&1, value=&2, description=&3, color=&4"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, vbValue.Value, txtDescription.Text, cbtnColor.color)
  Else
    ' name has changed
    If rResult!name <> tbName.Text Then
      If ConstantNameExist(tbName.Text) Then
        Balloon(("Please enter a unique name for this constant !"), tbName)
        Return
      Endif
    Endif
    ' update new event details
    sSql = "UPDATE thermostat_constant SET name=&1, value=&2, description=&3, color=&4 WHERE constant_id = &5"
    rResultUpdate = Main.hDB.Exec(sSql, tbName.Text, vbValue.Value, txtDescription.Text, cbtnColor.color, rResult!constant_id)
  Endif

  FThermostat.GetThermList()
  bAddConstant = False
  Me.Close

End

Private Sub ConstantNameExist(sName As String) As Boolean

  Dim rResultName As Result

  rResultName = Main.hDB.Exec("SELECT constant_id FROM thermostat_constant WHERE name=&1", sName)
  If rResultName Then
    If rResultName.Count Then
      Return True
    Endif
  Endif
  Return False

End

Public Sub tbName_Change()

  btnSave.Enabled = True

End

Public Sub vbValue_Change()

  If bInitial Then
    bInitial = False
  Else
    btnSave.Enabled = True
  Endif

End

Public Sub txtDescription_Change()

  btnSave.Enabled = True

End

Public Sub cbtnColor_Change()

   btnSave.Enabled = True

End

Public Sub vbValue_KeyPress()

  btnSave.Enabled = True

End
