' Gambas class file

' Description:
' CK8055.class
' Control ports of a Velleman K8055 board.

' Development Status:
' In progress.

' Links:
' http://libk8055.sourceforge.net/

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

PROPERTY K8055Debug AS Boolean
PROPERTY PollTime AS Integer
PROPERTY AddressBoard AS Integer
PROPERTY DebounceTime1 AS Integer
PROPERTY DebounceTime2 AS Integer

PUBLIC tK8055 AS Timer

PRIVATE bK8055Debug AS Boolean
PRIVATE iPollTime AS Integer
PRIVATE iAddressBoard AS Integer
PRIVATE iDebounceTime1 AS Integer
PRIVATE iDebounceTime2 AS Integer
PRIVATE bK8055FirstRun AS Boolean

' variables to store current states
PRIVATE bDO[8] AS Boolean      ' Digital Out
PRIVATE bDI[5] AS Boolean      ' Digital In
PRIVATE byAO[2] AS Byte        ' Analog Out
PRIVATE byAI[2] AS Byte        ' Analog In
PRIVATE iCounter[2] AS Integer ' Counters

PRIVATE bResetCounter[2] AS Boolean
PRIVATE bAODim[2] AS Boolean
PRIVATE bAOBright[2] AS Boolean

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' external library calls to Velleman K8055 routines based on libk8055
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
LIBRARY "libk8055"
PRIVATE EXTERN OpenDevice(BoardAddress AS Long) AS Integer
PRIVATE EXTERN CloseDevice() AS Integer
PRIVATE EXTERN ReadAnalogChannel(Channel AS Long) AS Long
PRIVATE EXTERN OutputAnalogChannel(Channel AS Long, Data AS Long) AS Integer
PRIVATE EXTERN ReadAllDigital() AS Long
PRIVATE EXTERN WriteAllDigital(Data AS Long) AS Integer
PRIVATE EXTERN ReadCounter(Counter AS Long) AS Long
PRIVATE EXTERN ResetCounter(Counter AS Long) AS Integer
PRIVATE EXTERN SetCounterDebounceTime(Counter AS Long, DebounceTime AS Long) AS Integer

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' open connection to K8055 board
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Connect() AS Boolean

  DIM OpenDeviceResult AS Integer
  DIM sOutput AS String

  ' test if K8055 board is connected
  TRY OpenDeviceResult = OpenDevice(CLong(iAddressBoard))
  IF OpenDeviceResult = iAddressBoard THEN
    ' set counter debounce timers
    ' WORKAROUND: Set debouncetimers by using the K8055 program as setting counters by using
    ' the direct functions result in strange behaviour of the counters
    EXEC ["k8055", "-p:" & iAddressBoard, "-dbt1:" & Str(iDebounceTime1), "-dbt2:" & Str(iDebounceTime2)] TO sOutput
    IF InStr(sOutput, ";") = 0 THEN
      Main.WriteLog("Velleman K8055 Error: " & sOutput)
    ELSE
      Main.WriteLog("Velleman K8055 configured with debounce timers of " & CStr(iDebounceTime1) & " mS, " & CStr(iDebounceTime2) & " mS")
      RETURN TRUE
    ENDIF
  ELSE
    RETURN FALSE
  ENDIF

  CATCH ' some errors
    Main.WriteLog(("Velleman K8055 Error: ") & ERROR.Text & " at " & ERROR.Where)
    RETURN FALSE

END

PUBLIC SUB Run()

  ' start poll timer for input port check
  tK8055 = NEW Timer AS "tK8055"
  tK8055.Delay = iPollTime * 1000 ' multiply for seconds
  tK8055.Start

END

PUBLIC SUB tK8055_Timer()

  CheckPorts()

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' check for changed port status on K8055 board
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB CheckPorts()

  DIM rResult AS Result
  DIM iCount, iPortNumber, iTotalCounter, iDeviceId AS Integer
  DIM DigitalOutValue AS Byte

'  TRY rResult = Main.hDB.Exec("SELECT * FROM devices WHERE enabled is TRUE AND interface = &1", Devices.FindInterface("Velleman K8055"))
  TRY rResult = devices.FindDevicesForInterface(Devices.FindInterface("Velleman K8055"))
  IF rResult.Count THEN
    ' get current state of all digital port only once to speed up this routine for all digital input ports
    IF NOT (Read_DI_Ports()) THEN
      IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to get current state of all digital input ports!")
    ENDIF

    FOR EACH rResult
      IF Len(rResult!address) = 3 THEN
        iPortNumber = CInt(Mid(rResult!address, 3, 1))
        iDeviceId = rResult!id
        SELECT UCase(Mid(rResult!address, 1, 2))
          CASE "DI"
            IF iPortNumber = 1 OR IF iPortNumber = 2 THEN
              ' digital input port with counter so save the counter values too
              ' and reset counter if reset was triggered before
              IF bResetCounter[iPortnumber - 1] THEN
                IF Read_Counter_Port(iPortnumber, TRUE) THEN
                  ' reset of counter is triggered, so reset counter of device
                  ' and add current counter value to last known value of value4
                  iTotalCounter = IIf(rResult!value3 = "", CInt(iCounter[iPortNumber - 1]), CInt(iCounter[iPortNumber - 1] + CInt(rResult!value3)))
                  IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, IIf(bDI[iPortNumber - 1], "On", "Off"), 0, iTotalCounter, "")
                  IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] " & rResult!name & " (" & rResult!address & ") = " & IIf(bDI[iPortNumber - 1], " On ", " Off ") & ", reset of counter = " & Str(iCounter[iPortNumber - 1]))
                ELSE
                  IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to reset counter of digital input port: " & Str(iPortNumber))
                ENDIF
              ELSE
                ' no reset of counter, so update only current counter value
                IF Read_Counter_Port(iPortnumber, FALSE) THEN
                  IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, IIf(bDI[iPortNumber - 1], "On", "Off"), iCounter[iPortNumber - 1], "", "")
                  IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] " & rResult!name & " '" & rResult!address & "' = " & IIf(bDI[iPortNumber - 1], " On ", " Off ") & ", counter = " & Str(iCounter[iPortNumber - 1]))
                ELSE
                  IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to reset counter of digital input port: " & Str(iPortNumber))
                ENDIF
              ENDIF
            ELSE
              ' digital input port without counter so only update value
              IF iPortNumber >= 3 AND IF iPortNumber <= 5 THEN
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] " & rResult!name & " '" & rResult!address & "' = " & IIf(bDI[iPortNumber - 1], " On ", " Off "))
                IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, IIf(bDI[iPortNumber - 1], "On", "Off"), "", "", "")
              ELSE
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid port '" & rResult!address & "' configured for device '" & rResult!name & "'")
              ENDIF
            ENDIF
          CASE "AI"
            IF iPortNumber = 1 OR IF iPortNumber = 2 THEN
              IF Read_AI_Port(iPortNumber) THEN
                IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, IIf(byAI[iPortNumber - 1] > 127, "On", "Off"), CStr(byAI[iPortNumber - 1]), "", "")
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] " & rResult!name & " '" & rResult!address & "' = " & Str(byAI[iPortNumber - 1]))
              ELSE
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to update analog input port: " & iPortNumber.Text)
              ENDIF
            ELSE
              IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid port '" & rResult!address & "' configured for device '" & rResult!name & "'")
            ENDIF
          CASE "DO"
            IF iPortNumber >= 1 AND IF iPortNumber <= 8 THEN
              IF bK8055FirstRun THEN
                ' set state of output port to last known state
                IF UCase(rResult!value) = "ON" THEN
                  bDO[CInt(Mid(rResult!address, 3, 1)) - 1] = TRUE
                ENDIF
              ENDIF
              ' write new output state
              IF (Write_DO_Ports()) THEN
                IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, IIf(bDO[iPortNumber - 1], "On", "Off"), "", "", "")
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] " & rResult!name & " (" & rResult!address & ") = " & IIf(bDO[iPortNumber - 1], "On", "Off"))
              ELSE
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to write new value to digital output ports")
              ENDIF
            ELSE
              IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid port (" & rResult!address & ") configured for device '" & rResult!name & "'")
            ENDIF
          CASE "AO"
            IF iPortNumber = 1 OR IF iPortNumber = 2 THEN
              IF bK8055FirstRun THEN
                byAO[CInt(Mid(rResult!address, 3, 1)) - 1] = CByte(rResult!value2)
              ENDIF
              ' change state of analog ports if currently be dimmed brighted
              IF bAODim[iPortNumber - 1] THEN
                IF byAO[iPortNumber - 1] >= 10 THEN byAO[iPortNumber - 1] = byAO[iPortNumber - 1] - 10
              ENDIF
              ' change state of analog ports if currently be brighted
              IF bAOBright[iPortNumber - 1] THEN
                IF byAO[iPortNumber - 1] <= 245 THEN byAO[iPortNumber - 1] = byAO[iPortNumber - 1] + 10
              ENDIF
              ' write new output value
              IF (Write_AO_Port(iPortNumber)) THEN
                IF iDeviceId THEN Devices.ValueUpdate(iDeviceId, IIf(byAO[iPortNumber - 1] > 127, "On", "Off"), Str(byAO[iPortNumber - 1]), "", "")
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] " & rResult!name & " '" & rResult!address & "' = " & CStr(byAO[iPortNumber - 1]))
              ELSE
                IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to set analog output port " & CStr(iPortNumber))
              ENDIF
            ELSE
              IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid port '" & rResult!address & "' configured for device '" & rResult!name & "'")
            ENDIF
          CASE ELSE
            IF Main.bK8055Debug THEN
              Main.WriteDebugLog("[K8055] Invalid port '" & rResult!address & "', valid ports are:")
              Main.WriteDebugLog("[K8055]   Digital In : DI1, DI2, DI3, DI4, DI5")
              Main.WriteDebugLog("[K8055]   Digital Out: DO1, DO2, DO3, DO4, DO5, DO6, DO7, DO8")
              Main.WriteDebugLog("[K8055]   Analog In  : AI1, AI2")
              Main.WriteDebugLog("[K8055]   Analog Out : AO1, AO2")
            ENDIF
        END SELECT
      ELSE
        IF Main.bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid port '" & rResult!address & "' configured for device '" & rResult!name & "'")
      ENDIF
    NEXT
    bK8055FirstRun = FALSE
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read current state of all digital input ports
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION Read_DI_Ports() AS Boolean

  DIM DigitalValue AS Long
  DIM iCount AS Integer

  DigitalValue = ReadAllDigital()
  IF DigitalValue >= 0 THEN
    FOR iCount = 0 TO 4
      bDI[iCount] = BTst(DigitalValue, iCount)
    NEXT
    RETURN TRUE
  ELSE
    RETURN FALSE
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read current state of an analog input port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION Read_AI_Port(PortNumber AS Integer) AS Boolean

  DIM AnalogValue AS Long

  AnalogValue = ReadAnalogChannel(PortNumber)
  IF AnalogValue >= 0 THEN
    byAI[PortNumber - 1] = CByte(AnalogValue)
    RETURN TRUE
  ELSE
    RETURN FALSE
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' read counters
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION Read_Counter_Port(PortNumber AS Integer, bReset AS Boolean) AS Boolean

  DIM CounterValue, ResetValue AS Integer

  IF bReset THEN
    IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Resetting counter of Digital input '" & Str(PortNumber) & "'")
    CounterValue = ReadCounter(CLong(PortNumber))
    ResetValue = ResetCounter(CLong(PortNumber))
    IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Reset counter of Digital input '" & Str(PortNumber) & "' executed, last countervalue=" & Str(CounterValue))
    bResetCounter[PortNumber - 1] = FALSE
  ELSE
    CounterValue = ReadCounter(CLong(PortNumber))
  ENDIF

  IF CounterValue >= 0 THEN
    iCounter[PortNumber - 1] = CInt(CounterValue)
    RETURN TRUE
  ELSE
    RETURN FALSE
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' write new state to all digital output ports
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION Write_DO_Ports() AS Boolean

  DIM DigitalOutValue AS Byte

  DigitalOutValue = IIf(bDO[0], 1, 0) + IIf(bDO[1], 2, 0) + IIf(bDO[2], 4, 0) + IIf(bDO[3], 8, 0) + IIf(bDO[4], 16, 0) + IIf(bDO[5], 32, 0) + IIf(bDO[6], 64, 0) + IIf(bDO[7], 128, 0)
  IF (WriteAllDigital(CLong(DigitalOutValue)) <> 0) THEN
    RETURN FALSE
  ELSE
    RETURN TRUE
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' write new state to analog output port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PRIVATE FUNCTION Write_AO_Port(PortNumber AS Integer) AS Boolean

  IF (OutputAnalogChannel(PortNumber, CLong(byAO[PortNumber - 1])) <> 0) THEN
    RETURN FALSE
  ELSE
    RETURN TRUE
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' execute commands send to K8055 interface
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC SUB SendCommand(sAddress AS String, sValue AS String)

  DIM PortAddress AS Integer

  IF Len(sAddress) = 3 AND (Len(sValue) > 0) THEN
    PortAddress = CInt(Mid(sAddress, 3, 1)) - 1
    SELECT Mid(sAddress, 1, 2)
      CASE "AO"
        IF (PortAddress >= 0) AND (PortAddress <= 1) THEN
          SELECT UCase(sValue)
            CASE "BRIGHT"
              IF byAO[PortAddress] < 255 THEN
                INC byAO[PortAddress]
              ENDIF
              bAOBright[PortAddress] = TRUE
            CASE "DIM"
              IF byAO[PortAddress] > 1 THEN
                DEC byAO[PortAddress]
              ENDIF
              bAODim[PortAddress] = TRUE
            CASE "STOP"
              bAODim[PortAddress] = FALSE
              bAOBright[PortAddress] = FALSE
            CASE "ON"
              byAO[PortAddress] = CByte(255)
              IF NOT (Write_AO_Port(PortAddress + 1)) THEN
                IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to write value '" & Str(byAO[PortAddress]) & "' to analog output port " & Str(PortAddress + 1))
              END IF
            CASE "OFF"
              byAO[PortAddress] = 0
              IF NOT (Write_AO_Port(PortAddress + 1)) THEN
                IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to write value '" & Str(byAO[PortAddress]) & "' to analog output port " & Str(PortAddress + 1))
              END IF
            CASE ELSE
              IF sValue > "" THEN
                IF (CInt(sValue) >= 0) AND (CInt(sValue) <= 255) THEN
                  byAO[PortAddress] = CInt(sValue)
                  IF NOT (Write_AO_Port(PortAddress + 1)) THEN
                    IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: failed to write value '" & Str(byAO[PortAddress]) & "' to analog output port " & Str(PortAddress + 1))
                  END IF
                ELSE
                  IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: invalid value '" & sValue & "' for analog port '" & Str(PortAddress + 1) & "'")
                ENDIF
              ELSE
                IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Error: invalid value '" & sValue & "' for analog port '" & Str(PortAddress + 1) & "'")
              ENDIF
          END SELECT
        ELSE
          IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid value '" & sValue & "' for analog port '" & Str(PortAddress + 1) & "'")
        ENDIF
      CASE "DO"
        IF (PortAddress >= 0) AND (PortAddress <= 7) THEN
          bDO[PortAddress] = IIf(UCase(sValue) = "ON", TRUE, FALSE)
          IF NOT (Write_DO_Ports()) THEN
            IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Failed to write new value to digital output ports")
          ENDIF
        ELSE
          IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid value '" & sValue & "' for digital port '" & Str(PortAddress) & "'")
        ENDIF
      CASE "DI"
        ' digital input port, so only reset of counter is possible for port 1 and 2
        IF (PortAddress >= 0) AND (PortAddress <= 1) AND (UCase(sValue) = "RESET") THEN
          bResetCounter[PortAddress] = TRUE
          IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Resetting counter of Digital input '" & Str(PortAddress + 1) & "' at next poll.")
        ENDIF
    END SELECT
  ELSE
    IF bK8055Debug THEN Main.WriteDebugLog("[K8055] Invalid address '" & sAddress & "' or value '" & sValue & "'")
  ENDIF

END

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' close port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
PUBLIC FUNCTION Disconnect() AS Boolean

  ' stop timer
  tK8055.Stop

  ' close the connection
  CloseDevice()
  Main.WriteLog("Velleman K8055 connection closed.")

  ' all ok
  RETURN TRUE

END

' implement properties
PRIVATE FUNCTION K8055Debug_Read() AS Boolean

  RETURN bK8055Debug

END

PRIVATE SUB K8055Debug_Write(Value AS Boolean)

  bK8055Debug = Value

END

PRIVATE FUNCTION PollTime_Read() AS Integer

  RETURN iPollTime

END

PRIVATE SUB PollTime_Write(Value AS Integer)

  iPollTime = Value

END

PRIVATE FUNCTION AddressBoard_Read() AS Integer

  RETURN iAddressBoard

END

PRIVATE SUB AddressBoard_Write(Value AS Integer)

  iAddressBoard = Value

END

PRIVATE FUNCTION DebounceTime1_Read() AS Integer

  RETURN iDebounceTime1

END

PRIVATE SUB DebounceTime1_Write(Value AS Integer)

  iDebounceTime1 = Value

END

PRIVATE FUNCTION DebounceTime2_Read() AS Integer

  RETURN iDebounceTime2

END

PRIVATE SUB DebounceTime2_Write(Value AS Integer)

  iDebounceTime2 = Value

END
