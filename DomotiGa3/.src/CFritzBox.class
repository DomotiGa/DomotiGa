' Gambas class file

' Description:
' FritzBox.class
' Provide support for Fritz!Box phonecall monitoring. (via call log port)

' Development Status:
' Just started to build this one.
' Incoming/outgoing call logging implemented.
' Tested with a Fritz!Box 7170 Annex A running Firmware upto 58.04.76

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Module/Class specific variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public PluginName As String = "FritzBox"
Public PluginType As Integer = Plugin.Type_Class
Public PluginFriendlyName As String = "Fritz!Box API"
Public PluginVersion As String = "1.00"
Public PluginAuthor As String = "Ron Klinkien"
Public PluginProtocols As String[]
Public PluginMaxInstances As Byte = 1

Public KeyName As String
Public LogLabel As String = "[FritzBox] "
Public Instance As Integer
Public IsRunning As Boolean
Public ErrorText As String
Public ErrorWhere As String

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Private Variables
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Private $bEnabled As Boolean
Private $sTCPHost As String
Private $bFritzBoxDebug As Boolean

Private hFritzBox As New Socket
Private sBuffer As String

Public tReConnect As Timer
Private iReConnectRetry As Integer = 0 ' retries
Private aReConnectDelay As Integer[] = [0, 1000, 5000, 10000, 30000, 60000, 300000, 900000, 1800000, 3600000, 7200000] ' 1sec, 5sec, 10sec, 30sec, 1min, 5min, 15min, 1/2hour, 1hour, 2hour

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory subroutine for each Module/Class to initialize:
' - The cPlugin[x].Settings are copied into local variables
' - Port/Connection will be started (any errors caught)
' - Any other code per Class
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StartPlugin(cPl As CPluginEntry)

  KeyName = cPl.KeyName
  Instance = cPl.Instance
  If Instance <> 1 Then LogLabel = Replace(LogLabel, "] ", "#" & Instance & "] ")

  ' Copy configuration items locally
  $bEnabled = cPl.Settings["enabled"]
  $sTCPHost = cPl.Settings["tcphost"]
  $bFritzBoxDebug = cPl.Settings["debug"]

  ' Define timer for TCP reconnect
  tReConnect = New Timer As "tReConnect"
  tReConnect.Delay = iReConnectRetry
  tReConnect.Stop

  ' Connect/Initialize connection
  Connect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Mandatory sub for each Module/Class to stop
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub StopPlugin()

  Try Disconnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' connect to the host:port
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Connect() As Boolean

  ' try to close the connection
  Try hFritzBox.Close

  ' get a new one
  hFritzBox = New Socket As "FritzBox"
  hFritzBox.Connect($sTCPHost, 1012)

  ' Write to main logfile we are trying to connect
  Main.WriteLog(LogLabel & PluginFriendlyName & " TCP interface connecting to " & $sTCPHost & ":1012")

  ' Don't set IsRunning=True, this has to be done in _Ready 

  ' All went ok
  Return True

Catch ' some errors
  Main.WriteLog(LogLabel & "ERROR: " & PluginFriendlyName & " TCP interface FAILED to connect to " & $sTCPHost & ":1012")
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  IsRunning = False
  ErrorText = Error.Text
  ErrorWhere = Error.Where

  Return False

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' tcp socket ready
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FritzBox_Ready()

  Main.WriteLog(LogLabel & "TCP interface connected.")
  tReConnect.Stop
  iReConnectRetry = 0
  IsRunning = True

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' peer closed tcp socket
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub FritzBox_Closed()

  Main.WriteLog(LogLabel & "ERROR: TCP socket closed by peer.")
  ReConnect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reconnect routine
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub tReConnect_Timer()

  Connect()

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' reconnect routine
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Sub ReConnect()

  Inc iReConnectRetry
  If iReConnectRetry < aReConnectDelay.Max Then
    Main.WriteLog(LogLabel & "Retry to connect in " & IIf(aReConnectDelay[iReConnectRetry] > 60000, CStr(aReConnectDelay[iReConnectRetry] / 60000) & " minutes.", CStr(aReConnectDelay[iReConnectRetry] / 1000) & " seconds."))
    tReConnect.Delay = aReConnectDelay[iReConnectRetry]
    tReConnect.Start
  Else
    tReConnect.Stop
  Endif

End

Public Sub FritzBox_Found()

  Log.Plugin_DNS_Found(LogLabel, $sTCPHost)

End

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' disconnect from the host
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Public Function Disconnect() As Boolean

  ' try to close the connection
  Try tReConnect.Stop
  Try hFritzBox.Close

  Main.WriteLog(LogLabel & PluginFriendlyName & " TCP connection closed.")

  ' all ok
  Return True

Finally
  IsRunning = False
  ErrorText = ""
  ErrorWhere = ""

Catch
  Main.WriteLog(LogLabel & "ERROR: " & Error.Text)
  Return False

End

Public Sub FritzBox_Read()

  Dim sData As String

  sData = Read #hFritzBox, 1
  If sData = Chr(10) Then ' buffer until linefeed then parse
    If Len(sBuffer) > 1 Then ParseLine(Left(sBuffer, Len(sBuffer) - 1))
    sBuffer = Null
  Else
    sBuffer &= sData
  Endif

End

Public Sub FritzBox_Error()

  Dim sMsg As String

  sMsg = Log.Text_TCP_Error(Last.Status, $sTCPHost)
  Main.WriteLog(LogLabel & "ERROR: " & sMsg)

  IsRunning = False
  ErrorText = sMsg
  ReConnect()

End

Private Sub ParseLine(sStr As String)

  Dim aScan As String[]
  Dim rResult As Result
  Dim iId As Integer
  Dim sName As String

  If $bFritzBoxDebug Then Main.WriteDebugLog(LogLabel & sStr)
  ' 20.01.09 20:29:10;RING;0;0XXXXXXXXX;0XXXXXXXXX;POTS;
  If InStr(sStr, ";RING;") Then
    aScan = Scan(sStr, "*;RING;*;*;*;*;")
    If aScan.Count = 5 Then
      sName = CallerID.ReturnCallerID(aScan[2])
      Main.WriteLog(LogLabel & "Incoming phone call from " & IIf(aScan[2], aScan[2] & " (" & sName & ")", "0000000000") & ".")
      Main.SetGlobalVar("PhoneCall_Incoming", aScan[2] & " (" & sName & ") at " & Format(Now(), "hh:nn:ss dd-mm-yyyy"))
      If $bFritzBoxDebug Then Main.WriteDebugLog(LogLabel & "Incoming call [id=" & aScan[1] & "] from " & aScan[2] & " on line " & aScan[3] & " (" & aScan[4] & ")")
      ' write data to cdr table
      Main.hDB.Begin()
      rResult = Main.hDB.Create("cdr")
      rResult!calldate = Now()
      rResult!uniqueid = aScan[1]
      rResult!clid = IIf(aScan[2], aScan[2], "0000000000")
      rResult!src = IIf(aScan[2], aScan[2], "0000000000")
      rResult!dcontext = "incoming"
      rResult!channel = aScan[3]
      rResult!disposition = "NO ANSWER"
      rResult.Update()
      Main.hDB.Commit()
      Main.RefreshPhoneCallList()
    Else
      Main.WriteDebugLog(LogLabel & "Error couldn't parse: " & sStr)
    Endif
  Endif
  ' 20.01.09 20:29:19;CONNECT;0;0;0XXXXXXXXX;
  If InStr(sStr, ";CONNECT;") Then
    aScan = Scan(sStr, "*;CONNECT;*;*;*;")
    If aScan.Count = 4 Then
      Main.WriteLog(LogLabel & "Phone call answered.")
      If $bFritzBoxDebug Then Main.WriteDebugLog(LogLabel & "Call [id=" & aScan[1] & "] to/from " & aScan[3] & " answered")
      aScan[3] = IIf(aScan[3], aScan[3], "0000000000")
      rResult = Main.hDB.Exec("SELECT * FROM cdr WHERE clid = &1 ORDER BY calldate DESC LIMIT 1", aScan[3])
      If rResult Then
        If rResult.Count Then
          iId = rResult!id
          rResult = Main.hDB.Exec("UPDATE cdr SET disposition = 'ANSWERED', calldate = &1 WHERE id = &2", Now(), iId)
          Try Main.hDB.Exec("UPDATE contacts SET lastseen = now(), callnr = callnr+1 WHERE phoneno = &1 or mobileno = &1", aScan[3])
        Endif
      Endif
    Else
      Main.WriteDebugLog(LogLabel & "Error couldn't parse: " & sStr)
    Endif
  Endif
  ' 20.01.09 20:50:11;DISCONNECT;0;1253;
  If InStr(sStr, ";DISCONNECT;") Then
    aScan = Scan(sStr, "*;DISCONNECT;*;*;")
    If aScan.Count = 3 Then
      Main.WriteLog(LogLabel & "Phone call ended.")
      If $bFritzBoxDebug Then Main.WriteDebugLog(LogLabel & "Call [id=" & aScan[1] & "] duration was " & aScan[2] & " seconds")
      rResult = Main.hDB.Exec("SELECT * FROM cdr ORDER BY calldate DESC LIMIT 1")
      If rResult.Available Then
        iId = rResult!id
        rResult = Main.hDB.Exec("UPDATE cdr SET duration = &1, billsec = &1 WHERE id = &2", aScan[2], iId)
        Main.RefreshPhoneCallList()
      Endif
    Else
      Main.WriteDebugLog(LogLabel & "Error couldn't parse: " & sStr)
    Endif
  Endif
  ' 20.01.09 22:28:50;CALL;0;0;0XXXXXXXXX;0XXXXXXXXX;POTS;
  If InStr(sStr, ";CALL;") Then
    aScan = Scan(sStr, "*;CALL;*;*;*;*;*;")
    If aScan.Count = 6 Then
      sName = CallerID.ReturnCallerID(aScan[4])
      Main.WriteLog(LogLabel & "Outgoing phone call to " & aScan[4] & " (" & sName & ")")
      Main.SetGlobalVar("PhoneCall_Outgoing", aScan[4] & " (" & sName & ") at " & Format(Now(), "hh:nn:ss dd-mm-yyyy"))
      If $bFritzBoxDebug Then Main.WriteDebugLog(LogLabel & " Outgoing call [id=" & aScan[1] & "] to " & aScan[4] & " with line " & aScan[3])
      ' write data to cdr table
      Main.hDB.Begin()
      rResult = Main.hDB.Create("cdr")
      rResult!calldate = Now()
      rResult!clid = aScan[4]
      rResult!uniqueid = aScan[1]
      rResult!src = aScan[3]
      rResult!dst = aScan[4]
      rResult!dcontext = "default"
      rResult!channel = aScan[3]
      rResult!disposition = "NO ANSWER"
      rResult.Update()
      Main.hDB.Commit()
      Main.RefreshPhoneCallList()
    Else
      Main.WriteDebugLog(LogLabel & "Error couldn't parse: " & sStr)
    Endif
  Endif

End
