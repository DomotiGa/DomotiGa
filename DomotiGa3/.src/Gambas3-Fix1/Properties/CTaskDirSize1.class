' Gambas class file

Inherits Task

Private $sDir As String
Private $fTime As Float
Private $nTime As Integer
Private $nFile As Integer
Private $nDir As Integer
Private $iSize As Long

Public Sub _new(sDir As String)
  
  $sDir = sDir
  
End

Private Sub ScanDir(sDir As String)

  Dim sFile As String
  Dim sPath As String
  Dim hStat As Stat
  Dim aDir As String[]
  
  Try aDir = Dir(sDir)
  If Error Then 
    Error Error.Text & ": " & sDir
    Return
  Endif
  
  For Each sFile In aDir
    
    sPath = sDir &/ sFile
    Try hStat = Stat(sPath)
    If Error Then 
      Error Error.Text & ": " & sPath
      Continue
    Endif
    
    With hStat
      If .Type = gb.Directory Then
        Inc $nDir
        ScanDir(sPath)
      Else If IsDir(sPath) Then
        Inc $nDir
      Else
        Inc $nFile
      Endif
      Try $iSize += .Size
    End With
    
    If (Timer - $fTime) >= ($nTime / 5) Then
      $nTime = CInt((Timer - $fTime) * 5) + 1
      Print $iSize;; $nFile;; $nDir
    Endif
    
  Next
  
End


Public Sub Main() As String

  $fTime = Timer
  $nTime = 1
  
  ScanDir($sDir)
  
  Return CStr($iSize) & " " & CStr($nFile) & " " & CStr($nDir)
  
End
