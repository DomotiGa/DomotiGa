' Gambas class file

Private $hBookmarkList As CBookmarkList1
Private $hBookmarks As CBookmark1[]

Public Sub Run(hBookmarkList As CBookmarkList1) 
  
  $hBookmarkList = hBookmarkList
  FEditBookmark1.ShowModal()
  
End


Public Sub Form_Open()

  $hBookmarks = $hBookmarkList.GetEditableBookmarks()

  With tvwBookmark
    .Columns.Count = 2
    .Columns[0].Text = ("Name")
    .Columns[1].Text = ("Path")
    .Columns[0].Width = Desktop.Scale * 24
    .Rows.Count = $hBookmarkList.GetEditableCount()
    .Rows.Height = -1
  End With
  
End

' Public Sub txtPath_Click()
' 
'   Dialog.Path = txtPath.Text
'   If Dialog.SelectDirectory() Then Return
'   txtPath.Text = Dialog.Path
' 
' End

Public Sub btnClose_Click()

  Me.Close

End

Public Sub tvwBookmark_Data(Row As Integer, Column As Integer)

  Select Case Column
    Case 0
      tvwBookmark.Data.Text = $hBookmarks[Row].Name
    Case 1
      tvwBookmark.Data.Text = $hBookmarks[Row].Link
  End Select
  tvwBookmark.Data.Padding = 4

End

Public Sub tvwBookmark_Activate()

  Dim hBookmark As CBookmark1 = $hBookmarks[tvwBookmark.Row]
  
  If Not hBookmark.IsEditable() Then Return
  
  If tvwBookmark.Column = 0 Then
    tvwBookmark.Edit
  Endif

End

Public Sub btnUp_Click()

  Dim iRow As Integer = tvwBookmark.Row
  Dim iOther As Integer
  
  If iRow <= 0 Then Return
  
  iOther = $hBookmarkList.Find($hBookmarks[iRow - 1])
  iRow = $hBookmarkList.Find($hBookmarks[iRow])
  
  Swap $hBookmarkList[iRow], $hBookmarkList[iOther]
  $hBookmarks = $hBookmarkList.GetEditableBookmarks()
  
  tvwBookmark.Refresh
  Dec tvwBookmark.Row

End

Public Sub btnDown_Click()

  Dim iRow As Integer = tvwBookmark.Row
  Dim iOther As Integer
  
  If iRow < 0 Or If iRow = $hBookmarks.Max Then Return
  
  iOther = $hBookmarkList.Find($hBookmarks[iRow + 1])
  iRow = $hBookmarkList.Find($hBookmarks[iRow])
  
  Swap $hBookmarkList[iRow], $hBookmarkList[iOther]
  $hBookmarks = $hBookmarkList.GetEditableBookmarks()
  
  tvwBookmark.Refresh
  Inc tvwBookmark.Row

End

Public Sub btnDelete_Click()

  Dim iRow As Integer = tvwBookmark.Row
  
  If iRow < 0 Then Return
  
  If Message.Delete(("Do you really want to remove this bookmark?"), ("Remove"), ("Cancel")) = 2 Then Return
  
  iRow = $hBookmarkList.Find($hBookmarks[iRow])
  
  $hBookmarkList.Remove(iRow)
  $hBookmarks = $hBookmarkList.GetEditableBookmarks()
  
  Dec tvwBookmark.Rows.Count
  tvwBookmark.Refresh
  tvwBookmark.Row = -1

End

Public Sub tvwBookmark_Save(Row As Integer, Column As Integer, Value As String)

  If Column = 0 Then
    If Value Then $hBookmarks[Row].SetName(Trim(Value))
  Endif

End
