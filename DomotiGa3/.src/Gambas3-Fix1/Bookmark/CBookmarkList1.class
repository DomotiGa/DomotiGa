' Gambas class file

Inherits CBookmark1[]

Property Private As Collection[]

Private $aPrivate As Collection[]

Public Sub _new()
  
  Load
  
End

Public Sub AddBookmark(sLink As String, Optional sName As String, sIcon As String)

  Dim hBookmark As CBookmark1

  hBookmark = New CBookmark1(sLink, sName, sIcon)
  Me.Add(hBookmark)

End

Public Sub RemoveBookmark(sLink As String)
  
  Dim hBookmark As CBookmark1
  Dim iInd As Integer
  
  hBookmark = FindBookmark(sLink)
  If Not hBookmark Then Return
  iInd = Me.Find(hBookmark)
  Me.Remove(iInd)
  
End


Public Sub Load()
  
  Dim iInd As Integer
  Dim hSettings As Settings 
  Dim aBookmark As String[]
  Dim cPrivate As Collection
  
  Me.Clear()
  
  AddBookmark("$" & System.User.Home, ("Home"), "icon:/small/home")
  If Component.IsLoaded("gb.desktop") Then
    AddBookmark("$" & Desktop.Path, ("Desktop"), "icon:/small/desktop")
  Endif
  AddBookmark("$/", ("System"), "icon:/small/system")

  If $aPrivate Then
    For Each cPrivate In $aPrivate
      If cPrivate["Path"] Then Try AddBookmark("#" & cPrivate["Path"], cPrivate["Name"], cPrivate["Icon"])
    Next
  Endif 

  If Component.IsLoaded("gb.settings") Then 
  
    hSettings = New Settings("gb.form")
    
    For iInd = 1 To hSettings["/Bookmarks/Count", 0]
      aBookmark = hSettings["/Bookmarks/Bookmark" & CStr(iInd)]
      AddBookmark(aBookmark[0], aBookmark[1])
    Next
    
  Endif

End

Public Sub Save()
  
  Dim hSettings As Settings 
  Dim iInd As Integer  
  Dim hBookmark As CBookmark1
  
  If Not Component.IsLoaded("gb.settings") Then Return
  
  hSettings = New Settings("gb.form")

  hSettings.Clear("/Bookmarks")
  
  For Each hBookmark In Me
    If Left(hBookmark.Link) <> "/" Then Continue
    Inc iInd
    hSettings["/Bookmarks/Bookmark" & CStr(iInd)] = [hBookmark.Link, hBookmark.Name]
  Next
  
  hSettings["/Bookmarks/Count"] = iInd

  hSettings.Save
    
End


Private Function Private_Read() As Collection[]

  Return $aPrivate

End

Private Sub Private_Write(Value As Collection[])

  $aPrivate = Value
  Load

End

Public Sub FindBookmark({Link} As String) As CBookmark1
  
  Dim hBookmark As CBookmark1
  
  For Each hBookmark In Me
    If hBookmark.Link = {Link} Then Return hBookmark
  Next
  
  If Left({Link}) = "/" Then
    For Each hBookmark In Me
      If hBookmark.Path = {Link} Then Return hBookmark
    Next
  Endif
  
End

Public Sub FindBest(sDir As String) As CBookmark1
  
  Dim hBookmark As CBookmark1
  Dim sBest As String
  Dim iScore As Integer
  Dim aBook As String[]
  Dim aDir As String[]
  Dim iInd As Integer
  
  aDir = Split(sDir, "/")
  
  For Each hBookmark In Me
    aBook = Split(hBookmark.Path, "/")
    For iInd = 0 To Min(aDir.Max, aBook.Max)
      If aBook[iInd] <> aDir[iInd] Then Break
    Next
    'DEBUG "Comp:";; aBook.Join("|");; aDir.Join("|");; "=";; iInd
    If iInd > iScore And If iInd > aBook.Max Then   
      iScore = iInd
      sBest = hBookmark.Link
    Endif
  Next  
  
  If iScore <= 1 Then sBest = "$/"
  
  Return FindBookmark(sBest)
  
End

Public Sub GetEditableCount() As Integer
  
  Dim hBookmark As CBookmark1
  Dim iCount As Integer
  
  For Each hBookmark In Me
    If hBookmark.IsEditable() Then Inc iCount
  Next
  
  Return iCount
  
End

Public Sub GetEditableBookmarks() As CBookmark1[]
  
  Dim hBookmark As CBookmark1
  Dim hBookmarks As New CBookmark1[]
  
  For Each hBookmark In Me
    If hBookmark.IsEditable() Then hBookmarks.Add(hBookmark)
  Next
  
  Return hBookmarks
  
End
