' Gambas class file

Inherits Task

Private $sDir As String
Private $iSize As Integer

Public Sub _new(sDir As String, iSize As Integer)
  
  $sDir = sDir
  $iSize = iSize
  
End

Private Sub PrintIcon(sFile As String, hImage As Image)

  Dim hIcon As Image
  Dim sTemp As String

  hIcon = New Image(hImage.W + 4, hImage.H + 4, Color.Transparent)
  Paint.Begin(hIcon)
  Paint.AntiAlias = False
  Paint.LineWidth = 2
  Paint.Rectangle(0, 0, hIcon.W, hIcon.H)
  Paint.Background = Color.Gray
  Paint.Stroke
  Paint.End
  hIcon.DrawImage(hImage, 2, 2)
  
  sTemp = File.SetExt(Temp$(), "png")
  hIcon.Save(sTemp)
  Print sFile; "\t"; sTemp
    
End

Public Sub Main()

  Dim sFile As String
  Dim sExt As String
  Dim sPath As String
  Dim hImage As Image
  Dim hSvgImage As SvgImage

  Application.Priority += 10

  For Each sFile In Dir($sDir).Sort(gb.Natural)
    
    sPath = $sDir &/ sFile
    sExt = LCase(File.Ext(sFile))
    
    If sExt = "jpg" Or If sExt = "jpeg" Or If sExt = "png" Or If sExt = "gif" Or If sExt = "bmp" Or If sExt = "xpm" Then
      
      If Stat(sPath).Size > 4194304 Then Continue
      
      Try hImage = Image.Load(sPath)
      If Error Then Continue
      
      If Not (hImage.Width = $iSize And hImage.Height = $iSize) Then 
        If hImage.Width > hImage.Height Then 
          hImage = hImage.Stretch($iSize, ($iSize * hImage.Height) \ hImage.Width)
        Else
          hImage = hImage.Stretch(($iSize * hImage.Width) \ hImage.Height, $iSize)
        Endif
      Endif
      
      PrintIcon(sFile, hImage)
    
    Else If sExt = "svg" Or If sExt = "svgz" Then
      
      Try hSvgImage = SvgImage.Load(sPath)
      If Error Then Continue
      
      If hSvgImage.Width > hSvgImage.Height Then 
        hSvgImage.Resize($iSize, $iSize * hSvgImage.Height / hSvgImage.Width)
      Else
        hSvgImage.Resize($iSize * hSvgImage.Width / hSvgImage.Height, $iSize)
      Endif

      hImage = New Image(hSvgImage.Width, hSvgImage.Height, Color.Transparent)
      Paint.Begin(hImage)
      hSvgImage.Paint()
      Paint.End
    
      PrintIcon(sFile, hImage)
      
    Endif
    
  Next
  
  Print "."
  
  Do
    Sleep 3600
  Loop
  
End
