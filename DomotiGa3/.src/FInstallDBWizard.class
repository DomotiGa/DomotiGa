' Gambas class file

' Description:
' FDBInstallWizard.class
' Install an empty or demo database.

' Development Status:
' Finished and working.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  lblVersion.Text = ("version ") & Main.sProgramVersion
  txtCopyright.Text = "Copyright (C) 2008-" & Year(Now) & " Ron Klinkien"
  txtHost.Text = Main.sSQLHost
  vbPort.Value = Main.iSQLPort
  txtDatabase.Text = Main.sSQLDatabase
  txtPassword.SetFocus

End

Public Sub CreateDatabase()

  Dim sDumpFile As String = IIf(rbEmpty.Value, "domotiga-empty.sql", "domotiga.sql")
  Dim sLog As String

  If Not Util.ProgramExist("mysql") Then Error.Raise("Couldn't find the mysql command, did you install mysql-client?")

  ' Test if we see expected output:
  ' +------------+
  ' | ctest_text |
  ' +------------+
  ' | Okay       |
  ' +------------+
  Shell "mysql --host=" & txtHost.Text & " --port=" & vbPort.Value & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --execute='SELECT \"Okay\" AS ctest_text;' 2>&1" To sLog
  If Not InStr(sLog, "\nOkay\n") Then
    WriteLog(sLog)
    Error.Raise("Not able to connect to database on '" & txtHost.Text & "' port " & vbPort.Value & " with username '" & txtUser.Text & "' and given password!")
  Endif

  Shell "mysql --host=" & txtHost.Text & " --port=" & vbPort.Value & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --execute='CREATE DATABASE " & txtDatabase.Text & ";' 2>&1" To sLog
  WriteLog(sLog)
  If sLog Like "ERROR" Then
    Error.Raise("Not able to create '" & txtDatabase.Text & "' Database")
  Else
    WriteLog("Created database called '" & txtDatabase.Text & "' ...")
  Endif

  Shell "mysql --host=" & txtHost.Text & " --port=" & vbPort.Value & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --database=" & txtDatabase.Text & " --execute='SOURCE " & Main.sBaseDir &/ "install" &/ sDumpFile & "' 2>&1" To sLog
  WriteLog(sLog)
  If sLog Like "ERROR" Then
    Error.Raise("Problem loading database tables from '" & sDumpFile & "'")
  Else
    WriteLog("Loaded database tables from '" & sDumpFile & "' ...")
  Endif

  Shell "mysql --host=" & txtHost.Text & " --port=" & vbPort.Value & " --user=" & txtUser.Text & " --password=" & txtPassword.Text & " --execute='FLUSH PRIVILEGES;' 2>&1" To sLog
  WriteLog(sLog)
  WriteLog("Flushed database privileges ...")

  ' Overwrite possible pre-existing Host & Database information, else DomotiGa will not start properly
  Main.sSQLHost = txtHost.Text
  Main.iSQLPort = vbPort.Value
  Main.sSQLDatabase = txtDatabase.Text
  Main.bSQLInitialized = True

  ' Update the domotiga.conf
  Main.SetConfig()

Catch
  Message.Error("Error creating the database '" & Error.Text & "'!")

End

Private Sub WriteLog(sText As String)

  txtLog.Pos = txtLog.Length
  txtLog.Insert(sText & "\n")

End

Public Sub wInstall_Cancel()

  Me.Close

End

Public Sub wInstall_Close()

  Me.Close

  ' Check again of the MySQL connection is available. If not, exit gracefully
  If Not Main.ConnectDatabase() Then Message.Error("DomotiGa cannot connect to the MySQL Database\nPlease check if the MySQL Server is running and/or\nuserid/password are correct")

End

Public Sub wInstall_BeforeChange()

  Select Case wInstall.Index
    Case 0
      If rbAgree.Value = False Then
        Balloon("Please agree or click cancel", rbAgree)
        Stop Event
        Return
      Endif
    Case 2
      If Len(txtHost.Text) = 0 Then
        Balloon("Please enter a hostname", txtHost)
        Stop Event
        Return
      Endif
      If vbPort.Value = 0 Then
        Balloon("Please enter a valid port number", vbPort)
        Stop Event
        Return
      Endif
      If Len(txtDatabase.Text) = 0 Then
        Balloon("Please enter a database name", txtDatabase)
        Stop Event
        Return
      Endif
      If Len(txtUser.Text) = 0 Then
        Balloon("Please enter a user", txtUser)
        Stop Event
        Return
      Endif
      If Len(txtPassword.Text) = 0 Then
        Balloon("Please enter a password", txtPassword)
        Stop Event
        Return
      Endif
  End Select

End

Public Sub wInstall_Change()

  Select Case wInstall.Index
    Case 3
      CreateDatabase()
  End Select

End
