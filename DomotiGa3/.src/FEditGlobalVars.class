' Gambas class file

' Description:
' FEditGlobalVars.class
' Add a way to edit/delete globalvars.

' Development Status:
' Just created.

' DomotiGa - an open source home automation program.
' Copyright (C) Ron Klinkien, The Netherlands.

' Read file called COPYING for license details.

Public Sub Form_Open()

  Me.Move(FMain.X + 50, FMain.Y + 70)
  GetVarList()

End

Public Sub btnClose_Click()

  Me.Close

End

Public Sub GetVarList()

  Dim iCount As Integer
  Dim vValue As Variant

  With tbvGlobalVar
    .Columns.Count = 2
    .Rows.Count = Main.GlobalVar.Count
    .Columns[0].Title = ("Name")
    .Columns[0].Width = 180
    .Columns[1].Title = ("Value")
    .Columns[1].Width = 150
  End With

  For Each vValue In Main.GlobalVar

    If vValue = "<EMPTY>" Then
      vValue = ""
    Endif

    tbvGlobalVar[iCount, 0].Text = Main.GlobalVar.Key
    If gb.Boolean = TypeOf(vValue) Then
      tbvGlobalVar[iCount, 1].Text = Main.DisplayBool(vValue)
    Else
      tbvGlobalVar[iCount, 1].Text = vValue
    End If
    iCount = iCount + 1
  Next

End

Public Sub tbvGlobalVar_ColumnClick(Column As Integer)

  FMain.SortTableView(Me.tbvGlobalVar, Column, True)

End

Public Sub btnRefresh_Click()

  GetVarList()

End

Public Sub btnDelete_Click()

  If Message.Question("Are you sure you want to delete var '" & tbvGlobalVar[tbvGlobalVar.Row, 0].Text & "'?", "Yes", "No") = 1 Then
    If Main.bServer Then
      Main.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
    Else
      Main.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
      XMLClient.DelGlobalVar(tbvGlobalVar[tbvGlobalVar.Row, 0].Text)
    Endif
    tbvGlobalVar.Rows.Remove(tbvGlobalVar.Row)
  Endif

End

Public Sub tbvGlobalVar_Activate()

  tbvGlobalVar.Edit

End

Public Sub tbvGlobalVar_Save(Row As Integer, Column As Integer, Value As String)
  
  Dim sOldGlobalVar As String

  If tbvGlobalVar.Column = 0 Then
    sOldGlobalVar = tbvGlobalVar[Row, 0].Text
    tbvGlobalVar[Row, Column].Text = Value
    Main.DelGlobalVar(sOldGlobalVar)
    Main.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)

    If Not Main.bServer Then
      XMLClient.DelGlobalVar(sOldGlobalVar)
      XMLClient.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)
    Endif
  Else If tbvGlobalVar.Column = 1 Then
    tbvGlobalVar[Row, Column].Text = Value
    Main.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)

    If Not Main.bServer Then
      XMLClient.SetGlobalVar(tbvGlobalVar[Row, 0].Text, tbvGlobalVar[Row, 1].Text)
    Endif
  Endif

End

Public Sub tbvGlobalVar_Insert()

   tbvGlobalVar.Cancel
   
End

Public Sub btnAdd_Click()

  If Len(txtName.Text) > 0 Then
    If Main.bServer Then
      Main.SetGlobalVar(txtName.Text, txtValue.Text)
    Else
      Main.SetGlobalVar(txtName.Text, txtValue.Text)
      XMLClient.SetGlobalVar(txtName.Text, txtValue.Text)
    Endif
    GetVarList()
    txtName.Clear
    txtValue.Clear
  Else
    Balloon("Fill in a variable name first!", txtName)
  Endif

End
